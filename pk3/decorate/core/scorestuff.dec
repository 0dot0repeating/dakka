actor DakkaScoreItem:    Counter { +INTERHUBSTRIP }

actor ScoreMonsterFinder
{
    +NOINTERACTION
    +SERVERSIDEONLY
    
    States
    {
    // (1 << 10) is RGF_NOSIGHT, but we can't just use that since Zandronum doesn't recognize it
    //  - keep in mind, this doesn't ever get used in Zandronum, but even then it'll still parse this
      Spawn:
        TNT1 A 1
        stop
      
      Find:
        TNT1 A 1 A_RadiusGive("ScoreCalcItem", 16383, RGF_MONSTERS | RGF_CUBE | (1 << 10))
        stop
    }
}

actor ScoreCalcItem: Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("Score_Count")
        stop
    }
}


actor RegenSoundLooper
{
    +NOINTERACTION
    +NOTIMEFREEZE

    Radius 4
    Height 4

    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_PlaySound("dakka/ammoregen_start", CHAN_ITEM, 1.0, false)
        TNT1 A 0 A_PlaySound("dakka/ammoregen",       CHAN_BODY | CHAN_LISTENERZ, 1.0, true)
        TNT1 A 1 A_Warp(AAPTR_TARGET, 0,0,0, 0, WARPF_NOCHECKPOSITION | WARPF_COPYINTERPOLATION)
        wait

      RegenDone:
        TNT1 A 0 A_PlaySound("dakka/fizzle", CHAN_BODY)
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_Warp(AAPTR_TARGET, 0,0,0, 0, WARPF_NOCHECKPOSITION | WARPF_COPYINTERPOLATION)
        stop
    }
}

actor DakkaReviveExplosion
{
    +NOINTERACTION
    +FORCERADIUSDMG
    +NODAMAGETHRUST

    Radius 4
    Height 4

    Obituary "%k witnessed %o's revival."

    var int user_firestep;

    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 12, 0)
        TNT1 A 0 A_PlaySound("dakka/revive", CHAN_BODY, 1.0, false, ATTN_NONE)
        TNT1 A 0 A_PlaySound("dakka/revive", CHAN_ITEM, 1.0, false, ATTN_NONE)
        TNT1 A 0 A_Explode(384, 256, false)
        goto FlameRevival

      FlameRevival:
        TNT1 A 0 A_SpawnItemEx("RevivalFlame", 80, 0, -32, frandom(-0.5, 0.5), frandom(-0.5, 0.5), 2.5,   0,   SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("RevivalFlame", 80, 0, -32, frandom(-0.5, 0.5), frandom(-0.5, 0.5), 2.5,  45,   SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("RevivalFlame", 80, 0, -32, frandom(-0.5, 0.5), frandom(-0.5, 0.5), 2.5,  90,   SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("RevivalFlame", 80, 0, -32, frandom(-0.5, 0.5), frandom(-0.5, 0.5), 2.5, 135,   SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("RevivalFlame", 80, 0, -32, frandom(-0.5, 0.5), frandom(-0.5, 0.5), 2.5, 180,   SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("RevivalFlame", 80, 0, -32, frandom(-0.5, 0.5), frandom(-0.5, 0.5), 2.5, 225,   SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("RevivalFlame", 80, 0, -32, frandom(-0.5, 0.5), frandom(-0.5, 0.5), 2.5, 270,   SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("RevivalFlame", 80, 0, -32, frandom(-0.5, 0.5), frandom(-0.5, 0.5), 2.5, 315,   SXF_TRANSFERPOINTERS)

        TNT1 A 0 A_SpawnItemEx("RevivalFlameGround", 80, 0, -32, frandom(-0.2, 0.2), frandom(0.8, 1.2), 0,   0,   SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("RevivalFlameGround", 80, 0, -32, frandom(-0.2, 0.2), frandom(0.8, 1.2), 0,  45,   SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("RevivalFlameGround", 80, 0, -32, frandom(-0.2, 0.2), frandom(0.8, 1.2), 0,  90,   SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("RevivalFlameGround", 80, 0, -32, frandom(-0.2, 0.2), frandom(0.8, 1.2), 0, 135,   SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("RevivalFlameGround", 80, 0, -32, frandom(-0.2, 0.2), frandom(0.8, 1.2), 0, 180,   SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("RevivalFlameGround", 80, 0, -32, frandom(-0.2, 0.2), frandom(0.8, 1.2), 0, 225,   SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("RevivalFlameGround", 80, 0, -32, frandom(-0.2, 0.2), frandom(0.8, 1.2), 0, 270,   SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("RevivalFlameGround", 80, 0, -32, frandom(-0.2, 0.2), frandom(0.8, 1.2), 0, 315,   SXF_TRANSFERPOINTERS)

        TNT1 A 1 A_SetAngle(angle + 2.5)
        TNT1 A 0 A_SetUserVar("user_firestep", user_firestep + 1)
        TNT1 A 0 A_JumpIf(user_firestep > 70, "FlameRevivalDone")
        goto FlameRevival

      FlameRevivalDone:
        TNT1 AAAA 0 A_SpawnItemEx("RevivalFlame", 80, 0, -32, frandom(-0.5, 0.5), frandom(-0.5, 0.5), 2.0,   0,   SXF_TRANSFERPOINTERS)
        TNT1 AAAA 0 A_SpawnItemEx("RevivalFlame", 80, 0, -32, frandom(-0.5, 0.5), frandom(-0.5, 0.5), 2.0,  45,   SXF_TRANSFERPOINTERS)
        TNT1 AAAA 0 A_SpawnItemEx("RevivalFlame", 80, 0, -32, frandom(-0.5, 0.5), frandom(-0.5, 0.5), 2.0,  90,   SXF_TRANSFERPOINTERS)
        TNT1 AAAA 0 A_SpawnItemEx("RevivalFlame", 80, 0, -32, frandom(-0.5, 0.5), frandom(-0.5, 0.5), 2.0, 135,   SXF_TRANSFERPOINTERS)
        TNT1 AAAA 0 A_SpawnItemEx("RevivalFlame", 80, 0, -32, frandom(-0.5, 0.5), frandom(-0.5, 0.5), 2.0, 180,   SXF_TRANSFERPOINTERS)
        TNT1 AAAA 0 A_SpawnItemEx("RevivalFlame", 80, 0, -32, frandom(-0.5, 0.5), frandom(-0.5, 0.5), 2.0, 225,   SXF_TRANSFERPOINTERS)
        TNT1 AAAA 0 A_SpawnItemEx("RevivalFlame", 80, 0, -32, frandom(-0.5, 0.5), frandom(-0.5, 0.5), 2.0, 270,   SXF_TRANSFERPOINTERS)
        TNT1 AAAA 0 A_SpawnItemEx("RevivalFlame", 80, 0, -32, frandom(-0.5, 0.5), frandom(-0.5, 0.5), 2.0, 315,   SXF_TRANSFERPOINTERS)
        TNT1 A 8
        stop
    }
}


actor FreezeRevivee: PowerSpeed
{
    +NOTRAIL
    Speed 0
    Powerup.Duration -1
}

actor RevivalIntervention: PowerProtection
{
    +NORADIUSDMG
    +DONTBLAST
    +DONTRIP
    +DONTMORPH
    +DONTSQUASH

    DamageFactor None, 0
    Powerup.Duration -2
}


actor RevivalFlame
{
    Radius 2
    Height 2

    Projectile
    Speed 25

    +MTHRUSPECIES
    +THRUACTORS
    +BLOODLESSIMPACT
    +NODAMAGETHRUST
    +FORCERADIUSDMG
    +FOILINVUL
    +PIERCEARMOR
    +SERVERSIDEONLY

    RenderStyle Add
    Scale 0.2
    Alpha 0.5

    Obituary "%k basked in the glory of %o's revival."

    var int user_counter;
    
    States
    {
      Spawn:
        DFLM E 0 bright
        TNT1 A 0 A_ScaleVelocity(random(90, 110) / 100.0)
        TNT1 A 0 A_ChangeFlag("PAINLESS", random(0, 6) > 0)
        TNT1 A 0 A_SetUserVar("user_counter", 24)
        TNT1 A 1 A_Explode(2, 64, 0, 0, 32)
        goto Spawn2

      Spawn2:
        TNT1 A 0 A_SpawnItemEx("RevivalFlameVisual", 0, 0, 0, 0,0,random(0, 100)/50.0, 0, SXF_ABSOLUTEPOSITION)
        TNT1 A 1 A_Explode(1, 64, 0, 0, 32)
        TNT1 A 1 A_SetUserVar("user_counter", user_counter - 1)
        TNT1 A 0 A_JumpIf(user_counter > 0, "Spawn2")
        goto EndItAll

      EndItAll:
        TNT1 A 1
        stop

      Death:
        TNT1 A 0 A_JumpIf(user_counter > 0, 1)
        goto EndItAll

        TNT1 A 0 A_SpawnItemEx("RevivalFlameVisual", 0, 0, 0, 0,0,random(0, 100)/50.0, 0, SXF_ABSOLUTEPOSITION)
        TNT1 A 0 A_Explode(1, 64, 0, 0, 32)
        TNT1 A 1 A_SetUserVar("user_counter", user_counter - 3)
        loop
    }
}

actor RevivalFlameGround
{
    Radius 2
    Height 2

    +NOINTERACTION
    +SERVERSIDEONLY

    RenderStyle Add
    Scale 0.2
    Alpha 0.5

    var int user_counter;
    
    States
    {
      Spawn:
        DFLM E 0 bright
        TNT1 A 0 A_SetUserVar("user_counter", 12)
        goto Spawn2

      Spawn2:
        TNT1 A 0 A_SpawnItemEx("RevivalFlameVisual", 0, 0, 0, 0,0,random(0, 100)/50.0, 0, SXF_ABSOLUTEPOSITION)
        TNT1 A 1 A_SetUserVar("user_counter", user_counter - 1)
        TNT1 A 0 A_JumpIf(user_counter > 0, "Spawn2")
        goto EndItAll

      EndItAll:
        TNT1 A 1
        stop

    }
}

actor RevivalFlameVisual
{
    Radius 0
    Height 0

    +NOINTERACTION
    +CLIENTSIDEONLY

    RenderStyle Add
    Scale 0.15
    Alpha 0.35

    States
    {
      Spawn:
        DFLM E 0 bright
        DFLM E 0 bright A_JumpIf(CallACS("Dakka_LessEffects"), "NoFancy")
        goto Fancy

      NoFancy:
        TNT1 A 0 A_Jump(128, 1)
        stop
        DFLM EIMO 1 bright
        stop

      Fancy:
        DFLM E 0 ThrustThing(random(0, 255), random(0, 16) / 4.0, 1, 0)
        DFLM E 0 ThrustThingZ(0, random(0, 16) / 4.0, random(0, 1), 1)
        goto Spawn2
      
      Spawn2:
        DFLM EILNO 1 bright
        stop
    }
}
