actor TracerDummy
{
    +NOINTERACTION
    Height 0
    Radius 0

    RenderStyle None

    States
    {
      Spawn:
        PLSS A 35
        stop
    }
}

actor NoBulletPuff
{
    +NOINTERACTION
    +BLOODLESSIMPACT
    RenderStyle None
    Decal ""

    States
    {
      Spawn:
        TNT1 A 0
        stop
    }
}

actor DakkaPuff: BulletPuff
{
    +PUFFGETSOWNER
    +ALWAYSPUFF
    +PUFFONACTORS
    +FORCEXYBILLBOARD
    +NOTIMEFREEZE
    +NOINTERACTION
    +SKYEXPLODE

    Radius 0
    Height 0

    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_JumpIf(z - floorz   < 8, "FloorHit")
        TNT1 A 0 A_JumpIf(ceilingz - z < 8, "CeilingHit")
        goto WallHit

      FloorHit:
        TNT1 A 0 A_SpawnItemEx("DakkaBulletSpark", 0,0,0, random(100, 160)/10.0, random(-50, 50)/10.0, random(25, 100)/10.0, 180+random(-30, 30))
        TNT1 AAA 0 A_SpawnItemEx("DakkaBulletSpark2", 0,0,0, random(40, 140)/10.0, random(-30, 30)/10.0, random(5, 40)/10.0, 180+random(-15, 15))
        TNT1 A 0 A_Jump(256, "Trace")
        goto Trace

      CeilingHit:
        TNT1 A 0 A_SpawnItemEx("DakkaBulletSpark", 0,0,-4, random(100, 320)/10.0, random(-50, 50)/10.0, -random(15, 120)/10.0, 180+random(-30, 30))
        TNT1 AAA 0 A_SpawnItemEx("DakkaBulletSpark2", 0,0,-4, random(40, 240)/10.0, random(-30, 30)/10.0, -random(5, 80)/10.0, 180+random(-15, 15))
        TNT1 A 0 A_Jump(256, "Trace")
        goto Trace

      WallHit:
        TNT1 A 0 A_SpawnItemEx("DakkaBulletSpark", 0,0,-4, random(30, 80)/10.0, random(-30, 30)/10.0, -random(-60, 60)/10.0, random(-30, 30))
        TNT1 AAA 0 A_SpawnItemEx("DakkaBulletSpark2", 0,0,-4, random(20, 60)/10.0, random(-20, 20)/10.0, -random(-40, 40)/10.0, random(-15, 15))
        TNT1 A 0 A_Jump(256, "Trace")
        goto Trace

      Trace:
        TNT1 A 0 bright ACS_NamedExecuteWithResult("Dakka_Tracer", TRACE_BULLET, 0, -6)
        DPUF AB 4 bright
        goto Melee

      Melee:
        DPUF CD 2
        Stop
    }
}

actor DakkaPuff_Left: DakkaPuff
{
    States
    {
      Trace:
        TNT1 A 0 bright ACS_NamedExecuteWithResult("Dakka_Tracer", TRACE_BULLET, -2, -4)
        DPUF AB 4 bright
        goto Melee
    }
}

actor DakkaPuff_Right: DakkaPuff
{
    States
    {
      Trace:
        TNT1 A 0 bright ACS_NamedExecuteWithResult("Dakka_Tracer", TRACE_BULLET,  2, -4)
        DPUF AB 4 bright
        goto Melee
    }
}

actor DakkaPuff_BigLeft: DakkaPuff
{
    States
    {
      Trace:
        TNT1 A 0 bright ACS_NamedExecuteWithResult("Dakka_Tracer", TRACE_BULLET_BIG, -500, -420, true)
        DPUF AB 4 bright
        goto Melee
    }
}

actor DakkaPuff_BigRight: DakkaPuff
{
    States
    {
      Trace:
        TNT1 A 0 bright ACS_NamedExecuteWithResult("Dakka_Tracer", TRACE_BULLET_BIG,  500, -420, true)
        DPUF AB 4 bright
        goto Melee
    }
}

actor DakkaPuff_Painless: DakkaPuff
{
    +PAINLESS
}


actor DakkaBulletSpark
{
    +CLIENTSIDEONLY

    Radius 1
    Height 1

    Projectile
    -NOGRAVITY
    +THRUACTORS
    +DOOMBOUNCE
    +NOTIMEFREEZE
    +FORCEXYBILLBOARD
    +DONTSPLASH
    +NOTRIGGER

    Gravity 0.5
    BounceFactor 0.1

    RenderStyle Add
    Scale 0.25

    States
    {
      Spawn:
        DPUF A 0
        DPUF A 0 ACS_NamedExecuteWithResult("Dakka_LessEffects", true)
        goto Spawn2

      Spawn2:
        DPUF A 105 bright
        stop

      Death:
        TNT1 A 0
        stop
    }
}

actor DakkaBulletSpark2: DakkaBulletSpark
{
    Scale 0.5
}

actor DakkaTracer
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    +NOTIMEFREEZE
    +FORCEXYBILLBOARD
    +DONTSPLASH
    +NOTRIGGER

    Radius 0
    Height 0

    RenderStyle Add
    Scale 0.5
    Alpha 0.5

    States
    {
      Spawn:
        DPUF A 0 bright nodelay A_SetScale(0.4 + (alpha * 0.1))
        DPUF A 1 bright         A_SetTranslucent(0.4 + (alpha * 0.1), 1)
        DPUF A 0 bright         A_Stop
        goto Spawn2

      Spawn2:
        DBUF A 0 bright A_SetScale(scalex - 0.1)
        DPUF A 1 bright A_FadeOut(0.1)
        loop
    }
}

actor DakkaBigTracer: DakkaTracer
{
    Scale 1.0
    
    States
    {
      Spawn:
        DPUF E 0 bright nodelay A_SetScale(0.8 + (alpha * 0.2))
        DPUF E 1 bright         A_SetTranslucent(0.4 + (alpha * 0.1), 1)
        DPUF E 0 bright         A_Stop
        goto Spawn2

      Spawn2:
        DBUF E 0 bright A_SetScale(scalex - 0.2)
        DPUF E 1 bright A_FadeOut(0.1)
        loop
    }
}

actor AmmoWaster
{
    Projectile

    States
    {
      Spawn:
        TNT1 A 0
        stop
    }
}

