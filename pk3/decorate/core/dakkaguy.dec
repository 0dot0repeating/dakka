actor Dakkaguy: PlayerPawn
{
    Health 100
    Radius 16
    Height 56
    Mass 100

    Player.ColorRange 112, 127

    Player.DisplayName "Dakkaguy"
    Player.JumpZ 8

    Player.ForwardMove 1.2
    Player.SideMove 1.25

    Player.HexenArmor 0, 20, 5, 15, 10
    Player.HealRadiusType "Health"

    Player.StartItem "DWep_Pistols"
    Player.StartItem "DWep_Fist"
    Player.StartItem "DakkaBullet_50AE", 70

    Player.CrouchSprite "DPLC"

    // blame vincentprice for these
    DamageFactor "Dakka_NoDamage",      0
    DamageFactor "Dakka_RetreatSignal", 0

    PainChance                      255
    PainChance Dakka_NoDamage,      0
    PainChance Dakka_RetreatSignal, 0

    +THRUSPECIES

    States
    {
      Spawn:
        DPLY A -1
        loop

      See:
        DPLY ABCD 4
        loop

      Missile:
        DPLY E 12
        goto Spawn

      Melee:
        DPLY F 6 BRIGHT
        goto Missile

      Pain:
        DPLY G 0 ACS_NamedExecuteWithResult("Score_ResetUntouchable") // in acs/score/score_update.h
        DPLY G 4 ACS_NamedExecuteWithResult("Score_CheckRevival")     // in acs/score/score_reward.h
        DPLY G 4 A_Pain
        goto Spawn

      Pain.Slime:
      Pain.Falling:
      Pain.Drowning:
        DPLY G 4 ACS_NamedExecuteWithResult("Score_CheckRevival")     // in acs/score/score_reward.h
        DPLY G 4 A_Pain
        goto Spawn

      Death:
        DPLY H 10
        DPLY I 10 A_PlayerScream
        DPLY J 10 A_NoBlocking
        DPLY KLM 10
        DPLY N -1
        stop

      XDeath:
        DPLY O 5
        DPLY P 5 A_XScream
        DPLY Q 5 A_NoBlocking
        DPLY RSTUV 5
        DPLY W -1
        stop

      AltSkinDeath:
        DPLY H 6
        DPLY I 6 A_PlayerScream
        DPLY JK 6
        DPLY L 6 A_NoBlocking
        DPLY MNO 6
        DPLY P -1
        stop

      AltSkinXDeath:
        DPLY Q 5 A_PlayerScream
        DPLY R 0 A_NoBlocking
        DPLY R 5 A_SkullPop
        DPLY STUVWX 5
        DPLY Y -1
        stop
    }
}
