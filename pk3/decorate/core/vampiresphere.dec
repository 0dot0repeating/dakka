actor DakkaVampireTimer:   Counter { +INTERHUBSTRIP }
actor DakkaVampireRenewed: Boolean {}
actor DakkaVampireDone:    Boolean {}

actor DakkaVampireEffect: PowerDamage
{
    DamageFactor "normal", 1
    Powerup.Duration 0x7FFFFFFF
    Powerup.Color "FF 00 00", 0.15
}


actor DakkaVampireSphere: Trigger replaces Berserk
{
    +BIGPOWERUP
    +FANCYPICKUPSOUND
    +ALWAYSPICKUP
    +COUNTITEM

    Inventory.PickupMessage "$DAKKA_PK_VAMPIRE"
    Inventory.PickupSound "misc/p_pkup"
    Inventory.RespawnTics 2100

    States
    {
      Spawn:
        DVMP ABCDCB 6 bright
        loop

      Pickup:
        TNT1 A 0 A_TakeInventory("DakkaVampireTimer")
        TNT1 A 0 A_GiveInventory("DakkaVampireTimer", 1050)
        TNT1 A 0 A_GiveInventory("DakkaVampireRenewed")
        stop
    }
}



actor DakkaVampireFlash_Pickup: Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_SetBlend("FF 40 40", 0.3, 35)
        stop
    }
}

actor DakkaVampireFlash_Destroy: Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_SetBlend("FF 40 40", 0.2, 35)
        stop
    }
}

actor VampireSoundLooper
{
    +NOINTERACTION
    +NOTIMEFREEZE

    Radius 4
    Height 4

    var int user_active;

    States
    {
      Spawn:
        TNT1 A -1
        stop

      Active:
        TNT1 A -1 A_PlaySound("dakka/vampire_loop", CHAN_BODY, 1, true)
        stop

      Silenced:
        TNT1 A -1 A_StopSound(CHAN_BODY)
        stop

      Activate:
        TNT1 A  0 A_GiveToTarget("DakkaVampireFlash_Pickup")
        TNT1 A -1 A_PlaySound("dakka/vampire_pickup", CHAN_ITEM)
        stop

      Deactivate:
        TNT1 A  0 A_GiveToTarget("DakkaVampireFlash_Destroy")
        TNT1 A -1 A_PlaySound("dakka/vampire_end", CHAN_ITEM)
        stop
    }
}


actor VampireHealSound
{
    +NOINTERACTION
    +NOTIMEFREEZE

    Radius 4
    Height 4

    States
    {
      Spawn:
        TNT1 A 1
        TNT1 A 69 A_PlaySound("dakka/vampire_heal", CHAN_BODY | CHAN_LISTENERZ)
        stop
    }
}