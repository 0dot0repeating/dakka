actor SSGTargetPractice
{
    Radius 16
    Height 56
    Speed 10

    Monster
    Health 100
    PainChance 256
    +FLOORCLIP

    PainSound   "*pain50"
    DeathSound  "*death"

    Obituary "%o missed."

    States
    {
      Spawn:
        PLAY A 1 A_Look
        loop

      See:
        PLAY A 0 A_JumpIfCloser(192, "Run")
        PLAY A 0 A_SetTranslucent(1)
        PLAY AABBCCDDAABBCCDDAABBCCDDAABBCCDD 1 A_Chase("", "")
        loop

      Run:
        PLAY A 0 A_SetTranslucent(0.5)
        PLAY AABBCCDDAABBCCDDAABBCCDDAABBCCDDAABBCCDDAABBCCDDAABBCCDDAABBCCDDAABBCCDD 1 A_Wander
        goto See

      Missile:
        PLAY E 12
        goto See

      Melee:
        PLAY F 6 bright
        goto See

      Pain:
        PLAY G 1 A_Pain
        PLAY G 0 HealThing(10000)
        goto See

      Death:
        PLAY H 10 A_GiveToTarget("DakkaShells", 3)
        PLAY I 10 A_Scream
        PLAY J 10 A_NoBlocking
        PLAY KLM 10
        PLAY N 35
        stop

      Raise:
        stop
    }
}

actor MeatShield: Demon
{
    Health 100000
    Speed 0
    Gravity 0
    Mass 0x7FFFFFFF

    States
    {
      See:
        SARG AABBCCDD 1 A_Chase("", "")
        Loop

      Melee:
        SARG EF 8 A_FaceTarget
        SARG G 8 A_SargAttack
        Goto See

      Pain:
        SARG H 0 A_Pain
        Goto See

      Death:
        SARG I 8
        SARG J 8 A_Scream
        SARG K 4
        SARG L 4 A_NoBlocking
        SARG M 4
        SARG N 35
        stop

      Raise:
        stop
    }
}

actor DamageReporter
{
    Health 0x7FFFFFFF
    Speed 0
    Gravity 0
    Mass 0x7FFFFFFF
    Radius 32
    Height 64

    +SHOOTABLE
    +NOBLOOD

    States
    {
      Spawn:
        TNT1 A 0 A_Stop
        TNT1 A 1 ACS_NamedExecuteWithResult("DAKKTEST_ReportDamage")
        loop
    }
}

actor DAKKTEST_TIDMonitorItem: Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("DAKKTEST_MonitorTID_Ping")
        stop
    }
}


actor DAKKTEST_RadiusDamageOff: Boolean {}

actor DAKKTEST_DisableRadiusDamage: Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_ChangeFlag("NORADIUSDMG", true)
        TNT1 A 0 A_GiveInventory("DAKKTEST_RadiusDamageOff")
        stop
    }
}

actor DAKKTEST_EnableRadiusDamage: Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_ChangeFlag("NORADIUSDMG", false)
        TNT1 A 0 A_TakeInventory("DAKKTEST_RadiusDamageOff")
        stop
    }
}

actor DAKKTEST_Bright: Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_ChangeFlag("BRIGHT", true)
        stop
    }
}

actor DAKKTEST_Unbright: Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_ChangeFlag("BRIGHT", false)
        stop
    }
}

actor DAKKTEST_NoDamage: Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_ChangeFlag("NODAMAGE", true)
        stop
    }
}


actor DAKKTEST_GonnaKickYourAss: Boolean {}

actor DAKKTEST_AssKicker
{
    Radius 32
    Height 72
    Mass 0x7FFFFFFF

    +SOLID
    +SHOOTABLE
    +NODAMAGE
    +QUICKTORETALIATE
    Health 1
    PainChance 255

    States
    {
      Spawn:
        BBRN A -1
        stop

      Pain:
        BBRN A 0 A_JumpIfInventory("DAKKTEST_GonnaKickYourAss", 1, "KickYourAss")
        BBRN A 0 A_GiveInventory("DAKKTEST_GonnaKickYourAss")
        BBRN A 0 A_ChangeFlag("NOPAIN", 1)
        BBRN A 0 A_PlaySound("brain/pain", CHAN_VOICE, 1, false, 0)
        BBRN A 0 A_Jump(256, 1, 2, 3, 4)
        goto TalkShit

        BBRN A 0 A_PrintBold("oi bruv u wanna go ill rek ur shit 1v1 hit me again i fukn dare u dickless shite")
        goto TalkShit

        BBRN A 0 A_PrintBold("u fukin wot m8 u wanna go il put u in the ground sware on me mums tits")
        goto TalkShit

        BBRN A 0 A_PrintBold("do we have a problem or r u 2 busy gobblin down fat ones 2 look where the fuk u aimin")
        goto TalkShit

        BBRN A 0 A_PrintBold("weenie hut jr pickin a fight wow real fuckin rich il give u sum u gormless shite")
        goto TalkShit

      TalkShit:
        BBRN BABABAAABAAA 1
        BBRN A 0 A_ChangeFlag("NOPAIN", 0)
        BBRN BBABBABBABABAABABABBABB 1
        BBRN BAAABBBBAAABABBAABAAABBAAABBBAAAABB 1
        BBRN BABABAAABAAABBABBABBABABAABABABBABB 1
        BBRN AABBAAABBBAABBABBBABABABAABBBBABABB 1
        goto Spawn

      KickYourAss:
        BBRN A 0 A_ChangeFlag("NOPAIN", 1)
        BBRN A 0 A_PrintBold("right then fukn ave it")
        BBRN A 0 A_PlaySound("brain/death", CHAN_VOICE, 1, false, 0)
        BBRN A 0 ACS_NamedExecuteWithResult("DAKKTEST_KickAsses")
        BBRN BAAABBBBAAABABBAABAAABBAAABBBAAAABB 1
        BBRN A -1 Exit_Normal(0)
        stop
    }
}

actor DAKKTEST_InstantDeathTester
{
    Radius 10
    Height 28
    
    Health 1
    Scale 0.33
    
    +SOLID
    +SHOOTABLE
    
    States
    {
      Spawn:
        SKEL A 1 A_SetAngle(angle + 45)
        loop
      
      Death:
        SKEL A 0 A_SpawnItemEx("DAKKTEST_InstantDeathFX")
        SKEL A 0 A_SpawnItemEx("DAKKTEST_InstantDeathRude", 0,0,24, 0,0,1)
        stop
    }
}

actor DAKKTEST_InstantDeathFX
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    
    RenderStyle Add
    Scale 0.5
    
    States
    {
      Spawn:
        FIRE A 0 nodelay A_PlaySound("vile/firestrt")
        FIRE ABCDEFGH 2 bright
        stop
    }
}

actor DAKKTEST_InstantDeathRude
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    +FORCEXYBILLBOARD
    
    Scale 0.5
    
    States
    {
      Spawn:
        RUDE A 12
        RUDE A 1 A_FadeOut(0.1)
        wait
    }
}



actor NotAZombieman:      Zombieman           {}
actor NotAShotgunner:     ShotgunGuy          {}
actor NotAChaingunner:    ChaingunGuy         {}
actor NotAnImp:           DoomImp             {}
actor NotADemon:          Demon               {}
actor NotASpectre:        Spectre             {}
actor NotATomato:         Cacodemon           {}
actor NotABonehead:       Revenant            {}
actor NotASkull:          LostSoul            {}
actor NotAMeatball:       PainElemental       {}
actor NotAHellKnight:     HellKnight          {}
actor NotABaron:          BaronOfHell         {}
actor NotAMancubus:       Fatso               {}
actor NotAnArachnotron:   Arachnotron         {}
actor NotAnAlien:         Archvile            {}
actor NotACyberdemon:     Cyberdemon          {}
actor NotASpiderdemon:    SpiderMastermind    {}
actor NotANazi:           WolfensteinSS       {}
actor NotCommanderKeen:   CommanderKeen       {}


actor DakkaCustomBridge: CustomBridge 29651
{
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_BridgeInit("DakkaBridgeBall")
        TNT1 A -1
        stop
    }
}

actor DakkaBridgeBall: BridgeBall
{
    Scale 0.4

    States
    {
      Spawn:
        PLSS AAAAAAAABBBBBBBB 1 bright A_BridgeOrbit
        loop
    }
}