actor BFGPointerTID: Counter {}

actor DWep_BFG9000: DoomWeapon
{
    Tag "BFG9000"

    Weapon.SlotNumber 7
    Weapon.SelectionOrder 1000

    Weapon.AmmoType1    "DakkaCells"
    Weapon.AmmoType2    "DakkaCells"
    Weapon.AmmoUse1     80
    Weapon.AmmoUse2     25
    Weapon.AmmoGive1    80
    
    Weapon.BobStyle InverseSmooth
    Weapon.BobSpeed 2.5
    Weapon.BobRangeX 0.2
    Weapon.BobRangeY 0.3

    Inventory.PickupMessage "$DAKKA_PK_BFG9000"
    Obituary "$DAKKA_MP_BFG9000SPLASH"

    States
    {
      Select:
        DBFG AA 0 A_Raise
        DBFG A 1 A_Raise
        loop

      Deselect:
        DBFG A   0 A_Light0
        DBFG AAA 0 A_Lower
        DBFG A   1 A_Lower
        loop

      Ready:
        DBFG A 1 A_WeaponReady
        loop

      Fire:
        DBFG B 30 bright A_PlaySound("dakka/bfgfire", 5)
        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 8, 0)
        DBFG B 0 A_GunFlash
        DBFG B 0 bright A_PlaySound("dakka/bfgfire2", CHAN_WEAPON)
        DBFG B 0 A_FireCustomMissile("AmmoWaster")
        DBFG B 0 A_SpawnItemEx("Dakka_BFGLaser", 0,0,32, 32767 * cos(pitch), 0, 32767 * -sin(pitch), angle, SXF_ABSOLUTEANGLE)
        DBFG B 0 A_FireBullets(0, 0, 1, 0, "BFGLaserPuff", 0)
        DBFG A 0 A_GiveInventory("GiveScrapCounter2", 80)
        DBFG A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, 10, 4, 18)
        DBFG B 0 A_JumpIfInventory("PowerSpread", 1, "Spread")
        DBFG B 0 A_JumpIfInventory("RuneSpread", 1, "Spread")
        goto Fire2

      Spread:
        DBFG B 0 A_SpawnItemEx("Dakka_BFGLaser", 0,0,32, 32767 * cos(pitch), 0, 32767 * -sin(pitch), angle-15, SXF_ABSOLUTEANGLE)
        DBFG B 0 A_SpawnItemEx("Dakka_BFGLaser", 0,0,32, 32767 * cos(pitch), 0, 32767 * -sin(pitch), angle+15, SXF_ABSOLUTEANGLE)
        goto Fire2

      Fire2:
        DBFG B 2 bright offset(0, 38)
        DBFG C 5 bright offset(0, 43)
        DBFG C 2 bright offset(0, 41)
        DBFG C 2 bright offset(0, 38)
        DBFG C 2 bright offset(0, 35)
        DBFG C 1 bright offset(0, 29)
        DBFG C 1 bright offset(0, 29) A_ReFire
        DBFG C 3 bright A_WeaponReady(WRF_NOBOB)
        DBFG B 4 bright offset(0, 32) A_WeaponReady(WRF_NOBOB)
        goto Ready

      Hold:
        DBFG C 1 bright offset(0, 26) A_PlaySound("dakka/bfgfire", 5)
        DBFG C 3 bright
        DBFG B 4 bright offset(0, 32)
        DBFG B 22 bright
        goto Fire+1

      AltFire:
        DBFG B 0 A_PlaySound("dakka/bfgaltfire", CHAN_WEAPON)
        DBFG A 0 A_GiveInventory("GiveScrapCounter2", 20)
        DBFG A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, 14, 4, 24)
        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 8, 1)
        DBFG B 0 A_FireCustomMissile("BFGHomingShotSpawner", 0, true, 0, 0, 0, 5)
        
        // BEAT 1 (14 tics)
        DBFG C 1 bright offset(0, 34) A_GunFlash
        DBFG C 3 bright offset(0, 39)
        DBFG C 2 bright offset(0, 38)
        DBFG C 2 bright offset(0, 37)
        DBFG C 2 bright offset(0, 35)
        DBFG C 2 bright offset(0, 32)
        DBFG C 2 bright offset(0, 28) A_PlaySound("dakka/ssgopen", 5)
        
        // BEAT 2 (15 tics)
        DBFG A 2 offset(0, 38)
        DBFG A 2 offset(0, 36)
        DBFG A 1 offset(0, 33)
        DBFG A 3 offset(0, 32)
        DBFG A 1 offset(0, 33)
        DBFG A 2 offset(0, 35) A_PlaySound("dakka/ssgload", 5)
        DBFG A 2 offset(0, 37)
        
        // BEAT 3 (14 tics)
        DBFG C 1 offset(0, 28)
        DBFG C 1 offset(0, 31)
        DBFG C 2 offset(0, 34)
        DBFG C 4 offset(0, 36)
        DBFG C 1 offset(0, 35)
        DBFG C 1 offset(0, 33)
        DBFG C 2 offset(0, 30)
        DBFG C 0 offset(0, 29) A_PlaySound("dakka/ssgclose", 5)
        DBFG C 1 offset(0, 29) A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DBFG C 1 offset(0, 27) A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        
        // BEAT 4 (18 tics)
        DBFG A 1 offset(0, 38) A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DBFG A 1 offset(0, 37) A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DBFG A 2 offset(0, 35) A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DBFG A 2 offset(0, 33) A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DBFG A 6 offset(0, 32) A_WeaponReady(WRF_NOSECONDARY | WRF_NOBOB)
        goto Ready

      Flash:
        DBFF B  4 bright A_Light(12)
        DBFF B  1 bright A_Light(11)
        DBFF B  1 bright A_Light(10)
        DBFF B  1 bright A_Light(9)
        DBFF A  1 bright A_Light(8)
        DBFF A  1 bright A_Light(7)
        DBFF A  1 bright A_Light(6)
        DBFF A  1 bright A_Light(5)
        DBFF A  1 bright A_Light(4)
        DBFF C  1 bright A_Light(3)
        DBFF C  1 bright A_Light(2)
        DBFF D  1 bright A_Light(1)
        DBFF E  1 bright A_Light(0)
        Goto LightDone

      AltFlash:
        DBFF A  1 bright A_Light(8)
        DBFF B  2 bright
        DBFF B  2 bright A_Light(7)
        DBFF B  1 bright A_Light(6)
        DBFF C  1 bright A_Light(5)
        DBFF C  1 bright A_Light(4)
        DBFF C  1 bright A_Light(3)
        DBFF D  1 bright A_Light(2)
        DBFF D  1 bright A_Light(1)
        DBFF E  1 bright A_Light(0)
        Goto LightDone

      Spawn:
        DBFG Z -1
        stop
    }
}



// AAPTR_TARGET is the player this belongs to
// AAPTR_MASTER is the thing this is currently targeting
// AAPTR_TRACER is a potential new target
actor BFGPointerDummy
{
    +NOINTERACTION
    Radius 0
    Height 0
    
    RenderStyle None
    var int user_timeout;
    
    States
    {
      Spawn:
        PLSS A -1
        stop
      
      KeepAlive:
        PLSS A 4
        stop
    }
}


actor BFGCrosshairTarget: FastProjectile
{
    Radius 2
    Height 4
    
    Speed 0
    +THRUACTORS
    +SKYEXPLODE
    +NOTIMEFREEZE
    -ACTIVATEIMPACT
    -ACTIVATEPCROSS
    +NOTRIGGER
    
    RenderStyle None
    
    States
    {
      Spawn:
        BAL1 A -1 bright
        stop
      
      Death:
        PLSS A 3 bright
        stop
    }
}


actor BFGHomingShotSpawner
{
    Radius 2
    Height 2
    Speed  4
    
    Projectile
    +NOINTERACTION
    
    var int user_spawncount;
    var int user_spawnvelx;
    var int user_spawnvely;
    var int user_spawnvelz;
    
    var int user_velx;
    var int user_vely;
    var int user_velz;
    var int user_t;
    
    Damage 6
    Scale 0.25
    
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_ProjUpdate")
        goto SpawnCommon
      
      Death:
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_ProjDeathUpdate")
        goto SpawnCommon
      
      SpawnCommon:
        TNT1 A 0 ACS_NamedExecuteWithResult("DSSG_CalcGrenadeAngle", 25, 7000, 2500)
        TNT1 A 0 A_SpawnItemEx("BFGHomingShot_Stage1", 0,0,0, user_spawnvelx/65536.0, user_spawnvely/65536.0, user_spawnvelz/65536.0, angle, SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SetUserVar("user_spawncount", user_spawncount+1)
        TNT1 A 0 A_JumpIf(user_spawncount >= 20, "SpawnEnd")
        loop
      
      SpawnEnd:
        TNT1 A 4
        stop
    }
}


actor BFGHomingShot_Stage1
{
    Radius 5
    Height 10
    Speed 25
    
    Projectile
    Damage (25)
    
    -NOGRAVITY
    Gravity 0.15
    
    +PAINLESS
    +MTHRUSPECIES
    +BLOODLESSIMPACT
    
    +ALLOWBOUNCEONACTORS
    +BOUNCEONACTORS
    +BOUNCEONWALLS
    +BOUNCEONFLOORS
    +BOUNCEONCEILINGS
    +CANBOUNCEWATER
    BounceFactor 1.0

    Renderstyle Add
    Alpha 0.75
    Scale 0.5
    Decal "BFGLightningSmall"
    +FORCEXYBILLBOARD

    DeathSound "dakka/bfgexplode"
    Obituary "$DAKKA_MP_BFG9000_BLAST"
    
    var int user_t;
    var int user_hometime;
    var int user_angle;
    
    States
    {
      Spawn:
        DBFP A 0 bright 
        DBFP A 0 bright ACS_NamedExecuteWithResult("Dakka_InheritVelocity", AAPTR_TARGET, 110, 20, 30)
        DBFP A 0 bright A_SetUserVar("user_hometime", random(12, 35))
        goto SpawnLoop
      
      SpawnLoop:
        DBFP A 0 bright A_JumpIf(user_t % 8 < 4, "SpawnB")
        goto Spawn2
      
      SpawnB:
        DBFP B 0 bright
        goto Spawn2
      
      Spawn2:
        DBFP "#" 0 bright A_ScaleVelocity(0.9)
        DBFP "#" 0 bright A_SpawnItemEx("BFGHomingTrail_Stage1")
        DBFP "#" 0 bright A_SetUserVar("user_t", user_t + 1)
        DBFP "#" 1 bright A_JumpIf(user_t > user_hometime, "Stage2")
        goto SpawnLoop
      
      Stage2:
      Death:
        DBFP A 0 A_Stop
        DBFP A 0 A_NoGravity
        DBFP A 0 A_JumpIf(CallACS("Dakka_SetupBFGStage2", true) == true, "Stage2_Success")
        goto Stage2_Fail
       
      Stage2_Fail:
        DBFP A 0 A_SpawnItemEx("TinyBFGTrail", 0,0,0, frandom(1.5,3),0,frandom(-0.2,0.2), user_angle + frandom(-10, 10))
        DBFP A 0 A_SetUserVar("user_angle", user_angle+30)
        DBFP A 0 A_JumpIf(user_angle >= 360, "S2F_Part2")
        loop
      
      S2F_Part2:
        DBFP C 0 bright A_Explode(20, 96,  true, false, 48)
        DBFP C 0 bright A_Explode(40, 128, true, false, 48)
        DBFP CDEFGHH 2 bright
        stop
        
      Stage2_Success:
        DBFP AAAAA 0 A_SpawnItemEx("TinyBFGTrail", 0,0,0, frandom(0,4), 0, frandom(-2,2), frandom(0, 360))
        DBFP A 0 A_SpawnItemEx("BFGHomingShot_Stage2", 0,0,0, 8 * cos(angle) * cos(pitch),
                                                              8 * sin(angle) * cos(pitch),
                                                              8 * -sin(pitch),
                                                              angle, SXF_TRANSFERPOINTERS | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPITCH)
        stop
    }
}


actor BFGHomingShot_Stage2
{
    Radius 3
    Height 6
    Speed 80

    Projectile
    Damage (20)
    DamageType "Disintegrate"

    +MTHRUSPECIES
    +RANDOMIZE
    +EXTREMEDEATH
    +FORCERADIUSDMG
    +SKYEXPLODE

    Renderstyle Add
    Alpha 0.75
    Scale 0.6
    Decal "BFGLightningSmall"
    +FORCEXYBILLBOARD

    DeathSound "dakka/bfgexplode"
    Obituary "$DAKKA_MP_BFG9000_BLAST"
    
    var int user_t;
    var int user_angle;
    
    var int user_hadtarget;
    var int user_targetx;
    var int user_targety;
    var int user_targetz;

    States
    {
      Spawn:
        DBFP A 0 bright A_JumpIf(user_t % 8 < 4, "SpawnB")
        goto Spawn2
      
      SpawnB:
        DBFP B 0 bright
        goto Spawn2
      
      Spawn2:
        DBFP "#" 0 bright A_SpawnItemEx("BFGHomingTrail_Stage2", frandom(-4,4) - (velx/2), frandom(-4,4) - (vely/2), frandom(-4,4) - (velz/2), 0,0,0, 0, SXF_ABSOLUTEPOSITION)
        DBFP "#" 0 bright A_SpawnItemEx("BFGHomingTrail_Stage2", frandom(-4,4),            frandom(-4,4),            frandom(-4,4),            0,0,0, 0, SXF_ABSOLUTEPOSITION)
        DBFP "#" 0 bright A_SetUserVar("user_t", user_t + 1)
        DBFP "#" 1 bright A_JumpIf(CallACS("Dakka_BFGHomeIn", 8, 80) == false, "WentTooFar")
        goto Spawn
        
      WentTooFar:
        DBFP "#" 1 bright A_Jump(96, "Death")
        goto Spawn

      Death:
        DBFP A 0 A_Stop
        DBFP A 0 A_SpawnItemEx("TinyBFGTrail", 0,0,0, frandom(1.5,3),0,frandom(-0.5,0.5), user_angle + frandom(-10, 10))
        DBFP A 0 A_SetUserVar("user_angle", user_angle+20)
        DBFP A 0 A_JumpIf(user_angle >= 360, "Death2")
        loop
      
      Death2:
        DBFP C 0 bright A_Explode(20, 96,  true, false, 48)
        DBFP C 0 bright A_Explode(40, 128, true, false, 48)

        DBFP CDEFGHH 2
        stop
    }
}


actor BFGHomingTrail_Stage1
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    +FORCEXYBILLBOARD

    RenderStyle Add
    Alpha 0.3
    Scale 0.25

    States
    {
      Spawn:
        DBFP AAAABBBB 1 bright A_FadeOut(0.02)
        loop
    }
}


actor BFGHomingTrail_Stage2
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    +FORCEXYBILLBOARD
    +NOTIMEFREEZE

    RenderStyle Add
    Alpha 0.5
    Scale 0.5

    States
    {
      Spawn:
        TNT1 A 2
        DBFE B 0 bright A_ChangeFlag("NOTIMEFREEZE", 0)
        goto Spawn2
      
      Spawn2:
        DBFE B 1 bright A_FadeOut(0.03)
        DBFE B 0 bright A_Jump(64, "Spawn2C")
        loop
        
      Spawn2C:
        DBFE C 1 bright A_FadeOut(0.03)
        DBFE C 0 bright A_Jump(64, "Spawn2")
        loop
    }
}


actor Dakka_BFGLaser: FastProjectile
{
    Radius 2
    Height 2
    Speed 4096    // gets massively sped up

    +THRUACTORS
    +NOTIMEFREEZE
    +PUFFGETSOWNER
    +SKYEXPLODE

    RenderStyle none
    Decal BFGLightning

    States
    {
      Spawn:
        TNT1 A 1
        loop

      Death:
        DBFP A 0 ACS_NamedExecuteWithResult("Dakka_Tracer", TRACE_BFG, 0, -14)
        DBFP A 0 A_SpawnItemEx("DakkaBFGBeamEnd")
        DBFP A 0 A_SpawnItemEx("DakkaBFGBeamEnd_Client")
        stop
    }
}


// This exists purely to make a decal on a wall.
actor BFGLaserPuff: BulletPuff
{
    +NOINTERACTION
    +CLIENTSIDEONLY

    +PAINLESS

    Decal BFGLightning

    States
    {
      Spawn:
        TNT1 A 0
        stop
    }
}


actor BFGTrail
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    +FORCEXYBILLBOARD

    RenderStyle Add
    Alpha 0.6
    Scale 0.75

    States
    {
      Spawn:
        DBFE BBBBCCCC 1 bright A_FadeOut(0.03)
        loop
    }
}


actor TinyBFGTrail: BFGTrail
{
    Alpha 0.5
    Scale 0.375
}


actor DakkaBFGInstantBeam
{
    Radius 4
    Height 4

    +NOINTERACTION
    +SERVERSIDEONLY
    +PUFFGETSOWNER
    +FORCERADIUSDMG
    +NODAMAGETHRUST
    +EXTREMEDEATH
    +FORCERADIUSDMG

    RenderStyle None

    Obituary "$DAKKA_MP_BFG9000"

    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 8 A_Explode(32, 20, 0, 0, 20)
        stop
    }
}

actor DakkaBFGMainBeam
{
    Radius 4
    Height 4

    +NOINTERACTION
    +SERVERSIDEONLY
    +PUFFGETSOWNER
    +FORCERADIUSDMG
    +NODAMAGETHRUST
    +EXTREMEDEATH
    +FORCERADIUSDMG

    RenderStyle None

    Obituary "$DAKKA_MP_BFG9000"

    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_Explode(192, 48,  0, 0, 32)
        stop
    }
}

actor DakkaBFGBeamStart
{
    Radius 4
    Height 4

    +NOINTERACTION
    +SERVERSIDEONLY
    +PUFFGETSOWNER
    +FORCERADIUSDMG
    +NODAMAGETHRUST
    +EXTREMEDEATH
    +FORCERADIUSDMG

    RenderStyle None

    Obituary "$DAKKA_MP_BFG9000"

    States
    {
      Spawn:
        TNT1 A 0
        DBFP C 0 bright A_Explode(128, 160, 0)
        DBFP C 0 bright A_Explode(96,  256, 0)
        stop
    }
}

actor DakkaBFGBeamEnd
{
    Radius 4
    Height 4

    +NOINTERACTION
    +SERVERSIDEONLY
    +PUFFGETSOWNER
    +FORCERADIUSDMG
    +EXTREMEDEATH
    +FORCERADIUSDMG

    RenderStyle None

    Obituary "$DAKKA_MP_BFG9000"

    States
    {
      Spawn:
        DBFP C 0
        DBFP C 0 bright A_Explode(96, 256)
        DBFP C 0 bright A_Explode(128, 160)
        DBFP C 0 A_ChangeFlag("NODAMAGETHRUST", 1)
        DBFP C 0 bright A_Explode(96, 256, 0)
        DBFP C 0 bright A_Explode(256, 512, 0)
        stop
    }
}

actor DakkaBFGBeam_Client
{
    Radius 0
    Height 0

    Scale 0.3
    RenderStyle Add
    Alpha 0.25

    +NOINTERACTION
    +CLIENTSIDEONLY
    +FORCEXYBILLBOARD

    States
    {
      Spawn:
        TNT1 A 0
        DBFP AB 4 bright
        goto Spawn2

      Spawn2:
        DBFP AAAABBBB 1 bright A_FadeOut(0.003)
        loop
    }
}


// Used on the outside of the beam.
actor DakkaBFGBeam_Helix
{
    Radius 0
    Height 0

    +NOINTERACTION
    +CLIENTSIDEONLY
    +FORCEXYBILLBOARD

    RenderStyle Add
    Scale 0.5

    States
    {
      Spawn:
        DBFE B 0 bright
        DBFE BBBBCCCC 1 bright A_FadeOut(0.015)
        loop
    }
}


// Used in the center of the beam.
actor DakkaBFGBeam_HelixCenter
{
    Radius 0
    Height 0

    +NOINTERACTION
    +CLIENTSIDEONLY
    +FORCEXYBILLBOARD

    RenderStyle Add
    Scale 0.9

    States
    {
      Spawn:
        TNT1 A 0
        DBFP AA 0 A_SpawnItemEx("BFGBeam_HelixCenter2", 0,0,0, random(-40,40)/10.0, 0, random(-40,40)/10.0, random(0,360))
        DBFP AB 4 bright
        goto Spawn2

      Spawn2:
        DBFP AAAABBBB 1 A_FadeOut(0.03)
        loop
    }
}

actor BFGBeam_HelixCenter2
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    +FORCEXYBILLBOARD

    RenderStyle Add
    Alpha 0.6
    Scale 0.6

    States
    {
      Spawn:
        TNT1 A 0
        DBFE BBBBCCCC 1 bright A_FadeOut(0.03)
        loop
    }
}


actor DakkaBFGBeam_HelixCenter_LessEffects: DakkaBFGBeam_HelixCenter
{
    States
    {
      Spawn:
        DBFP AB 4 bright
        goto Spawn2
    }
}


actor BFGEndTrail
{
    +NOINTERACTION
    +CLIENTSIDEONLY

    RenderStyle Add
    Alpha 0.6
    Scale 0.75

    States
    {
      Spawn:
        TNT1 A 0
        DBFE BBBBCCCC 1 bright A_FadeOut(0.005)
        loop
    }
}

actor DakkaBFGBeamEnd_Client
{
    Radius 0
    Height 0
    +NOINTERACTION
    +CLIENTSIDEONLY

    RenderStyle Add

    States
    {
      Spawn:
        DBFP A 0
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 14.095389311788626,0,-1.3073361412148725, 0)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 15.035081932574535,0,-1.3944918839625307, 20)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 12.21600407021681,0,-1.1330246557195562, 40)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 13.155696691002717,0,-1.2201803984672144, 60)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 12.21600407021681,0,-1.1330246557195562, 80)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 12.21600407021681,0,-1.1330246557195562, 100)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 13.155696691002717,0,-1.2201803984672144, 120)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 12.21600407021681,0,-1.1330246557195562, 140)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 13.155696691002717,0,-1.2201803984672144, 160)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 13.155696691002717,0,-1.2201803984672144, 180)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 15.035081932574535,0,-1.3944918839625307, 200)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 14.095389311788626,0,-1.3073361412148725, 220)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 14.095389311788626,0,-1.3073361412148725, 240)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 12.21600407021681,0,-1.1330246557195562, 260)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 14.095389311788626,0,-1.3073361412148725, 280)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 12.21600407021681,0,-1.1330246557195562, 300)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 15.035081932574535,0,-1.3944918839625307, 320)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 13.155696691002717,0,-1.2201803984672144, 340)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 12.0,0,0.0, 0)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 16.0,0,0.0, 20)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 14.0,0,0.0, 40)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 16.0,0,0.0, 60)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 12.0,0,0.0, 80)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 13.0,0,0.0, 100)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 12.0,0,0.0, 120)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 15.0,0,0.0, 140)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 12.0,0,0.0, 160)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 15.0,0,0.0, 180)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 14.0,0,0.0, 200)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 13.0,0,0.0, 220)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 15.0,0,0.0, 240)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 15.0,0,0.0, 260)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 16.0,0,0.0, 280)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 13.0,0,0.0, 300)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 13.0,0,0.0, 320)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 16.0,0,0.0, 340)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 13.155696691002717,0,1.2201803984672144, 0)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 11.276311449430901,0,1.045868912971898, 20)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 11.276311449430901,0,1.045868912971898, 40)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 11.276311449430901,0,1.045868912971898, 60)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 13.155696691002717,0,1.2201803984672144, 80)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 13.155696691002717,0,1.2201803984672144, 100)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 13.155696691002717,0,1.2201803984672144, 120)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 12.21600407021681,0,1.1330246557195562, 140)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 14.095389311788626,0,1.3073361412148725, 160)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 12.21600407021681,0,1.1330246557195562, 180)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 13.155696691002717,0,1.2201803984672144, 200)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 13.155696691002717,0,1.2201803984672144, 220)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 12.21600407021681,0,1.1330246557195562, 240)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 11.276311449430901,0,1.045868912971898, 260)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 14.095389311788626,0,1.3073361412148725, 280)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 11.276311449430901,0,1.045868912971898, 300)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 13.155696691002717,0,1.2201803984672144, 320)
        DBFP A 0 A_SpawnItemEx("BFGTrail", 0,0,0, 11.276311449430901,0,1.045868912971898, 340)

        DBFP AAAAAAAAAA 0 A_SpawnItemEx("BFGEndTrail", 0,0,0, random(-20,20)/10.0, 0, random(-20,20)/10.0, random(0,360))
        DBFP CDEFGHH 8 bright
        stop
    }
}