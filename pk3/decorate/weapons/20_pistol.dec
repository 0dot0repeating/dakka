actor DWep_Pistol: DoomWeapon
{
    Tag "Pistol"

    Weapon.SlotNumber 2
    Weapon.SelectionOrder 1900
    Weapon.AmmoUse1 1
    Weapon.AmmoUse2 3
    Weapon.AmmoGive 0
    Weapon.AmmoType1 "DakkaBullet_10mm"
    Weapon.AmmoType2 "DakkaBullet_10mm"
    
    Weapon.BobStyle InverseSmooth
    Weapon.BobSpeed 2.5
    Weapon.BobRangeX 0.3
    Weapon.BobRangeY 0.4

    +WEAPON.WIMPY_WEAPON
    +UNDROPPABLE
    +ALT_AMMO_OPTIONAL

    Decal BulletChip

    Inventory.PickupMessage "$DAKKA_PK_PISTOL"
    Obituary "$DAKKA_MP_PISTOL"

    States
    {
      Select:
        DPST EE 0 A_Raise
        DPST E 1 A_Raise
        loop

      Deselect:
        DPST C 0 A_Light0
        DPST CCC 0 A_Lower
        DPST C 1 A_Lower
        loop

      Ready:
        DPST A 1 A_WeaponReady
        loop

      Fire:
        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 2, 0)
        DPST B   0 A_GunFlash
        DPST B   0 A_PlaySound("dakka/pistolfire", CHAN_WEAPON)
        DPST B   0 bright A_FireBullets(0.25, 0.25, -1, 12, "DakkaPuff", FBF_NORANDOM | FBF_USEAMMO)
        DPST B   0 A_GiveInventory("GiveScrapCounter", 5)
        DPST B   0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, -45, 2, 5)
        DPST B   1 bright A_Light(2)
        DPST C   1 bright
        DPST E   1 A_Light(1)
        DPST E   1 A_Light(0)
        DPST EDB  1 A_WeaponReady(WRF_NOBOB | WRF_NOPRIMARY)
        goto Ready

      AltFire:
        DPST A 0 A_JumpIfNoAmmo("AltNo")
        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 2, 1)
        DPST B 0 A_GunFlash
        DPST B 0 A_PlaySound("dakka/pistolaltfire", CHAN_WEAPON)
        DPST B 0 A_FireBullets(8, 6.5, 3, 12, "DakkaPuff", FBF_NORANDOM | FBF_USEAMMO)
        DPST B 0 A_GiveInventory("GiveScrapCounter", 20)
        DPST B 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, 2, 2, 16)
        DPST C 1 bright A_Light(3)
        DPST E 1 bright
        DPST E 1 A_Light(2)
        DPST E 1 A_Light(1)
        DPST E 2 A_Light(0)
        DPST E 4 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DPST D 3 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DPST B 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DPST A 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        goto Ready
      
      AltNo:
        DPST A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready

      Flash:
        DPSF A 2 bright
        stop

      Spawn:
        DPST Z -1
        stop
    }
}
