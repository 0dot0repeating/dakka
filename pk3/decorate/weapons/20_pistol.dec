actor DWep_Pistol: DoomWeapon
{
    Tag "Pistol"

    Weapon.SlotNumber 2
    Weapon.SelectionOrder 1900
    Weapon.AmmoUse1 1
    Weapon.AmmoUse2 4
    Weapon.AmmoGive 0
    Weapon.AmmoType1 "DakkaBullet_10mm"
    Weapon.AmmoType2 "DakkaBullet_10mm"

    +WEAPON.WIMPY_WEAPON
    +UNDROPPABLE

    Decal BulletChip

    Inventory.PickupMessage "$DAKKA_PK_PISTOL"
    Obituary "$DAKKA_MP_PISTOL"

    States
    {
      Select:
        DPST EE 0 A_Raise
        DPST E 1 A_Raise
        loop

      Deselect:
        DPST CCC 0 A_Lower
        DPST C 1 A_Lower
        loop

      Ready:
        DPST A 1 A_WeaponReady
        loop

      Fire:
        "----" A 0 ACS_ExecuteWithResult(DAKKA_UPDATE_SWITCHAROO, 2, 0)
        DPST B   0 A_GunFlash
        DPST B   0 A_PlaySound("dakka/pistolfire", CHAN_WEAPON)
        DPST B   0 bright A_FireBullets(0.25, 0.25, -1, 12, "DakkaPuff", FBF_NORANDOM | FBF_USEAMMO)
        DPST B   0 A_GiveInventory("GiveScrapCounter", 5)
        DPST B   0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, -45, 2, 5)
        DPST B   1 bright A_Light2
        DPST C   1 bright A_Light1
        DPST E   1 A_Light0
        DPST EDCB  1 A_WeaponReady(WRF_NOBOB | WRF_NOPRIMARY)
        goto Ready

      AltFire:
        "----" A 0 ACS_ExecuteWithResult(DAKKA_UPDATE_SWITCHAROO, 2, 1)
        DPST B 0 A_GunFlash
        DPST B 0 A_PlaySound("dakka/pistolaltfire", CHAN_WEAPON)
        DPST B 0 A_FireBullets(8, 6.5, 4, 12, "DakkaPuff", FBF_NORANDOM | FBF_USEAMMO)
        DPST B 0 A_GiveInventory("GiveScrapCounter", 20)
        DPST B 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, 2, 2, 16)
        DPST CE 1 bright A_Light2
        DPST E 2 A_Light1
        DPST E 6 A_Light0
        DPST D 3 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DPST C 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DPST B 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DPST A 0 A_Refire
        goto Ready

      Flash:
        DPSF A 2 Bright A_Light1
        goto LightDone

      Spawn:
        PIST A -1
        stop
    }
}
