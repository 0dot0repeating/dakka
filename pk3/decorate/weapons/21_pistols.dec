actor DWep_Pistols: DoomWeapon
{
    Tag "Pistols"

    Weapon.SelectionOrder 1500
    Weapon.AmmoUse1 1
    Weapon.AmmoUse2 8
    Weapon.AmmoGive 20
    Weapon.AmmoType1 "DakkaBullet_10mm"
    Weapon.AmmoType2 "DakkaBullet_10mm"

    +UNDROPPABLE

    Decal BulletChip

    Inventory.PickupMessage "$DAKKA_PK_PISTOLS"
    Obituary "$DAKKA_MP_PISTOLS"

    States
    {
      Select:
        BPST D 0 A_JumpIfInventory("DWep_Pistol",1, 2)
        BPST D 0 A_GiveInventory("DWep_Pistol")

        BPST DD 0 A_Raise
        BPST D 1 A_Raise
        loop

      Deselect:
        BPST CCC 0 A_Lower
        BPST C 1 A_Lower
        loop

      Ready:
        BPST A 1 A_WeaponReady
        loop

      Fire:
        "----" A 0 ACS_ExecuteWithResult(DAKKA_UPDATE_SWITCHAROO, 2, 0)
        BPST F    0 A_PlaySound("dakka/pistolfire", 5)
        BPST F    0 A_FireBullets(4, 2.5, -1, 12, "DakkaPuff_Left", FBF_NORANDOM | FBF_USEAMMO)
        BPST F    0 A_GiveInventory("GiveScrapCounter", 5)
        BPST F 0 ACS_ExecuteAlways(DAKKA_RECOIL, 0, -45, 2, 5)
        BPST G 1 bright A_Light2
        BPST H 1 bright A_Light0
        //BPST I 1 bright A_Light0
        BPST I 0 A_JumpIfNoAmmo("NoFire")
        BPST I 0 A_JumpIfInventory("MainFired", 1, "Fire2")
        goto NoFire

      NoFire:
        BPST V 0 A_ReFire
        BPST V 2 A_WeaponReady(WRF_NOBOB | WRF_NOPRIMARY)
        BPST W 0 A_ReFire
        BPST W 1 A_WeaponReady(WRF_NOBOB | WRF_NOPRIMARY)
        BPST X 0 A_ReFire
        BPST X 1 A_WeaponReady(WRF_NOBOB | WRF_NOPRIMARY)
        BPST Y 0 A_ReFire
        BPST Y 1 A_WeaponReady(WRF_NOBOB | WRF_NOPRIMARY)
        goto Ready

      Fire2:
        BPST J 1 A_WeaponReady(WRF_NOBOB | WRF_NOPRIMARY)
        BPST I 0 A_JumpIfNoAmmo("NoFire")
        BPST I 0 A_JumpIfInventory("MainFired", 1, 1)
        goto NoFire

        "----" A 0 ACS_ExecuteWithResult(DAKKA_UPDATE_SWITCHAROO, 2, 0)
        BPST K    0 A_PlaySound("dakka/pistolfire", 6)
        BPST K    0 A_FireBullets(4, 2.5, -1, 12, "DakkaPuff_Right", FBF_NORANDOM | FBF_USEAMMO)
        BPST F    0 A_GiveInventory("GiveScrapCounter", 5)
        BPST F 0 ACS_ExecuteAlways(DAKKA_RECOIL, 0, -45, 2, 5)
        BPST K 1 bright A_Light2
        BPST L 1 bright A_Light1
        BPST M 0 bright A_Light0
        BPST MN 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        BPST I 0 A_JumpIfNoAmmo("NoHold")
        BPST O 0 A_ReFire
        BPST O 1 A_WeaponReady(WRF_NOBOB | WRF_NOPRIMARY)
        BPST P 0 A_ReFire
        BPST P 1 A_WeaponReady(WRF_NOBOB | WRF_NOPRIMARY)
        BPST Q 0 A_ReFire
        BPST Q 1 A_WeaponReady(WRF_NOBOB | WRF_NOPRIMARY)
        BPST R 0 A_ReFire
        BPST R 1 A_WeaponReady(WRF_NOBOB | WRF_NOPRIMARY)
        goto Ready

      NoHold:
        BPST Q 0 A_ReFire
        BPST Q 1 A_WeaponReady(WRF_NOBOB | WRF_NOPRIMARY)
        BPST R 0 A_ReFire
        BPST R 1 A_WeaponReady(WRF_NOBOB | WRF_NOPRIMARY)
        goto Ready

      Hold:
        "----" A 0 ACS_ExecuteWithResult(DAKKA_UPDATE_SWITCHAROO, 2, 0)
        BPST T    0 A_PlaySound("dakka/pistolfire", 5)
        BPST T    0 A_FireBullets(4, 2.5, -1, 12, "DakkaPuff_Left", FBF_NORANDOM | FBF_USEAMMO)
        BPST F    0 A_GiveInventory("GiveScrapCounter", 5)
        BPST F 0 ACS_ExecuteAlways(DAKKA_RECOIL, 0, -45, 2, 5)
        BPST T 1 bright A_Light2
        BPST U 1 bright A_Light1
        BPST U 0 A_Light0
        BPST I 0 A_JumpIfNoAmmo("NoFire")
        BPST I 0 A_JumpIfInventory("MainFired", 1, "Fire2")
        goto NoFire

      AltFire:
        "----" A 0 ACS_ExecuteWithResult(DAKKA_UPDATE_SWITCHAROO, 2, 1)
        BPST F    0 A_PlaySound("dakka/pistolaltfire", 5, 0.7)
        BPST F    0 A_PlaySound("dakka/pistolaltfire", 6, 0.7)
        BPST F    0 A_FireBullets(10, 7, 4, 12, "DakkaPuff_Left", FBF_NORANDOM | FBF_USEAMMO)
        BPST F    0 A_FireBullets(10, 7, 4, 12, "DakkaPuff_Right", FBF_NORANDOM)
        BPST F    0 A_GiveInventory("GiveScrapCounter", 40)
        BPST F    0 ACS_ExecuteAlways(DAKKA_RECOIL, 0, 5, 2, 16)
        BPST FD 1 bright A_Light2
        BPST E 2 A_Light1
        BPST E 6 A_Light0
        BPST D 3 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        BPST C 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        BPST B 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        goto Ready


      Spawn:
        PIST A -1
        stop
    }
}
