actor LeftPistolNext: Boolean {}

actor DakkaPistolFired: Trigger
{
    States
    {
      Pickup:
        DPS2 A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 2, 0)
        DPS2 A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, -45, 2, 5)
        DPS2 A 0 A_GiveInventory("GiveScrapCounter", 5)
        stop
    }
}


actor DWep_Pistols: DoomWeapon
{
    Tag "Pistols"

    Weapon.SlotNumber 2
    Weapon.SelectionOrder 1500

    Weapon.AmmoType1    "DakkaBullet_10mm"
    Weapon.AmmoType2    "DakkaBullet_10mm"
    Weapon.AmmoUse1     1
    Weapon.AmmoUse2     6
    Weapon.AmmoGive     20

    Weapon.BobStyle InverseSmooth
    Weapon.BobSpeed 2.5
    Weapon.BobRangeX 0.3
    Weapon.BobRangeY 0.4

    +UNDROPPABLE
    +ALT_AMMO_OPTIONAL

    Decal BulletChip

    Inventory.PickupMessage "$DAKKA_PK_PISTOLS"
    Obituary "$DAKKA_MP_PISTOLS"

    States
    {
      Select:
        DPS2 DD 0 A_Raise
        DPS2 D 1 A_Raise
        loop

      Deselect:
        DPS2 D 0 A_Light0
        DPS2 DDD 0 A_Lower
        DPS2 D 1 A_Lower
        loop

      Ready:
        DPS2 D 0 A_JumpIfInventory("DWep_Pistol", 1, "Ready2")
        DPS2 D 0 A_GiveInventory("DWep_Pistol")
        goto Ready2

      Ready2:
        DPS2 A 1 A_WeaponReady
        loop

      Fire:
        DPS2 A 0 A_GiveInventory("DakkaPistolFired")
        DPS2 A 0 A_JumpIfInventory("LeftPistolNext", 1, "FireLeft")
        goto FireRight

      HoldTop:
        DPS2 A 0 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        DPS2 A 0 A_GiveInventory("DakkaPistolFired")
        DPS2 A 0 A_JumpIfInventory("LeftPistolNext", 1, "HoldTop_Left")
        goto HoldTop_Right

      HoldMid:
        DPS2 A 0 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        DPS2 A 0 A_GiveInventory("DakkaPistolFired")
        DPS2 A 0 A_JumpIfInventory("LeftPistolNext", 1, "HoldMid_Left")
        goto HoldMid_Right

      HoldLow:
        DPS2 A 0 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        DPS2 A 0 A_GiveInventory("DakkaPistolFired")
        DPS2 A 0 A_JumpIfInventory("LeftPistolNext", 1, "HoldLow_Left")
        goto HoldLow_Right


      FireLeft:
        DPS2 G 0 A_PlaySound("dakka/pistolfire", 5)
        DPS2 G 0 A_FireBullets(2, 1.5, -1, 12, "DakkaPuff_Left", FBF_NORANDOM | FBF_USEAMMO)
        DPS2 G 0 A_TakeInventory("LeftPistolNext")
        DPS2 G 1 bright A_GunFlash("LeftFlash")
        DPS2 H 1 bright
        DPS2 J 1
        DPS2 J 1 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        DPS2 I 0 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        DPS2 I 1 A_Refire("HoldMid")
        DPS2 G 0 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        DPS2 G 1 A_Refire("HoldLow")
        goto Ready

      FireRight:
        DPS2 K 0 A_PlaySound("dakka/pistolfire", 6)
        DPS2 K 0 A_FireBullets(2, 1.5, -1, 12, "DakkaPuff_Right", FBF_NORANDOM | FBF_USEAMMO)
        DPS2 K 0 A_GiveInventory("LeftPistolNext")
        DPS2 K 1 bright A_GunFlash("RightFlash")
        DPS2 L 1 bright
        DPS2 N 1
        DPS2 N 0 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        DPS2 N 1 A_Refire("HoldTop")
        DPS2 M 0 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        DPS2 M 1 A_Refire("HoldMid")
        DPS2 K 0 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        DPS2 K 1 A_Refire("HoldLow")
        goto Ready


      HoldTop_Left:
        DPS2 Q 0 A_PlaySound("dakka/pistolfire", 5)
        DPS2 Q 0 A_FireBullets(2, 1.5, -1, 12, "DakkaPuff_Left", FBF_NORANDOM | FBF_USEAMMO)
        DPS2 Q 0 A_TakeInventory("LeftPistolNext")
        DPS2 Q 1 bright A_GunFlash("LeftFlash")
        DPS2 R 1 bright
        DPS2 S 1
        DPS2 J 1 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        DPS2 I 0 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        DPS2 I 1 A_Refire("HoldMid")
        DPS2 G 0 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        DPS2 G 1 A_Refire("HoldLow")
        goto Ready

      HoldTop_Right:
        DPS2 S 0 A_PlaySound("dakka/pistolfire", 6)
        DPS2 S 0 A_FireBullets(2, 1.5, -1, 12, "DakkaPuff_Right", FBF_NORANDOM | FBF_USEAMMO)
        DPS2 S 0 A_GiveInventory("LeftPistolNext")
        DPS2 S 1 bright A_GunFlash("RightFlash")
        DPS2 P 1 bright
        DPS2 Q 1
        DPS2 N 0 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        DPS2 N 1 A_Refire("HoldTop")
        DPS2 M 0 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        DPS2 M 1 A_Refire("HoldMid")
        DPS2 K 0 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        DPS2 K 1 A_Refire("HoldLow")
        goto Ready


      HoldMid_Left:
        DPS2 V 0 A_PlaySound("dakka/pistolfire", 5)
        DPS2 V 0 A_FireBullets(2, 1.5, -1, 12, "DakkaPuff_Left", FBF_NORANDOM | FBF_USEAMMO)
        DPS2 V 0 A_TakeInventory("LeftPistolNext")
        DPS2 V 1 bright A_GunFlash("LeftFlash")
        DPS2 W 1 bright
        DPS2 J 1
        DPS2 J 1 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        DPS2 I 0 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        DPS2 I 1 A_Refire("HoldMid")
        DPS2 G 0 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        DPS2 G 1 A_Refire("HoldLow")
        goto Ready

      HoldMid_Right:
        DPS2 T 0 A_PlaySound("dakka/pistolfire", 6)
        DPS2 T 0 A_FireBullets(2, 1.5, -1, 12, "DakkaPuff_Right", FBF_NORANDOM | FBF_USEAMMO)
        DPS2 T 0 A_GiveInventory("LeftPistolNext")
        DPS2 T 1 bright A_GunFlash("RightFlash")
        DPS2 U 1 bright
        DPS2 N 1
        DPS2 N 0 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        DPS2 N 1 A_Refire("HoldTop")
        DPS2 M 0 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        DPS2 M 1 A_Refire("HoldMid")
        DPS2 K 0 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        DPS2 K 1 A_Refire("HoldLow")
        goto Ready


      HoldLow_Left:
        DPS2 X 0 A_PlaySound("dakka/pistolfire", 5)
        DPS2 X 0 A_FireBullets(2, 1.5, -1, 12, "DakkaPuff_Left", FBF_NORANDOM | FBF_USEAMMO)
        DPS2 X 0 A_TakeInventory("LeftPistolNext")
        DPS2 X 1 bright A_GunFlash("LeftFlash")
        DPS2 H 1 bright
        DPS2 J 1
        DPS2 J 1 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        DPS2 I 0 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        DPS2 I 1 A_Refire("HoldMid")
        DPS2 G 0 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        DPS2 G 1 A_Refire("HoldLow")
        goto Ready

      HoldLow_Right:
        DPS2 X 0 A_PlaySound("dakka/pistolfire", 6)
        DPS2 X 0 A_FireBullets(2, 1.5, -1, 12, "DakkaPuff_Right", FBF_NORANDOM | FBF_USEAMMO)
        DPS2 X 0 A_GiveInventory("LeftPistolNext")
        DPS2 X 1 bright A_GunFlash("RightFlash")
        DPS2 L 1 bright
        DPS2 N 1
        DPS2 N 0 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        DPS2 N 1 A_Refire("HoldTop")
        DPS2 M 0 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        DPS2 M 1 A_Refire("HoldMid")
        DPS2 K 0 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        DPS2 K 1 A_Refire("HoldLow")
        goto Ready


      LeftFlash:
        DPS2 Y 1 bright A_Light(3)
        DPS2 Y 1 A_Light(2)
        TNT1 A 1 A_Light(1)
        TNT1 A 1 A_Light(0)
        stop


      RightFlash:
        DPS2 Z 1 bright A_Light(3)
        DPS2 Z 1 A_Light(2)
        TNT1 A 1 A_Light(1)
        TNT1 A 1 A_Light(0)
        stop


      AltFire:
        DPS2 A 0 A_JumpIfNoAmmo("AltNo")
        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 2, 1)
        DPS2 F    0 A_PlaySound("dakka/pistolaltfire", 5, 0.7)
        DPS2 F    0 A_PlaySound("dakka/pistolaltfire", 6, 0.7)
        DPS2 F    0 A_FireBullets(10, 7, 3, 12, "DakkaPuff_Left",  FBF_NORANDOM | FBF_USEAMMO)
        DPS2 F    0 A_FireBullets(10, 7, 3, 12, "DakkaPuff_Right", FBF_NORANDOM)
        DPS2 F    0 A_GiveInventory("GiveScrapCounter", 40)
        DPS2 F    0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, 5, 2, 16)
        DPS2 FD 1 bright A_Light(5)
        DPS2 E 1 A_Light(4)
        DPS2 E 1 A_Light(3)
        DPS2 E 1 A_Light(2)
        DPS2 E 1 A_Light(1)
        DPS2 E 0 A_Light0
        DPS2 E 4 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DPS2 D 3 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DPS2 B 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DPS2 A 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        goto Ready

      AltNo:
        DPS2 A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready

      Spawn:
        DPST Z -1
        stop
    }
}
