// DakkaFistConnected is used in the primary fire to check if the puff spawned at all
// but it's also used in the alt-fire to check whether the punch directly did damage

actor LeftPunchNext:        Boolean {}
actor DakkaFistConnected:   Boolean {}
actor DakkaFistSpeed:       Counter { Inventory.MaxAmount 15 }
actor DakkaFistTap_Main:    Counter {}
actor DakkaFistTap_Alt:     Counter {}
actor DakkaFistFinisher:    Boolean {}
actor DakkaFistInAlt:       Boolean {}
actor DakkaFistAltLevel:    Counter { Inventory.MaxAmount 3 }
actor DakkaFistAttackOver:  Boolean {}

actor DakkaFist_StepUp:     Counter { Inventory.MaxAmount 3 }
actor DakkaFist_Release:    Boolean {}


actor DWep_Fist: DoomWeapon
{
    Tag "Fists"

    Weapon.SlotNumber 1
    Weapon.SelectionOrder 3700

    Weapon.BobStyle InverseSmooth
    Weapon.BobSpeed 2.5
    Weapon.BobRangeX 0.5
    Weapon.BobRangeY 0.4

    Inventory.PickupMessage "$DAKKA_PK_FISTS"
    Obituary "$DAKKA_MP_FISTS"

    +WEAPON.MELEEWEAPON
    +UNDROPPABLE
    +NOALERT

    States
    {
      Select:
        DFIT A 0 A_ZoomFactor(1)
        DFIT A 0 ACS_NamedExecuteWithResult("Dakka_ModFistSpeed", AAPTR_DEFAULT, -0x7FFFFFFF)
        goto Select2

      Select2:
        DFIT AA 0 A_Raise
        DFIT A 1 A_Raise
        loop

      Deselect:
        "####" A 0 ACS_NamedExecuteWithResult("Dakka_ModFistSpeed", AAPTR_DEFAULT, -0x7FFFFFFF)
        "####" A 0 A_JumpIfInventory("DakkaFistAttackOver", 1, "InstantDeselect")
        "####" A 0 A_JumpIfInventory("DakkaFistInAlt", 1, "AltDeselect")
        goto DeselectNormal

      DeselectNormal:
        DFIT AAA 0 A_Lower
        DFIT A 1 A_Lower
        loop

      AltDeselect:
        "####" K 0 A_TakeInventory("DakkaFistAltLevel")
        "####" K 0 A_TakeInventory("DakkaFistInAlt")
        "####" K 0 A_TakeInventory("DakkaFist_StepUp")
        "####" K 0 A_TakeInventory("DakkaFist_Release")
        "####" K 0 A_ZoomFactor(1)
        "####" KIG 1
        TNT1 A 2
        goto InstantDeselect

      InstantDeselect:
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 0 A_Lower
        wait

      Ready:
        DFIT A 0 A_TakeInventory("DakkaFistAltLevel")
        DFIT A 0 A_TakeInventory("DakkaFistAttackOver")
        DFIT A 0 A_TakeInventory("DakkaFist_StepUp")
        DFIT A 0 A_TakeInventory("DakkaFist_Release")
        goto Ready2

      Ready2:
        DFIT AA 1 A_WeaponReady
        DFIT A 0 ACS_NamedExecuteWithResult("Dakka_ModFistSpeed", AAPTR_DEFAULT, -1)
        DFIT AAA 1 A_WeaponReady
        DFIT A 0 ACS_NamedExecuteWithResult("Dakka_ModFistSpeed", AAPTR_DEFAULT, -1)
        loop


      Fire:
        TNT1 B 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 0, 0)
        TNT1 A 0 A_TakeInventory("DakkaFistConnected")
        TNT1 A 0 A_TakeInventory("DakkaFistAttackOver")
        TNT1 A 0 A_JumpIfInventory("LeftPunchNext", 1, "FireLeft")
        RFIT Z 0 A_GiveInventory("LeftPunchNext")
        goto FireCommon

      FireLeft:
        LFIT Z 0 A_TakeInventory("LeftPunchNext")
        goto FireCommon

      FireCommon:
        "####" Z 0 A_JumpIfInventory("DakkaFistSpeed", 6, "Fire2")
        "####" Z 0 A_JumpIfInventory("DakkaFistSpeed", 4, "Fire3")
        "####" Z 0 A_JumpIfInventory("DakkaFistSpeed", 2, "Fire4")
        "####" Z 0 A_JumpIfInventory("DakkaFistSpeed", 1, "Fire5")
        goto Fire6



      Fire6:
        "####" E 1 A_FireBullets(0, 0, 1, 10, "FistPunchPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 80)
        "####" E 0 A_JumpIfInventory("DakkaFistConnected", 1, 2)
        "####" E 0 ACS_NamedExecuteWithResult("Dakka_ModFistSpeed", AAPTR_DEFAULT, -1)
        "####" D 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" B 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" Z 0 A_JumpIfInventory("DakkaFistTap_Alt", 1, "AltFire_FromPrimary")
        "####" Z 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" Z 0 A_JumpIfInventory("DakkaFistTap_Alt", 1, "AltFire_FromPrimary")
        "####" Z 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd


      Fire5:
        "####" E 1 A_FireBullets(0, 0, 1, 11, "FistPunchPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 80)
        "####" E 0 A_JumpIfInventory("DakkaFistConnected", 1, 2)
        "####" E 0 ACS_NamedExecuteWithResult("Dakka_ModFistSpeed", AAPTR_DEFAULT, -1)
        "####" D 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" B 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" Z 0 A_JumpIfInventory("DakkaFistTap_Alt", 1, "AltFire_FromPrimary")
        "####" Z 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd


      Fire4:
        "####" E 1 A_FireBullets(0, 0, 1, 12, "FistPunchPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 80)
        "####" E 0 A_JumpIfInventory("DakkaFistConnected", 1, 2)
        "####" E 0 ACS_NamedExecuteWithResult("Dakka_ModFistSpeed", AAPTR_DEFAULT, -1)
        "####" C 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" Z 0 A_JumpIfInventory("DakkaFistTap_Alt", 1, "AltFire_FromPrimary")
        "####" Z 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd


      Fire3:
        "####" E 1 A_FireBullets(0, 0, 1, 13, "FistPunchPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 80)
        "####" E 0 A_JumpIfInventory("DakkaFistConnected", 1, 2)
        "####" E 0 ACS_NamedExecuteWithResult("Dakka_ModFistSpeed", AAPTR_DEFAULT, -1)
        "####" B 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" Z 0 A_JumpIfInventory("DakkaFistTap_Alt", 1, "AltFire_FromPrimary")
        "####" Z 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd


      Fire2:
        "####" E 1 A_FireBullets(0, 0, 1, 13, "FistPunchPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 80)
        "####" E 0 A_JumpIfInventory("DakkaFistConnected", 1, 2)
        "####" E 0 ACS_NamedExecuteWithResult("Dakka_ModFistSpeed", AAPTR_DEFAULT, -1)
        "####" Z 0 A_JumpIfInventory("DakkaFistTap_Alt", 1, "AltFire_FromPrimary")
        "####" B 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd




      AltFire:
        TNT1 A 0 A_ClearRefire
        TNT1 A 0 A_GiveInventory("DakkaFistInAlt")
        TNT1 A 0 A_TakeInventory("DakkaFistAltLevel")
        TNT1 A 0 A_GiveInventory("DakkaFistAltLevel")
        TNT1 A 0 A_TakeInventory("DakkaFistAttackOver")
        TNT1 A 0 A_PlaySound("dakka/fistcharge1", 5)

        DFIT A 0 offset(0, 40) A_ZoomFactor(0.97, ZOOM_NOSCALETURNING)
        DFIT A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)
        DFIT A 0 offset(0, 56) A_ZoomFactor(0.96, ZOOM_NOSCALETURNING)
        DFIT A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)
        TNT1 A 0 offset(0, 32) A_ZoomFactor(0.95, ZOOM_NOSCALETURNING)
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)
        goto Altfire_ChooseSide

      AltHold:
      AltFire_FromPrimary:
        TNT1 A 0 A_ClearRefire
        TNT1 A 0 A_GiveInventory("DakkaFistInAlt")
        "----" A 0 A_TakeInventory("DakkaFistAltLevel")
        "----" A 0 A_GiveInventory("DakkaFistAltLevel")
        TNT1 A 0 A_TakeInventory("DakkaFistAttackOver")
        TNT1 A 0 A_PlaySound("dakka/fistcharge1", 5)

        TNT1 A 0 A_ZoomFactor(0.97, ZOOM_NOSCALETURNING)
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)
        TNT1 A 0 A_ZoomFactor(0.96, ZOOM_NOSCALETURNING)
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)
        TNT1 A 0 A_ZoomFactor(0.95, ZOOM_NOSCALETURNING)
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)
        goto Altfire_ChooseSide

      AltHold_Quick3:
        "----" A 0 A_ClearRefire
        "----" A 0 A_GiveInventory("DakkaFistInAlt")
        "----" A 0 A_TakeInventory("DakkaFistAltLevel")
        "----" A 0 A_GiveInventory("DakkaFistAltLevel")
        "----" A 0 A_TakeInventory("DakkaFistAttackOver")
        "----" A 0 A_PlaySound("dakka/fistcharge1", 5)

        "####" I 0 A_ZoomFactor(0.97, ZOOM_NOSCALETURNING)
        "####" I 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)
        "####" H 0 A_ZoomFactor(0.96, ZOOM_NOSCALETURNING)
        "####" H 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)
        "####" G 0 A_ZoomFactor(0.95, ZOOM_NOSCALETURNING)
        "####" G 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)
        goto Altfire_ChooseSide

      AltHold_Quick2:
        "----" A 0 A_ClearRefire
        "----" A 0 A_GiveInventory("DakkaFistInAlt")
        "----" A 0 A_TakeInventory("DakkaFistAltLevel")
        "----" A 0 A_GiveInventory("DakkaFistAltLevel")
        "----" A 0 A_TakeInventory("DakkaFistAttackOver")
        "----" A 0 A_PlaySound("dakka/fistcharge1", 5)

        "####" H 0 A_ZoomFactor(0.97, ZOOM_NOSCALETURNING)
        "####" H 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)
        "####" G 0 A_ZoomFactor(0.96, ZOOM_NOSCALETURNING)
        "####" G 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)
        "####" Z 0 A_ZoomFactor(0.95, ZOOM_NOSCALETURNING)
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)
        goto Altfire_ChooseSide

      AltHold_Quick1:
        "----" A 0 A_ClearRefire
        "----" A 0 A_GiveInventory("DakkaFistInAlt")
        "----" A 0 A_TakeInventory("DakkaFistAltLevel")
        "----" A 0 A_GiveInventory("DakkaFistAltLevel")
        "----" A 0 A_TakeInventory("DakkaFistAttackOver")
        "----" A 0 A_PlaySound("dakka/fistcharge1", 5)

        "####" G 0 A_ZoomFactor(0.97, ZOOM_NOSCALETURNING)
        "####" G 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)
        "####" Z 0 A_ZoomFactor(0.96, ZOOM_NOSCALETURNING)
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)
        "####" Z 0 A_ZoomFactor(0.95, ZOOM_NOSCALETURNING)
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)
        goto Altfire_ChooseSide


      AltFire_ChooseSide:
        RFIT Z 0 A_JumpIfInventory("LeftPunchNext", 1, "AltFire_Left")
        RFIT Z 0 A_GiveInventory("LeftPunchNext")
        goto AltFire_Weak

      AltFire_Left:
        LFIT Z 0 A_TakeInventory("LeftPunchNext")
        goto AltFire_Weak


      AltFire_Weak:
        "####" Z 0 ACS_NamedExecuteWithResult("Dakka_AltFistStepUp", 1)
        "####" Z 0 A_JumpIfInventory("DakkaFist_StepUp",  1, "AltFire_Medium")
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)

        "####" Z 0 ACS_NamedExecuteWithResult("Dakka_AltFistStepUp", 1)
        "####" Z 0 A_JumpIfInventory("DakkaFist_StepUp",  1, "AltFire_Medium")
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)

        "####" Z 0 ACS_NamedExecuteWithResult("Dakka_AltFistStepUp", 1)
        "####" Z 0 A_JumpIfInventory("DakkaFist_StepUp",  1, "AltFire_Medium")
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)

        "####" Z 0 ACS_NamedExecuteWithResult("Dakka_AltFistStepUp", 1)
        "####" Z 0 A_JumpIfInventory("DakkaFist_StepUp",  1, "AltFire_Medium")
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)

        "####" Z 0 ACS_NamedExecuteWithResult("Dakka_AltFistStepUp", 1)
        "####" Z 0 A_JumpIfInventory("DakkaFist_StepUp",  1, "AltFire_Medium")
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)

        "####" Z 0 ACS_NamedExecuteWithResult("Dakka_AltFistStepUp", 1)
        "####" Z 0 A_JumpIfInventory("DakkaFist_StepUp",  1, "AltFire_Medium")
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)

        "####" Z 0 ACS_NamedExecuteWithResult("Dakka_AltFistStepUp", 1)
        "####" Z 0 A_JumpIfInventory("DakkaFist_StepUp",  1, "AltFire_Medium")
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)
        goto AFSpinloop_Weak

      AFSpinloop_Weak:
        "####" Z 0 ACS_NamedExecuteWithResult("Dakka_AltFistStepUp",  1)
        "####" Z 0 ACS_NamedExecuteWithResult("Dakka_AltFistRelease")
        "####" Z 0 A_JumpIfInventory("DakkaFist_StepUp",  1, "AltFire_Medium")
        "####" Z 1 A_JumpIfInventory("DakkaFist_Release", 1, "AltPunch_Weak")
        loop


      AltPunch_Weak:
        "####" Z 0 A_TakeInventory("DakkaFist_StepUp")
        "####" Z 0 A_TakeInventory("DakkaFist_Release")

        "####" Z 0 A_ZoomFactor(1.01, ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistHeavyTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)

        "####" I 0 A_FireBullets(0, 0, 1, 0, "FistHeavyTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)
        "####" I 1 A_ZoomFactor(1.22, ZOOM_NOSCALETURNING | ZOOM_INSTANT)

        "####" L 0 A_TakeInventory("DakkaFistConnected")
        "####" L 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 0, 1)
        "####" L 0 A_TakeInventory("DakkaFistAltLevel")
        "####" L 0 A_PlaySound("dakka/explode", 7)
        "####" L 0 A_FireBullets(0, 0, 1, 80, "FistPowerPunch1", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)
        "####" L 0 A_ZoomFactor(1.35,  ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        "####" L 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)

        "####" L 0 A_JumpIfInventory("DakkaFistConnected", 1, 2)
        "####" L 0 ACS_NamedExecuteWithResult("Dakka_ModFistSpeed", AAPTR_DEFAULT, -0x7FFFFFFF)

        "####" LLLLLLL 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" L 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" L 1  A_ZoomFactor(1.3,  ZOOM_NOSCALETURNING)
        "####" L 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" L 1  A_ZoomFactor(1.25, ZOOM_NOSCALETURNING)

        "####" K 1  A_ZoomFactor(1.1,  ZOOM_NOSCALETURNING)
        "####" I 0  A_ZoomFactor(1,    ZOOM_NOSCALETURNING)
        "####" I 1 A_JumpIfInventory("AltFired", 1, "AltHold_Quick3")
        "####" H 1 A_JumpIfInventory("AltFired", 1, "AltHold_Quick2")
        "####" G 1 A_JumpIfInventory("AltFired", 1, "AltHold_Quick1")
        "####" Z 1 A_JumpIfInventory("AltFired", 1, "AltHold")
        goto AttackEnd



      AltFire_Medium:
        "####" Z 0 A_PlaySound("dakka/fistcharge2", 6)
        "####" Z 0 A_GiveInventory("DakkaFistAltLevel")
        "####" Z 0 A_ZoomFactor(0.92, ZOOM_NOSCALETURNING)
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)
        "####" Z 0 A_ZoomFactor(0.91, ZOOM_NOSCALETURNING)
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)
        "####" Z 0 A_ZoomFactor(0.9, ZOOM_NOSCALETURNING)

        "####" Z 0 ACS_NamedExecuteWithResult("Dakka_AltFistStepUp", 2)
        "####" Z 0 A_JumpIfInventory("DakkaFist_StepUp", 2, "AltFire_Strong")
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)

        "####" Z 0 ACS_NamedExecuteWithResult("Dakka_AltFistStepUp", 2)
        "####" Z 0 A_JumpIfInventory("DakkaFist_StepUp", 2, "AltFire_Strong")
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)

        "####" Z 0 ACS_NamedExecuteWithResult("Dakka_AltFistStepUp", 2)
        "####" Z 0 A_JumpIfInventory("DakkaFist_StepUp", 2, "AltFire_Strong")
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)

        "####" Z 0 ACS_NamedExecuteWithResult("Dakka_AltFistStepUp", 2)
        "####" Z 0 A_JumpIfInventory("DakkaFist_StepUp", 2, "AltFire_Strong")
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)

        "####" Z 0 ACS_NamedExecuteWithResult("Dakka_AltFistStepUp", 2)
        "####" Z 0 A_JumpIfInventory("DakkaFist_StepUp", 2, "AltFire_Strong")
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)

        "####" Z 0 ACS_NamedExecuteWithResult("Dakka_AltFistStepUp", 2)
        "####" Z 0 A_JumpIfInventory("DakkaFist_StepUp", 2, "AltFire_Strong")
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)

        "####" Z 0 ACS_NamedExecuteWithResult("Dakka_AltFistStepUp", 2)
        "####" Z 0 A_JumpIfInventory("DakkaFist_StepUp", 2, "AltFire_Strong")
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)

        "####" Z 0 ACS_NamedExecuteWithResult("Dakka_AltFistStepUp", 2)
        "####" Z 0 A_JumpIfInventory("DakkaFist_StepUp", 2, "AltFire_Strong")
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)
        goto AFSpinloop_Medium

      AFSpinloop_Medium:
        "####" Z 0 ACS_NamedExecuteWithResult("Dakka_AltFistStepUp",  2)
        "####" Z 0 ACS_NamedExecuteWithResult("Dakka_AltFistRelease")
        "####" Z 0 A_JumpIfInventory("DakkaFist_StepUp",  2, "AltFire_Strong")
        "####" Z 1 A_JumpIfInventory("DakkaFist_Release", 1, "AltPunch_Medium")
        loop

      AltPunch_Medium:
        "####" Z 0 A_TakeInventory("DakkaFist_StepUp")
        "####" Z 0 A_TakeInventory("DakkaFist_Release")

        "####" Z 0 A_ZoomFactor(1.01, ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistHeavyTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)

        "####" I 0 A_FireBullets(0, 0, 1, 0, "FistHeavyTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)
        "####" I 1 A_ZoomFactor(1.22, ZOOM_NOSCALETURNING | ZOOM_INSTANT)

        "####" L 0 A_TakeInventory("DakkaFistConnected")
        "####" L 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 0, 1)
        "####" L 0 A_TakeInventory("DakkaFistAltLevel", 2)
        "####" L 0 A_PlaySound("dakka/explode", 7)
        "####" L 0 A_FireBullets(0, 0, 1, 150, "FistPowerPunch2", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)
        "####" L 0 A_ZoomFactor(1.35,  ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        "####" L 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)

        "####" L 0 A_JumpIfInventory("DakkaFistConnected", 1, 2)
        "####" L 0 ACS_NamedExecuteWithResult("Dakka_ModFistSpeed", AAPTR_DEFAULT, -0x7FFFFFFF)

        "####" LLLLLLLLL 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" L 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" L 1  A_ZoomFactor(1.3,  ZOOM_NOSCALETURNING)
        "####" L 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" L 1  A_ZoomFactor(1.25, ZOOM_NOSCALETURNING)
        "####" L 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" L 1  A_ZoomFactor(1.2,  ZOOM_NOSCALETURNING)
        "####" L 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" L 1  A_ZoomFactor(1.15, ZOOM_NOSCALETURNING)

        "####" K 1  A_ZoomFactor(1.1,  ZOOM_NOSCALETURNING)
        "####" I 0  A_ZoomFactor(1,    ZOOM_NOSCALETURNING)
        "####" I 1 A_JumpIfInventory("AltFired", 1, "AltHold_Quick3")
        "####" H 1 A_JumpIfInventory("AltFired", 1, "AltHold_Quick2")
        "####" G 1 A_JumpIfInventory("AltFired", 1, "AltHold_Quick1")
        goto AttackEnd


      AltFire_Strong:
        "####" Z 0 A_TakeInventory("DakkaFist_StepUp")
        "####" Z 0 A_TakeInventory("DakkaFist_Release")

        "####" Z 0 A_PlaySound("dakka/fistcharge3", 5)
        "####" Z 0 A_GiveInventory("DakkaFistAltLevel")
        "####" Z 0 A_ZoomFactor(0.87, ZOOM_NOSCALETURNING)
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)
        "####" Z 0 A_ZoomFactor(0.86, ZOOM_NOSCALETURNING)
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)
        "####" Z 0 A_ZoomFactor(0.85, ZOOM_NOSCALETURNING)
        "####" ZZZZZZZ 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)

        "####" Z 0 A_FireBullets(0, 0, 1, 0, "FistHeavyTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)
        "####" Z 1 A_ZoomFactor(1.0,  ZOOM_NOSCALETURNING | ZOOM_INSTANT)


        "####" I 0 A_ZoomFactor(1.2,  ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        "####" I 1 A_FireBullets(0, 0, 1, 0, "FistHeavyTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)
        "####" L 0 A_ZoomFactor(1.35, ZOOM_NOSCALETURNING | ZOOM_INSTANT)

        "####" L 0 A_TakeInventory("DakkaFistConnected")
        "####" L 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 0, 1)
        "####" L 0 A_TakeInventory("DakkaFistAltLevel", 1)
        "####" L 0 A_PlaySound("dakka/explode", 7)
        "####" L 1 A_FireBullets(0, 0, 1, 30, "FistPowerPunch3_1", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)

        "####" J 0 A_ZoomFactor(1.325, ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        "####" J 1 A_FireBullets(0, 0, 1, 0, "FistHeavyTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)
        "####" G 0 A_ZoomFactor(1.3,   ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        "####" G 1 A_FireBullets(0, 0, 1, 0, "FistHeavyTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)


        "####" N 1 A_FireBullets(0, 0, 1, 0, "FistHeavyTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)
        "####" P 0 A_ZoomFactor(1.35, ZOOM_NOSCALETURNING | ZOOM_INSTANT)

        "####" P 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 0, 1)
        "####" P 0 A_TakeInventory("DakkaFistAltLevel", 1)
        "####" P 0 A_PlaySound("dakka/explode", 7)
        "####" P 1 A_FireBullets(0, 0, 1, 30, "FistPowerPunch3_1", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)

        "####" O 0 A_ZoomFactor(1.325, ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        "####" O 1 A_FireBullets(0, 0, 1, 0, "FistHeavyTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)
        "####" M 0 A_ZoomFactor(1.3,   ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        "####" M 1 A_FireBullets(0, 0, 1, 0, "FistHeavyTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)
        "####" I 1 A_FireBullets(0, 0, 1, 0, "FistHeavyTrackingPuff", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)

        "####" L 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 0, 1)
        "####" L 0 A_TakeInventory("DakkaFistAltLevel")
        "####" L 0 A_PlaySound("dakka/explode", 7)
        "####" L 0 A_FireBullets(0, 0, 1, 40, "FistPowerPunch3_2", FBF_NORANDOM | FBF_NOFLASH | FBF_NORANDOMPUFFZ, 112)

        "####" L 0 A_ZoomFactor(1.35,  ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        "####" L 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)

        "####" L 0 A_JumpIfInventory("DakkaFistConnected", 1, 2)
        "####" L 0 ACS_NamedExecuteWithResult("Dakka_ModFistSpeed", AAPTR_DEFAULT, -0x7FFFFFFF)

        "####" LLLLLLLLLL 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" L 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" L 1  A_ZoomFactor(1.3,  ZOOM_NOSCALETURNING)
        "####" L 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" L 1  A_ZoomFactor(1.25, ZOOM_NOSCALETURNING)
        "####" L 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" L 1  A_ZoomFactor(1.2,  ZOOM_NOSCALETURNING)
        "####" L 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" L 1  A_ZoomFactor(1.15, ZOOM_NOSCALETURNING)

        "####" K 1  A_ZoomFactor(1.1,  ZOOM_NOSCALETURNING)
        "####" I 0  A_ZoomFactor(1,    ZOOM_NOSCALETURNING)
        "####" I 1 A_JumpIfInventory("AltFired", 1, "AltHold_Quick3")
        "####" H 1 A_JumpIfInventory("AltFired", 1, "AltHold_Quick2")
        "####" G 1 A_JumpIfInventory("AltFired", 1, "AltHold_Quick1")
        goto AttackEnd





      AttackEnd:
        TNT1 A 0 A_TakeInventory("DakkaFistInAlt")
        TNT1 A 0 A_TakeInventory("DakkaFistAltLevel")
        TNT1 A 0 A_GiveInventory("DakkaFistAttackOver")
        DFIT A 0               A_JumpIfInventory("AltFired",  1, "AltHold")
        DFIT A 0               A_JumpIfInventory("MainFired", 1, "Fire")
        DFIT A 1 offset(0, 40) A_WeaponReady(WRF_NOBOB)
        DFIT A 0               A_JumpIfInventory("AltFired", 1, "AltHold")
        DFIT A 0               A_JumpIfInventory("MainFired", 1, "Fire")
        DFIT A 1 offset(0, 35) A_WeaponReady(WRF_NOBOB)
        DFIT A 0               A_JumpIfInventory("AltFired", 1, "AltHold")
        DFIT A 0               A_JumpIfInventory("MainFired", 1, "Fire")
        DFIT A 1 offset(0, 32) A_WeaponReady(WRF_NOBOB)
        goto Ready
    }
}



actor FistPunchPuff: BulletPuff
{
    +HITTRACER
    +PUFFGETSOWNER
    +PUFFONACTORS
    +NOEXTREMEDEATH
    +NODAMAGETHRUST

    SeeSound    "dakka/silent"
    AttackSound "dakka/silent"

    States
    {
      Spawn:
      Melee:
      XDeath:
        TNT1 A 0
        TNT1 A 0 A_GiveToTarget("DakkaFistConnected")
        TNT1 A 0 A_PlaySound("dakka/punch", CHAN_WEAPON)
        TNT1 A 0 A_Quake(1, 3, 0, 96, "")
        TNT1 A 0 A_JumpIf(CallACS("Dakka_FistHit"), "MeatHit")
        TNT1 A 8 ACS_NamedExecuteWithResult("Dakka_ModFistSpeed", AAPTR_TARGET, -1)
        stop

      MeatHit:
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_ModFistSpeed", AAPTR_TARGET, 1)
        TNT1 A 8
        stop

      Crash:
        TNT1 A 0 A_GiveToTarget("DakkaFistConnected")
        TNT1 A 0 A_PlaySound("dakka/punchwall", CHAN_WEAPON)
        TNT1 A 0 A_JumpIf(CallACS("Dakka_FistHit"), "SparkHit")
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_ModFistSpeed", AAPTR_TARGET, -1)
        BPUF CD 4
        stop

      SparkHit:
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_ModFistSpeed", AAPTR_TARGET, 1)
        TNT1 A 0 A_Quake(1, 3, 0, 96, "")
        BPUF CD 4
        stop
    }
}



actor FistTrackingPuff: BulletPuff
{
    +HITTRACER
    +PUFFGETSOWNER
    +PUFFONACTORS
    +NOEXTREMEDEATH
    +NODAMAGETHRUST
    +BLOODLESSIMPACT

    SeeSound    "dakka/silent"
    AttackSound "dakka/silent"

    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 8 ACS_NamedExecuteWithResult("Dakka_FistHit", 0, 1, 1)
        stop
    }
}


actor FistHeavyTrackingPuff: BulletPuff
{
    +HITTRACER
    +PUFFGETSOWNER
    +PUFFONACTORS
    +NOEXTREMEDEATH
    +NODAMAGETHRUST
    +BLOODLESSIMPACT

    SeeSound    "dakka/silent"
    AttackSound "dakka/silent"

    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 8 ACS_NamedExecuteWithResult("Dakka_FistHit", 0, 8, 1)
        stop
    }
}



actor FistPowerPunch1: BulletPuff
{
    +HITTRACER
    +PUFFGETSOWNER
    +PUFFONACTORS
    +EXTREMEDEATH
    +NODAMAGETHRUST
    +FORCEPAIN

    SeeSound    "dakka/silent"
    AttackSound "dakka/silent"

    Damage 100
    DamageType "FistLightStun"
    ReactionTime 5

    States
    {
      Spawn:
      Melee:
      XDeath:
        TNT1 A 0
        TNT1 A 0 A_JumpIf(CallACS("Dakka_FistHit", damage), "MeatHit")
        goto MeatMiss

      MeatHit:
        TNT1 A 0 A_GiveToTarget("DakkaFistConnected")
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_ModFistSpeed", AAPTR_TARGET, reactiontime)
        TNT1 A 1 A_Jump(256, "MeatEnd")
        wait

      MeatMiss:
        TNT1 A 1 A_Jump(256, "MeatEnd")
        wait

      MeatEnd:
        TNT1 A 0 A_PlaySound("dakka/punch", CHAN_WEAPON)
        TNT1 A 0 A_Quake(4, 4, 0, 96, "")
        TNT1 A 0 A_Quake(2, 8, 0, 128, "")
        TNT1 A 0 A_Quake(1, 12, 0, 160, "")
        TNT1 A 8
        stop


      Crash:
        TNT1 A 0 A_JumpIf(CallACS("Dakka_FistHit", damage), "SparkHit")
        goto SparkMiss

      SparkHit:
        TNT1 A 0 A_GiveToTarget("DakkaFistConnected")
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_ModFistSpeed", AAPTR_TARGET, reactiontime)
        TNT1 A 1 A_Jump(256, "SparkEnd")
        wait

      SparkMiss:
        TNT1 A 1 A_Jump(256, "SparkEnd")
        wait

      SparkEnd:
        TNT1 A 0 A_PlaySound("dakka/punchwall", CHAN_WEAPON)
        TNT1 A 0 A_Quake(4, 4, 0, 96, "")
        TNT1 A 0 A_Quake(2, 8, 0, 128, "")
        TNT1 A 0 A_Quake(1, 12, 0, 160, "")
        BPUF CD 4
        stop
    }
}


actor FistPowerPunch2: FistPowerPunch1
{
    Damage 250
    DamageType "FistMediumStun"
    ReactionTime 8

    States
    {

      MeatEnd:
        TNT1 A 0 A_PlaySound("dakka/punch", CHAN_WEAPON)
        TNT1 A 0 A_Quake(5, 6,  0, 96, "")
        TNT1 A 0 A_Quake(3, 10, 0, 128, "")
        TNT1 A 0 A_Quake(1, 15, 0, 160, "")
        TNT1 A 8
        stop

      SparkEnd:
        TNT1 A 0 A_PlaySound("dakka/punchwall", CHAN_WEAPON)
        TNT1 A 0 A_Quake(5, 6,  0, 96, "")
        TNT1 A 0 A_Quake(3, 10, 0, 128, "")
        TNT1 A 0 A_Quake(1, 15, 0, 160, "")
        BPUF CD 4
        stop
    }
}

actor FistPowerPunch3_1: FistPowerPunch1
{
    Damage 50
    DamageType "FistLightStun"
    ReactionTime 3

    States
    {
      MeatMiss:
        TNT1 A 1 A_Jump(256, "MeatEnd")
        wait

      MeatEnd:
        TNT1 A 0 A_PlaySound("dakka/punch", CHAN_WEAPON)
        TNT1 A 0 A_Quake(3, 3,  0, 64, "")
        TNT1 A 0 A_Quake(1, 8,  0, 96, "")
        TNT1 A 8
        stop

      SparkEnd:
        TNT1 A 0 A_PlaySound("dakka/punchwall", CHAN_WEAPON)
        TNT1 A 0 A_Quake(3, 3,  0, 64, "")
        TNT1 A 0 A_Quake(1, 8,  0, 96, "")
        BPUF CD 4
        stop
    }
}

actor FistPowerPunch3_2: FistPowerPunch1
{
    Damage 400
    DamageType "FistHeavyStun"
    ReactionTime 9

    +ALWAYSPUFF
    +FORCERADIUSDMG
    +PUFFGETSOWNER

    States
    {
      MeatEnd:
        TNT1 A 0 A_PlaySound("dakka/punch", CHAN_WEAPON)
        goto SparkEnd

      SparkEnd:
        TNT1 A 0 A_Explode(100, 128, false, false, 64)
        TNT1 A 8 A_SpawnItemEx("FistShockwave_Client")
        TNT1 A 0 A_Quake(6, 6,  0, 96, "")
        TNT1 A 0 A_Quake(4, 12, 0, 128, "")
        TNT1 A 0 A_Quake(2, 19, 0, 160, "")
        stop
    }
}


actor FistShockwave_Client
{
	+NOINTERACTION
	+CLIENTSIDEONLY

	RenderStyle Add

	var int user_ringangle;

	States
	{
	  Spawn:
	    MISL B 0 bright
		goto RingSpawn

	  RingSpawn:
	    MISL B 0 A_SpawnItemEx("ImpalerRingParticle", 0,0,0, 32, 0, 0, user_ringangle)
		MISL B 0 A_SetUserVar("user_ringangle", user_ringangle + 10)
		MISL B 0 A_JumpIf(user_ringangle >= 360, "ParticleSpawn")
		loop

	  ParticleSpawn:
	    MISL BBBBBBBBBBBBBBBB 0 A_SpawnItemEx("ImpalerExplosionSpark", frandom(-6, 6), frandom(-1, 1), frandom(-3, 3), frandom(4, 12), frandom(-3, 3), frandom(-4, 8), frandom(0, 360))
		stop
	}
}