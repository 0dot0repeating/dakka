actor LeftPunchNext:        Boolean {}
actor DakkaFistConnected:   Boolean {}
actor DakkaFistSpeed:       Counter { Inventory.MaxAmount 15 }
actor DakkaFistTap_Main:    Counter {}
actor DakkaFistTap_Alt:     Counter {}
actor DakkaFistFinisher:    Boolean {}
actor DakkaFistInAlt:       Boolean {}
actor DakkaFistAttackOver:  Boolean {}


actor DWep_Fist: DoomWeapon
{
    Tag "Fists"

    Weapon.SlotNumber 1
    Weapon.SelectionOrder 3700
    +WEAPON.MELEEWEAPON
    +UNDROPPABLE
    +NOALERT

    Inventory.PickupMessage "$DAKKA_PK_FISTS"
    Obituary "$DAKKA_MP_FISTS"

    States
    {
      Select:
        DFIT AA 0 A_Raise
        DFIT A 1 A_Raise
        loop

      Deselect:
        "####" A 0 A_JumpIfInventory("DakkaFistAttackOver", 1, "InstantDeselect")
        "####" A 0 A_JumpIfInventory("DakkaFistInAlt", 1, "AltDeselect")
        DFIT AAA 0 A_Lower
        DFIT A 1 A_Lower
        loop
      
      AltDeselect:
        "####" K 0 A_TakeInventory("DakkaFistInAlt")
        "####" K 0 A_ZoomFactor(1.0, ZOOM_NOSCALETURNING)
        "####" KIG 1
        TNT1 A 2
        goto InstantDeselect
      
      InstantDeselect:
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 0 A_Lower
        wait

      Ready:
        TNT1 A 0 A_TakeInventory("DakkaFistAttackOver")
        DFIT A 1 A_WeaponReady
        wait
        
        
      Fire:
        TNT1 A 0 A_TakeInventory("DakkaFistSpeed")
        TNT1 A 0 A_TakeInventory("DakkaFistConnected")
        TNT1 A 0 A_TakeInventory("DakkaFistAttackOver")
        TNT1 A 0 A_JumpIfInventory("LeftPunchNext", 1, "FireLeft")
        RFIT Z 0 A_GiveInventory("LeftPunchNext")
        goto Fire6
      
      FireLeft:
        LFIT Z 0 A_TakeInventory("LeftPunchNext")
        goto Fire6
      
      
      Hold:
        TNT1 A 0 A_TakeInventory("DakkaFistSpeed", 1)
        TNT1 A 0 A_TakeInventory("DakkaFistConnected")
        TNT1 A 0 A_TakeInventory("DakkaFistAttackOver")
        TNT1 A 0 A_JumpIfInventory("LeftPunchNext", 1, "HoldLeft")
        RFIT Z 0 A_GiveInventory("LeftPunchNext")
        goto HoldCommon
      
      HoldLeft:
        LFIT Z 0 A_TakeInventory("LeftPunchNext")
        goto HoldCommon
      
      HoldCommon:
        "####" Z 0 A_JumpIfInventory("DakkaFistSpeed", 6, "Hold2")
        "####" Z 0 A_JumpIfInventory("DakkaFistSpeed", 4, "Hold3")
        "####" Z 0 A_JumpIfInventory("DakkaFistSpeed", 2, "Hold4")
        "####" Z 0 A_JumpIfInventory("DakkaFistSpeed", 1, "Hold5")
        goto Hold6
      
      
      
      Fire6: Hold6:
        "####" B 1
        "####" E 1 A_FireBullets(0, 0, 1, 8, "FistPunchPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80)
        "####" D 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" Z 0 A_JumpIfInventory("DakkaFistTap_Alt", 1, "AltFire_FromPrimary")
        "####" Z 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" Z 0 A_JumpIfInventory("DakkaFistTap_Alt", 1, "AltFire_FromPrimary")
        "####" Z 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
      
      
      Fire5: Hold5:
        "####" B 1
        "####" E 1 A_FireBullets(0, 0, 1, 8, "FistPunchPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80)
        "####" D 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" Z 0 A_JumpIfInventory("DakkaFistTap_Alt", 1, "AltFire_FromPrimary")
        "####" Z 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
      
      
      Fire4: Hold4:
        "####" B 1
        "####" E 1 A_FireBullets(0, 0, 1, 9, "FistPunchPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80)
        "####" C 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" Z 0 A_JumpIfInventory("DakkaFistTap_Alt", 1, "AltFire_FromPrimary")
        "####" Z 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
      
      
      Fire3: Hold3:
        "####" E 1 A_FireBullets(0, 0, 1, 9, "FistPunchPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80)
        "####" B 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" Z 0 A_JumpIfInventory("DakkaFistTap_Alt", 1, "AltFire_FromPrimary")
        "####" Z 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
      
      
      Fire2: Hold2:
        "####" E 1 A_FireBullets(0, 0, 1, 10, "FistPunchPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80)
        "####" Z 0 A_JumpIfInventory("DakkaFistTap_Alt", 1, "AltFire_FromPrimary")
        "####" B 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
      
      
      
      
      AltFire:
        TNT1 A 0 A_ClearRefire
        TNT1 A 0 A_GiveInventory("DakkaFistInAlt")
        TNT1 A 0 A_TakeInventory("DakkaFistAttackOver")
        TNT1 A 0 A_PlaySound("dakka/fistcharge1", 5)
        
        DFIT A 0 offset(0, 40) A_ZoomFactor(0.97, ZOOM_NOSCALETURNING)
        DFIT A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        DFIT A 0 offset(0, 56) A_ZoomFactor(0.96, ZOOM_NOSCALETURNING)
        DFIT A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        TNT1 A 0 offset(0, 32) A_ZoomFactor(0.95, ZOOM_NOSCALETURNING)
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        goto Altfire_Weak

      AltHold:
      AltFire_FromPrimary:
        TNT1 A 0 A_ClearRefire
        TNT1 A 0 A_GiveInventory("DakkaFistInAlt")
        TNT1 A 0 A_TakeInventory("DakkaFistAttackOver")
        TNT1 A 0 A_PlaySound("dakka/fistcharge1", 5)
        
        TNT1 A 0 A_ZoomFactor(0.97, ZOOM_NOSCALETURNING)
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        TNT1 A 0 A_ZoomFactor(0.96, ZOOM_NOSCALETURNING)
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        TNT1 A 0 A_ZoomFactor(0.95, ZOOM_NOSCALETURNING)
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        goto AltFire_Weak
      
      
      AltFire_Weak:
        TNT1 AAAAA 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        TNT1 A 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Medium")
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        TNT1 A 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Medium")
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        TNT1 A 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Medium")
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        TNT1 A 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Medium")
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        TNT1 A 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Medium")
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        TNT1 A 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Medium")
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        LFIT H 0 A_FireBullets(0, 0, 1, 0, "FistHeavyTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        LFIT H 1 A_ZoomFactor(1.05, ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        LFIT J 0 A_FireBullets(0, 0, 1, 0, "FistHeavyTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        LFIT J 1 A_ZoomFactor(1.22, ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        
        LFIT L 0 A_TakeInventory("DakkaFistConnected")
        LFIT L 0 A_FireBullets(0, 0, 1, 120, "FistPowerPunch1", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        LFIT L 0 A_PlaySound("dakka/explode", CHAN_WEAPON)
        LFIT L 0 A_ZoomFactor(1.35,  ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        
        LFIT LLLLLLLLLL 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        LFIT L 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        LFIT L 1  A_ZoomFactor(1.3,  ZOOM_NOSCALETURNING)
        LFIT L 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        LFIT L 1  A_ZoomFactor(1.25, ZOOM_NOSCALETURNING)
        
        LFIT K 1  A_ZoomFactor(1.2,  ZOOM_NOSCALETURNING)
        LFIT K 1  A_ZoomFactor(1.15, ZOOM_NOSCALETURNING)
        LFIT J 1  A_ZoomFactor(1.1,  ZOOM_NOSCALETURNING)
        LFIT I 1  A_ZoomFactor(1.05, ZOOM_NOSCALETURNING)
        LFIT H 1  A_ZoomFactor(1,    ZOOM_NOSCALETURNING)
        LFIT G 1
        TNT1 A 1
        goto AttackEnd
      
      
      AltFire_Medium:
        TNT1 A 0 A_PlaySound("dakka/fistcharge2", 6)
        TNT1 A 0 A_ZoomFactor(0.92, ZOOM_NOSCALETURNING)
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        TNT1 A 0 A_ZoomFactor(0.91, ZOOM_NOSCALETURNING)
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        TNT1 A 0 A_ZoomFactor(0.9, ZOOM_NOSCALETURNING)
        TNT1 AAAAAA 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        TNT1 A 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Strong")
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        TNT1 A 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Strong")
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        TNT1 A 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Strong")
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        TNT1 A 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Strong")
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        TNT1 A 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Strong")
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        TNT1 A 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Strong")
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        LFIT H 0 A_FireBullets(0, 0, 1, 0, "FistHeavyTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        LFIT H 1 A_ZoomFactor(1.02, ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        LFIT J 0 A_FireBullets(0, 0, 1, 0, "FistHeavyTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        LFIT J 1 A_ZoomFactor(1.21, ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        
        LFIT L 0 A_TakeInventory("DakkaFistConnected")
        LFIT L 0 A_FireBullets(0, 0, 1, 160, "FistPowerPunch2", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        LFIT L 0 A_PlaySound("dakka/explode", CHAN_WEAPON)
        LFIT L 0 A_ZoomFactor(1.35,  ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        
        LFIT LLLLLLLLLLLLLL 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        LFIT L 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        LFIT L 1  A_ZoomFactor(1.3,  ZOOM_NOSCALETURNING)
        LFIT L 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        LFIT L 1  A_ZoomFactor(1.25, ZOOM_NOSCALETURNING)
        LFIT L 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        LFIT L 1  A_ZoomFactor(1.2,  ZOOM_NOSCALETURNING)
        LFIT L 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        LFIT L 1  A_ZoomFactor(1.15, ZOOM_NOSCALETURNING)
        
        LFIT K 1  A_ZoomFactor(1.1,  ZOOM_NOSCALETURNING)
        LFIT K 1  A_ZoomFactor(1.05, ZOOM_NOSCALETURNING)
        LFIT J 1  A_ZoomFactor(1,    ZOOM_NOSCALETURNING)
        LFIT JIHG 1
        TNT1 A 1
        goto AttackEnd
      
      
      AltFire_Strong:
        TNT1 A 0 A_PlaySound("dakka/fistcharge3", 5)
        TNT1 A 0 A_ZoomFactor(0.87, ZOOM_NOSCALETURNING)
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        TNT1 A 0 A_ZoomFactor(0.86, ZOOM_NOSCALETURNING)
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        TNT1 A 0 A_ZoomFactor(0.85, ZOOM_NOSCALETURNING)
        TNT1 AAAAAAAAAAAA 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        LFIT H 0 A_FireBullets(0, 0, 1, 0, "FistHeavyTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        LFIT H 1 A_ZoomFactor(1.00, ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        LFIT J 0 A_FireBullets(0, 0, 1, 0, "FistHeavyTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        LFIT J 1 A_ZoomFactor(1.2,  ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        
        LFIT L 0 A_TakeInventory("DakkaFistConnected")
        LFIT L 0 A_FireBullets(0, 0, 1, 200, "FistPowerPunch3", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        LFIT L 0 A_PlaySound("dakka/explode", CHAN_WEAPON)
        LFIT L 0 A_ZoomFactor(1.35,  ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        
        LFIT LLLLLLLLLL 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        LFIT L 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        LFIT L 1
        LFIT L 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        LFIT L 1
        
        LFIT L 1  A_ZoomFactor(1.3,  ZOOM_NOSCALETURNING)
        LFIT L 1  A_ZoomFactor(1.25, ZOOM_NOSCALETURNING)
        LFIT K 1  A_ZoomFactor(1.2,  ZOOM_NOSCALETURNING)
        LFIT K 1  A_ZoomFactor(1.15, ZOOM_NOSCALETURNING)
        LFIT J 1  A_ZoomFactor(1.1,  ZOOM_NOSCALETURNING)
        LFIT I 1  A_ZoomFactor(1.05, ZOOM_NOSCALETURNING)
        LFIT H 1  A_ZoomFactor(1,    ZOOM_NOSCALETURNING)
        LFIT G 1
        TNT1 A 1
        goto AttackEnd
      
      
    


      AttackEnd:
        TNT1 A 0 A_TakeInventory("DakkaFistInAlt")
        TNT1 A 0 A_GiveInventory("DakkaFistAttackOver")
        DIFT A 0               A_Refire
        DFIT A 1 offset(0, 40) A_WeaponReady(WRF_NOBOB)
        DIFT A 0               A_Refire
        DFIT A 1 offset(0, 35) A_WeaponReady(WRF_NOBOB)
        DIFT A 0               A_Refire
        DFIT A 1 offset(0, 32) A_WeaponReady(WRF_NOBOB)
        goto Ready
    }
}

actor FistPunchPuff: BulletPuff
{
    +HITTRACER
    +PUFFGETSOWNER
    +PUFFONACTORS
    +NOEXTREMEDEATH
    +NODAMAGETHRUST

    SeeSound    "dakka/silent"
    AttackSound "dakka/silent"

    States
    {
      Spawn:
      Melee:
      XDeath:
        TNT1 A 0
        TNT1 A 0 A_PlaySound("dakka/punch", CHAN_WEAPON)
        TNT1 A 0 A_GiveToTarget("DakkaFistConnected")
        TNT1 A 0 A_Quake(1, 3, 0, 96, "")
        TNT1 A 8 A_JumpIf(CallACS("Dakka_FistHit"), "MeatHit")
        stop
      
      MeatHit:
        TNT1 A 0 A_GiveToTarget("DakkaFistSpeed", 2)
        TNT1 A 8
        stop

      Crash:
        TNT1 A 0 A_PlaySound("dakka/punchwall", CHAN_WEAPON)
        TNT1 A 0 A_GiveToTarget("DakkaFistConnected")
        TNT1 A 0 A_JumpIf(CallACS("Dakka_FistHit"), "SparkHit")
        BPUF CD 4
        stop
      
      SparkHit:
        TNT1 A 0 A_GiveToTarget("DakkaFistSpeed", 2)
        TNT1 A 0 A_Quake(1, 3, 0, 96, "")
        BPUF CD 4
        stop
    }
}



actor FistTrackingPuff: BulletPuff
{
    +HITTRACER
    +PUFFGETSOWNER
    +PUFFONACTORS
    +NOEXTREMEDEATH
    +NODAMAGETHRUST
    +BLOODLESSIMPACT

    SeeSound    "dakka/silent"
    AttackSound "dakka/silent"

    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 8 ACS_NamedExecuteWithResult("Dakka_FistHit", 0, 1, 1)
        stop
    }
}


actor FistHeavyTrackingPuff: BulletPuff
{
    +HITTRACER
    +PUFFGETSOWNER
    +PUFFONACTORS
    +NOEXTREMEDEATH
    +NODAMAGETHRUST
    +BLOODLESSIMPACT

    SeeSound    "dakka/silent"
    AttackSound "dakka/silent"

    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 8 ACS_NamedExecuteWithResult("Dakka_FistHit", 0, 8, 1)
        stop
    }
}



actor FistPowerPunch1: BulletPuff
{
    +HITTRACER
    +PUFFGETSOWNER
    +PUFFONACTORS
    +EXTREMEDEATH
    +NODAMAGETHRUST
    +FORCEPAIN

    SeeSound    "dakka/silent"
    AttackSound "dakka/silent"
    
    Damage 100
    DamageType "FistLightStun"

    States
    {
      Spawn:
      Melee:
      XDeath:
        TNT1 A 0
        TNT1 A 0 A_PlaySound("dakka/punch", CHAN_WEAPON)
        TNT1 A 0 A_GiveToTarget("DakkaFistConnected")
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_FistHit", damage)
        TNT1 A 0 A_JumpIf(1, "MeatEnd") // inheritance
        goto MeatEnd
      
      MeatEnd:
        TNT1 A 0 A_Quake(4, 4, 0, 96, "")
        TNT1 A 0 A_Quake(2, 8, 0, 128, "")
        TNT1 A 0 A_Quake(1, 12, 0, 160, "")
        TNT1 A 8
        stop

      Crash:
        TNT1 A 0 A_PlaySound("dakka/punchwall", CHAN_WEAPON)
        TNT1 A 0 A_GiveToTarget("DakkaFistConnected")
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_FistHit", damage)
        TNT1 A 0 A_JumpIf(1, "SparkEnd") // inheritance
        goto SparkEnd
      
      SparkEnd:
        TNT1 A 0 A_Quake(4, 4, 0, 96, "")
        TNT1 A 0 A_Quake(2, 8, 0, 128, "")
        TNT1 A 0 A_Quake(1, 12, 0, 160, "")
        BPUF CD 4
        stop
    }
}

actor FistPowerPunch2: FistPowerPunch1
{ 
    Damage 250
    DamageType "FistMediumStun"
    
    States
    {
      
      MeatEnd:
        TNT1 A 0 A_Quake(5, 6,  0, 96, "")
        TNT1 A 0 A_Quake(3, 10, 0, 128, "")
        TNT1 A 0 A_Quake(1, 15, 0, 160, "")
        TNT1 A 8
        stop
      
      SparkEnd:
        TNT1 A 0 A_Quake(5, 6,  0, 96, "")
        TNT1 A 0 A_Quake(3, 10, 0, 128, "")
        TNT1 A 0 A_Quake(1, 15, 0, 160, "")
        BPUF CD 4
        stop
    }
}

actor FistPowerPunch3: FistPowerPunch1
{ 
    Damage 400
    DamageType "FistHeavyStun"
    
    States
    {
      
      MeatEnd:
        TNT1 A 0 A_Quake(6, 6,  0, 96, "")
        TNT1 A 0 A_Quake(4, 12, 0, 128, "")
        TNT1 A 0 A_Quake(2, 19, 0, 160, "")
        TNT1 A 8
        stop
      
      SparkEnd:
        TNT1 A 0 A_Quake(6, 6,  0, 96, "")
        TNT1 A 0 A_Quake(4, 12, 0, 128, "")
        TNT1 A 0 A_Quake(2, 19, 0, 160, "")
        BPUF CD 4
        stop
    }
}


actor DakkaFistQuake2: Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_Quake(4, 4, 0, 96, "")
        TNT1 A 0 A_Quake(2, 8, 0, 128, "")
        TNT1 A 0 A_Quake(1, 12, 0, 160, "")
        stop
    }
}