actor LeftPunchNext:        Boolean {}
actor DakkaFistConnected:   Boolean {}
actor DakkaFistSpeed:       Counter { Inventory.MaxAmount 15 }
actor DakkaFistTap_Main:    Counter {}
actor DakkaFistTap_Alt:     Counter {}
actor DakkaFistFinisher:    Boolean {}
actor DakkaFistInAlt:       Boolean {}
actor DakkaFistAttackOver:  Boolean {}


actor DWep_Fist: DoomWeapon
{
    Tag "Fists"

    Weapon.SlotNumber 1
    Weapon.SelectionOrder 3700
    +WEAPON.MELEEWEAPON
    +UNDROPPABLE
    +NOALERT

    Inventory.PickupMessage "$DAKKA_PK_FISTS"
    Obituary "$DAKKA_MP_FISTS"

    States
    {
      Select:
        DFIT A 0 A_TakeInventory("DakkaFistSpeed")
        goto Select2
        
      Select2:
        DFIT AA 0 A_Raise
        DFIT A 1 A_Raise
        loop

      Deselect:
        "####" A 0 A_JumpIfInventory("DakkaFistAttackOver", 1, "InstantDeselect")
        "####" A 0 A_JumpIfInventory("DakkaFistInAlt", 1, "AltDeselect")
        DFIT AAA 0 A_Lower
        DFIT A 1 A_Lower
        loop
      
      AltDeselect:
        "####" K 0 A_TakeInventory("DakkaFistInAlt")
        "####" K 0 A_ZoomFactor(1.0, ZOOM_NOSCALETURNING)
        "####" KIG 1
        TNT1 A 2
        goto InstantDeselect
      
      InstantDeselect:
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 0 A_Lower
        wait

      Ready:
        DFIT A 0 A_TakeInventory("DakkaFistAttackOver")
        goto Ready2
      
      Ready2:
        DFIT A 0 A_TakeInventory("DakkaFistSpeed", 1)
        DFIT A 1 A_WeaponReady
        loop
        
        
      Fire:
        TNT1 A 0 A_TakeInventory("DakkaFistConnected")
        TNT1 A 0 A_TakeInventory("DakkaFistAttackOver")
        TNT1 A 0 A_JumpIfInventory("LeftPunchNext", 1, "FireLeft")
        RFIT Z 0 A_GiveInventory("LeftPunchNext")
        goto FireCommon
      
      FireLeft:
        LFIT Z 0 A_TakeInventory("LeftPunchNext")
        goto FireCommon
      
      FireCommon:
        "####" Z 0 A_JumpIfInventory("DakkaFistSpeed", 6, "Fire2")
        "####" Z 0 A_JumpIfInventory("DakkaFistSpeed", 4, "Fire3")
        "####" Z 0 A_JumpIfInventory("DakkaFistSpeed", 2, "Fire4")
        "####" Z 0 A_JumpIfInventory("DakkaFistSpeed", 1, "Fire5")
        goto Fire6
      
      
      Hold:
        TNT1 A 0 A_TakeInventory("DakkaFistSpeed", 1)
        TNT1 A 0 A_TakeInventory("DakkaFistConnected")
        TNT1 A 0 A_TakeInventory("DakkaFistAttackOver")
        TNT1 A 0 A_JumpIfInventory("LeftPunchNext", 1, "HoldLeft")
        RFIT Z 0 A_GiveInventory("LeftPunchNext")
        goto HoldCommon
      
      HoldLeft:
        LFIT Z 0 A_TakeInventory("LeftPunchNext")
        goto HoldCommon
      
      HoldCommon:
        "####" Z 0 A_JumpIfInventory("DakkaFistSpeed", 6, "Hold2")
        "####" Z 0 A_JumpIfInventory("DakkaFistSpeed", 4, "Hold3")
        "####" Z 0 A_JumpIfInventory("DakkaFistSpeed", 2, "Hold4")
        "####" Z 0 A_JumpIfInventory("DakkaFistSpeed", 1, "Hold5")
        goto Hold6
      
      
      
      Fire6: Hold6:
        "####" B 1
        "####" E 1 A_FireBullets(0, 0, 1, 8, "FistPunchPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80)
        "####" D 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" Z 0 A_JumpIfInventory("DakkaFistTap_Alt", 1, "AltFire_FromPrimary")
        "####" Z 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" Z 0 A_JumpIfInventory("DakkaFistTap_Alt", 1, "AltFire_FromPrimary")
        "####" Z 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
      
      
      Fire5: Hold5:
        "####" B 1
        "####" E 1 A_FireBullets(0, 0, 1, 8, "FistPunchPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80)
        "####" D 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" Z 0 A_JumpIfInventory("DakkaFistTap_Alt", 1, "AltFire_FromPrimary")
        "####" Z 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
      
      
      Fire4: Hold4:
        "####" B 1
        "####" E 1 A_FireBullets(0, 0, 1, 9, "FistPunchPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80)
        "####" C 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" Z 0 A_JumpIfInventory("DakkaFistTap_Alt", 1, "AltFire_FromPrimary")
        "####" Z 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
      
      
      Fire3: Hold3:
        "####" E 1 A_FireBullets(0, 0, 1, 9, "FistPunchPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80)
        "####" B 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" Z 0 A_JumpIfInventory("DakkaFistTap_Alt", 1, "AltFire_FromPrimary")
        "####" Z 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
      
      
      Fire2: Hold2:
        "####" E 1 A_FireBullets(0, 0, 1, 10, "FistPunchPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80)
        "####" Z 0 A_JumpIfInventory("DakkaFistTap_Alt", 1, "AltFire_FromPrimary")
        "####" B 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
      
      
      
      
      AltFire:
        TNT1 A 0 A_ClearRefire
        TNT1 A 0 A_GiveInventory("DakkaFistInAlt")
        TNT1 A 0 A_TakeInventory("DakkaFistAttackOver")
        TNT1 A 0 A_PlaySound("dakka/fistcharge1", 5)
        
        DFIT A 0 offset(0, 40) A_ZoomFactor(0.97, ZOOM_NOSCALETURNING)
        DFIT A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        DFIT A 0 offset(0, 56) A_ZoomFactor(0.96, ZOOM_NOSCALETURNING)
        DFIT A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        TNT1 A 0 offset(0, 32) A_ZoomFactor(0.95, ZOOM_NOSCALETURNING)
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        goto Altfire_ChooseSide

      AltHold:
      AltFire_FromPrimary:
        TNT1 A 0 A_ClearRefire
        TNT1 A 0 A_GiveInventory("DakkaFistInAlt")
        TNT1 A 0 A_TakeInventory("DakkaFistAttackOver")
        TNT1 A 0 A_PlaySound("dakka/fistcharge1", 5)
        
        TNT1 A 0 A_ZoomFactor(0.97, ZOOM_NOSCALETURNING)
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        TNT1 A 0 A_ZoomFactor(0.96, ZOOM_NOSCALETURNING)
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        TNT1 A 0 A_ZoomFactor(0.95, ZOOM_NOSCALETURNING)
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        goto Altfire_ChooseSide
      
      AltHold_Quick3:
        "----" A 0 A_ClearRefire
        "----" A 0 A_GiveInventory("DakkaFistInAlt")
        "----" A 0 A_TakeInventory("DakkaFistAttackOver")
        "----" A 0 A_PlaySound("dakka/fistcharge1", 5)
        
        "####" I 0 A_ZoomFactor(0.97, ZOOM_NOSCALETURNING)
        "####" I 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        "####" H 0 A_ZoomFactor(0.96, ZOOM_NOSCALETURNING)
        "####" H 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        "####" G 0 A_ZoomFactor(0.95, ZOOM_NOSCALETURNING)
        "####" G 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        goto Altfire_ChooseSide
        
      AltHold_Quick2:
        "----" A 0 A_ClearRefire
        "----" A 0 A_GiveInventory("DakkaFistInAlt")
        "----" A 0 A_TakeInventory("DakkaFistAttackOver")
        "----" A 0 A_PlaySound("dakka/fistcharge1", 5)
        
        "####" H 0 A_ZoomFactor(0.97, ZOOM_NOSCALETURNING)
        "####" H 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        "####" G 0 A_ZoomFactor(0.96, ZOOM_NOSCALETURNING)
        "####" G 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        "####" Z 0 A_ZoomFactor(0.95, ZOOM_NOSCALETURNING)
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        goto Altfire_ChooseSide
        
      AltHold_Quick1:
        "----" A 0 A_ClearRefire
        "----" A 0 A_GiveInventory("DakkaFistInAlt")
        "----" A 0 A_TakeInventory("DakkaFistAttackOver")
        "----" A 0 A_PlaySound("dakka/fistcharge1", 5)
        
        "####" G 0 A_ZoomFactor(0.97, ZOOM_NOSCALETURNING)
        "####" G 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        "####" Z 0 A_ZoomFactor(0.96, ZOOM_NOSCALETURNING)
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        "####" Z 0 A_ZoomFactor(0.95, ZOOM_NOSCALETURNING)
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        goto Altfire_ChooseSide
      
      
      AltFire_ChooseSide:
        RFIT Z 0 A_JumpIfInventory("LeftPunchNext", 1, "AltFire_Left")
        RFIT Z 0 A_GiveInventory("LeftPunchNext")
        goto AltFire_Weak
      
      AltFire_Left:
        LFIT Z 0 A_TakeInventory("LeftPunchNext")
        goto AltFire_Weak
      
      
      AltFire_Weak:
        "####" ZZZZZ 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        "####" Z 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Medium")
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        "####" Z 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Medium")
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        "####" Z 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Medium")
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        "####" Z 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Medium")
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        "####" Z 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Medium")
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        "####" Z 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Medium")
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        "####" H 0 A_FireBullets(0, 0, 1, 0, "FistHeavyTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        "####" H 1 A_ZoomFactor(1.05, ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        "####" J 0 A_FireBullets(0, 0, 1, 0, "FistHeavyTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        "####" J 1 A_ZoomFactor(1.22, ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        
        "####" L 0 A_TakeInventory("DakkaFistConnected")
        "####" L 0 A_FireBullets(0, 0, 1, 100, "FistPowerPunch1", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        "####" L 0 A_PlaySound("dakka/explode", CHAN_WEAPON)
        "####" L 0 A_ZoomFactor(1.35,  ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        
        "####" LLLLLLLLLL 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" L 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" L 1  A_ZoomFactor(1.3,  ZOOM_NOSCALETURNING)
        "####" L 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" L 1  A_ZoomFactor(1.25, ZOOM_NOSCALETURNING)
        
        "####" K 1  A_ZoomFactor(1.2,  ZOOM_NOSCALETURNING)
        "####" K 1  A_ZoomFactor(1.15, ZOOM_NOSCALETURNING)
        "####" J 1  A_ZoomFactor(1.1,  ZOOM_NOSCALETURNING)
        "####" I 0  A_Refire("AltHold_Quick3")
        "####" I 1  A_ZoomFactor(1.05, ZOOM_NOSCALETURNING)
        "####" H 0  A_Refire("AltHold_Quick2")
        "####" H 1  A_ZoomFactor(1,    ZOOM_NOSCALETURNING)
        "####" G 1  A_Refire("AltHold_Quick1")
        
        "####" Z 1 A_Refire
        goto AttackEnd
      
      
      AltFire_Medium:
        "####" Z 0 A_PlaySound("dakka/fistcharge2", 6)
        "####" Z 0 A_ZoomFactor(0.92, ZOOM_NOSCALETURNING)
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        "####" Z 0 A_ZoomFactor(0.91, ZOOM_NOSCALETURNING)
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        "####" Z 0 A_ZoomFactor(0.9, ZOOM_NOSCALETURNING)
        "####" ZZZZZZ 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        "####" Z 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Strong")
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        "####" Z 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Strong")
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        "####" Z 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Strong")
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        "####" Z 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Strong")
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        "####" Z 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Strong")
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        "####" Z 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Strong")
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        "####" H 0 A_FireBullets(0, 0, 1, 0, "FistHeavyTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        "####" H 1 A_ZoomFactor(1.02, ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        "####" J 0 A_FireBullets(0, 0, 1, 0, "FistHeavyTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        "####" J 1 A_ZoomFactor(1.21, ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        
        "####" L 0 A_TakeInventory("DakkaFistConnected")
        "####" L 0 A_FireBullets(0, 0, 1, 150, "FistPowerPunch2", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        "####" L 0 A_PlaySound("dakka/explode", CHAN_WEAPON)
        "####" L 0 A_ZoomFactor(1.35,  ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        
        "####" LLLLLLLLLLLLLL 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" L 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" L 1  A_ZoomFactor(1.3,  ZOOM_NOSCALETURNING)
        "####" L 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" L 1  A_ZoomFactor(1.25, ZOOM_NOSCALETURNING)
        "####" L 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" L 1  A_ZoomFactor(1.2,  ZOOM_NOSCALETURNING)
        "####" L 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" L 1  A_ZoomFactor(1.15, ZOOM_NOSCALETURNING)
        
        "####" K 1  A_ZoomFactor(1.1,  ZOOM_NOSCALETURNING)
        "####" K 1  A_ZoomFactor(1.05, ZOOM_NOSCALETURNING)
        "####" J 1  A_ZoomFactor(1,    ZOOM_NOSCALETURNING)
        "####" J 1
        "####" I 1  A_Refire("AltHold_Quick3")
        "####" H 1  A_Refire("AltHold_Quick2")
        "####" G 1  A_Refire("AltHold_Quick1")
        "####" Z 1 A_Refire
        goto AttackEnd
      
      
      AltFire_Strong:
        "####" Z 0 A_PlaySound("dakka/fistcharge3", 5)
        "####" Z 0 A_ZoomFactor(0.87, ZOOM_NOSCALETURNING)
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        "####" Z 0 A_ZoomFactor(0.86, ZOOM_NOSCALETURNING)
        "####" Z 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        "####" Z 0 A_ZoomFactor(0.85, ZOOM_NOSCALETURNING)
        "####" ZZZZZZZZZZZZZ 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        "####" Z 0 A_FireBullets(0, 0, 1, 0, "FistHeavyTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        "####" Z 1 A_ZoomFactor(1.0,  ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        
        
        "####" I 0 A_ZoomFactor(1.2,  ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        "####" I 1 A_FireBullets(0, 0, 1, 0, "FistHeavyTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        "####" L 0 A_ZoomFactor(1.35, ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        "####" L 0 A_TakeInventory("DakkaFistConnected")
        "####" L 0 A_PlaySound("dakka/explode", CHAN_WEAPON)
        "####" L 1 A_FireBullets(0, 0, 1, 30, "FistPowerPunch1", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        "####" J 0 A_ZoomFactor(1.325, ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        "####" J 1 A_FireBullets(0, 0, 1, 0, "FistHeavyTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        "####" G 0 A_ZoomFactor(1.3,   ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        "####" G 1 A_FireBullets(0, 0, 1, 0, "FistHeavyTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        
        "####" N 1 A_FireBullets(0, 0, 1, 0, "FistHeavyTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        "####" P 0 A_ZoomFactor(1.35, ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        "####" P 0 A_TakeInventory("DakkaFistConnected")
        "####" P 0 A_PlaySound("dakka/explode", CHAN_WEAPON)
        "####" P 1 A_FireBullets(0, 0, 1, 30, "FistPowerPunch1", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        "####" O 0 A_ZoomFactor(1.325, ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        "####" O 1 A_FireBullets(0, 0, 1, 0, "FistHeavyTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        "####" M 0 A_ZoomFactor(1.3,   ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        "####" M 1 A_FireBullets(0, 0, 1, 0, "FistHeavyTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        
        "####" H 1 A_FireBullets(0, 0, 1, 0, "FistHeavyTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        "####" J 1 A_FireBullets(0, 0, 1, 0, "FistHeavyTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        "####" L 0 A_TakeInventory("DakkaFistConnected")
        "####" L 0 A_FireBullets(0, 0, 1, 140, "FistPowerPunch2", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        "####" L 0 A_PlaySound("dakka/explode", CHAN_WEAPON)
        "####" L 0 A_ZoomFactor(1.35,  ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        
        "####" LLLLLLLLLLLLLL 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" L 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" L 1  A_ZoomFactor(1.3,  ZOOM_NOSCALETURNING)
        "####" L 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" L 1  A_ZoomFactor(1.25, ZOOM_NOSCALETURNING)
        "####" L 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" L 1  A_ZoomFactor(1.2,  ZOOM_NOSCALETURNING)
        "####" L 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        "####" L 1  A_ZoomFactor(1.15, ZOOM_NOSCALETURNING)
        
        "####" K 1  A_ZoomFactor(1.1,  ZOOM_NOSCALETURNING)
        "####" K 1  A_ZoomFactor(1.05, ZOOM_NOSCALETURNING)
        "####" J 1  A_ZoomFactor(1,    ZOOM_NOSCALETURNING)
        "####" J 1
        "####" I 1  A_Refire("AltHold_Quick3")
        "####" H 1  A_Refire("AltHold_Quick2")
        "####" G 1  A_Refire("AltHold_Quick1")
        "####" Z 1 A_Refire
        "####" Z 1 A_Refire
        goto AttackEnd
      
      
    


      AttackEnd:
        TNT1 A 0 A_TakeInventory("DakkaFistInAlt")
        TNT1 A 0 A_GiveInventory("DakkaFistAttackOver")
        DIFT A 0               A_Refire
        DFIT A 1 offset(0, 40) A_WeaponReady(WRF_NOBOB)
        DIFT A 0               A_Refire
        DFIT A 1 offset(0, 35) A_WeaponReady(WRF_NOBOB)
        DIFT A 0               A_Refire
        DFIT A 1 offset(0, 32) A_WeaponReady(WRF_NOBOB)
        goto Ready
    }
}

actor FistPunchPuff: BulletPuff
{
    +HITTRACER
    +PUFFGETSOWNER
    +PUFFONACTORS
    +NOEXTREMEDEATH
    +NODAMAGETHRUST

    SeeSound    "dakka/silent"
    AttackSound "dakka/silent"

    States
    {
      Spawn:
      Melee:
      XDeath:
        TNT1 A 0
        TNT1 A 0 A_PlaySound("dakka/punch", CHAN_WEAPON)
        TNT1 A 0 A_GiveToTarget("DakkaFistConnected")
        TNT1 A 0 A_Quake(1, 3, 0, 96, "")
        TNT1 A 8 A_JumpIf(CallACS("Dakka_FistHit"), "MeatHit")
        stop
      
      MeatHit:
        TNT1 A 0 A_GiveToTarget("DakkaFistSpeed", 2)
        TNT1 A 8
        stop

      Crash:
        TNT1 A 0 A_PlaySound("dakka/punchwall", CHAN_WEAPON)
        TNT1 A 0 A_GiveToTarget("DakkaFistConnected")
        TNT1 A 0 A_JumpIf(CallACS("Dakka_FistHit"), "SparkHit")
        BPUF CD 4
        stop
      
      SparkHit:
        TNT1 A 0 A_GiveToTarget("DakkaFistSpeed", 2)
        TNT1 A 0 A_Quake(1, 3, 0, 96, "")
        BPUF CD 4
        stop
    }
}



actor FistTrackingPuff: BulletPuff
{
    +HITTRACER
    +PUFFGETSOWNER
    +PUFFONACTORS
    +NOEXTREMEDEATH
    +NODAMAGETHRUST
    +BLOODLESSIMPACT

    SeeSound    "dakka/silent"
    AttackSound "dakka/silent"

    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 8 ACS_NamedExecuteWithResult("Dakka_FistHit", 0, 1, 1)
        stop
    }
}


actor FistHeavyTrackingPuff: BulletPuff
{
    +HITTRACER
    +PUFFGETSOWNER
    +PUFFONACTORS
    +NOEXTREMEDEATH
    +NODAMAGETHRUST
    +BLOODLESSIMPACT

    SeeSound    "dakka/silent"
    AttackSound "dakka/silent"

    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 8 ACS_NamedExecuteWithResult("Dakka_FistHit", 0, 8, 1)
        stop
    }
}



actor FistPowerPunch1: BulletPuff
{
    +HITTRACER
    +PUFFGETSOWNER
    +PUFFONACTORS
    +EXTREMEDEATH
    +NODAMAGETHRUST
    +FORCEPAIN

    SeeSound    "dakka/silent"
    AttackSound "dakka/silent"
    
    Damage 100
    DamageType "FistLightStun"

    States
    {
      Spawn:
      Melee:
      XDeath:
        TNT1 A 0
        TNT1 A 0 A_PlaySound("dakka/punch", CHAN_WEAPON)
        TNT1 A 0 A_GiveToTarget("DakkaFistConnected")
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_FistHit", damage)
        TNT1 A 0 A_JumpIf(1, "MeatEnd") // inheritance
        goto MeatEnd
      
      MeatEnd:
        TNT1 A 0 A_Quake(4, 4, 0, 96, "")
        TNT1 A 0 A_Quake(2, 8, 0, 128, "")
        TNT1 A 0 A_Quake(1, 12, 0, 160, "")
        TNT1 A 8
        stop

      Crash:
        TNT1 A 0 A_PlaySound("dakka/punchwall", CHAN_WEAPON)
        TNT1 A 0 A_GiveToTarget("DakkaFistConnected")
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_FistHit", damage)
        TNT1 A 0 A_JumpIf(1, "SparkEnd") // inheritance
        goto SparkEnd
      
      SparkEnd:
        TNT1 A 0 A_Quake(4, 4, 0, 96, "")
        TNT1 A 0 A_Quake(2, 8, 0, 128, "")
        TNT1 A 0 A_Quake(1, 12, 0, 160, "")
        BPUF CD 4
        stop
    }
}

actor FistPowerPunch2: FistPowerPunch1
{ 
    Damage 250
    DamageType "FistMediumStun"
    
    States
    {
      
      MeatEnd:
        TNT1 A 0 A_Quake(5, 6,  0, 96, "")
        TNT1 A 0 A_Quake(3, 10, 0, 128, "")
        TNT1 A 0 A_Quake(1, 15, 0, 160, "")
        TNT1 A 8
        stop
      
      SparkEnd:
        TNT1 A 0 A_Quake(5, 6,  0, 96, "")
        TNT1 A 0 A_Quake(3, 10, 0, 128, "")
        TNT1 A 0 A_Quake(1, 15, 0, 160, "")
        BPUF CD 4
        stop
    }
}

actor FistPowerPunch3: FistPowerPunch1
{ 
    Damage 400
    DamageType "FistHeavyStun"
    
    States
    {
      
      MeatEnd:
        TNT1 A 0 A_Quake(6, 6,  0, 96, "")
        TNT1 A 0 A_Quake(4, 12, 0, 128, "")
        TNT1 A 0 A_Quake(2, 19, 0, 160, "")
        TNT1 A 8
        stop
      
      SparkEnd:
        TNT1 A 0 A_Quake(6, 6,  0, 96, "")
        TNT1 A 0 A_Quake(4, 12, 0, 128, "")
        TNT1 A 0 A_Quake(2, 19, 0, 160, "")
        BPUF CD 4
        stop
    }
}


actor DakkaFistQuake2: Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_Quake(4, 4, 0, 96, "")
        TNT1 A 0 A_Quake(2, 8, 0, 128, "")
        TNT1 A 0 A_Quake(1, 12, 0, 160, "")
        stop
    }
}