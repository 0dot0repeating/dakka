actor LeftPunchNext:        Boolean {}
actor DakkaFistConnected:   Boolean {}
actor DakkaFistSpeed:       Counter { Inventory.MaxAmount 30 }
actor DakkaFistTap_Main:    Counter {}
actor DakkaFistTap_Alt:     Counter {}
actor DakkaFistFinisher:    Boolean {}


actor DWep_Fist: DoomWeapon
{
    Tag "Fists"

    Weapon.SlotNumber 1
    Weapon.SelectionOrder 3700
    +WEAPON.MELEEWEAPON
    +UNDROPPABLE
    +NOALERT

    Inventory.PickupMessage "$DAKKA_PK_FISTS"
    Obituary "$DAKKA_MP_FISTS"

    States
    {
      Select:
        DFIT AA 0 A_Raise
        DFIT A 1 A_Raise
        loop

      Deselect:
        DFIT AAA 0 A_Lower
        DFIT A 1 A_Lower
        loop

      Ready:
        DFIT A 1 A_WeaponReady
        loop
        
        
      Fire:
        TNT1 A 0 A_TakeInventory("DakkaFistSpeed")
        TNT1 A 0 A_TakeInventory("DakkaFistConnected")
        TNT1 A 0 A_JumpIfInventory("LeftPunchNext", 1, "FireLeft")
        TNT1 A 0 A_GiveInventory("LeftPunchNext")
        goto Right6
      
      FireLeft:
        TNT1 A 0 A_TakeInventory("LeftPunchNext")
        goto Left6
      
      
      Hold:
        TNT1 A 0 A_TakeInventory("DakkaFistSpeed", 1)
        TNT1 A 0 A_TakeInventory("DakkaFistConnected")
        TNT1 A 0 A_JumpIfInventory("LeftPunchNext", 1, "HoldLeft")
        TNT1 A 0 A_GiveInventory("LeftPunchNext")
        TNT1 A 0 A_JumpIfInventory("DakkaFistSpeed", 6, "RightHold2")
        TNT1 A 0 A_JumpIfInventory("DakkaFistSpeed", 4, "RightHold3")
        TNT1 A 0 A_JumpIfInventory("DakkaFistSpeed", 2, "RightHold4")
        TNT1 A 0 A_JumpIfInventory("DakkaFistSpeed", 1, "RightHold5")
        goto RightHold6
      
      HoldLeft:
        TNT1 A 0 A_TakeInventory("LeftPunchNext")
        TNT1 A 0 A_JumpIfInventory("DakkaFistSpeed", 6, "LeftHold2")
        TNT1 A 0 A_JumpIfInventory("DakkaFistSpeed", 4, "LeftHold3")
        TNT1 A 0 A_JumpIfInventory("DakkaFistSpeed", 2, "LeftHold4")
        TNT1 A 0 A_JumpIfInventory("DakkaFistSpeed", 1, "LeftHold5")
        goto LeftHold6
      
      
      
      Right6:
      RightHold6:
        DFIT C 1
        DFIT F 1 A_FireBullets(0, 0, 1, 0, "FistPunchPuff", FBF_NORANDOMPUFFZ, 80)
        DFIT F 0 A_JumpIfInventory("DakkaFistConnected", 1, "RightHit6")
        DFIT E 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DFIT B 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        TNT1 A 2 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
        
      
      RightHit6:
        DFIT F 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DFIT D 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DFIT B 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        TNT1 A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
        
      Left6:
      LeftHold6:
        DFIT H 1
        DFIT K 1 A_FireBullets(0, 0, 1, 0, "FistPunchPuff", FBF_NORANDOMPUFFZ, 80)
        DFIT K 0 A_JumpIfInventory("DakkaFistConnected", 1, "LeftHit6")
        DFIT J 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DFIT G 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        TNT1 A 2 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
        
      
      LeftHit6:
        DFIT K 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DFIT I 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DFIT G 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        TNT1 A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
      
      
      Right5:
      RightHold5:
        DFIT C 1
        DFIT F 1 A_FireBullets(0, 0, 1, 0, "FistPunchPuff", FBF_NORANDOMPUFFZ, 80)
        DFIT F 0 A_JumpIfInventory("DakkaFistConnected", 1, "RightHit5")
        DFIT E 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DFIT B 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        TNT1 A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
        
      
      RightHit5:
        DFIT F 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DFIT D 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DFIT B 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        TNT1 A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
        
      Left5:
      LeftHold5:
        DFIT H 1
        DFIT K 1 A_FireBullets(0, 0, 1, 0, "FistPunchPuff", FBF_NORANDOMPUFFZ, 80)
        DFIT K 0 A_JumpIfInventory("DakkaFistConnected", 1, "LeftHit5")
        DFIT J 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DFIT G 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        TNT1 A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
        
      
      LeftHit5:
        DFIT K 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DFIT I 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DFIT G 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
      
      
      Right4:
      RightHold4:
        DFIT C 1
        DFIT F 1 A_FireBullets(0, 0, 1, 0, "FistPunchPuff", FBF_NORANDOMPUFFZ, 80)
        DFIT D 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        TNT1 A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
        
      Left4:
      LeftHold4:
        DFIT H 1
        DFIT K 1 A_FireBullets(0, 0, 1, 0, "FistPunchPuff", FBF_NORANDOMPUFFZ, 80)
        DFIT I 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        TNT1 A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
      
      
      Right3:
      RightHold3:
        DFIT F 1 A_FireBullets(0, 0, 1, 0, "FistPunchPuff", FBF_NORANDOMPUFFZ, 80)
        DFIT C 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        TNT1 A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
        
      Left3:
      LeftHold3:
        DFIT K 1 A_FireBullets(0, 0, 1, 0, "FistPunchPuff", FBF_NORANDOMPUFFZ, 80)
        DFIT H 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        TNT1 A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
      
      
      Right2:
      RightHold2:
        DFIT F 1 A_FireBullets(0, 0, 1, 0, "FistPunchPuff", FBF_NORANDOMPUFFZ, 80)
        DFIT C 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
        
      Left2:
      LeftHold2:
        DFIT K 1 A_FireBullets(0, 0, 1, 0, "FistPunchPuff", FBF_NORANDOMPUFFZ, 80)
        DFIT H 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
    


      AttackEnd:
        DFIT A 0 offset(0, 40) A_Refire
        DFIT A 1               A_WeaponReady(WRF_NOBOB)
        DFIT A 0 offset(0, 35) A_Refire
        DFIT A 1               A_WeaponReady(WRF_NOBOB)
        DFIT A 0 offset(0, 32) A_Refire
        DFIT A 1               A_WeaponReady(WRF_NOBOB)
        goto Ready
    }
}

actor FistPunchPuff: BulletPuff
{
    +HITTRACER
    +PUFFGETSOWNER
    +PUFFONACTORS
    +NOEXTREMEDEATH

    SeeSound    "dakka/silent"
    AttackSound "dakka/punchwall"

    States
    {
      Spawn:
      Melee:
      XDeath:
        TNT1 A 0
        TNT1 A 0 A_GiveToTarget("DakkaFistConnected")
        TNT1 A 8 ACS_NamedExecuteWithResult("Dakka_FistHit")
        stop

      Crash:
        TNT1 A 0 A_GiveToTarget("DakkaFistConnected")
        BPUF CD 4
        stop
    }
}

actor DakkaHammerPuff: BulletPuff
{
    +HITTRACER
    +PUFFGETSOWNER
    +PUFFONACTORS
    +NOEXTREMEDEATH

    SeeSound    "dakka/silent"
    AttackSound "dakka/punchwall"

    States
    {
      Spawn:
      Melee:
      XDeath:
        TNT1 A 8
        stop

      Crash:
        BPUF CD 4
        stop
    }
}


actor DakkaFistQuake1: Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_Quake(1, 3, 0, 96, "")
        stop
    }
}

actor DakkaFistQuake2: Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_Quake(4, 4, 0, 96, "")
        TNT1 A 0 A_Quake(2, 8, 0, 128, "")
        TNT1 A 0 A_Quake(1, 12, 0, 160, "")
        stop
    }
}


actor DakkaBlockResistance: PowerProtection
{
    DamageFactor "Normal", 0.25
    Powerup.Duration 0x7FFFFFFF
    +INTERHUBSTRIP
}

actor DakkaBlockInvuln: PowerInvulnerable
{
    Powerup.Duration 0x7FFFFFFF
}

actor DakkaBlockReflection: Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_ChangeFlag("REFLECTIVE",    true)
        TNT1 A 0 A_ChangeFlag("SHIELDREFLECT", true)
        stop
    }
}

actor DakkaUnblockReflection: Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_ChangeFlag("REFLECTIVE",    false)
        TNT1 A 0 A_ChangeFlag("SHIELDREFLECT", false)
        stop
    }
}