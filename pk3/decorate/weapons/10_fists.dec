actor DWep_Fist: DoomWeapon
{
    Tag "Fists"

    Weapon.SelectionOrder 3700
    +WEAPON.MELEEWEAPON
    +UNDROPPABLE
    +NOALERT

    Inventory.PickupMessage "$DAKKA_PK_FISTS"
    Obituary "$DAKKA_MP_FISTS"

    States
    {
      Select:
        RPUN A 0 A_TakeInventory("PunchIndicator")
        RPUN AA 0 A_Raise
        RPUN A 1 A_Raise
        loop

      Ready:
        RPUN A 0 A_JumpIfInventory("PunchIndicator", 1, "LeftReady")
        goto RightReady

      LeftReady:
        LPUN A 1 A_WeaponReady
        loop

      RightReady:
        RPUN A 1 A_WeaponReady
        loop

      Deselect:
        RPUN A 1 A_JumpIfInventory("PunchIndicator", 1, "LeftDeselect")
        goto RightDeselect

      LeftDeselect:
        LPUN AA 0 A_Lower
        LPUN A 1 A_Lower
        loop

      RightDeselect:
        RPUN AA 0 A_Lower
        RPUN A 1 A_Lower
        loop

      Fire:
        "----" A 0 ACS_ExecuteWithResult(DAKKA_UPDATE_SWITCHAROO, 0, 0)
        LPUN A 0 offset(0, 32) A_JumpIfInventory("PunchIndicator",  1, "RightJab")
        goto LeftJab

      LeftJab:
        LPUN A 0 A_GiveInventory("PunchIndicator")
        LPUN B 1
        LPUN C 0 A_JumpIfInventory("PowerStrength", 1, "ZerkLeftJab")
        LPUN D 0 A_CustomPunch(30, 1, 0, "FistPunchPuff", 80)
        goto LeftJab2

      ZerkLeftJab:
        LPUN D 0 A_CustomPunch(120, 1, 0, "FistPunchPuff", 80)
        goto LeftJab2

      LeftJab2:
        LPUN C 2
        LPUN D 3
        LPUN EFG 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        LPUN B 0 A_ReFire
        goto Ready

      RightJab:
        RPUN A 0 A_TakeInventory("PunchIndicator")
        RPUN B 1
        RPUN C 0 A_JumpIfInventory("PowerStrength", 1, "ZerkRightJab")
        RPUN D 0 A_CustomPunch(30, 1, 0, "FistPunchPuff", 80)
        goto RightJab2

      ZerkRightJab:
        RPUN D 0 A_CustomPunch(120, 1, 0, "FistPunchPuff", 80)
        goto RightJab2

      RightJab2:
        RPUN C 2
        RPUN D 3
        RPUN EFG 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        RPUN B 0 A_ReFire
        goto Ready

      AltFire:
        "----" A 0 ACS_ExecuteWithResult(DAKKA_UPDATE_SWITCHAROO, 0, 1)
        LPUN A 0 A_JumpIfInventory("PunchIndicator",  1, "RightJabAlt")
        goto LeftJabAlt

      LeftJabAlt:
        LPUN A 0 A_GiveInventory("PunchIndicator")
        LPUN B 1 offset(4, 64)
        LPUN D 0 A_JumpIfInventory("PowerStrength", 1, "ZerkLeftAlt")
        LPUN D 0 A_CustomPunch(100, 1, 0, "FistSlamPuff", 80)
        goto LeftAlt2

      ZerkLeftAlt:
        LPUN D 0 A_CustomPunch(400, 1, 0, "FistSlamPuff", 80)  // this will probably go badly!
        goto LeftAlt2

      LeftAlt2:
        LPUN C 3 offset(9, 38)
        LPUN D 1 offset(21, 32)
        LPUN D 1 offset(25, 35)
        LPUN D 1 offset(29, 39)
        LPUN D 1 offset(33, 44)
        LPUN E 1 offset(35, 49)
        LPUN E 2 offset(36, 52)
        LPUN F 2 offset(37, 55)
        LPUN G 2 offset(38, 64)
        LPUN G 2 offset(34, 69)
        LPUN G 1 offset(28, 75)
        LPUN G 1 offset(21, 84)
        LPUN A 0 A_Refire
        LPUN A 2 offset(8, 64) A_WeaponReady(WRF_NOBOB | WRF_NOSECONDARY)
        LPUN A 0 A_Refire
        LPUN A 2 offset(4, 54) A_WeaponReady(WRF_NOBOB | WRF_NOSECONDARY)
        LPUN A 0 A_Refire
        LPUN A 2 offset(2, 44) A_WeaponReady(WRF_NOBOB | WRF_NOSECONDARY)
        LPUN A 0 A_Refire
        LPUN A 2 offset(1, 34) A_WeaponReady(WRF_NOBOB | WRF_NOSECONDARY)
        goto Ready

      RightJabAlt:
        RPUN A 0 A_TakeInventory("PunchIndicator")
        RPUN B 1 offset(-4, 64)
        RPUN D 0 A_JumpIfInventory("PowerStrength", 1, "ZerkRightAlt")
        RPUN D 0 A_CustomPunch(100, 1, 0, "FistSlamPuff", 80)
        goto RightAlt2

      ZerkRightAlt:
        RPUN D 0 A_CustomPunch(400, 1, 0, "FistSlamPuff", 80)  // still will probably go badly!
        goto RightAlt2

      RightAlt2:
        RPUN C 3 offset(-9, 38)
        RPUN D 1 offset(-21, 32)
        RPUN D 1 offset(-25, 35)
        RPUN D 1 offset(-29, 39)
        RPUN D 1 offset(-33, 44)
        RPUN E 1 offset(-35, 49)
        RPUN E 2 offset(-36, 52)
        RPUN F 2 offset(-37, 55)
        RPUN G 2 offset(-38, 64)
        RPUN G 2 offset(-34, 69)
        RPUN G 1 offset(-28, 75)
        RPUN G 1 offset(-21, 84)
        LPUN A 0 A_Refire
        RPUN A 2 offset(-8, 64) A_WeaponReady(WRF_NOBOB | WRF_NOSECONDARY)
        LPUN A 0 A_Refire
        RPUN A 2 offset(-4, 54) A_WeaponReady(WRF_NOBOB | WRF_NOSECONDARY)
        LPUN A 0 A_Refire
        RPUN A 2 offset(-2, 44) A_WeaponReady(WRF_NOBOB | WRF_NOSECONDARY)
        LPUN A 0 A_Refire
        RPUN A 2 offset(-1, 34) A_WeaponReady(WRF_NOBOB | WRF_NOSECONDARY)
        goto Ready
    }
}

actor PunchIndicator: Boolean {}

actor FistPunchPuff: BulletPuff
{
    +PUFFGETSOWNER
    +PUFFONACTORS
    +NOEXTREMEDEATH

    SeeSound "dakka/punch"
    AttackSound "dakka/punchWall"

    States
    {
      Spawn:
        TNT1 A 0
        stop

      Melee:
        TNT1 A 0
        stop

      Crash:
        BPUF CD 4
        stop
    }
}

actor FistSlamPuff: FistPunchPuff
{
    SeeSound "dakka/slam"
    AttackSound "dakka/slamwall"

    Obituary "$DAKKA_MP_FISTS_SLAM"
}
