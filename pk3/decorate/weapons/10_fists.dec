actor LeftPunchNext:        Boolean {}
actor DakkaFistConnected:   Boolean {}
actor DakkaFistSpeed:       Counter { Inventory.MaxAmount 10 }
actor DakkaFistTap_Main:    Counter {}
actor DakkaFistTap_Alt:     Counter {}
actor DakkaFistFinisher:    Boolean {}
actor DakkaFistInAlt:       Boolean {}


actor DWep_Fist: DoomWeapon
{
    Tag "Fists"

    Weapon.SlotNumber 1
    Weapon.SelectionOrder 3700
    +WEAPON.MELEEWEAPON
    +UNDROPPABLE
    +NOALERT

    Inventory.PickupMessage "$DAKKA_PK_FISTS"
    Obituary "$DAKKA_MP_FISTS"

    States
    {
      Select:
        DFIT AA 0 A_Raise
        DFIT A 1 A_Raise
        loop

      Deselect:
        DFIT A 0 A_JumpIfInventory("DakkaFistInAlt", 1, "AltDeselect")
        DFIT AAA 0 A_Lower
        DFIT A 1 A_Lower
        loop
      
      AltDeselect:
        DFIT Q 0 A_TakeInventory("DakkaFistInAlt")
        DFIT Q 0 A_ZoomFactor(1.0, ZOOM_NOSCALETURNING)
        DFIT QOM 1
        TNT1 A 2
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 0 A_Lower
        wait

      Ready:
        DFIT A 1 A_WeaponReady
        loop
        
        
      Fire:
        TNT1 A 0 A_TakeInventory("DakkaFistSpeed")
        TNT1 A 0 A_TakeInventory("DakkaFistConnected")
        TNT1 A 0 A_JumpIfInventory("LeftPunchNext", 1, "FireLeft")
        TNT1 A 0 A_GiveInventory("LeftPunchNext")
        goto Right6
      
      FireLeft:
        TNT1 A 0 A_TakeInventory("LeftPunchNext")
        goto Left6
      
      
      Hold:
        TNT1 A 0 A_TakeInventory("DakkaFistSpeed", 1)
        TNT1 A 0 A_TakeInventory("DakkaFistConnected")
        TNT1 A 0 A_JumpIfInventory("LeftPunchNext", 1, "HoldLeft")
        TNT1 A 0 A_GiveInventory("LeftPunchNext")
        TNT1 A 0 A_JumpIfInventory("DakkaFistSpeed", 6, "RightHold2")
        TNT1 A 0 A_JumpIfInventory("DakkaFistSpeed", 4, "RightHold3")
        TNT1 A 0 A_JumpIfInventory("DakkaFistSpeed", 2, "RightHold4")
        TNT1 A 0 A_JumpIfInventory("DakkaFistSpeed", 1, "RightHold5")
        goto RightHold6
      
      HoldLeft:
        TNT1 A 0 A_TakeInventory("LeftPunchNext")
        TNT1 A 0 A_JumpIfInventory("DakkaFistSpeed", 6, "LeftHold2")
        TNT1 A 0 A_JumpIfInventory("DakkaFistSpeed", 4, "LeftHold3")
        TNT1 A 0 A_JumpIfInventory("DakkaFistSpeed", 2, "LeftHold4")
        TNT1 A 0 A_JumpIfInventory("DakkaFistSpeed", 1, "LeftHold5")
        goto LeftHold6
      
      
      
      Right6:
      RightHold6:
        DFIT C 1
        DFIT F 1 A_FireBullets(0, 0, 1, 8, "FistPunchPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80)
        DFIT E 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DFIT B 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        TNT1 A 0 A_JumpIfInventory("DakkaFistTap_Alt", 1, "AltFire_FromPrimary")
        TNT1 A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        TNT1 A 0 A_JumpIfInventory("DakkaFistTap_Alt", 1, "AltFire_FromPrimary")
        TNT1 A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
        
      Left6:
      LeftHold6:
        DFIT H 1
        DFIT K 1 A_FireBullets(0, 0, 1, 8, "FistPunchPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80)
        DFIT J 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DFIT G 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        TNT1 A 0 A_JumpIfInventory("DakkaFistTap_Alt", 1, "AltFire_FromPrimary")
        TNT1 A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        TNT1 A 0 A_JumpIfInventory("DakkaFistTap_Alt", 1, "AltFire_FromPrimary")
        TNT1 A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
      
      
      Right5:
      RightHold5:
        DFIT C 1
        DFIT F 1 A_FireBullets(0, 0, 1, 8, "FistPunchPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80)
        DFIT E 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DFIT B 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        TNT1 A 0 A_JumpIfInventory("DakkaFistTap_Alt", 1, "AltFire_FromPrimary")
        TNT1 A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
        
      Left5:
      LeftHold5:
        DFIT H 1
        DFIT K 1 A_FireBullets(0, 0, 1, 8, "FistPunchPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80)
        DFIT J 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DFIT G 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        TNT1 A 0 A_JumpIfInventory("DakkaFistTap_Alt", 1, "AltFire_FromPrimary")
        TNT1 A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
      
      
      Right4:
      RightHold4:
        DFIT C 1
        DFIT F 1 A_FireBullets(0, 0, 1, 9, "FistPunchPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80)
        DFIT D 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        TNT1 A 0 A_JumpIfInventory("DakkaFistTap_Alt", 1, "AltFire_FromPrimary")
        TNT1 A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
        
      Left4:
      LeftHold4:
        DFIT H 1
        DFIT K 1 A_FireBullets(0, 0, 1, 9, "FistPunchPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80)
        DFIT I 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        TNT1 A 0 A_JumpIfInventory("DakkaFistTap_Alt", 1, "AltFire_FromPrimary")
        TNT1 A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
      
      
      Right3:
      RightHold3:
        DFIT F 1 A_FireBullets(0, 0, 1, 9, "FistPunchPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80)
        DFIT C 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        TNT1 A 0 A_JumpIfInventory("DakkaFistTap_Alt", 1, "AltFire_FromPrimary")
        TNT1 A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
        
      Left3:
      LeftHold3:
        DFIT K 1 A_FireBullets(0, 0, 1, 9, "FistPunchPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80)
        DFIT H 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        TNT1 A 0 A_JumpIfInventory("DakkaFistTap_Alt", 1, "AltFire_FromPrimary")
        TNT1 A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
      
      
      Right2:
      RightHold2:
        DFIT F 1 A_FireBullets(0, 0, 1, 10, "FistPunchPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80)
        TNT1 A 0 A_JumpIfInventory("DakkaFistTap_Alt", 1, "AltFire_FromPrimary")
        DFIT C 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
        
      Left2:
      LeftHold2:
        DFIT K 1 A_FireBullets(0, 0, 1, 10, "FistPunchPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 80)
        TNT1 A 0 A_JumpIfInventory("DakkaFistTap_Alt", 1, "AltFire_FromPrimary")
        DFIT H 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        goto AttackEnd
      
      
      
      
      AltFire:
        TNT1 A 0 A_ClearRefire
        TNT1 A 0 A_GiveInventory("DakkaFistInAlt")
        TNT1 A 0 A_PlaySound("dakka/fistcharge1", 5)
        
        DFIT A 0 offset(0, 40) A_ZoomFactor(0.97, ZOOM_NOSCALETURNING)
        DFIT A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        DFIT A 0 offset(0, 56) A_ZoomFactor(0.96, ZOOM_NOSCALETURNING)
        DFIT A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        TNT1 A 0 offset(0, 32) A_ZoomFactor(0.95, ZOOM_NOSCALETURNING)
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        goto Altfire_Weak

      AltFire_FromPrimary:
        TNT1 A 0 A_ClearRefire
        TNT1 A 0 A_GiveInventory("DakkaFistInAlt")
        TNT1 A 0 A_PlaySound("dakka/fistcharge1", 5)
        
        TNT1 A 0 A_ZoomFactor(0.97, ZOOM_NOSCALETURNING)
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        TNT1 A 0 A_ZoomFactor(0.96, ZOOM_NOSCALETURNING)
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        TNT1 A 0 A_ZoomFactor(0.95, ZOOM_NOSCALETURNING)
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        goto AltFire_Weak
      
      
      AltFire_Weak:
        TNT1 AAAAA 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        TNT1 A 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Medium")
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        TNT1 A 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Medium")
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        TNT1 A 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Medium")
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        TNT1 A 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Medium")
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        TNT1 A 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Medium")
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        TNT1 A 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Medium")
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        DFIT N 1 A_ZoomFactor(1.05, ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        DFIT P 1 A_ZoomFactor(1.22, ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        
        DFIT R 0 A_TakeInventory("DakkaFistConnected")
        DFIT R 0 A_FireBullets(0, 0, 1, 120, "FistPowerPunch1", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        DFIT R 0 A_PlaySound("dakka/explode", CHAN_WEAPON)
        DFIT R 0 A_ZoomFactor(1.35,  ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        
        DFIT RRRRRRRRRR 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DFIT R 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DFIT R 1  A_ZoomFactor(1.3,  ZOOM_NOSCALETURNING)
        DFIT R 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DFIT R 1  A_ZoomFactor(1.25, ZOOM_NOSCALETURNING)
        
        DFIT Q 1  A_ZoomFactor(1.2,  ZOOM_NOSCALETURNING)
        DFIT Q 1  A_ZoomFactor(1.15, ZOOM_NOSCALETURNING)
        DFIT P 1  A_ZoomFactor(1.1,  ZOOM_NOSCALETURNING)
        DFIT O 1  A_ZoomFactor(1.05, ZOOM_NOSCALETURNING)
        DFIT N 1  A_ZoomFactor(1,    ZOOM_NOSCALETURNING)
        DFIT M 1
        TNT1 A 1
        goto AttackEnd
      
      
      AltFire_Medium:
        TNT1 A 0 A_PlaySound("dakka/fistcharge2", 6)
        TNT1 A 0 A_ZoomFactor(0.92, ZOOM_NOSCALETURNING)
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        TNT1 A 0 A_ZoomFactor(0.91, ZOOM_NOSCALETURNING)
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        TNT1 A 0 A_ZoomFactor(0.9, ZOOM_NOSCALETURNING)
        TNT1 AAAAAA 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        TNT1 A 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Strong")
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        TNT1 A 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Strong")
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        TNT1 A 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Strong")
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        TNT1 A 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Strong")
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        TNT1 A 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Strong")
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        TNT1 A 0 A_JumpIfInventory("AltClicked", 1, "AltFire_Strong")
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        DFIT N 1 A_ZoomFactor(1.02, ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        DFIT P 1 A_ZoomFactor(1.21, ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        
        DFIT R 0 A_TakeInventory("DakkaFistConnected")
        DFIT R 0 A_FireBullets(0, 0, 1, 160, "FistPowerPunch2", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        DFIT R 0 A_PlaySound("dakka/explode", CHAN_WEAPON)
        DFIT R 0 A_ZoomFactor(1.35,  ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        
        DFIT RRRRRRRRRRRRRR 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DFIT R 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DFIT R 1  A_ZoomFactor(1.3,  ZOOM_NOSCALETURNING)
        DFIT R 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DFIT R 1  A_ZoomFactor(1.25, ZOOM_NOSCALETURNING)
        DFIT R 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DFIT R 1  A_ZoomFactor(1.3,  ZOOM_NOSCALETURNING)
        DFIT R 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DFIT R 1  A_ZoomFactor(1.25, ZOOM_NOSCALETURNING)
        DFIT R 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DFIT R 1  A_ZoomFactor(1.2,  ZOOM_NOSCALETURNING)
        DFIT R 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DFIT R 1  A_ZoomFactor(1.15, ZOOM_NOSCALETURNING)
        
        DFIT Q 1  A_ZoomFactor(1.1,  ZOOM_NOSCALETURNING)
        DFIT Q 1  A_ZoomFactor(1.05, ZOOM_NOSCALETURNING)
        DFIT P 1  A_ZoomFactor(1,    ZOOM_NOSCALETURNING)
        DFIT PONM 1
        TNT1 A 1
        goto AttackEnd
      
      
      AltFire_Strong:
        TNT1 A 0 A_PlaySound("dakka/fistcharge3", 5)
        TNT1 A 0 A_ZoomFactor(0.87, ZOOM_NOSCALETURNING)
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        TNT1 A 0 A_ZoomFactor(0.86, ZOOM_NOSCALETURNING)
        TNT1 A 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        TNT1 A 0 A_ZoomFactor(0.85, ZOOM_NOSCALETURNING)
        TNT1 AAAAAAAAAAAA 1 A_FireBullets(0, 0, 1, 0, "FistTrackingPuff", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        
        DFIT N 1 A_ZoomFactor(1.00, ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        DFIT P 1 A_ZoomFactor(1.2,  ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        
        DFIT R 0 A_TakeInventory("DakkaFistConnected")
        DFIT R 0 A_FireBullets(0, 0, 1, 200, "FistPowerPunch3", FBF_NORANDOM | FBF_NORANDOMPUFFZ, 112)
        DFIT R 0 A_PlaySound("dakka/explode", CHAN_WEAPON)
        DFIT R 0 A_ZoomFactor(1.35,  ZOOM_NOSCALETURNING | ZOOM_INSTANT)
        
        DFIT RRRRRRRRRR 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DFIT R 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DFIT R 1
        DFIT R 0  A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DFIT R 1
        
        DFIT R 1  A_ZoomFactor(1.3,  ZOOM_NOSCALETURNING)
        DFIT R 1  A_ZoomFactor(1.25, ZOOM_NOSCALETURNING)
        DFIT Q 1  A_ZoomFactor(1.2,  ZOOM_NOSCALETURNING)
        DFIT Q 1  A_ZoomFactor(1.15, ZOOM_NOSCALETURNING)
        DFIT P 1  A_ZoomFactor(1.1,  ZOOM_NOSCALETURNING)
        DFIT O 1  A_ZoomFactor(1.05, ZOOM_NOSCALETURNING)
        DFIT N 1  A_ZoomFactor(1,    ZOOM_NOSCALETURNING)
        DFIT M 1
        TNT1 A 1
        goto AttackEnd
      
      
    


      AttackEnd:
        TNT1 A 0 A_TakeInventory("DakkaFistInAlt")
        DIFT A 0               A_Refire
        DFIT A 1 offset(0, 40) A_WeaponReady(WRF_NOBOB)
        DIFT A 0               A_Refire
        DFIT A 1 offset(0, 35) A_WeaponReady(WRF_NOBOB)
        DIFT A 0               A_Refire
        DFIT A 1 offset(0, 32) A_WeaponReady(WRF_NOBOB)
        goto Ready
    }
}

actor FistPunchPuff: BulletPuff
{
    +HITTRACER
    +PUFFGETSOWNER
    +PUFFONACTORS
    +NOEXTREMEDEATH
    +NODAMAGETHRUST

    SeeSound    "dakka/silent"
    AttackSound "dakka/silent"

    States
    {
      Spawn:
      Melee:
      XDeath:
        TNT1 A 0
        TNT1 A 0 A_PlaySound("dakka/punch", CHAN_WEAPON)
        TNT1 A 0 A_GiveToTarget("DakkaFistConnected")
        TNT1 A 0 A_Quake(1, 3, 0, 96, "")
        TNT1 A 8 A_JumpIf(CallACS("Dakka_FistHit"), "MeatHit")
        stop
      
      MeatHit:
        TNT1 A 0 A_GiveToTarget("DakkaFistSpeed", 2)
        TNT1 A 8
        stop

      Crash:
        TNT1 A 0 A_PlaySound("dakka/punchwall", CHAN_WEAPON)
        TNT1 A 0 A_GiveToTarget("DakkaFistConnected")
        TNT1 A 0 A_JumpIf(CallACS("Dakka_FistHit"), "SparkHit")
        BPUF CD 4
        stop
      
      SparkHit:
        TNT1 A 0 A_GiveToTarget("DakkaFistSpeed", 2)
        TNT1 A 0 A_Quake(1, 3, 0, 96, "")
        BPUF CD 4
        stop
    }
}

actor FistTrackingPuff: BulletPuff
{
    +HITTRACER
    +PUFFGETSOWNER
    +PUFFONACTORS
    +NOEXTREMEDEATH
    +NODAMAGETHRUST
    +BLOODLESSIMPACT

    SeeSound    "dakka/silent"
    AttackSound "dakka/silent"

    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 8 ACS_NamedExecuteWithResult("Dakka_FistHit", 0, 3, 1)
        stop
    }
}



actor FistPowerPunch1: BulletPuff
{
    +HITTRACER
    +PUFFGETSOWNER
    +PUFFONACTORS
    +EXTREMEDEATH
    +NODAMAGETHRUST
    +FORCEPAIN

    SeeSound    "dakka/silent"
    AttackSound "dakka/silent"
    
    Damage 100
    DamageType "FistLightStun"

    States
    {
      Spawn:
      Melee:
      XDeath:
        TNT1 A 0
        TNT1 A 0 A_PlaySound("dakka/punch", CHAN_WEAPON)
        TNT1 A 0 A_GiveToTarget("DakkaFistConnected")
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_FistHit", damage)
        TNT1 A 0 A_JumpIf(1, "MeatEnd") // inheritance
        goto MeatEnd
      
      MeatEnd:
        TNT1 A 0 A_Quake(4, 4, 0, 96, "")
        TNT1 A 0 A_Quake(2, 8, 0, 128, "")
        TNT1 A 0 A_Quake(1, 12, 0, 160, "")
        TNT1 A 8
        stop

      Crash:
        TNT1 A 0 A_PlaySound("dakka/punchwall", CHAN_WEAPON)
        TNT1 A 0 A_GiveToTarget("DakkaFistConnected")
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_FistHit", damage)
        TNT1 A 0 A_JumpIf(1, "SparkEnd") // inheritance
        goto SparkEnd
      
      SparkEnd:
        TNT1 A 0 A_Quake(4, 4, 0, 96, "")
        TNT1 A 0 A_Quake(2, 8, 0, 128, "")
        TNT1 A 0 A_Quake(1, 12, 0, 160, "")
        BPUF CD 4
        stop
    }
}

actor FistPowerPunch2: FistPowerPunch1
{ 
    Damage 250
    DamageType "FistMediumStun"
    
    States
    {
      
      MeatEnd:
        TNT1 A 0 A_Quake(5, 6,  0, 96, "")
        TNT1 A 0 A_Quake(3, 10, 0, 128, "")
        TNT1 A 0 A_Quake(1, 15, 0, 160, "")
        TNT1 A 8
        stop
      
      SparkEnd:
        TNT1 A 0 A_Quake(5, 6,  0, 96, "")
        TNT1 A 0 A_Quake(3, 10, 0, 128, "")
        TNT1 A 0 A_Quake(1, 15, 0, 160, "")
        BPUF CD 4
        stop
    }
}

actor FistPowerPunch3: FistPowerPunch1
{ 
    Damage 400
    DamageType "FistHeavyStun"
    
    States
    {
      
      MeatEnd:
        TNT1 A 0 A_Quake(6, 6,  0, 96, "")
        TNT1 A 0 A_Quake(4, 12, 0, 128, "")
        TNT1 A 0 A_Quake(2, 19, 0, 160, "")
        TNT1 A 8
        stop
      
      SparkEnd:
        TNT1 A 0 A_Quake(6, 6,  0, 96, "")
        TNT1 A 0 A_Quake(4, 12, 0, 128, "")
        TNT1 A 0 A_Quake(2, 19, 0, 160, "")
        BPUF CD 4
        stop
    }
}


actor DakkaFistQuake2: Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_Quake(4, 4, 0, 96, "")
        TNT1 A 0 A_Quake(2, 8, 0, 128, "")
        TNT1 A 0 A_Quake(1, 12, 0, 160, "")
        stop
    }
}