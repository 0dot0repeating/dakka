actor DWep_RocketLauncher: DoomWeapon
{
    Tag "Rocket Launcher"

    Weapon.SelectionOrder 300

    Weapon.AmmoType1 "DakkaRockets"
    Weapon.AmmoType2 "DakkaRockets"
    Weapon.AmmoUse1  1
    Weapon.AmmoUse2  1
    Weapon.AmmoGive1 10

    Inventory.PickupMessage "$DAKKA_PK_ROCKETLAUNCHER"

    +NOALERT

    States
    {
      Select:
        DKRL A 0 A_TakeInventory("DakkaRocketsLoaded")
        DKRL A 0 A_GiveInventory("DakkaRocketsLoaded")
        goto Select2

      Select2:
        DKRL AA 0 A_Raise
        DKRL A 1 A_Raise
        loop

      Deselect:
        DKRL AAA 0 A_Lower
        DKRL A 1 A_Lower
        loop

      Ready:
        DKRL A 1 A_WeaponReady
        loop

      Fire:
        "----" A 0 ACS_ExecuteWithResult(DAKKA_UPDATE_SWITCHAROO, 7, 0)
        DKRL A 0 A_PlaySound("dakka/rocketfire", CHAN_WEAPON)
        DKRL A 0 A_FireCustomMissile("DakkaRocket")
        DKRL A 0 ACS_ExecuteAlways(DAKKA_RECOIL, 0, 4, 3, 16)
        DKRL A 0 A_AlertMonsters
        DKRL A 2 bright offset(0, 38) A_GunFlash
        DKRL B 1 bright offset(0, 35)
        DKRL D 3 bright offset(0, 32)
        DKRL CB 1 bright
        DKRL E 4 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKRL FG 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKRL A 0 A_ReFire
        goto Ready

      AltFire:
        DKRL A 0 A_TakeInventory("DakkaRocketsLoaded")
        DKRL A 0 A_GiveInventory("DakkaRocketsLoaded")
        goto AltHold

      AltHold:
        DKRL A 0 A_JumpIfInventory("DakkaRocketsLoaded", 0, "AltLaunch")
        DKRL A 0 A_JumpIf(ACS_ExecuteWithResult(DAKKA_DECORATE, DEC_RL_ALTFIRE) == 0, "AltLaunch")
        DKRL G 2 A_PlaySound("dakka/rocketload", ACS_ExecuteWithResult(DAKKA_DECORATE, DEC_ROTATESOUND))
        DKRL F 0 A_JumpIfInventory("AltFired", 1, 1)
        goto AltLaunch

        DKRL F 2
        DKRL B 0 A_JumpIfInventory("AltFired", 1, 1)
        goto AltLaunch

        DKRL B 3
        DKRL E 0 A_JumpIfInventory("AltFired", 1, 1)
        goto AltLaunch

        DKRL E 3
        DKRL A 0 A_GiveInventory("DakkaRocketsLoaded")
        DKRL F 2
        DKRL G 1
        DKRL A 0 A_Refire
        DKRL A 0 A_JumpIfInventory("DakkaRocketsLoaded", 1, "AltLaunch")
        goto Ready

      AltLaunch:
        "----" A 0 ACS_ExecuteWithResult(DAKKA_UPDATE_SWITCHAROO, 7, 1)
        DKRL A 0 A_JumpIfInventory("DakkaRocketsLoaded", 3, "AltLaunch3")
        DKRL A 0 A_JumpIfInventory("DakkaRocketsLoaded", 2, "AltLaunch2")
        DKRL A 0 A_JumpIfInventory("DakkaRocketsLoaded", 1, "AltLaunch1")
        goto AltDone

      AltLaunchMany:
        DKRL A 0 A_FireCustomMissile("WhatTheFuckIsAPropellent", random(-10,10)/2.0, 1, 0,0, 0, random(-4, 4))
        DKRL A 0 A_TakeInventory("DakkaRocketsLoaded", 1)
        DKRL A 0 ACS_ExecuteAlways(DAKKA_RECOIL, 0, 3, 3, 20)
        DKRL A 0 A_JumpIfInventory("DakkaRocketsLoaded", 1, "AltLaunchMany")
        goto AltDone

      AltLaunch3:
        DKRL A 0 A_FireCustomMissile("WhatTheFuckIsAPropellent",  0,    1, 0,0, 0, 1)
        DKRL A 0 A_FireCustomMissile("WhatTheFuckIsAPropellent",  1.73, 1, 0,0, 0, 4)
        DKRL A 0 A_FireCustomMissile("WhatTheFuckIsAPropellent", -1.73, 1, 0,0, 0, 4)
        DKRL A 0 ACS_ExecuteAlways(DAKKA_RECOIL, 0, 9, 4, 14)
        goto AltDone

      AltLaunch2:
        DKRL A 0 A_FireCustomMissile("WhatTheFuckIsAPropellent",  1.73, 1, 0,0, 0, 2.5)
        DKRL A 0 A_FireCustomMissile("WhatTheFuckIsAPropellent", -1.73, 1, 0,0, 0, 2.5)
        DKRL A 0 ACS_ExecuteAlways(DAKKA_RECOIL, 0, 6, 4, 14)
        goto AltDone

      AltLaunch1:
        DKRL A 0 A_FireCustomMissile("WhatTheFuckIsAPropellent",     0, 1, 0,0, 0, 2.5)
        DKRL A 0 ACS_ExecuteAlways(DAKKA_RECOIL, 0, 3, 4, 14)
        goto AltDone

      AltDone:
        DKRL A 0 A_TakeInventory("DakkaRocketsLoaded")
        DKRL A 0 A_PlaySound("dakka/rocketaltfire", CHAN_WEAPON)
        DKRL A 0 A_AlertMonsters
        DKRL C 1 offset(0, 36)
        DKRL D 3 offset(0, 39)
        DKRL C 1 offset(0, 38)
        DKRL B 1 offset(0, 36)
        DKRL E 2 offset(0, 34)
        DKRL E 2 offset(0, 32) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKRL FG 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKRL G 0 A_PlaySound("dakka/rocketload", ACS_ExecuteWithResult(DAKKA_DECORATE, DEC_ROTATESOUND))
        DKRL G 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKRL F 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKRL B 4 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKRL A 0 A_GiveInventory("DakkaRocketsLoaded")
        DKRL E 3 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKRL F 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKRL G 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKRL A 0 A_ReFire
        goto Ready

      Flash:
        DKRF A 1 bright offset(0, 38) A_Light2
        DKRF B 1 bright offset(0, 34)
        DKRF C 1 bright A_Light1
        DKRF D 3 bright
        DKRF E 3 bright
        goto LightDone

      Spawn:
        DKRL Z -1
        stop
    }
}

actor DakkaRocketsLoaded: Counter { Inventory.MaxAmount 3 }

actor DakkaAintGibbinMyself: PowerProtection
{
    DamageFactor "DakkaRocket", 0.5
}

// Total damage: 225
actor DakkaRocket
{
    Radius 6
    Height 6
    Speed 40

    Projectile
    Damage (59)
    DamageType "DakkaRocket"

    +MTHRUSPECIES
    +RANDOMIZE
    +DEHEXPLOSION
    +ROCKETTRAIL
    +EXTREMEDEATH
    +SKYEXPLODE

    Decal Scorch

    DeathSound "dakka/explode"
    Obituary "$DAKKA_MP_ROCKETLAUNCHER"

    States
    {
      Spawn:
        DMIS A 1 bright
        loop

    // One day I will make this look better.
      Death:
        DMIS B 0 bright A_GiveToTarget("DakkaAintGibbinMyself")
        DMIS B 0 A_ChangeFlag("FORCERADIUSDMG", 1)
        DMIS B 0        A_Explode(70, 160, 0, 0, 64)
        DMIS B 0        A_Explode(40, 160, 1, 0, 64)
        DMIS B 0 A_ChangeFlag("FORCERADIUSDMG", 0)
        DMIS B 0        A_Explode(56, 160, 1, 0, 64)
        DMIS F 5 bright A_TakeFromTarget("DakkaAintGibbinMyself")
        DMIS G 4 bright
        DMIS HIJ 3 bright
        stop
    }
}

// Damage: also 225, but all radius damage
actor WhatTheFuckIsAPropellent: DakkaRocket
{
    -NOGRAVITY
    Gravity 0.65
    Speed 35

    Damage (0)

    States
    {
      Spawn:
        DMIS A 0 bright
        DMIS A 0 A_SetAngle(angle + random(-90, 90))
        goto Spawn2

      Spawn2:
        DMIS AB 3 bright
        DMIS C 3 bright A_SetAngle(angle + 180)
        DMIS BAD 3 bright
        DMIS E 3 bright A_SetAngle(angle + 180)
        DMIS D 3 bright
        loop

      Death:
        DMIS B 0 A_NoGravity
        DMIS B 0 bright A_GiveToTarget("DakkaAintGibbinMyself")
        DMIS B 0        A_Explode(100, 176, 0, 0, 80)
        DMIS B 0 A_ChangeFlag("FORCERADIUSDMG", 1)
        DMIS B 0        A_Explode(125, 176, 1, 0, 80)
        DMIS B 0 A_ChangeFlag("FORCERADIUSDMG", 0)
        DMIS F 5 bright A_TakeFromTarget("DakkaAintGibbinMyself")
        DMIS G 4 bright
        DMIS HIJ 3 bright
        stop
    }
}
