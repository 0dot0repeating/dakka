actor DWep_RocketLauncher: DoomWeapon
{
    Tag "Rocket Launcher"

    Weapon.SelectionOrder 300

    Weapon.AmmoType1 "DakkaRockets"
    Weapon.AmmoType2 "DakkaRockets"
    Weapon.AmmoUse1  1
    Weapon.AmmoUse2  1
    Weapon.AmmoGive1 10

    Inventory.PickupMessage "$DAKKA_PK_ROCKETLAUNCHER"

    +NOALERT

    States
    {
      Select:
        PKRL A 0 A_TakeInventory("DakkaRocketsLoaded")
        PKRL A 0 A_GiveInventory("DakkaRocketsLoaded")
        goto Select2

      Select2:
        PKRL AA 0 A_Raise
        PKRL A 1 A_Raise
        loop

      Deselect:
        PKRL AAA 0 A_Lower
        PKRL A 1 A_Lower
        loop

      Ready:
        PKRL A 1 A_WeaponReady
        loop

      Fire:
        "----" A 0 ACS_ExecuteWithResult(397, 7, 0)
        PKRL A 0 A_PlaySound("dakka/rocketfire", CHAN_WEAPON)
        PKRL A 0 A_FireCustomMissile("DakkaRocket")
        PKCG A 0 ACS_ExecuteAlways(467, 0, 4, 4, 10)
        PKRL A 0 A_AlertMonsters
        PKRL A 2 bright offset(0, 38) A_GunFlash
        PKRL B 1 bright offset(0, 35)
        PKRL D 3 bright offset(0, 32)
        PKRL CB 1 bright
        PKRL E 4 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        PKRL FG 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        PKRL A 0 A_ReFire
        goto Ready

      AltFire:
        PKRL A 0 A_TakeInventory("DakkaRocketsLoaded")
        PKRL A 0 A_GiveInventory("DakkaRocketsLoaded")
        goto AltHold

      AltHold:
        PKRL A 0 A_JumpIfInventory("DakkaRocketsLoaded", 0, "AltLaunch")
        PKRL A 0 A_JumpIf(ACS_ExecuteWithResult(648, 1) == 0, "AltLaunch")
        PKRL G 2 A_PlaySound("dakka/rocketload", ACS_ExecuteWithResult(648))
        PKRL F 0 A_JumpIfInventory("AltFired", 1, 1)
        goto AltLaunch

        PKRL F 2
        PKRL B 0 A_JumpIfInventory("AltFired", 1, 1)
        goto AltLaunch

        PKRL B 3
        PKRL E 0 A_JumpIfInventory("AltFired", 1, 1)
        goto AltLaunch

        PKRL E 3
        PKRL A 0 A_GiveInventory("DakkaRocketsLoaded")
        PKRL F 2
        PKRL G 1
        PKRL A 0 A_Refire
        PKRL A 0 A_JumpIfInventory("DakkaRocketsLoaded", 1, "AltLaunch")
        goto Ready

      AltLaunch:
        "----" A 0 ACS_ExecuteWithResult(397, 7, 1)
        PKRL A 0 A_JumpIfInventory("DakkaRocketsLoaded", 3, "AltLaunch3")
        PKRL A 0 A_JumpIfInventory("DakkaRocketsLoaded", 2, "AltLaunch2")
        PKRL A 0 A_JumpIfInventory("DakkaRocketsLoaded", 1, "AltLaunch1")
        goto AltDone

      AltLaunchMany:
        PKRL A 0 A_FireCustomMissile("WhatTheFuckIsAPropellent", random(-10,10)/2.0, 1, 0,0, 0, random(-4, 4))
        PKRL A 0 A_TakeInventory("DakkaRocketsLoaded", 1)
        PKRL A 0 ACS_ExecuteAlways(467, 0, 3, 4, 14)
        PKRL A 0 A_JumpIfInventory("DakkaRocketsLoaded", 1, "AltLaunchMany")
        goto AltDone

      AltLaunch3:
        PKRL A 0 A_FireCustomMissile("WhatTheFuckIsAPropellent",  0,    1, 0,0, 0, 1)
        PKRL A 0 A_FireCustomMissile("WhatTheFuckIsAPropellent",  1.73, 1, 0,0, 0, 4)
        PKRL A 0 A_FireCustomMissile("WhatTheFuckIsAPropellent", -1.73, 1, 0,0, 0, 4)
        PKRL A 0 ACS_ExecuteAlways(467, 0, 9, 4, 14)
        goto AltDone

      AltLaunch2:
        PKRL A 0 A_FireCustomMissile("WhatTheFuckIsAPropellent",  1.73, 1, 0,0, 0, 2.5)
        PKRL A 0 A_FireCustomMissile("WhatTheFuckIsAPropellent", -1.73, 1, 0,0, 0, 2.5)
        PKRL A 0 ACS_ExecuteAlways(467, 0, 6, 4, 14)
        goto AltDone

      AltLaunch1:
        PKRL A 0 A_FireCustomMissile("WhatTheFuckIsAPropellent",     0, 1, 0,0, 0, 2.5)
        PKRL A 0 ACS_ExecuteAlways(467, 0, 3, 4, 14)
        goto AltDone

      AltDone:
        PKRL A 0 A_TakeInventory("DakkaRocketsLoaded")
        PKRL A 0 A_PlaySound("dakka/rocketaltfire", CHAN_WEAPON)
        PKRL A 0 A_AlertMonsters
        PKRL C 1 offset(0, 36)
        PKRL D 3 offset(0, 39)
        PKRL C 1 offset(0, 38)
        PKRL B 1 offset(0, 36)
        PKRL E 2 offset(0, 34)
        PKRL E 2 offset(0, 32) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        PKRL FG 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        PKRL G 0 A_PlaySound("dakka/rocketload", ACS_ExecuteWithResult(648))
        PKRL G 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        PKRL F 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        PKRL B 3 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        PKRL A 0 A_GiveInventory("DakkaRocketsLoaded")
        PKRL E 3 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        PKRL F 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        PKRL G 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        PKRL A 0 A_ReFire
        goto Ready

      Flash:
        PKRF A 1 bright offset(0, 38) A_Light2
        PKRF B 1 bright offset(0, 34)
        PKRF C 1 bright A_Light1
        PKRF D 3 bright
        PKRF E 3 bright
        goto LightDone

      Spawn:
        BLAU A -1
        stop
    }
}

actor DakkaRocketsLoaded: Counter { Inventory.MaxAmount 3 }

actor DakkaAintGibbinMyself: PowerProtection
{
    DamageFactor "DakkaRocket", 0.5
}

actor DakkaRocket
{
    Radius 6
    Height 6
    Speed 40

    Projectile
    Damage (58)
    DamageType "DakkaRocket"

    +MTHRUSPECIES
    +RANDOMIZE
    +DEHEXPLOSION
    +ROCKETTRAIL
    +EXTREMEDEATH
    +SKYEXPLODE

    Decal Scorch

    DeathSound "dakka/explode"
    Obituary "$DAKKA_MP_ROCKETLAUNCHER"

    States
    {
      Spawn:
        MIS2 A 1 bright
        loop

    // One day I will make this look better.
      Death:
        MIS2 B 0 bright A_GiveToTarget("DakkaAintGibbinMyself")
        MIS2 B 0 A_ChangeFlag("FORCERADIUSDMG", 1)
        MIS2 B 0        A_Explode(46, 144, 0, 0, 64)
        MIS2 B 0        A_Explode(50, 144, 1, 0, 64)
        MIS2 B 0 A_ChangeFlag("FORCERADIUSDMG", 0)
        MIS2 B 0        A_Explode(46, 144, 1, 0, 64)
        MIS2 F 5 bright A_TakeFromTarget("DakkaAintGibbinMyself")
        MIS2 G 4 bright
        MIS2 HIJ 3 bright
        stop
    }
}

actor WhatTheFuckIsAPropellent: DakkaRocket
{
    -NOGRAVITY
    Gravity 0.65
    Speed 35

    Damage (8)

    States
    {
      Spawn:
        MIS2 A 0 bright
        MIS2 A 0 A_SetAngle(angle + random(-90, 90))
        goto Spawn2

      Spawn2:
        MIS2 AB 3 bright
        MIS2 C 3 bright A_SetAngle(angle + 180)
        MIS2 BAD 3 bright
        MIS2 E 3 bright A_SetAngle(angle + 180)
        MIS2 D 3 bright
        loop

      Death:
        MIS2 B 0 A_NoGravity
        MIS2 B 0 bright A_GiveToTarget("DakkaAintGibbinMyself")
        MIS2 B 0        A_Explode(80,  176, 0, 0, 80)
        MIS2 B 0 A_ChangeFlag("FORCERADIUSDMG", 1)
        MIS2 B 0        A_Explode(112, 176, 1, 0, 80)
        MIS2 B 0 A_ChangeFlag("FORCERADIUSDMG", 0)
        MIS2 F 5 bright A_TakeFromTarget("DakkaAintGibbinMyself")
        MIS2 G 4 bright
        MIS2 HIJ 3 bright
        stop
    }
}
