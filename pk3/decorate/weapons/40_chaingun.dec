actor DWep_Chaingun: DoomWeapon
{
    Tag "Chaingun"

    Weapon.SlotNumber 4
    Weapon.SelectionOrder 700

    Weapon.AmmoType1 "DakkaBullet_5mm"
    Weapon.AmmoType2 "DakkaRainGrenades"
    Weapon.AmmoUse1 1
    Weapon.AmmoUse2 1
    Weapon.AmmoGive1 80
    Weapon.AmmoGive2 3

    +NOALERT
    +ALT_AMMO_OPTIONAL

    Decal BulletChip

    Weapon.Kickback 50

    Inventory.PickupMessage "$DAKKA_PK_CHAINGUN"
    Obituary "$DAKKA_MP_CHAINGUN"

    States
    {
      Select:
        DCGN AA 0 A_Raise
        DCGN A 1 A_Raise
        loop

      Deselect:
        DCGN AAA 0 A_Lower
        DCGN A 1 A_Lower
        loop

      Ready:
        DCGN A 1 A_WeaponReady
        loop

      Fire:
        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 5, 0)
        DCGN A 0 A_GunFlash("Flash")
        DCGN A 0 A_AlertMonsters
        DCGN A 0 A_PlaySound("dakka/chaingunfire", CHAN_WEAPON)
        DCGN B 0 A_GiveInventory("GiveScrapCounter", 7)
        DCGN A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, -85, 1, 14)
        DCGN A 1 bright offset(0, 33) A_FireBullets(0.75, 0.75, -1, 16, "DakkaPuff", FBF_NORANDOM | FBF_USEAMMO)
        DCGN B 1 bright
        DCGN D 1 bright offset(0, 32)
        DCGN A 0 A_Refire
        goto FireEnd

      Hold:
        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 5, 0)
        DCGN A 0 A_GunFlash("Flash2")
        DCGN A 0 A_AlertMonsters
        DCGN A 0 A_PlaySound("dakka/chaingunfire", CHAN_WEAPON)
        DCGN B 0 A_GiveInventory("GiveScrapCounter", 7)
        DCGN A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, -85, 1, 14)
        DCGN A 1 bright offset(0, 33) A_FireBullets(0.75, 0.75, -1, 16, "DakkaPuff", FBF_NORANDOM | FBF_USEAMMO)
        DCGN C 1 bright
        DCGN D 1 bright offset(0, 32)
        DCGN A 0 A_ClearRefire
        DCGN A 0 A_Refire
        goto FireEnd

      FireEnd:
        DCGN A 2 A_WeaponReady(WRF_NOBOB)
        DCGN B 2 A_WeaponReady(WRF_NOBOB)
        DCGN C 3 A_WeaponReady(WRF_NOBOB)
        DCGN D 4 A_WeaponReady(WRF_NOBOB)
        goto Ready

      AltFire:
        PKS2 A 0 A_JumpIfInventory("DakkaInfiniteAmmo", 1, 2)
        DCGN A 0 A_JumpIfInventory("DakkaRainGrenades", 1, 1)
        goto AltFail

        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 5, 1)
        DCGN A 0 A_PlaySound("dakka/chaingunaltfire", CallACS("Dakka_RotateSound"))
        DCGN A 0 A_AlertMonsters
        DCGN A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, 2, 2, 18)
        DCGN B 0 A_GiveInventory("GiveScrapCounter", 30)
        DCGN A 1 bright A_FireCustomMissile("DakkaChainNade")
        DCGN B 1 bright
        DCGN C 2 bright
        DCGN D 3
        DCGN D 0 A_Refire
        goto Ready

      AltFail:
        DCGN A 0 A_ClearRefire
        DCGN A 1 A_WeaponReady(WRF_NOSECONDARY)
        DCGN A 0 A_JumpIfInventory("AltFired", 1, "AltFail")
        goto Ready

      Flash:
        DCGF A 1 bright offset(0, 33) A_Light(3)
        DCGF B 1 bright
        DCGF C 1 bright A_Light(2)
        TNT1 A 1 A_Light(2)
        TNT1 A 1 A_Light(1)
        goto LightDone

      Flash2:
        DCGF A 1 bright offset(0, 33) A_Light(3)
        DCGF C 1 bright
        DCGF D 1 bright A_Light(2)
        TNT1 A 1 A_Light(2)
        TNT1 A 1 A_Light(1)
        goto LightDone

      Spawn:
        DCGN Z -1
        loop
    }
}


actor DakkaChainNade
{
    Radius 8
    Height 6

    Projectile
    Speed 35

    +MTHRUSPECIES
    +DOOMBOUNCE
    +SKYEXPLODE
    -NOGRAVITY

    Gravity 0.65
    BounceFactor 0.01
    WallBounceFactor 0.15

    Obituary "$DAKKA_MP_CHAINGUN_RAINNADE"

    States
    {
      Spawn:
        DGRN A 1 bright
        loop

      Death:
        DGRN A 0 A_Stop
        DGRN A 0 A_ChangeFlag("NOGRAVITY", 1)
        DMIS B 0 A_SpawnItemEx("DakkaGrenade_LaunchSplosion")
        DMIS B 0 bright A_Explode(48, 96, 0)
        DMIS B 0 bright A_Explode(16, 96)
        DMIS B 0 A_SpawnItemEx("ChainRainer", 0,0,0, 0,0,15)
        stop
    }
}

actor ChainRainCount: Counter {}

actor ChainRainer
{
    Radius 8
    Height 12
    Projectile

    Gravity 1

    Scale 0.5

    +MTHRUSPECIES
    +THRUACTORS
    +SKYEXPLODE
    +PUFFGETSOWNER
    -NOGRAVITY

    Obituary "$DAKKA_MP_CHAINGUN_RAINNADE"

    States
    {
      Spawn:
        CGRN A 1 bright A_JumpIf(momz < 1 && momz > -1, "BringThePain")
        loop

      Death:
      BringThePain:
        CGRN A 0 A_ChangeFlag("NOGRAVITY", 1)
        CGRN A 0 A_ChangeFlag("MISSILE", 0)
        CGRN A 0 A_ChangeFlag("ACTIVATEPCROSS", 0)
        CGRN A 0 A_ChangeFlag("ACTIVATEIMPACT", 0)
        CGRN A 0 A_Stop
        CGRN A 8 bright
        goto MakeItRain

      MakeItRain:
        CGRN A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 5, 1)
        CGRN A 0 A_SpawnItemEx("ChainRain", 0,0,0, random(2, 12)/1.5, 0, random(-6, -3), random(0,   180))
        CGRN ABC 2 bright
        CGRN A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 5, 1)
        CGRN A 0 A_SpawnItemEx("ChainRain", 0,0,0, random(2, 12)/1.5, 0, random(-6, -3), random(180, 360))
        CGRN ABC 2 bright
        CGRN A 0 A_GiveInventory("ChainRainCount", 1)
        CGRN A 0 A_JumpIfInventory("ChainRainCount", 4, "NoMoreRain")
        loop

      NoMoreRain:
        DMIS B 0 A_PlaySound("dakka/rainexplode")
        DMIS B 0 A_SetTranslucent(1, 1)
        DMIS B 0 A_Explode(16, 128, 0)
        DMIS B 0 A_Explode(32, 128)
        DMIS F 4 bright
        DMIS G 3 bright
        DMIS HIJ 2 bright
        stop
    }
}

actor ChainRain
{
    Radius 6
    Height 4

    Projectile
    +MTHRUSPECIES
    +PUFFGETSOWNER
    +DEHEXPLOSION

    -NOGRAVITY
    Gravity 0.5
    Scale 0.5

    Obituary "$DAKKA_MP_CHAINGUN_RAINNADE"

    States
    {
      Spawn:
        DGRN A 1 bright
        loop

      Death:
        DMIS B 0 A_SetScale(0.75)
        DMIS B 0 A_Explode(20, 96, true, false, 48)
        DMIS F 4 bright A_PlaySound("dakka/gyrojetexplode")
        DMIS G 3 bright
        DMIS HIJ 2 bright
        stop
    }
}

actor DakkaGrenade_LaunchSplosion
{
    +NOINTERACTION
    RenderStyle Add

    Scale 0.75

    States
    {
      Spawn:
        DMIS F 0 bright
        DMIS F 4 bright A_PlaySound("dakka/explode")
        DMIS G 3 bright
        DMIS HIJ 2 bright
        stop
    }
}
