actor DWep_Chaingun: DoomWeapon
{
    Tag "Chaingun"

    Weapon.SelectionOrder 700

    Weapon.AmmoType1 "DakkaBullet_5mm"
    Weapon.AmmoType2 "DakkaRainGrenades"
    Weapon.AmmoUse1 1
    Weapon.AmmoUse2 1
    Weapon.AmmoGive1 80
    Weapon.AmmoGive2 4

    +NOALERT
    +ALT_AMMO_OPTIONAL

    Decal BulletChip

    Weapon.Kickback 50

    Inventory.PickupMessage "$DAKKA_PK_CHAINGUN"
    Obituary "$DAKKA_MP_CHAINGUN"

    States
    {
      Select:
        PKCG AA 0 A_Raise
        PKCG A 1 A_Raise
        loop

      Deselect:
        PKCG AAA 0 A_Lower
        PKCG A 1 A_Lower
        loop

      Ready:
        PKCG A 1 A_WeaponReady
        loop

      Fire:
        "----" A 0 ACS_ExecuteWithResult(397, 5, 0)
        PKCG A 0 A_GunFlash
        PKCG A 0 A_AlertMonsters
        PKCG A 0 A_PlaySound("dakka/chaingunfire", CHAN_WEAPON)
        PKCG B 0 A_GiveInventory("GiveScrapCounter", 700)
        PKCG A 0 ACS_ExecuteAlways(467, 0, -85, 3, 6)
        PKCG A 1 bright offset(0, 33) A_FireBullets(0.75, 0.75, -1, 20, "DakkaPuff", FBF_NORANDOM | FBF_USEAMMO)
        PKCG B 1 bright
        PKCG D 1 bright offset(0, 32)
        PKCG A 0 A_Refire
        goto FireEnd

      Hold:
        "----" A 0 ACS_ExecuteWithResult(397, 5, 0)
        PKCG A 0 A_GunFlash
        PKCG A 0 A_AlertMonsters
        PKCG A 0 A_PlaySound("dakka/chaingunfire", CHAN_WEAPON)
        PKCG B 0 A_GiveInventory("GiveScrapCounter", 700)
        PKCG A 0 ACS_ExecuteAlways(467, 0, -85, 3, 6)
        PKCG A 1 bright offset(0, 33) A_FireBullets(0.75, 0.75, -1, 20, "DakkaPuff", FBF_NORANDOM | FBF_USEAMMO)
        PKCG C 1 bright
        PKCG D 1 bright offset(0, 32)
        PKCG A 0 A_ClearRefire
        PKCG A 0 A_Refire
        goto FireEnd

      FireEnd:
        PKCG A 2 A_WeaponReady(WRF_NOBOB)
        PKCG B 2 A_WeaponReady(WRF_NOBOB)
        PKCG C 3 A_WeaponReady(WRF_NOBOB)
        PKCG D 4 A_WeaponReady(WRF_NOBOB)
        goto Ready

      AltFire:
        PKS2 A 0 A_JumpIfInventory("DakkaInfiniteAmmo", 1, 2)
        PKCG A 0 A_JumpIfInventory("DakkaRainGrenades", 1, 1)
        goto AltFail

        "----" A 0 ACS_ExecuteWithResult(397, 5, 1)
        PKCG A 0 A_PlaySound("dakka/chaingunaltfire", ACS_ExecuteWithResult(648))
        PKCG A 0 A_AlertMonsters
        PKCG A 0 ACS_ExecuteAlways(467, 0, 2, 4, 8)
        PKCG A 1 bright A_FireCustomMissile("DakkaChainNade")
        PKCG B 1 bright
        PKCG C 2 bright
        PKCG D 3
        PKCG D 0 A_Refire
        goto Ready

      AltFail:
        PKCG A 0 A_ClearRefire
        PKCG A 1 A_WeaponReady(WRF_NOSECONDARY)
        PKCG A 0 A_JumpIfInventory("AltFired", 1, "AltFail")
        goto Ready

      Flash:
        TNT1 A 0 A_Jump(128,4)
        PKCF A 1 bright offset(0, 33) A_Light2
        PKCF A 1 bright A_Light2
        PKCF B 1 bright A_Light1
        TNT1 A 2 A_Light1
        goto LightDone
        PKCF C 1 bright offset(0, 33) A_Light2
        PKCF C 1 bright A_Light2
        PKCF D 1 bright A_Light1
        TNT1 A 2 A_Light1
        goto LightDone

      Spawn:
        BCGN A -1
        loop
    }
}


actor DakkaChainNade
{
    Radius 8
    Height 6

    Projectile
    Speed 35

    +MTHRUSPECIES
    +DOOMBOUNCE
    +SKYEXPLODE
    -NOGRAVITY

    Gravity 0.65
    BounceFactor 0.01
    WallBounceFactor 0.15

    Obituary "$DAKKA_MP_CHAINGUN_RAINNADE"

    States
    {
      Spawn:
        SGRN A 1 bright
        loop

      Death:
        SGRN A 0 A_Stop
        SGRN A 0 A_ChangeFlag("NOGRAVITY", 1)
        MISL B 0 A_SpawnItemEx("DakkaGrenade_LaunchSplosion")
        MISL B 0 bright A_Explode(48, 96, 0)
        MISL B 0 bright A_Explode(16, 96)
        MISL B 0 A_SpawnItemEx("ChainRainer", 0,0,0, 0,0,15)
        stop
    }
}

actor ChainRainCount: Counter {}

actor ChainRainer
{
    Radius 8
    Height 12
    Projectile

    Gravity 1

    Scale 0.5

    +MTHRUSPECIES
    +THRUACTORS
    +SKYEXPLODE
    +PUFFGETSOWNER
    -NOGRAVITY

    Obituary "$DAKKA_MP_CHAINGUN_RAINNADE"

    States
    {
      Spawn:
        CGRN A 1 bright A_JumpIf(momz < 1 && momz > -1, "BringThePain")
        loop

      Death:
      BringThePain:
        CGRN A 0 A_ChangeFlag("NOGRAVITY", 1)
        CGRN A 0 A_ChangeFlag("MISSILE", 0)
        CGRN A 0 A_ChangeFlag("ACTIVATEPCROSS", 0)
        CGRN A 0 A_ChangeFlag("ACTIVATEIMPACT", 0)
        CGRN A 0 A_Stop
        CGRN A 8 bright
        goto MakeItRain

      MakeItRain:
        CGRN A 0 ACS_ExecuteWithResult(397, 5, 1)
        CGRN A 0 A_SpawnItemEx("ChainRain", 0,0,0, random(2, 12)/1.5, 0, random(-6, -3), random(0,   180))
        CGRN AB 3 bright
        CGRN A 0 ACS_ExecuteWithResult(397, 5, 1)
        CGRN A 0 A_SpawnItemEx("ChainRain", 0,0,0, random(2, 12)/1.5, 0, random(-6, -3), random(180, 360))
        CGRN AB 3 bright
        CGRN A 0 A_GiveInventory("ChainRainCount", 1)
        CGRN A 0 A_JumpIfInventory("ChainRainCount", 4, "NoMoreRain")
        loop

      NoMoreRain:
        MISL B 0 A_PlaySound("dakka/rainexplode")
        MISL B 0 A_SetTranslucent(1, 1)
        MISL B 0 A_Explode(16, 128, 0)
        MISL B 0 A_Explode(32, 128)
        MIS2 F 4 bright
        MIS2 G 3 bright
        MIS2 HIJ 2 bright
        stop
    }
}

actor ChainRain
{
    Radius 6
    Height 4

    Projectile
    +MTHRUSPECIES
    +PUFFGETSOWNER
    +DEHEXPLOSION

    -NOGRAVITY
    Gravity 0.5
    Scale 0.5

    Obituary "$DAKKA_MP_CHAINGUN_RAINNADE"

    States
    {
      Spawn:
        SGRN A 1 bright
        loop

      Death:
        MISL B 0 A_Explode(32, 96)
        MIS2 F 4 bright A_PlaySound("dakka/gyrojetexplode")
        MIS2 G 3 bright
        MIS2 HIJ 2 bright
        stop
    }
}

actor DakkaGrenade_LaunchSplosion
{
    +NOINTERACTION
    RenderStyle Add

    Scale 0.75

    States
    {
      Spawn:
        MIS2 F 0 bright
        MIS2 F 4 bright A_PlaySound("dakka/explode")
        MIS2 G 3 bright
        MIS2 HIJ 2 bright
        stop
    }
}
