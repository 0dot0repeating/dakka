actor LastLanceBarrelUsed: Boolean {}
actor LanceAltfireCharge:  Counter {}

actor DWep_PlasmaLance: DoomWeapon
{
    Tag "Lancer"

    Weapon.SlotNumber 6
    Weapon.SelectionOrder 100
    Weapon.SlotPriority 1

    Weapon.AmmoType1 "DakkaCells"
    Weapon.AmmoType2 "DakkaCells"
    Weapon.AmmoUse1  1
    Weapon.AmmoUse2  3
    Weapon.AmmoGive1 50

    Inventory.PickupMessage "$DAKKA_PK_PLASMALANCE"
    
    +NOALERT
    +ALT_AMMO_OPTIONAL

    States
    {
      Select:
        PLNC FF 0 A_Raise
        PLNC F 1 A_Raise
        loop

      Deselect:
        PLNC F 0 A_Light(0)
        PLNC FFF 0 A_Lower
        PLNC F 1 A_Lower
        loop

      Ready:
        PLNC F 1 A_WeaponReady
        loop

      Fire:
        PLNC G 0 A_JumpIfInventory("LastLanceBarrelUsed", 1, "FireRight")
        goto FireLeft
      
      FireLeft:
        PLNC G 0                      A_CheckReload
        PLNC G 0                      A_AlertMonsters
        PLNC G 0                      ACS_NamedExecuteWithResult("Dakka_Switcharoo", 13, 0)
        PLNC G 0                      ACS_NamedExecuteAlways("Dakka_Recoil", 0, -115, 1, 8)
        PLNC G 0                      A_GiveInventory("LastLanceBarrelUsed")
        PLNC G 0                      A_GiveInventory("GiveScrapCounter2", 1)
        PLNC G 0                      A_FireCustomMissile("PLPrimaryShot", 0, true, -1.5)
        PLNC G 0                      A_GunFlash
        PLNC G 1 bright offset(0, 37) A_PlaySound("arc/fire", 5, 0.75)
        PLNC G 1 bright offset(0, 36)
        PLNC H 1 bright offset(0, 39) A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        PLNC H 0                      A_JumpIfInventory("MainFired", 1, "HoldRight")
        goto FireLeftDone
      
      FireLeftDone:
        PLNC H 0                      A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        PLNC H 1        offset(0, 38) A_JumpIfInventory("MainFired", 1, "HoldRight")
        PLNC H 0                      A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        PLNC H 1        offset(0, 38) A_JumpIfInventory("MainFired", 1, "HoldRight")
        PLNC F 0                      A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        PLNC F 1        offset(0, 35) A_JumpIfInventory("MainFired", 1, "HoldRight")
        PLNC F 0                      A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        PLNC F 1        offset(0, 35) A_JumpIfInventory("MainFired", 1, "HoldRight")
        PLNC F 0                      A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        PLNC F 1        offset(0, 33) A_JumpIfInventory("MainFired", 1, "FireRight")
        PLNC F 0                      A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        PLNC F 1        offset(0, 33) A_JumpIfInventory("MainFired", 1, "FireRight")
        PLNC F 0                      A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        PLNC F 1        offset(0, 32) A_JumpIfInventory("MainFired", 1, "FireRight")
        PLNC F 0                      A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        PLNC F 1        offset(0, 32) A_JumpIfInventory("MainFired", 1, "FireRight")
        goto Ready
      
      FireRight:
        PLNC I 0                      A_CheckReload
        PLNC G 0                      A_AlertMonsters
        PLNC I 0                      ACS_NamedExecuteWithResult("Dakka_Switcharoo", 13, 0)
        PLNC I 0                      ACS_NamedExecuteAlways("Dakka_Recoil", 0, -130, 1, 8)
        PLNC I 0                      A_TakeInventory("LastLanceBarrelUsed")
        PLNC I 0                      A_GiveInventory("GiveScrapCounter2", 1)
        PLNC I 0                      A_FireCustomMissile("PLPrimaryShot", 0, true, 1.5)
        PLNC I 0                      A_GunFlash
        PLNC I 1 bright offset(0, 37) A_PlaySound("arc/fire", 6, 0.75)
        PLNC I 1 bright offset(0, 36)
        PLNC J 1 bright offset(0, 39) A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        PLNC J 0                      A_JumpIfInventory("MainFired", 1, "HoldLeft")
        goto FireRightDone
      
      FireRightDone:
        PLNC J 0                      A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        PLNC J 1        offset(0, 37) A_JumpIfInventory("MainFired", 1, "HoldLeft")
        PLNC J 0                      A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        PLNC J 1        offset(0, 37) A_JumpIfInventory("MainFired", 1, "HoldLeft")
        PLNC F 0                      A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        PLNC F 1        offset(0, 35) A_JumpIfInventory("MainFired", 1, "HoldLeft")
        PLNC F 0                      A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        PLNC F 1        offset(0, 35) A_JumpIfInventory("MainFired", 1, "HoldLeft")
        PLNC F 0                      A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        PLNC F 1        offset(0, 33) A_JumpIfInventory("MainFired", 1, "FireLeft")
        PLNC F 0                      A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        PLNC F 1        offset(0, 33) A_JumpIfInventory("MainFired", 1, "FireLeft")
        PLNC F 0                      A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        PLNC F 1        offset(0, 32) A_JumpIfInventory("MainFired", 1, "FireLeft")
        PLNC F 0                      A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        PLNC F 1        offset(0, 32) A_JumpIfInventory("MainFired", 1, "FireLeft")
        goto Ready

      HoldLeft:
        PLNC G 0                      A_CheckReload
        PLNC G 0                      A_AlertMonsters
        PLNC G 0                      ACS_NamedExecuteWithResult("Dakka_Switcharoo", 13, 0)
        PLNC G 0                      ACS_NamedExecuteAlways("Dakka_Recoil", 0, -115, 1, 8)
        PLNC G 0                      A_GiveInventory("LastLanceBarrelUsed")
        PLNC G 0                      A_GiveInventory("GiveScrapCounter2", 1)
        PLNC G 0                      A_FireCustomMissile("PLPrimaryShot", 0, true, -1.5)
        PLNC G 0                      A_GunFlash
        PLNC G 1 bright offset(0, 40) A_PlaySound("arc/fire", 5, 0.75)
        PLNC G 1 bright offset(0, 39)
        PLNC H 1 bright offset(0, 42) A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        PLNC H 0                      A_JumpIfInventory("MainFired", 1, "HoldRight")
        goto HoldLeftDone
      
      HoldLeftDone:
        PLNC H 0                      A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        PLNC H 1        offset(0, 40) A_JumpIfInventory("MainFired", 1, "HoldRight")
        PLNC H 0                      A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        PLNC H 1        offset(0, 40) A_JumpIfInventory("MainFired", 1, "HoldRight")
        PLNC F 0                      A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        PLNC F 1        offset(0, 37) A_JumpIfInventory("MainFired", 1, "HoldRight")
        PLNC F 0                      A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        PLNC F 1        offset(0, 37) A_JumpIfInventory("MainFired", 1, "HoldRight")
        PLNC F 0                      A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        PLNC F 1        offset(0, 34) A_JumpIfInventory("MainFired", 1, "HoldRight")
        PLNC F 0                      A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        PLNC F 1        offset(0, 34) A_JumpIfInventory("MainFired", 1, "HoldRight")
        PLNC F 0                      A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        PLNC F 1        offset(0, 32) A_JumpIfInventory("MainFired", 1, "FireRight")
        PLNC F 0                      A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        PLNC F 1        offset(0, 32) A_JumpIfInventory("MainFired", 1, "FireRight")
        goto Ready
      
      HoldRight:
        PLNC I 0                      A_CheckReload
        PLNC G 0                      A_AlertMonsters
        PLNC I 0                      ACS_NamedExecuteWithResult("Dakka_Switcharoo", 13, 0)
        PLNC I 0                      ACS_NamedExecuteAlways("Dakka_Recoil", 0, -115, 1, 8)
        PLNC I 0                      A_TakeInventory("LastLanceBarrelUsed")
        PLNC I 0                      A_GiveInventory("GiveScrapCounter2", 1)
        PLNC I 0                      A_FireCustomMissile("PLPrimaryShot", 0, true, 1.5)
        PLNC I 0                      A_GunFlash
        PLNC I 1 bright offset(0, 40) A_PlaySound("arc/fire", 6, 0.75)
        PLNC I 1 bright offset(0, 39)
        PLNC J 1 bright offset(0, 42) A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        PLNC J 0                      A_JumpIfInventory("MainFired", 1, "HoldLeft")
        goto HoldRightDone
      
      HoldRightDone:
        PLNC J 0                      A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        PLNC J 1        offset(0, 40) A_JumpIfInventory("MainFired", 1, "HoldLeft")
        PLNC J 0                      A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        PLNC J 1        offset(0, 40) A_JumpIfInventory("MainFired", 1, "HoldLeft")
        PLNC F 0                      A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        PLNC F 1        offset(0, 37) A_JumpIfInventory("MainFired", 1, "HoldLeft")
        PLNC F 0                      A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        PLNC F 1        offset(0, 37) A_JumpIfInventory("MainFired", 1, "HoldLeft")
        PLNC F 0                      A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        PLNC F 1        offset(0, 34) A_JumpIfInventory("MainFired", 1, "HoldLeft")
        PLNC F 0                      A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        PLNC F 1        offset(0, 34) A_JumpIfInventory("MainFired", 1, "HoldLeft")
        PLNC F 0                      A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        PLNC F 1        offset(0, 32) A_JumpIfInventory("MainFired", 1, "FireLeft")
        PLNC F 0                      A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        PLNC F 1        offset(0, 32) A_JumpIfInventory("MainFired", 1, "FireLeft")
        goto Ready
      
      Flash:
        TNT1 A 3 bright A_Light(3)
        TNT1 A 1 bright A_Light(2)
        TNT1 A 2 bright A_Light(1)
        goto LightDone

        
      AltFire:
        PLNC F 0 A_JumpIfNoAmmo("AltNo")
        PLNC F 0 A_PlaySound("arc/altcharge",      CHAN_WEAPON)
        PLNC F 0 A_GunFlash("Charging")
        
        PLNC A 1 offset(0, 32)
        PLNC A 0 A_AlertMonsters(64)
        PLNC A 0 A_FireCustomMissile("AmmoWaster")
        PLNC AAABBBB 1 A_JumpIfInventory("AltReleased", 1, "AltFire_Charge0")
        
        PLNC B 0 A_JumpIfNoAmmo("Altfire_Charge0")
        PLNC B 0 A_FireCustomMissile("AmmoWaster")
        PLNC B 0 A_AlertMonsters(128)
        PLNC CCCCDDDD 1 A_JumpIfInventory("AltReleased", 1, "AltFire_Charge1")
        
        PLNC B 0 A_JumpIfNoAmmo("Altfire_Charge1")
        PLNC B 0 A_FireCustomMissile("AmmoWaster")
        PLNC B 0 A_AlertMonsters(224)
        PLNC E 1 offset(-1, 32) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge2")
        PLNC E 1 offset( 0, 32) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge2")
        PLNC E 1 offset( 0, 32) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge2")
        PLNC B 1 offset( 1, 32) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge2")
        PLNC B 1 offset( 0, 32) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge2")
        PLNC B 1 offset( 0, 32) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge2")
        PLNC C 1 offset( 1, 32) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge2")
        PLNC C 1 offset(-1, 32) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge2")
        
        PLNC B 0 A_JumpIfNoAmmo("Altfire_Charge2")
        PLNC B 0 A_FireCustomMissile("AmmoWaster")
        PLNC B 0 A_AlertMonsters(320)
        PLNC C 1 offset( 1, 32) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge3")
        PLNC D 1 offset( 0, 32) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge3")
        PLNC D 1 offset( 1, 32) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge3")
        PLNC D 1 offset(-1, 32) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge3")
        PLNC E 1 offset( 0, 32) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge3")
        PLNC E 1 offset( 1, 32) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge3")
        PLNC E 1 offset( 0, 32) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge3")
        PLNC B 1 offset(-1, 32) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge3")
        
        PLNC B 0 A_JumpIfNoAmmo("Altfire_Charge3")
        PLNC B 0 A_FireCustomMissile("AmmoWaster")
        PLNC B 0 A_AlertMonsters(448)
        PLNC B 1 offset( 0, 32) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge4")
        PLNC B 1 offset( 2, 32) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge4")
        PLNC C 1 offset( 1, 32) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge4")
        PLNC C 1 offset( 0, 32) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge4")
        PLNC C 1 offset(-1, 33) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge4")
        PLNC D 1 offset(-2, 32) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge4")
        PLNC D 1 offset(-2, 33) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge4")
        PLNC D 1 offset( 1, 32) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge4")
        
        PLNC B 0 A_JumpIfNoAmmo("Altfire_Charge4")
        PLNC B 0 A_FireCustomMissile("AmmoWaster")
        PLNC B 0 A_AlertMonsters(576)
        PLNC E 1 offset( 0, 33) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge5")
        PLNC E 1 offset(-2, 33) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge5")
        PLNC B 1 offset(-1, 32) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge5")
        PLNC B 1 offset( 1, 32) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge5")
        PLNC C 1 offset(-1, 33) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge5")
        PLNC C 1 offset( 1, 33) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge5")
        PLNC D 1 offset( 0, 32) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge5")
        PLNC D 1 offset(-2, 32) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge5")

        PLNC B 0 A_JumpIfNoAmmo("Altfire_Charge5")
        PLNC B 0 A_FireCustomMissile("AmmoWaster")
        PLNC B 0 A_AlertMonsters(704)
        PLNC E 1 offset(-2, 33) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge6")
        PLNC E 1 offset( 3, 32) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge6")
        PLNC B 1 offset(-1, 33) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge6")
        PLNC B 1 offset( 1, 33) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge6")
        PLNC C 1 offset(-3, 32) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge6")
        PLNC C 1 offset( 3, 33) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge6")
        PLNC D 1 offset(-1, 32) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge6")
        PLNC D 1 offset( 2, 33) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge6")
        PLNC E 1 offset( 2, 33) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge6")
        PLNC E 1 offset( 0, 32) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge6")
        PLNC B 1 offset(-1, 33) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge6")
        PLNC B 1 offset(-3, 33) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge6")
        PLNC C 1 offset(-1, 32) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge6")
        PLNC C 1 offset( 2, 33) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge6")
        PLNC D 1 offset( 0, 32) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge6")
        PLNC D 1 offset(-1, 33) A_JumpIfInventory("AltReleased", 1, "AltFire_Charge6")
        
        PLNC B 0 A_AlertMonsters(832)
        goto AltFire_Charge6
      
      AltFire_Charge0:
        PLNC B 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 13, 1)
        PLNC B 0 A_FireBullets(12, 2, 2, 0, "LanceArcShot_Power0",  false, 320)
        goto AltFire_End
        
      AltFire_Charge1:
        PLNC B 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 13, 1)
        PLNC B 0 A_FireBullets(12, 2, 2, 0, "LanceArcShot_Power1",  false, 352)
        goto AltFire_End
        
      AltFire_Charge2:
        PLNC B 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 13, 1)
        PLNC B 0 A_FireBullets(12, 2, 3, 0, "LanceArcShot_Power2",  false, 384)
        goto AltFire_End
        
      AltFire_Charge3:
        PLNC B 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 13, 1)
        PLNC B 0 A_FireBullets(12, 2, 3, 0, "LanceArcShot_Power3",  false, 416)
        goto AltFire_End
        
      AltFire_Charge4:
        PLNC B 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 13, 1)
        PLNC B 0 A_FireBullets(12, 2, 4, 0, "LanceArcShot_Power4",  false, 448)
        goto AltFire_End
        
      AltFire_Charge5:
        PLNC B 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 13, 1)
        PLNC B 0 A_FireBullets(12, 2, 4, 0, "LanceArcShot_Power5",  false, 480)
        goto AltFire_End
      
      AltFire_Charge6:
        PLNC B 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 13, 1)
        PLNC B 0 A_FireBullets(12, 2, 4, 0, "LanceArcShot_Power6",  false, 512)
        goto AltFire_End
      
      AltFire_End:
        PLNC B 0 A_AlertMonsters
        PLNC K 0 bright A_StopSound(CHAN_WEAPON)
        PLNC K 0 bright A_PlaySound("arc/altfire", 7)
        PLNC K 0 bright A_GunFlash("AltFlash")
        PLNC K 2 bright offset(0, 41)
        PLNC K 2 bright offset(0, 43)
        PLNC L 2 bright offset(0, 43)
        PLNC L 2        offset(0, 43) A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        PLNC L 2        offset(0, 42) A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        PLNC F 2        offset(0, 40) A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        PLNC F 2        offset(0, 38) A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        PLNC F 1        offset(0, 34) A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        PLNC F 1        offset(0, 33) A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        PLNC F 8        offset(0, 32) A_WeaponReady(WRF_NOSECONDARY | WRF_NOBOB)
        goto Ready
        
      AltNo:
        PLNC F 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready
        
      Charging:
        PLNF DEFGHI 8 bright
        PLNF J -1 bright
        stop
      
      
      AltFlash:
        TNT1 A 5 bright A_Light(5)
        TNT1 A 4 bright A_Light(4)
        TNT1 A 3 bright A_Light(3)
        TNT1 A 2 bright A_Light(2)
        TNT1 A 2 bright A_Light(1)
        goto LightDone
      
      
      Spawn:
        DPLS Z -1
        stop
    }
}

actor PLPrimaryShot: FastProjectile
{
    Radius 4
    Height 4
    
    Projectile
    Speed 100
    
    Damage (10)
    
    +FORCERADIUSDMG
    +BLOODLESSIMPACT
    +NODAMAGETHRUST
    +SERVERSIDEONLY
    +HITTRACER
    
    RenderStyle Add
    
    var int user_velx;
    var int user_vely;
    var int user_velz;
    var int user_init;
    
    DeathSound "dakka/plasmaexplode"
    Obituary "$DAKKA_MP_PLASMALANCE"
    
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_SetUserVar("user_init", true)
        TNT1 A 0 A_SetUserVar("user_velx", velx * 65536)
        TNT1 A 0 A_SetUserVar("user_vely", vely * 65536)
        TNT1 A 0 A_SetUserVar("user_velz", velz * 65536)
        TNT1 A 1 A_SpawnItemEx("PLPrimaryTrail", -1.8 * velx, -1.8 * vely, -1.8 * velz, velx,vely,velz, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPOINTERS)
        loop
      
      Death:
        PLZM A 0 ACS_NamedExecuteWithResult("Dakka_InitLanceHit", 112, 24, 25)
        PLZM A 0 A_RadiusGive("PLPrimaryHitChecker", 384, RGF_MONSTERS | RGF_OBJECTS | RGF_VOODOO | RGF_CUBE)
        PLZM HIJKL 4 bright
        stop
    }
}

actor PLPrimaryHitChecker: Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 ACS_NamedExecutewithResult("Dakka_CheckLanceHit")
        stop
    }
}

actor PLPrimaryHurter
{
    +FORCERADIUSDMG
    +BLOODLESSIMPACT
    +NODAMAGETHRUST
    
    var int user_damage;
    
    States
    {
      Spawn:
        TNT1 A 0
        goto DoHurt
        
      DoHurt:
        TNT1 A 0 A_Explode(user_damage, 4, false, false, 8)
        stop
    }
}


actor PLPrimaryTrail
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_SpawnItemEx("PLPrimaryParticle1",    0,              0,                0,             velx, vely, velz, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEVELOCITY)
        TNT1 A 0 A_SpawnItemEx("PLPrimaryParticle2",  -(velx * 0.05),  -(vely * 0.05),  -(velz * 0.05),  velx, vely, velz, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEVELOCITY)
        TNT1 A 0 A_SpawnItemEx("PLPrimaryParticle3",  -(velx * 0.1),   -(vely * 0.1),   -(velz * 0.1),   velx, vely, velz, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEVELOCITY)
        TNT1 A 0 A_SpawnItemEx("PLPrimaryParticle4",  -(velx * 0.15),  -(vely * 0.15),  -(velz * 0.15),  velx, vely, velz, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEVELOCITY)
        TNT1 A 0 A_SpawnItemEx("PLPrimaryParticle6",  -(velx * 0.2),   -(vely * 0.2),   -(velz * 0.2),   velx, vely, velz, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEVELOCITY)
        TNT1 A 0 A_SpawnItemEx("PLPrimaryParticle7",  -(velx * 0.25),  -(vely * 0.25),  -(velz * 0.25),  velx, vely, velz, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEVELOCITY)
        TNT1 A 0 A_SpawnItemEx("PLPrimaryParticle8",  -(velx * 0.3),   -(vely * 0.3),   -(velz * 0.3),   velx, vely, velz, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEVELOCITY)
        TNT1 A 0 A_SpawnItemEx("PLPrimaryParticle9",  -(velx * 0.35),  -(vely * 0.35),  -(velz * 0.35),  velx, vely, velz, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEVELOCITY)
        TNT1 A 0 A_SpawnItemEx("PLPrimaryParticle10", -(velx * 0.4),   -(vely * 0.4),   -(velz * 0.4),   velx, vely, velz, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEVELOCITY)
        TNT1 A 0 A_SpawnItemEx("PLPrimaryParticle11", -(velx * 0.45),  -(vely * 0.45),  -(velz * 0.45),  velx, vely, velz, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEVELOCITY)
        
        TNT1 A 0 A_SpawnItemEx("PLPrimaryCloud1",     -(velx * 1),     -(vely * 1),     -(velz * 1),     velx, vely, velz, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEVELOCITY)
        TNT1 A 0 A_SpawnItemEx("PLPrimaryCloud1",     -(velx * 0.875), -(vely * 0.875), -(velz * 0.875), velx, vely, velz, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEVELOCITY)
        TNT1 A 0 A_SpawnItemEx("PLPrimaryCloud1",     -(velx * 0.75),  -(vely * 0.75),  -(velz * 0.75),  velx, vely, velz, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEVELOCITY)
        TNT1 A 0 A_SpawnItemEx("PLPrimaryCloud1",     -(velx * 0.625), -(vely * 0.625), -(velz * 0.625), velx, vely, velz, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEVELOCITY)
        TNT1 A 0 A_SpawnItemEx("PLPrimaryCloud1",     -(velx * 0.5),   -(vely * 0.5),   -(velz * 0.5),   velx, vely, velz, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEVELOCITY)
        TNT1 A 0 A_SpawnItemEx("PLPrimaryCloud1",     -(velx * 0.375), -(vely * 0.375), -(velz * 0.375), velx, vely, velz, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEVELOCITY)
        TNT1 A 0 A_SpawnItemEx("PLPrimaryCloud1",     -(velx * 0.25),  -(vely * 0.25),  -(velz * 0.25),  velx, vely, velz, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEVELOCITY)
        stop
    }
}

actor PLPrimaryParticle1
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    RenderStyle Add
    
    Scale 0.1
    
    States
    {
      Spawn:
        PLZM F 2 bright
        stop
    }
}


actor PLPrimaryParticle2:  PLPrimaryParticle1 { Scale 0.133 }
actor PLPrimaryParticle3:  PLPrimaryParticle1 { Scale 0.167 }
actor PLPrimaryParticle4:  PLPrimaryParticle1 { Scale 0.2   }
actor PLPrimaryParticle5:  PLPrimaryParticle1 { Scale 0.233 }
actor PLPrimaryParticle6:  PLPrimaryParticle1 { Scale 0.267 }
actor PLPrimaryParticle7:  PLPrimaryParticle1 { Scale 0.3   }
actor PLPrimaryParticle8:  PLPrimaryParticle1 { Scale 0.333 }
actor PLPrimaryParticle9:  PLPrimaryParticle1 { Scale 0.367 }
actor PLPrimaryParticle10: PLPrimaryParticle1 { Scale 0.4   }
actor PLPrimaryParticle11: PLPrimaryParticle1 { Scale 0.433 }

actor PLPrimaryCloud1
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    RenderStyle Add
    Scale 0.25
    Alpha 0.05
    
    States
    {
      Spawn:
        PLZM H 0 bright
        PLZM H 0 bright A_ChangeVelocity(frandom(-1, 1), frandom(-1, 1), frandom(-1, 1))
        goto FadeA
      
      FadeA:
        PLZM H 1 bright A_FadeOut(frandom(0.001, 0.004))
        PLZM H 0 bright A_ScaleVelocity(frandom(0.4, 0.8))
        PLZM H 0 bright A_SetScale(scalex * frandom(1.05, 1.15))
        PLZM H 0 A_Jump(64, "FadeB")
        loop
      
      FadeB:
        PLZM I 1 bright A_FadeOut(frandom(0.001, 0.004))
        PLZM I 0 bright A_ScaleVelocity(frandom(0.4, 0.8))
        PLZM I 0 bright A_SetScale(scalex * frandom(1.05, 1.15))
        PLZM I 0 A_Jump(64, "FadeC")
        loop
      
      FadeC:
        PLZM J 1 bright A_FadeOut(frandom(0.001, 0.004))
        PLZM J 0 bright A_ScaleVelocity(frandom(0.4, 0.8))
        PLZM J 0 bright A_SetScale(scalex * frandom(1.05, 1.15))
        PLZM J 0 A_Jump(64, "FadeD")
        loop
      
      FadeD:
        PLZM K 1 bright A_FadeOut(frandom(0.001, 0.004))
        PLZM K 0 bright A_ScaleVelocity(frandom(0.4, 0.8))
        PLZM K 0 bright A_SetScale(scalex * frandom(1.05, 1.15))
        PLZM K 0 A_Jump(64, "FadeE")
        loop
      
      FadeE:
        PLZM L 1 bright A_FadeOut(frandom(0.001, 0.004))
        PLZM L 0 bright A_ScaleVelocity(frandom(0.4, 0.8))
        PLZM L 0 bright A_SetScale(scalex * frandom(1.05, 1.15))
        PLZM L 0 A_Jump(64, "Done")
        loop
      
      Done:
        TNT1 A 0
        stop
    }
}


actor LanceArcShot: Arc_Base
{
    +NOEXTREMEDEATH
    +FORCERADIUSDMG

    +PUFFGETSOWNER
    +ALWAYSPUFF
    +PUFFONACTORS
    +BLOODLESSIMPACT
    
    // in order: arc index, max depth, damage
    Args 0, 3, 40

    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_SetAngle(angle + 180)
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_Tracer", TRACE_ARC_FIRER, 0, -8)
        TNT1 A 0 A_SpawnItemEx("ChannelerBeamImpact")
        TNT1 A 1 ACS_NamedExecuteWithResult("Arc_Main", args[0])
        stop

      Arc_Spawn:
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_Tracer", TRACE_ARC_MASTER, 0, 0)
        TNT1 A 0 A_Jump(128, 2)
        TNT1 A 0 A_PlaySound("arc/fry")
        TNT1 A 1 ACS_NamedExecuteWithResult("Arc_Main", args[0])
        stop

      Arc_Query:
        TNT1 A 0 A_JumpIf(user_depth >= args[1], "Arc_StopArcing")
        goto Arc_KeepArcing

      Arc_FoundTarget:
        TNT1 A 1 ACS_NamedExecuteWithResult("Arc_Damage", args[2])
        stop

      Arc_NoTarget:
        TNT1 A 0 A_JumpIf(user_depth == 0, "HitNothing")
        stop
      
      HitNothing:
        TNT1 A 0 A_Explode(args[2], 32, false, false, 32)
        stop
    }
}

actor LanceArcShot_Power0: LanceArcShot { Args 0, 2, 50  }
actor LanceArcShot_Power1: LanceArcShot { Args 1, 2, 60  }
actor LanceArcShot_Power2: LanceArcShot { Args 2, 2, 70  }
actor LanceArcShot_Power3: LanceArcShot { Args 3, 3, 70  }
actor LanceArcShot_Power4: LanceArcShot { Args 4, 4, 80  }
actor LanceArcShot_Power5: LanceArcShot { Args 5, 5, 90  }
actor LanceArcShot_Power6: LanceArcShot { Args 6, 6, 100 }


actor LanceArcTrail
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    +FORCEXYBILLBOARD
    
    Radius 0
    Height 0
    RenderStyle Add
    Alpha 1
    Scale 0.3

    States
    {
      Spawn:
        PLZM M 0
        PLZM M 0 A_Jump(128, "N")
        goto Spawn2

      N:
        PLZM N 0
        goto Spawn2

      Spawn2:
        PLZM "#" 8 bright
        PLZM "####################" 1 bright A_FadeOut(0.1)
        stop
    }
}