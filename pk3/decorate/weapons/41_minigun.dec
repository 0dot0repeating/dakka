actor MinigunFireState: Counter {}
actor MinigunFlamethrowing: Boolean {}
actor GoToC: Boolean {}

actor DWep_Minigun: DoomWeapon
{
    Tag "Minigun"

    Weapon.SelectionOrder 350

    Weapon.AmmoType1 "DakkaBullet_5mm"
    Weapon.AmmoType2 "DakkaFlamerFuel"
    Weapon.AmmoUse1 1
    Weapon.AmmoUse2 1
    Weapon.AmmoGive1 160
    Weapon.AmmoGive2 24

    Weapon.Kickback 50

    +NOALERT

    Decal BulletChip

    Inventory.PickupMessage "$DAKKA_PK_MINIGUN"
    Obituary "$DAKKA_MP_MINIGUN"

    States
    {
      Select:
        DMNG AA 0 A_Raise
        DMNG A 1 A_Raise
        loop

      Deselect:
        DMNG A 0 A_JumpIfInventory("MinigunFlamethrowing", 1, "FlameDeselect")
        goto Deselect2
     
      Deselect2:
        DMNG A 0 A_StopSound(6)
        DMNG A 0 A_StopSound(7)
        DMNG AAA 0 A_Lower
        DMNG A 1 A_Lower
        loop

      FlameDeselect:
        DMNG A 0 A_TakeInventory("MinigunFlamethrowing")
        DMNG A 0 A_PlaySound("dakka/minigun/flamestop", 5)
        goto Deselect2

      Ready:
        DMNG A 0 A_TakeInventory("MinigunFireState")
        DMNG A 1 A_WeaponReady
        loop

      Fire:
        "----" A 0 ACS_ExecuteWithResult(DAKKA_UPDATE_SWITCHAROO, 6, 0)
        DMNG A 0 A_AlertMonsters
        DMNG A 0 ACS_ExecuteAlways(DAKKA_RECOIL, 0, -50, 1, 6)
        DMNG B 0 A_GiveInventory("GiveScrapCounter", 7)
        DMNG A 0 A_JumpIfInventory("MinigunFireState", 5, "FireA5_Double")
        DMNG A 0 bright A_FireBullets(7.5, 7.5, -1, 20, "DakkaPuff", FBF_NORANDOM | FBF_USEAMMO)
        goto FireAnim

      FireAnim:
        DMNG A 0 A_JumpIfInventory("MinigunFireState", 5, "FireA5")
        DMNG A 0 A_GiveInventory("MinigunFireState")
        DMNG A 0 A_JumpIfInventory("MinigunFireState", 4, "FireA4")
        DMNG A 0 A_JumpIfInventory("MinigunFireState", 3, "FireA3")
        DMNG A 0 A_JumpIfInventory("MinigunFireState", 2, "FireA2")
        DMNG A 0 A_JumpIfInventory("MinigunFireState", 1, "FireA1")
        goto FireA1
      
      FireA1:
        DMNG A 0 A_PlaySound("dakka/minigunfire", 4)
        DMNG A 1 bright offset(0, 34) A_GunFlash
        DMNG B 2 bright offset(0, 33) A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DMNG C 2 offset(0, 32) A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DMNG D 2 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DMNG C 0 A_Refire
        DMNG A 3 A_WeaponReady(WRF_NOBOB)
        DMNG B 3 A_WeaponReady(WRF_NOBOB)
        DMNG C 4 A_WeaponReady(WRF_NOBOB)
        DMNG D 5 A_WeaponReady(WRF_NOBOB)
        goto Ready
      
      FireA2:
        DMNG A 0 A_PlaySound("dakka/minigunfire", 4)
        DMNG A 1 offset(0, 34) bright A_GunFlash
        DMNG B 1 offset(0, 33) bright A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DMNG C 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DMNG D 2 offset(0, 32) A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DMNG C 0 A_Refire
        DMNG A 2 A_WeaponReady(WRF_NOBOB)
        DMNG B 2 A_WeaponReady(WRF_NOBOB)
        DMNG A 0 A_TakeInventory("MinigunFireState", 1)
        DMNG C 2 A_WeaponReady(WRF_NOBOB)
        DMNG D 3 A_WeaponReady(WRF_NOBOB)
        DMNG A 3 A_WeaponReady(WRF_NOBOB)
        DMNG B 3 A_WeaponReady(WRF_NOBOB)
        DMNG C 4 A_WeaponReady(WRF_NOBOB)
        DMNG D 5 A_WeaponReady(WRF_NOBOB)
        goto Ready
      
      FireA3:
        DMNG A 0 A_PlaySound("dakka/minigunfire", 4)
        DMNG A 1 offset(0, 34) bright A_GunFlash
        DMNG C 1 offset(0, 33) A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DMNG D 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DMNG C 0 A_Refire
        DMNG AB 1 A_WeaponReady(WRF_NOBOB)
        DMNG A 0 A_TakeInventory("MinigunFireState", 1)
        DMNG CD 2 A_WeaponReady(WRF_NOBOB)
        DMNG B 2 A_WeaponReady(WRF_NOBOB)
        DMNG A 0 A_TakeInventory("MinigunFireState", 1)
        DMNG C 2 A_WeaponReady(WRF_NOBOB)
        DMNG D 2 A_WeaponReady(WRF_NOBOB)
        DMNG A 3 A_WeaponReady(WRF_NOBOB)
        DMNG B 3 A_WeaponReady(WRF_NOBOB)
        DMNG C 4 A_WeaponReady(WRF_NOBOB)
        DMNG D 5 A_WeaponReady(WRF_NOBOB)
        goto Ready
      
      FireA4:
        DMNG A 0 A_PlaySound("dakka/minigunfire", 4)
        DMNG A 1 offset(0, 35) bright A_GunFlash
        DMNG C 1 offset(0, 34) A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DMNG C 0 A_Refire
        DMNG AB 1 offset(0, 33) A_WeaponReady(WRF_NOBOB)
        DMNG CD 1 offset(0, 32) A_WeaponReady(WRF_NOBOB)
        DMNG A 0 A_TakeInventory("MinigunFireState", 1)
        DMNG ABCD 1 A_WeaponReady(WRF_NOBOB)
        DMNG A 0 A_TakeInventory("MinigunFireState", 1)
        DMNG A 2 A_WeaponReady(WRF_NOBOB)
        DMNG B 2 A_WeaponReady(WRF_NOBOB)
        DMNG A 0 A_TakeInventory("MinigunFireState", 1)
        DMNG C 2 A_WeaponReady(WRF_NOBOB)
        DMNG D 3 A_WeaponReady(WRF_NOBOB)
        DMNG A 3 A_WeaponReady(WRF_NOBOB)
        DMNG B 3 A_WeaponReady(WRF_NOBOB)
        DMNG C 4 A_WeaponReady(WRF_NOBOB)
        DMNG D 5 A_WeaponReady(WRF_NOBOB)
        goto Ready


      FireA5_Double:
        DMNG A 0 bright A_FireBullets(7.5, 7.5, -1, 10, "DakkaPuff", FBF_NORANDOM | FBF_USEAMMO)
        DMNG A 0 bright A_FireBullets(7.5, 7.5, -1, 10, "DakkaPuff", FBF_NORANDOM)
        goto FireA5

      FireA5:
        DMNG A 0 A_PlaySound("dakka/minigunfire", 4)
        DMNG A 0 A_JumpIfInventory("GoToC", 1, "FireA5_C")
        goto FireA5_A

      FireA5_A:
        DMNG A 0 A_GiveInventory("GoToC")
        DMNG A 1 bright offset(0, 36) A_GunFlash
        DMNG C 0 A_Refire
        DMNG CA 1 bright offset(0, 35) A_WeaponReady(WRF_NOBOB)
        DMNG A 0 A_TakeInventory("MinigunFireState", 1)
        DMNG CA 1 offset(0, 34) A_WeaponReady(WRF_NOBOB)
        DMNG BC 1 offset(0, 33) A_WeaponReady(WRF_NOBOB)
        DMNG D  1 offset(0, 32) A_WeaponReady(WRF_NOBOB)
        DMNG A 0 A_TakeInventory("MinigunFireState", 1)
        DMNG ABCD 1 A_WeaponReady(WRF_NOBOB)
        DMNG A 0 A_TakeInventory("MinigunFireState", 1)
        DMNG ABCD 2 A_WeaponReady(WRF_NOBOB)
        DMNG A 0 A_TakeInventory("MinigunFireState", 1)
        DMNG A 3 A_WeaponReady(WRF_NOBOB)
        DMNG B 3 A_WeaponReady(WRF_NOBOB)
        DMNG C 4 A_WeaponReady(WRF_NOBOB)
        DMNG D 5 A_WeaponReady(WRF_NOBOB)
        goto Ready

      FireA5_C:
        DMNG A 0 A_TakeInventory("GoToC")
        DMNG C 1 bright offset(0, 37) A_GunFlash
        DMNG C 0 A_Refire
        DMNG AC 1 bright offset(0, 35) A_WeaponReady(WRF_NOBOB)
        DMNG A 0 A_TakeInventory("MinigunFireState", 1)
        DMNG AC 1 offset(0, 34) A_WeaponReady(WRF_NOBOB)
        DMNG AB 1 offset(0, 33) A_WeaponReady(WRF_NOBOB)
        DMNG CD 1 offset(0, 32) A_WeaponReady(WRF_NOBOB)
        DMNG A 0 A_TakeInventory("MinigunFireState", 1)
        DMNG ABCD 1 A_WeaponReady(WRF_NOBOB)
        DMNG A 0 A_TakeInventory("MinigunFireState", 1)
        DMNG ABCD 2 A_WeaponReady(WRF_NOBOB)
        DMNG A 0 A_TakeInventory("MinigunFireState", 1)
        DMNG A 3 A_WeaponReady(WRF_NOBOB)
        DMNG B 3 A_WeaponReady(WRF_NOBOB)
        DMNG C 4 A_WeaponReady(WRF_NOBOB)
        DMNG D 5 A_WeaponReady(WRF_NOBOB)
        goto Ready

      Flash:
        DMNF A 0 A_JumpIfInventory("GoToC", 1, "FlashC")
        DMNF A 1 bright offset(0, 34) A_Light2
        DMNF A 2 bright offset(0, 32) A_Light2
        DMNF B 1 bright A_Light1
        TNT1 A 2 A_Light1
        goto LightDone

      FlashC:
        DMNF B 2 bright A_Light2
        TNT1 A 2 A_Light1
        goto LightDone


      AltFire:
        DMNG A 0 A_AlertMonsters
        DMNG A 0 A_GiveInventory("MinigunFlamethrowing")
        DMNG A 0 A_TakeInventory("MinigunFireState")
        DMNG A 0 A_PlaySound("dakka/minigun/flamepuff", CHAN_WEAPON)
        DMNG A 0 A_PlaySound("dakka/minigun/flamestart", 5)
        DMNG A 0 A_PlaySound("dakka/minigun/flameloop",  6, 1, 1)
        DMNG A 0 A_PlaySound("dakka/firecrackle",        7, 1, 1)

        "----" A 0 ACS_ExecuteWithResult(DAKKA_UPDATE_SWITCHAROO, 6, 1)
        "----" A 0 A_Light1
        DMNG E 0 A_GiveInventory("GiveScrapCounter3", 1)
        DMNG E 0 A_FireCustomMissile("MinigunFlame",  random(-20,20)/5.0, 1, 0, -4)
        DMNG E 0 A_FireCustomMissile("MinigunFlameV", random(-20,20)/5.0, 0, 0, -4)
        DMNG E 1 bright offset(0, 33) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DMNG E 0 A_FireCustomMissile("MinigunFlame", random(-20,20)/5.0, 0, 0, -4)
        DMNG E 0 A_FireCustomMissile("MinigunFlameV", random(-20,20)/5.0, 0, 0, -4)
        DMNG E 1 bright offset(0, 33) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)

        "----" A 0 ACS_ExecuteWithResult(DAKKA_UPDATE_SWITCHAROO, 6, 1)
        "----" A 0 A_Light2
        DMNG F 0 A_GiveInventory("GiveScrapCounter3", 1)
        DMNG F 0 A_FireCustomMissile("MinigunFlame", random(-20,20)/5.0, 0, 0, -4)
        DMNG F 0 A_FireCustomMissile("MinigunFlameV", random(-20,20)/5.0, 0, 0, -4)
        DMNG F 1 bright offset(0, 34) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DMNG F 0 A_FireCustomMissile("MinigunFlame", random(-20,20)/5.0, 0, 0, -4)
        DMNG F 0 A_FireCustomMissile("MinigunFlameV", random(-20,20)/5.0, 0, 0, -4)
        DMNG F 1 bright offset(0, 34) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)

        "----" A 0 ACS_ExecuteWithResult(DAKKA_UPDATE_SWITCHAROO, 6, 1)
        "----" A 0 A_Light1
        DMNG I 0 A_GiveInventory("GiveScrapCounter3", 1)
        DMNG I 0 A_FireCustomMissile("MinigunFlame", random(-20,20)/5.0, 0, 0, -4)
        DMNG I 0 A_FireCustomMissile("MinigunFlameV", random(-20,20)/5.0, 0, 0, -4)
        DMNG I 1 bright offset(0, 35) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DMNG I 0 A_FireCustomMissile("MinigunFlame", random(-20,20)/5.0, 0, 0, -4)
        DMNG I 0 A_FireCustomMissile("MinigunFlameV", random(-20,20)/5.0, 0, 0, -4)
        DMNG I 1 bright offset(0, 35) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)

        "----" A 0 ACS_ExecuteWithResult(DAKKA_UPDATE_SWITCHAROO, 6, 1)
        "----" A 0 A_Light2
        DMNG J 0 A_GiveInventory("GiveScrapCounter3", 1)
        DMNG J 0 A_FireCustomMissile("MinigunFlame", random(-20,20)/5.0, 0, 0, -4)
        DMNG J 0 A_FireCustomMissile("MinigunFlameV", random(-20,20)/5.0, 0, 0, -4)
        DMNG J 1 bright offset(0, 36) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DMNG J 0 A_FireCustomMissile("MinigunFlame", random(-20,20)/5.0, 0, 0, -4)
        DMNG J 0 A_FireCustomMissile("MinigunFlameV", random(-20,20)/5.0, 0, 0, -4)
        DMNG J 1 bright offset(0, 36) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)

        DMNG A 0 A_Refire
        goto AltEnd

      AltHold:
        DMNG A 0 A_AlertMonsters
        DMNG A 0 A_TakeInventory("MinigunFireState")

        "----" A 0 ACS_ExecuteWithResult(DAKKA_UPDATE_SWITCHAROO, 6, 1)
        "----" A 0 A_Light1
        DMNG G 0 A_GiveInventory("GiveScrapCounter3", 1)
        DMNG G 0 A_FireCustomMissile("MinigunFlame", random(-20,20)/5.0, 1, 0, -4)
        DMNG G 0 A_FireCustomMissile("MinigunFlameV", random(-20,20)/5.0, 0, 0, -4)
        DMNG G 1 bright offset(0, 37) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DMNG G 0 A_FireCustomMissile("MinigunFlame", random(-20,20)/5.0, 0, 0, -4)
        DMNG G 0 A_FireCustomMissile("MinigunFlameV", random(-20,20)/5.0, 0, 0, -4)
        DMNG G 1 bright offset(0, 37) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)

        "----" A 0 ACS_ExecuteWithResult(DAKKA_UPDATE_SWITCHAROO, 6, 1)
        "----" A 0 A_Light2
        DMNG H 0 A_GiveInventory("GiveScrapCounter3", 1)
        DMNG H 0 A_FireCustomMissile("MinigunFlame", random(-20,20)/5.0, 0, 0, -4)
        DMNG H 0 A_FireCustomMissile("MinigunFlameV", random(-20,20)/5.0, 0, 0, -4)
        DMNG H 1 bright               A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DMNG H 0 A_FireCustomMissile("MinigunFlame", random(-20,20)/5.0, 0, 0, -4)
        DMNG H 0 A_FireCustomMissile("MinigunFlameV", random(-20,20)/5.0, 0, 0, -4)
        DMNG H 1 bright               A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)

        "----" A 0 ACS_ExecuteWithResult(DAKKA_UPDATE_SWITCHAROO, 6, 1)
        "----" A 0 A_Light1
        DMNG I 0 A_GiveInventory("GiveScrapCounter3", 1)
        DMNG I 0 A_FireCustomMissile("MinigunFlame", random(-20,20)/5.0, 0, 0, -4)
        DMNG I 0 A_FireCustomMissile("MinigunFlameV", random(-20,20)/5.0, 0, 0, -4)
        DMNG I 1 bright               A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DMNG I 0 A_FireCustomMissile("MinigunFlame", random(-20,20)/5.0, 0, 0, -4)
        DMNG I 0 A_FireCustomMissile("MinigunFlameV", random(-20,20)/5.0, 0, 0, -4)
        DMNG I 1 bright               A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)

        "----" A 0 ACS_ExecuteWithResult(DAKKA_UPDATE_SWITCHAROO, 6, 1)
        "----" A 0 A_Light2
        DMNG J 0 A_GiveInventory("GiveScrapCounter3", 1)
        DMNG J 0 A_FireCustomMissile("MinigunFlame", random(-20,20)/5.0, 0, 0, -4)
        DMNG J 0 A_FireCustomMissile("MinigunFlameV", random(-20,20)/5.0, 0, 0, -4)
        DMNG J 1 bright               A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DMNG J 0 A_FireCustomMissile("MinigunFlame", random(-20,20)/5.0, 0, 0, -4)
        DMNG J 0 A_FireCustomMissile("MinigunFlameV", random(-20,20)/5.0, 0, 0, -4)
        DMNG J 1 bright               A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)

        DMNG A 0 A_Refire
        goto AltEnd

      AltEnd:
        "----" A 0 A_Light1
        DMNG A 0 A_TakeInventory("MinigunFlamethrowing")
        DMNG A 0 A_PlaySound("dakka/minigun/flamestop", 5)
        DMNG K 0 A_StopSound(6)
        DMNG K 0 A_StopSound(7)
        DMNG K 2 bright offset(0, 36) A_WeaponReady(WRF_NOBOB | WRF_NOSECONDARY)
        DMNG L 0 A_StopSound(6)
        DMNG L 0 A_StopSound(7)
        DMNG L 2 bright offset(0, 34) A_WeaponReady(WRF_NOBOB | WRF_NOSECONDARY)
        DMNG C 3 offset(0, 32) A_WeaponReady(WRF_NOBOB)
        "----" A 0 A_Light0
        DMNG D 5 A_WeaponReady(WRF_NOBOB)
        goto Ready


      Spawn:
        DMNG Z -1
        loop
    }
}

actor MinigunFlameCounter: Counter {}

actor MinigunFlame
{
    Radius 2
    Height 2

    Projectile
    Speed 25

    +MTHRUSPECIES
    +THRUACTORS
    +BLOODLESSIMPACT
    +NODAMAGETHRUST
    +FORCERADIUSDMG
    +FOILINVUL
    +PIERCEARMOR

    RenderStyle Add
    Scale 0.2
    Alpha 0.5

    Obituary "$DAKKA_MP_MINIGUN_FLAME"

    var int user_counter;
    
    States
    {
      Spawn:
        DFLM E 0 bright
        TNT1 A 0 A_ScaleVelocity(random(90, 110) / 100.0)
        TNT1 A 0 A_ChangeFlag("PAINLESS", random(0, 6) > 0)
        TNT1 A 0 A_SetUserVar("user_counter", 24)
        goto Spawn2

      Spawn2:
        TNT1 A 0 A_SpawnItemEx("MinigunFlameVisual_Main",        0,             0,             0, 0,0,random(0, 100)/50.0, 0, SXF_ABSOLUTEPOSITION)
        TNT1 A 0 A_SpawnItemEx("MinigunFlameVisual", (momx/3) * -1, (momy/3) * -1, (momz/3) * -1, 0,0,random(0, 100)/50.0, 0, SXF_ABSOLUTEPOSITION)
        TNT1 A 0 A_SpawnItemEx("MinigunFlameVisual", (momx/3) *  1, (momy/3) *  1, (momz/3) *  1, 0,0,random(0, 100)/50.0, 0, SXF_ABSOLUTEPOSITION)
        TNT1 A 1 A_Explode(2, 64, 0, 0, 32)
        TNT1 A 1 A_SetUserVar("user_counter", user_counter - 1)
        TNT1 A 0 A_JumpIf(user_counter > 0, "Spawn2")
        goto EndItAll

      EndItAll:
        TNT1 A 1
        stop

      Death:
        TNT1 A 0 A_JumpIf(user_counter > 0, 1)
        goto EndItAll

        TNT1 A 0 A_SpawnItemEx("MinigunFlameVisual_Main",        0,             0,             0, 0,0,0, 0, SXF_ABSOLUTEPOSITION)
        TNT1 A 0 A_SpawnItemEx("MinigunFlameVisual", random(-150,150)/20.0, 0, random(-150,150)/20.0, random(-20,20)/20.0, 0, random(-20,20)/20.0, random(0,360))
        TNT1 A 0 A_SpawnItemEx("MinigunFlameVisual", random(-150,150)/10.0, 0, random(-150,150)/10.0, random(-20,20)/10.0, 0, random(-20,20)/10.0, random(0,360))
        TNT1 A 0 A_Explode(2, 64, 0)
        TNT1 A 1 A_SetUserVar("user_counter", user_counter - 3)
        loop
    }
}

actor MinigunFlameV: MinigunFlame
{
    +CLIENTSIDEONLY

    States
    {
      Spawn:
        DFLM E 0 bright
        DFLM E 0 bright A_JumpIf(ACS_ExecuteWithResult(DAKKA_DECORATE_CLIENT, CDEC_GETLESSEFFECTS), "NoFancy")
        TNT1 A 0 A_ScaleVelocity(random(90, 110) / 100.0)
        TNT1 A 0 A_SetUserVar("user_counter", 24)
        goto Fancy

      NoFancy:
        TNT1 A 0
        stop

      Fancy:
        TNT1 A 0 A_SpawnItemEx("MinigunFlameVisual_Main",        0,             0,             0, 0,0,random(0, 100)/50.0, 0, SXF_ABSOLUTEPOSITION)
        TNT1 A 0 A_SpawnItemEx("MinigunFlameVisual", (momx/3) * -1, (momy/3) * -1, (momz/3) * -1, 0,0,random(0, 100)/50.0, 0, SXF_ABSOLUTEPOSITION)
        TNT1 A 0 A_SpawnItemEx("MinigunFlameVisual", (momx/3) *  1, (momy/3) *  1, (momz/3) *  1, 0,0,random(0, 100)/50.0, 0, SXF_ABSOLUTEPOSITION)
        TNT1 A 1 A_SetUserVar("user_counter", user_counter - 1)
        TNT1 A 0 A_JumpIf(user_counter > 0, "Fancy")
        goto EndItAll

      EndItAll:
        TNT1 A 1
        stop

      Death:
        TNT1 A 0 A_JumpIf(user_counter > 0, 1)
        goto EndItAll
        TNT1 A 0 A_SpawnItemEx("MinigunFlameVisual_Main",        0,             0,             0, 0,0,0, 0, SXF_ABSOLUTEPOSITION)
        TNT1 A 0 A_SpawnItemEx("MinigunFlameVisual", random(-150,150)/20.0, 0, random(-150,150)/20.0, random(-20,20)/20.0, 0, random(-20,20)/20.0, random(0,360))
        TNT1 A 0 A_SpawnItemEx("MinigunFlameVisual", random(-150,150)/10.0, 0, random(-150,150)/10.0, random(-20,20)/10.0, 0, random(-20,20)/10.0, random(0,360))
        TNT1 A 1 A_SetUserVar("user_counter", user_counter - 3)
        loop
    }
}
        

actor MinigunFlameVisual
{
    Radius 0
    Height 0

    +NOINTERACTION
    +CLIENTSIDEONLY

    RenderStyle Add
    Scale 0.125
    Alpha 0.25

    States
    {
      Spawn:
        DFLM E 0 bright
        DFLM E 0 bright A_JumpIf(ACS_ExecuteWithResult(DAKKA_DECORATE_CLIENT, CDEC_GETLESSEFFECTS), "NoFancy")
        goto Fancy

      NoFancy:
        TNT1 A 0
        stop

      Fancy:
        DFLM E 0 ThrustThing(random(0, 255), random(0, 16) / 4.0, 1, 0)
        DFLM E 0 ThrustThingZ(0, random(0, 16) / 4.0, random(0, 1), 1)
        goto Spawn2
      
      Spawn2:
        DFLM EGIKLMNO 1 bright
        stop
    }
}

actor MinigunFlameVisual_Main: MinigunFlameVisual
{
    States
    {
      NoFancy:
        DFLM EGIKMO 1 bright
        stop
    }
}
