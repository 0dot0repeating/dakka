actor MinigunFireState: Counter {}
actor MinigunFlamethrowing: Boolean {}
actor GoToC: Boolean {}

actor DWep_Minigun: DoomWeapon
{
    Tag "Minigun"

    Weapon.SlotNumber 4
    Weapon.SelectionOrder 350

    Weapon.AmmoType1 "DakkaBullet_5mm"
    Weapon.AmmoType2 "DakkaFlamerFuel"
    Weapon.AmmoUse1 1
    Weapon.AmmoUse2 1
    Weapon.AmmoGive1 160
    Weapon.AmmoGive2 10

    Weapon.Kickback 50

    +NOALERT
    +ALT_AMMO_OPTIONAL

    Decal BulletChip

    Inventory.PickupMessage "$DAKKA_PK_MINIGUN"
    Obituary "$DAKKA_MP_MINIGUN"

    States
    {
      Select:
        DMNG AA 0 A_Raise
        DMNG A 1 A_Raise
        loop

      Deselect:
        DMNG A 0 A_JumpIfInventory("MinigunFlamethrowing", 1, "FlameDeselect")
        goto Deselect2
     
      Deselect2:
        DMNG A 0 A_StopSound(6)
        DMNG A 0 A_StopSound(7)
        DMNG AAA 0 A_Lower
        DMNG A 1 A_Lower
        loop

      FlameDeselect:
        DMNG A 0 A_TakeInventory("MinigunFlamethrowing")
        DMNG A 0 A_PlaySound("dakka/minigun/flamestop", 5)
        goto Deselect3
     
      Deselect3:
        DMNG A 0 A_StopSound(6)
        DMNG A 0 A_StopSound(7)
        DMNG A 0 A_Light(4)
        DMNG AAA 0 A_Lower
        DMNG A 1 A_Lower
        DMNG A 0 A_Light(2)
        DMNG AAA 0 A_Lower
        DMNG A 1 A_Lower
        DMNG A 0 A_Light(0)
        DMNG AAA 0 A_Lower
        DMNG A 1 A_Lower
        goto Deselect2

      Ready:
        DMNG A 0 A_TakeInventory("MinigunFireState")
        DMNG A 1 A_WeaponReady
        loop

      Fire:
        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 6, 0)
        DMNG A 0 A_AlertMonsters
        DMNG B 0 A_GiveInventory("GiveScrapCounter", 7)
        DMNG A 0 A_JumpIfInventory("MinigunFireState", 5, "FireA5_Double")
        DMNG A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, -150, 1, 9)
        DMNG A 0 bright A_FireBullets(7.5, 7.5, -1, 12, "DakkaPuff", FBF_NORANDOM | FBF_USEAMMO)
        goto FireAnim

      FireAnim:
        DMNG A 0 A_JumpIfInventory("MinigunFireState", 5, "FireA5")
        DMNG A 0 A_GiveInventory("MinigunFireState")
        DMNG A 0 A_JumpIfInventory("MinigunFireState", 4, "FireA4")
        DMNG A 0 A_JumpIfInventory("MinigunFireState", 3, "FireA3")
        DMNG A 0 A_JumpIfInventory("MinigunFireState", 2, "FireA2")
        DMNG A 0 A_JumpIfInventory("MinigunFireState", 1, "FireA1")
        goto FireA1
      
      FireA1:
        DMNG A 0 A_PlaySound("dakka/minigunfire", 4)
        DMNG A 1 bright offset(0, 34) A_GunFlash
        DMNG B 2 bright offset(0, 33) A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DMNG C 2 offset(0, 32) A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DMNG D 2 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DMNG C 0 A_Refire
        DMNG A 3 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG B 3 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG C 4 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG D 5 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        goto Ready
      
      FireA2:
        DMNG A 0 A_PlaySound("dakka/minigunfire", 4)
        DMNG A 1 offset(0, 34) bright A_GunFlash
        DMNG B 1 offset(0, 33) bright A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DMNG C 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DMNG D 2 offset(0, 32) A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DMNG C 0 A_Refire
        DMNG A 2 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG B 2 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG A 0 A_TakeInventory("MinigunFireState", 1)
        DMNG C 2 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG D 3 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG A 3 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG B 3 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG C 4 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG D 5 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        goto Ready
      
      FireA3:
        DMNG A 0 A_PlaySound("dakka/minigunfire", 4)
        DMNG A 1 offset(0, 34) bright A_GunFlash
        DMNG C 1 offset(0, 33) A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DMNG D 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DMNG C 0 A_Refire
        DMNG AB 1 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG A 0 A_TakeInventory("MinigunFireState", 1)
        DMNG CD 2 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG B 2 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG A 0 A_TakeInventory("MinigunFireState", 1)
        DMNG C 2 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG D 2 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG A 3 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG B 3 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG C 4 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG D 5 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        goto Ready
      
      FireA4:
        DMNG A 0 A_PlaySound("dakka/minigunfire", 4)
        DMNG A 1 offset(0, 35) bright A_GunFlash
        DMNG C 1 offset(0, 34) A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DMNG C 0 A_Refire
        DMNG AB 1 offset(0, 33) A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG CD 1 offset(0, 32) A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG A 0 A_TakeInventory("MinigunFireState", 1)
        DMNG ABCD 1 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG A 0 A_TakeInventory("MinigunFireState", 1)
        DMNG A 2 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG B 2 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG A 0 A_TakeInventory("MinigunFireState", 1)
        DMNG C 2 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG D 3 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG A 3 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG B 3 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG C 4 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG D 5 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        goto Ready


      FireA5_Double:
        DMNG A 0 bright A_FireBullets(7.5, 7.5, 2, 6, "DakkaPuff", FBF_NORANDOM | FBF_USEAMMO)
        goto FireA5

      FireA5:
        DMNG A 0 A_PlaySound("dakka/minigunfire", 4)
        DMNG A 0 A_JumpIfInventory("GoToC", 1, "FireA5_C")
        goto FireA5_A

      FireA5_A:
        DMNG A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, -250, 1, 9)
        DMNG A 0 A_GiveInventory("GoToC")
        DMNG A 1 bright offset(0, 36) A_GunFlash
        DMNG C 0 A_Refire
        DMNG CA 1 bright offset(0, 35) A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG A 0 A_TakeInventory("MinigunFireState", 1)
        DMNG CA 1 offset(0, 34) A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG BC 1 offset(0, 33) A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG D  1 offset(0, 32) A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG A 0 A_TakeInventory("MinigunFireState", 1)
        DMNG ABCD 1 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG A 0 A_TakeInventory("MinigunFireState", 1)
        DMNG ABCD 2 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG A 0 A_TakeInventory("MinigunFireState", 1)
        DMNG A 3 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG B 3 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG C 4 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG D 5 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        goto Ready

      FireA5_C:
        DMNG A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, -25, 5, 24)
        DMNG A 0 A_TakeInventory("GoToC")
        DMNG C 1 bright offset(0, 37) A_GunFlash
        DMNG C 0 A_Refire
        DMNG AC 1 bright offset(0, 35) A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG A 0 A_TakeInventory("MinigunFireState", 1)
        DMNG AC 1 offset(0, 34) A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG AB 1 offset(0, 33) A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG CD 1 offset(0, 32) A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG A 0 A_TakeInventory("MinigunFireState", 1)
        DMNG ABCD 1 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG A 0 A_TakeInventory("MinigunFireState", 1)
        DMNG ABCD 2 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG A 0 A_TakeInventory("MinigunFireState", 1)
        DMNG A 3 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG B 3 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG C 4 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        DMNG D 5 A_WeaponReady(WRF_NOBOB | CallACS("Dakka_MinigunReady"))
        goto Ready

      Flash:
        DMNF A 0 A_JumpIfInventory("GoToC", 1, "FlashC")
        DMNF A 1 bright offset(0, 34) A_Light(3)
        DMNF A 1 bright offset(0, 32)
        DMNF A 1 bright offset(0, 32) A_Light(2)
        DMNF B 1 bright A_Light(1)
        goto LightDone

      FlashC:
        DMNF B 1 bright A_Light(random(3,5))
        DMNF B 1 bright A_Light(3)
        TNT1 A 1 A_Light(2)
        TNT1 A 1 A_Light(1)
        goto LightDone


      AltFire:
        DMNG A 0 A_JumpIfInventory("DakkaFlamerFuel", 1, 1)
        goto AltNo

        DMNG A 0 A_AlertMonsters
        DMNG A 0 A_GiveInventory("MinigunFlamethrowing")
        DMNG A 0 A_TakeInventory("MinigunFireState")
        DMNG A 0 A_PlaySound("dakka/minigun/flamepuff", CHAN_WEAPON)
        DMNG A 0 A_PlaySound("dakka/minigun/flamestart", 5)
        DMNG A 0 A_PlaySound("dakka/minigun/flameloop",  6, 1, 1)
        DMNG A 0 A_PlaySound("dakka/firecrackle",        7, 1, 1)

        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 6, 1)
        "----" A 0 A_Light(1)
        DMNG E 0 A_GiveInventory("GiveScrapCounter3", 1)
        DMNG E 0 A_FireCustomMissile("MinigunFlame",  random(-20,20)/5.0, 1, 0, -4)
        DMNG E 1 bright offset(0, 33) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DMNG E 0 A_FireCustomMissile("MinigunFlame", random(-20,20)/5.0, 0, 0, -4)
        DMNG E 1 bright offset(0, 33) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)

        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 6, 1)
        "----" A 0 A_Light(2)
        DMNG F 0 A_GiveInventory("GiveScrapCounter3", 1)
        DMNG F 0 A_FireCustomMissile("MinigunFlame", random(-20,20)/5.0, 0, 0, -4)
        DMNG F 1 bright offset(0, 34) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DMNG F 0 A_FireCustomMissile("MinigunFlame", random(-20,20)/5.0, 0, 0, -4)
        DMNG F 1 bright offset(0, 34) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)

        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 6, 1)
        "----" A 0 A_Light(3)
        DMNG I 0 A_GiveInventory("GiveScrapCounter3", 1)
        DMNG I 0 A_FireCustomMissile("MinigunFlame", random(-20,20)/5.0, 0, 0, -4)
        DMNG I 1 bright offset(0, 35) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DMNG I 0 A_FireCustomMissile("MinigunFlame", random(-20,20)/5.0, 0, 0, -4)
        DMNG I 1 bright offset(0, 35) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)

        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 6, 1)
        "----" A 0 A_Light(4)
        DMNG J 0 A_GiveInventory("GiveScrapCounter3", 1)
        DMNG J 0 A_FireCustomMissile("MinigunFlame", random(-20,20)/5.0, 0, 0, -4)
        DMNG J 1 bright offset(0, 36) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DMNG J 0 A_FireCustomMissile("MinigunFlame", random(-20,20)/5.0, 0, 0, -4)
        DMNG J 1 bright offset(0, 36) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)

        DMNG A 0 A_Refire
        goto AltEnd

      AltHold:
        DMNG A 0 A_JumpIfInventory("DakkaFlamerFuel", 1, 1)
        goto AltEnd_NoAmmo

        DMNG A 0 A_AlertMonsters
        DMNG A 0 A_TakeInventory("MinigunFireState")

        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 6, 1)
        "----" A 0 A_Light(5)
        DMNG G 0 A_GiveInventory("GiveScrapCounter3", 1)
        DMNG G 0 A_FireCustomMissile("MinigunFlame", random(-20,20)/5.0, 1, 0, -4)
        DMNG G 1 bright offset(0, 37) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DMNG G 0 A_FireCustomMissile("MinigunFlame", random(-20,20)/5.0, 0, 0, -4)
        DMNG G 1 bright offset(0, 37) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)

        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 6, 1)
        "----" A 0 A_Light(6)
        DMNG H 0 A_GiveInventory("GiveScrapCounter3", 1)
        DMNG H 0 A_FireCustomMissile("MinigunFlame", random(-20,20)/5.0, 0, 0, -4)
        DMNG H 1 bright               A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DMNG H 0 A_FireCustomMissile("MinigunFlame", random(-20,20)/5.0, 0, 0, -4)
        DMNG H 1 bright               A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)

        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 6, 1)
        "----" A 0 A_Light(5)
        DMNG I 0 A_GiveInventory("GiveScrapCounter3", 1)
        DMNG I 0 A_FireCustomMissile("MinigunFlame", random(-20,20)/5.0, 0, 0, -4)
        DMNG I 1 bright               A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DMNG I 0 A_FireCustomMissile("MinigunFlame", random(-20,20)/5.0, 0, 0, -4)
        DMNG I 1 bright               A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)

        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 6, 1)
        "----" A 0 A_Light(4)
        DMNG J 0 A_GiveInventory("GiveScrapCounter3", 1)
        DMNG J 0 A_FireCustomMissile("MinigunFlame", random(-20,20)/5.0, 0, 0, -4)
        DMNG J 1 bright               A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DMNG J 0 A_FireCustomMissile("MinigunFlame", random(-20,20)/5.0, 0, 0, -4)
        DMNG J 1 bright               A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)

        DMNG A 0 A_Refire
        goto AltEnd

      AltEnd:
        DMNG K 0 A_Light(3)
        DMNG K 0 A_ClearRefire
        DMNG K 0 A_TakeInventory("MinigunFlamethrowing")
        DMNG K 0 A_PlaySound("dakka/minigun/flamestop", 5)
        DMNG K 0 A_StopSound(6)
        DMNG K 0 A_StopSound(7)
        DMNG K 2 bright offset(0, 36) A_WeaponReady(WRF_NOBOB | WRF_NOSECONDARY)
        
        DMNG L 0 A_Light(2)
        DMNG L 0 A_StopSound(6)
        DMNG L 0 A_StopSound(7)
        DMNG L 2 bright offset(0, 34) A_WeaponReady(WRF_NOBOB | WRF_NOSECONDARY)
        
        DMNG C 0 A_Light(1)
        DMNG C 3 bright offset(0, 32) A_WeaponReady(WRF_NOBOB)
        
        DMNG D 0 A_Light(0)
        DMNG D 5 A_WeaponReady(WRF_NOBOB)
        goto Ready

      AltEnd_NoAmmo:
        DMNG K 0 A_Light(3)
        DMNG K 0 A_ClearRefire
        DMNG K 0 A_TakeInventory("MinigunFlamethrowing")
        DMNG K 0 A_PlaySound("dakka/minigun/flamestop", 5)
        DMNG K 0 A_StopSound(6)
        DMNG K 0 A_StopSound(7)
        DMNG K 2 bright offset(0, 36) A_WeaponReady(WRF_NOBOB | WRF_NOSECONDARY)
        
        DMNG L 0 A_Light(2)
        DMNG L 0 A_StopSound(6)
        DMNG L 0 A_StopSound(7)
        DMNG L 2 bright offset(0, 34) A_WeaponReady(WRF_NOBOB | WRF_NOSECONDARY)
        
        DMNG C 0 A_Light(1)
        DMNG C 3 bright offset(0, 32) A_WeaponReady(WRF_NOBOB | WRF_NOSECONDARY)
        
        DMNG D 0 A_Light(0)
        DMNG D 5 A_WeaponReady(WRF_NOBOB | WRF_NOSECONDARY)
        goto Ready

      AltNo:
        DMNG A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready


      Spawn:
        DMNG Z -1
        loop
    }
}

actor MinigunFlameCounter: Counter {}

actor MinigunFlame
{
    Radius 2
    Height 2

    Projectile
    Speed 15

    +MTHRUSPECIES
    +THRUACTORS
    +BLOODLESSIMPACT
    +NODAMAGETHRUST
    +FORCERADIUSDMG
    +FOILINVUL
    +PIERCEARMOR

    RenderStyle Add
    Scale 0.2
    Alpha 0.5

    Obituary "$DAKKA_MP_MINIGUN_FLAME"

    var int user_counter;
    
    States
    {
      Spawn:
        DFLM E 0 bright
        DFLM E 0 bright A_ScaleVelocity(random(90, 110) / 100.0)
        DFLM E 0 bright A_SetUserVar("user_counter", 24)
        DFLM E 0 bright A_SpawnItemEx("MinigunFlameV", 0,0,0, velx,vely,velz, angle, SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_CLIENTSIDE)
        DFLM E 1 bright A_Explode(4, 64, 0, 0, 32)
        goto Spawn2

      Spawn2:
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_SetupMinigunBurn", 32, 5, 105)
        TNT1 A 0 A_RadiusGive("MinigunBurnItem", 96, RGF_PLAYERS | RGF_MONSTERS | RGF_OBJECTS)
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_UnsetupMinigunBurn")
        TNT1 A 1 A_SetUserVar("user_counter", user_counter - 1)
        TNT1 A 0 A_JumpIf(user_counter > 0, "Spawn2")
        goto EndItAll

      EndItAll:
        TNT1 A 1
        stop

      Death:
        TNT1 A 1
        loop
    }
}


actor MinigunBurnTimer: Counter {}   // used to keep track of afterburn duration
actor MinigunNewBurn:   Counter {}   // used to signal "do this much initial burn damage"

actor MinigunBurnItem: Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_MinigunBurn")
        stop
    }
}

actor MinigunBurner
{
    +SERVERSIDEONLY
    +NOINTERACTION
    +FORCERADIUSDMG
    +NODAMAGETHRUST
    +FOILINVUL
    
    DamageType Fire
    var int user_damage;
    var int user_painfactor; // 3 if directly burning, 15    otherwise
    
    States
    {
      Spawn:
        TNT1 A 1
        stop
      
      Burn:
        TNT1 A 0 A_ChangeFlag("PAINLESS", random(1, user_painfactor) != 1)
        TNT1 A 0 A_Explode(user_damage, 8, 0, false, 8)
        stop
    }
}

actor MinigunPointerDummy
{
    +SERVERSIDEONLY
    +NOINTERACTION
    RenderStyle None
    
    States
    {
      Spawn: 
        TNT1 A -1
        stop
    }
}



actor MinigunFlameV: MinigunFlame
{
    +CLIENTSIDEONLY

    States
    {
      Spawn:
        DFLM E 0 bright
        DFMN E 0 bright A_SetUserVar("user_counter", 24)
        DFLM E 0 bright A_ScaleVelocity(random(90, 110) / 100.0)
        DFLM E 0 bright A_JumpIf(CallACS("Dakka_LessEffects"), "NoFancy")
        goto Fancy

      NoFancy:
        TNT1 A 0 A_SpawnItemEx("MinigunFlameVisual_Main", 0,0,0, 0,0,random(0, 100)/50.0, 0, SXF_ABSOLUTEPOSITION)
        TNT1 A 1 A_SetUserVar("user_counter", user_counter - 1)
        TNT1 A 0 A_JumpIf(user_counter > 0, "NoFancy")
        goto EndItAll

      Fancy:
        TNT1 A  0 A_SpawnItemEx("MinigunFlameVisual_Main",       0,             0,             0, 0,0,random(0, 100)/50.0, 0, SXF_ABSOLUTEPOSITION)
        TNT1 A 0 A_SpawnItemEx("MinigunFlameVisual", (momx/3) *  1, (momy/3) *  1, (momz/3) *  1, 0,0,random(0, 100)/50.0, 0, SXF_ABSOLUTEPOSITION)
        TNT1 A 1 A_SetUserVar("user_counter", user_counter - 1)
        TNT1 A 0 A_JumpIf(user_counter > 0, "Fancy")
        goto EndItAll

      EndItAll:
        TNT1 A 1
        stop

      Death:
        TNT1 A 0 A_JumpIf(user_counter > 0, 1)
        goto EndItAll
        
        TNT1 A 0  A_SpawnItemEx("MinigunFlameVisual_Main", 0,0,0, 0,0,0, 0, SXF_ABSOLUTEPOSITION)
        TNT1 AA 0 A_SpawnItemEx("MinigunFlameVisual", random(-150,150)/10.0, 0, random(-150,150)/10.0, random(-20,20)/10.0, 0, random(-20,20)/10.0, random(0,360))
        TNT1 A 1 A_SetUserVar("user_counter", user_counter - 3)
        loop
    }
}
        

actor MinigunFlameVisual
{
    Radius 0
    Height 0

    +NOINTERACTION
    +CLIENTSIDEONLY
    +FORCEXYBILLBOARD

    RenderStyle Add
    Scale 0.125
    Alpha 0.25

    States
    {
      Spawn:
        DFLM E 0 bright
        DFLM E 0 bright A_JumpIf(CallACS("Dakka_LessEffects"), "NoFancy")
        goto Fancy

      NoFancy:
        TNT1 A 0
        stop

      Fancy:
        DFLM E 0 ThrustThing(random(0, 255), random(0, 16) / 4.0, 1, 0)
        DFLM E 0 ThrustThingZ(0, random(0, 16) / 4.0, random(0, 1), 1)
        goto Spawn2
      
      Spawn2:
        DFLM EGIKLMNO 1 bright
        stop
    }
}

actor MinigunFlameVisual_Main: MinigunFlameVisual
{
    States
    {
      NoFancy:
        DFLM EHJNO 1 bright
        stop
    }
}

actor MinigunAfterburnFlame
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    
    RenderStyle Add
    Alpha 0.8
    
    States
    {
      Spawn:
        CFCF A 0
        CFCF A 0 A_SetScale(frandom(0.6, 0.8))
        CFCF ABCDEFGHIJKLMNOP 2 bright
        stop
    }
}