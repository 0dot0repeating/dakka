actor PlasmaCounter: Ammo
{
    +IGNORESKILL
    Inventory.Amount       1
    Inventory.MaxAmount    15

    Ammo.BackpackAmount    0
    Ammo.BackpackMaxAmount 15

    Inventory.Icon "CELLA0"
}

actor DWep_PlasmaRifle: DoomWeapon
{
    Tag "Plasma Rifle"

    Weapon.SlotNumber 6
    Weapon.SelectionOrder 100
    Weapon.SlotPriority 0.95
    Weapon.Kickback 0

    Weapon.AmmoType1 "DakkaCells"
    Weapon.AmmoType2 "DakkaCells"
    Weapon.AmmoGive1 50
    Weapon.AmmoUse1  1
    Weapon.AmmoUse2  2

    Inventory.PickupMessage "$DAKKA_PK_PLASMARIFLE"
    Obituary "$DAKKA_MP_PLASMARIFLE_BEAM"
    
    +ALT_AMMO_OPTIONAL
    +NOALERT

    States
    {
      Ready:
        DPLS A 1 A_WeaponReady
        loop

      Select:
        DPLS AA 0 A_Raise
        DPLS A 1 A_Raise
        loop

      Deselect:
        DPLS AAA 0 A_Lower
        DPLS A 1 A_Lower
        loop

      Fire:
        "----" A 0 ACS_ExecuteWithResult(DAKKA_UPDATE_SWITCHAROO, 8, 0)
        DPLS A 0 A_AlertMonsters
        DPLS A 0 A_GiveInventory("GiveScrapCounter2", 1)
        DPLS A 0 ACS_ExecuteAlways(DAKKA_RECOIL, 0, -75, 2, 6)
        DPLS A 0 A_FireCustomMissile("DakkaPlasma")
        DPLS C 2 bright A_Light2
        DPLS B 1 bright A_Light1
        DPLS B 0 A_ReFire
        goto FireDone
    
      FireDone:
        DPLS D 0 A_Light0
        DPLS D 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DPLS E 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DPLS F 4 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DPLS F 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DPLS E 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DPLS DA 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        goto Ready

      AltFire:
        DPLS A 0 A_JumpIfInventory("DakkaCells", 2, "ActualAlt")
        goto NoFire

      AltHold:
        DPLS A 0 A_JumpIfInventory("DakkaCells", 2, "ActualAlt")
        DPLS A 0 A_ClearRefire
        goto Reload

      ActualAlt:
        "----" A 0 ACS_ExecuteWithResult(DAKKA_UPDATE_SWITCHAROO, 8, 1)
        DPLS A 0 A_GunFlash
        DPLS A 0 A_AlertMonsters
        DPLS A 0 A_PlaySound("dakka/plasmabeam", CallACS("Dakka_RotateSound"))
        DPLS A 0 A_FireBullets(1.5, 1.5, 3, 20, "DakkaPlasmaPuff", FBF_NORANDOM | FBF_USEAMMO)
        DPLS A 0 A_GiveInventory("GiveScrapCounter2", 2)
        PKCG A 0 ACS_ExecuteAlways(DAKKA_RECOIL, 0, -135, 2, 7)
        DPLS C 1 bright offset(0, 35)
        DPLS B 2 bright offset(0, 36)
        DPLS A 1 bright offset(0, 35)
        DPLS A 1 bright offset(0, 34)
        DPLS A 1 bright offset(0, 32)
        DPLS B 0 A_ReFire
        goto Reload

      Reload:
        DPLS D 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DPLS E 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DPLS F 5 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DPLS F 3 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DPLS E 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DPLS DA 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        goto Ready

      NoFire:
        DPLS A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready

      Flash:
        DPLS B 2 Bright A_Light1
        DPLS A 3 Bright A_Light1
        TNT1 A 1 Bright A_Light1
        goto LightDone

      Spawn:
        DPLS Z -1
        stop
    }
}

actor DakkaGottaClimb: PowerProtection
{
    DamageFactor "DakkaPlasma", 0
}

actor PlasmaDidSpawn: Boolean {}

actor DakkaPlasma
{
    Radius 13
    Height 8
    Speed 50

    Projectile
    Damage (32)

    +MTHRUSPECIES
    +RANDOMIZE
    +FORCERADIUSDMG
    +DONTREFLECT
    +NOTIMEFREEZE
    +NOEXTREMEDEATH

    DamageType "DakkaPlasma"

    RenderStyle Add
    Alpha 0.75
    Decal PlasmaScorchNorm

    DeathSound "dakka/plasmaexplode"
    Obituary "$DAKKA_MP_PLASMARIFLE"

    States
    {
      Spawn:
        DPLA A 0 bright
        DPLA A 0 A_PlaySound("dakka/plasmafire", CHAN_WEAPON)
        DPLA A 0 A_GiveInventory("PlasmaDidSpawn")
        DPLA A 0 A_ChangeFlag("NOTIMEFREEZE", 0)
        goto Spawn2

      Spawn2:
        DPLA AAAABBBB 1 bright A_SpawnItemEx("PlasmaTrail", 0,0,0, momx,momy,momz, angle, SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPITCH)
        loop

      Death:
        DPLA A 0 A_JumpIfInventory("PlasmaDidSpawn", 1, 3)
        DPLA A 0 A_PlaySound("dakka/plasmafire", CHAN_WEAPON)
        DPLA A 0 A_ChangeFlag("NOTIMEFREEZE", 0)
        DPLA A 0 A_GiveToTarget("DakkaGottaClimb")
        DPLA A 0 A_ChangeFlag("PAINLESS", 1)
        DPLA A 0 A_Explode(20, 32, 1, 0, 32)
        DPLA A 0 A_Explode(8,  32, 0, 0, 32)
        DPLA A 0 A_TakeFromTarget("DakkaGottaClimb")
        DPLA AAAAA 0 A_SpawnItemEx("PlasmaDebris", 0,0,0, frandom(0, 5.0), 0, frandom(-1.5, 10), 180 + random(-80, 80))
        DPLA CDEFG 4 bright
        stop
    }
}

actor PlasmaTrail
{
    +NOINTERACTION
    +CLIENTSIDEONLY

    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_SpawnItemEx("PlasmaTrail_CS2", -momx * 1.33, -momy * 1.33, -momz * 1.33, 0,0,0, 0, SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPITCH)
        TNT1 A 0 A_SpawnItemEx("PlasmaTrail_CS2", -momx * 1.67, -momy * 1.67, -momz * 1.67, 0,0,0, 0, SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPITCH)
        TNT1 A 0 A_SpawnItemEx("PlasmaTrail_CS2", -momx * 2.00, -momy * 2.00, -momz * 2.00, 0,0,0, 0, SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPITCH)

        TNT1 A 0 A_JumpIf(CallACS("Dakka_LessEffects") == 1, "NoTrail")
        TNT1 A 0 A_JumpIf(CallACS("Dakka_LessEffects") == 1, "NoTrail")

        TNT1 A 0 A_SpawnItemEx("PlasmaTrail_CS",  -momx * 1.33, -momy * 1.33, -momz * 1.33, 0,0,0, 0, SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPITCH)
        TNT1 A 0 A_SpawnItemEx("PlasmaTrail_CS",  -momx * 1.67, -momy * 1.67, -momz * 1.67, 0,0,0, 0, SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPITCH)
        TNT1 A 0 A_SpawnItemEx("PlasmaTrail_CS",  -momx * 2.00, -momy * 2.00, -momz * 2.00, 0,0,0, 0, SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPITCH)
        stop

      NoTrail:
        TNT1 A 0
        stop
    }
}

actor PlasmaTrail_CS
{
    +NOINTERACTION
    +CLIENTSIDEONLY

    Scale 0.5
    RenderStyle Add
    Alpha 0.125

    States
    {
      Spawn:
        DPLA B 4 bright
        goto SpawnFade

      SpawnFade:
        DPLA AAAABBBB 1 A_FadeOut(0.025)
        loop
    }
}

actor PlasmaTrail_CS2: PlasmaTrail_CS
{
    Scale 1
    Alpha 0.5

    States
    {
      Spawn:
        DPLA B 0 bright
        goto SpawnFade

      SpawnFade:
        DPLA AAAABBBB 1 A_FadeOut(0.2)
        loop
    }
}

actor PlasmaDebris
{
    +CLIENTSIDEONLY

    Scale 0.15
    RenderStyle Add

    Projectile

    -NOGRAVITY
    +DOOMBOUNCE
    +THRUACTORS
    +NOCLIP

    Gravity 1
    BounceFactor 0.25

    States
    {
      Spawn:
        DPLA A 0 bright
        DPLA A 0 bright A_JumpIf(CallACS("Dakka_LessEffects") == 1, "Die")
        DPLA A 0 bright A_JumpIf(CallACS("Dakka_LessEffects") == 1, "Die")
        DPLA C 4 bright
        goto Spawn2

      Die:
        DPLA A 0 bright
        stop

      Spawn2:
        DPLA C 1 bright A_FadeOut(0.1)
        loop
      
      Death:
        DPLA C 1 A_FadeOut(0.1)
        loop
    }
}


actor DakkaPlasmaPuff: BulletPuff
{
    VSpeed 0

    RenderStyle Add
    Alpha 0.9
    Scale 1

    +PUFFGETSOWNER
    +PUFFONACTORS
    +ALWAYSPUFF
    +NOTIMEFREEZE
    +FORCEXYBILLBOARD
    +BLOODLESSIMPACT
    +NOEXTREMEDEATH

    +FORCERADIUSDMG
    DamageType "DakkaPlasma"

    Decal PlasmaScorchBigger

    Obituary "$DAKKA_MP_PLASMARIFLE_BEAM"

    States
    {
      Spawn:
        DPLA A 0 bright
        DPLA A 0 A_GiveToTarget("DakkaGottaClimb")
        DPLA A 0 A_ChangeFlag("PAINLESS", 1)
        DPLA A 0 A_Explode(13,  16, 0, 0, 16)
        DPLA A 0 A_Explode(7,   16, 1, 0, 16)
        DPLA A 0 A_TakeFromTarget("DakkaGottaClimb")
        DPLA A 0 ACS_NamedExecuteWithResult("Dakka_Tracer", TRACE_PLASMA, 0, -9)
        DPLA A 0 A_ChangeFlag("NOTIMEFREEZE", 0)
        DPLA CDEFG 3 bright
        stop
    }
}

actor DakkaPlasmaTrail
{
    Radius 0
    Height 0

    +NOINTERACTION
    +CLIENTSIDEONLY
    +FORCEXYBILLBOARD

    RenderStyle Add
    Scale 0.65
    Alpha 0.8

    States
    {
      Spawn:
        DPLA A 1 bright A_FadeOut(0.1)
        loop
    }
}
