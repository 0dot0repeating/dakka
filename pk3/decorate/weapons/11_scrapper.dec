actor ScrapperSide: Boolean {}
actor ScrapperAtoP: Boolean {}
actor ScrapperAltTimer: Counter {}

actor DWep_Scrappers: DoomWeapon
{
    Tag "Scrappers"

    Weapon.SlotNumber 1
    Weapon.SelectionOrder 1100

    Weapon.AmmoType1    "DakkaScrap"
    Weapon.AmmoType2    "DakkaScrap"
    Weapon.AmmoUse1     6
    Weapon.AmmoUse2     12
    Weapon.AmmoGive1    48

    Weapon.BobStyle InverseSmooth
    Weapon.BobSpeed 2.5
    Weapon.BobRangeX 0.3
    Weapon.BobRangeY 0.4

    Inventory.PickupMessage "$DAKKA_PK_SCRAPPER"
    Obituary "$DAKKA_MP_SCRAPPER"

    +ALT_AMMO_OPTIONAL
    +NOALERT

    States
    {
      Select:
        SCPR AA 0 A_Raise
        SCPR A 1 A_Raise
        loop

      Deselect:
        SCPR A   0 A_Light0
        SCPR AAA 0 A_Lower
        SCPR A   1 A_Lower
        loop

      Ready:
        SCPR A 0 A_TakeInventory("ScrapperAtoP")
        SCPR A 0 A_TakeInventory("ScrapperAltTimer")
        SCPR A 1 A_WeaponReady
        loop

      Fire:
        RSCP A 0 A_JumpIfInventory("ScrapperAtoP", 1, "FromAlt")
        RSCP A 0 A_TakeInventory("ScrapperAltTimer")
        RSCP A 0 A_GiveInventory("ScrapperAltTimer", 2)
        RSCP A 0 A_JumpIfInventory("ScrapperSide", 1, "RStill_LStart")
        goto RStart_LStill

      FromAlt:
        SCPR A 0 A_TakeInventory("ScrapperAtoP")
        RSCP A 0 A_JumpIfInventory("ScrapperSide", 1, "RAlmost_LStart")
        goto RStart_LAlmost

      RStart_LStill:
        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 1, 0)
        RSCP A 0 A_AlertMonsters
        RSCP A 0 A_GiveInventory("ScrapperSide")
        RSCP A 0 A_TakeInventory("ScrapperAltTimer", 1)
        RSCP A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, 2, 2, 7)
        RSCP AA 0 A_PlaySound("dakka/scrapperfire", CallACS("Dakka_RotateSound"))
        RSCP A 0 ACS_NamedExecuteWithResult("Dakka_UseAmmo", AMMO_SCRAP, 6)
        RSCP A 0 A_FireCustomMissile("ScrapperShotSpawner", 0, false, 11)
        RSCP A 1 bright A_Light(6)
        RSCP B 1 bright A_Light(5)
        RSCP C 1 bright A_Light(4)
        RSCP D 1 bright A_Light(3)
        RSCP E 0 bright A_Light(2)
        RSCP E 1 bright A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        RSCP E 0 bright A_Light(1)
        RSCP E 1 bright A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        RSCP E 0 bright A_Light(0)
        RSCP F 3 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        RSCP F 0 A_CheckReload
        RSCP F 0 A_JumpIfInventory("ScrapperAltTimer", 1, 2)
        RSCP F 0 A_JumpIfInventory("AltFired", 1, "PrimaryToAlt")
        RSCP F 0 A_JumpIfInventory("MainFired", 1, "REnd_LStart")
        goto REnd_LStill

      RStill_LStart:
        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 1, 0)
        RSCP A 0 A_AlertMonsters
        RSCP A 0 A_TakeInventory("ScrapperSide")
        RSCP A 0 A_TakeInventory("ScrapperAltTimer", 1)
        RSCP A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, 2, 2, 7)
        RSCP AA 0 A_PlaySound("dakka/scrapperfire", CallACS("Dakka_RotateSound"))
        RSCP A 0 ACS_NamedExecuteWithResult("Dakka_UseAmmo", AMMO_SCRAP, 6)
        RSCP A 0 A_FireCustomMissile("ScrapperShotSpawner", 0, false, -11)
        LSCP A 1 bright A_Light(6)
        LSCP B 1 bright A_Light(5)
        LSCP C 1 bright A_Light(4)
        LSCP D 1 bright A_Light(3)
        LSCP E 0 bright A_Light(2)
        LSCP E 1 bright A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        LSCP E 0 bright A_Light(1)
        LSCP E 1 bright A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        LSCP E 0 bright A_Light(0)
        LSCP F 3 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        RSCP F 0 A_CheckReload
        RSCP F 0 A_JumpIfInventory("ScrapperAltTimer", 1, 2)
        RSCP F 0 A_JumpIfInventory("AltFired", 1, "PrimaryToAlt")
        RSCP F 0 A_JumpIfInventory("MainFired", 1, "RStart_LEnd")
        goto RStill_LEnd


      RStart_LEnd:
        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 1, 0)
        RSCP A 0 A_AlertMonsters
        RSCP A 0 A_GiveInventory("ScrapperSide")
        RSCP A 0 A_TakeInventory("ScrapperAltTimer", 1)
        RSCP A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, 2, 2, 7)
        RSCP AA 0 A_PlaySound("dakka/scrapperfire", CallACS("Dakka_RotateSound"))
        RSCP A 0 ACS_NamedExecuteWithResult("Dakka_UseAmmo", AMMO_SCRAP, 6)
        RSCP A 0 A_FireCustomMissile("ScrapperShotSpawner", 0, false, 11)
        DSCP J 1 bright A_Light(7)
        DSCP K 1 bright A_Light(6)
        DSCP L 1 bright A_Light(5)
        DSCP M 1 bright A_Light(4)
        DSCP N 0 bright A_Light(3)
        DSCP O 1 bright A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DSCP O 0 bright A_Light(2)
        DSCP O 1 bright A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DSCP P 0 bright A_Light(1)
        DSCP P 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DSCP P 0 A_Light(0)
        DSCP QR 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DSCP R 0 A_CheckReload
        DSCP R 0 A_JumpIfInventory("ScrapperAltTimer", 1, 2)
        DSCP R 0 A_JumpIfInventory("AltFired", 1, "PrimaryToAlt")
        DSCP R 0 A_JumpIfInventory("MainFired", 1, "REnd_LStart")
        goto REnd_LStill

      REnd_LStart:
        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 1, 0)
        RSCP A 0 A_AlertMonsters
        RSCP A 0 A_TakeInventory("ScrapperSide")
        RSCP A 0 A_TakeInventory("ScrapperAltTimer", 1)
        RSCP A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, 2, 2, 7)
        RSCP AA 0 A_PlaySound("dakka/scrapperfire", CallACS("Dakka_RotateSound"))
        RSCP A 0 ACS_NamedExecuteWithResult("Dakka_UseAmmo", AMMO_SCRAP, 6)
        RSCP A 0 A_FireCustomMissile("ScrapperShotSpawner", 0, false, -11)
        DSCP A 1 bright A_Light(7)
        DSCP B 1 bright A_Light(6)
        DSCP C 1 bright A_Light(5)
        DSCP D 1 bright A_Light(4)
        DSCP E 0 bright A_Light(3)
        DSCP F 1 bright A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DSCP F 0 bright A_Light(2)
        DSCP F 1 bright A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DSCP G 0 bright A_Light(1)
        DSCP G 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DSCP G 0 A_Light(0)
        DSCP HI 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DSCP I 0 A_CheckReload
        DSCP I 0 A_JumpIfInventory("ScrapperAltTimer", 1, 2)
        DSCP I 0 A_JumpIfInventory("AltFired", 1, "PrimaryToAlt")
        DSCP I 0 A_JumpIfInventory("MainFired", 1, "RStart_LEnd")
        goto RStill_LEnd


      RStart_LAlmost:
        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 1, 0)
        RSCP A 0 A_AlertMonsters
        RSCP A 0 A_GiveInventory("ScrapperSide")
        RSCP A 0 A_TakeInventory("ScrapperAltTimer", 1)
        RSCP A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, 2, 2, 7)
        RSCP AA 0 A_PlaySound("dakka/scrapperfire", CallACS("Dakka_RotateSound"))
        RSCP A 0 ACS_NamedExecuteWithResult("Dakka_UseAmmo", AMMO_SCRAP, 6)
        RSCP A 0 A_FireCustomMissile("ScrapperShotSpawner", 0, false, 11)
        RSCP L 1 bright A_Light(8)
        RSCP M 1 bright A_Light(7)
        RSCP N 1 bright A_Light(5)
        RSCP O 1 bright A_Light(4)
        RSCP P 0 bright A_Light(3)
        RSCP P 1 bright A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        RSCP Q 0 bright A_Light(2)
        RSCP Q 1 bright A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        RSCP R 0 bright A_Light(1)
        RSCP R 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        RSCP R 0 bright A_Light(0)
        RSCP R 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        RSCP R 0 A_CheckReload
        RSCP R 0 A_JumpIfInventory("ScrapperAltTimer", 1, 2)
        RSCP R 0 A_JumpIfInventory("AltFired", 1, "PrimaryToAlt")
        RSCP R 0 A_JumpIfInventory("MainFired", 1, "REnd_LStart")
        goto REnd_LStill

      RAlmost_LStart:
        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 1, 0)
        RSCP A 0 A_AlertMonsters
        LSCP A 0 A_TakeInventory("ScrapperSide")
        RSCP A 0 A_TakeInventory("ScrapperAltTimer", 1)
        RSCP A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, 2, 2, 7)
        RSCP AA 0 A_PlaySound("dakka/scrapperfire", CallACS("Dakka_RotateSound"))
        RSCP A 0 ACS_NamedExecuteWithResult("Dakka_UseAmmo", AMMO_SCRAP, 6)
        RSCP A 0 A_FireCustomMissile("ScrapperShotSpawner", 0, false, -11)
        LSCP L 1 bright A_Light(8)
        LSCP M 1 bright A_Light(7)
        LSCP N 1 bright A_Light(5)
        LSCP O 1 bright A_Light(4)
        LSCP P 0 bright A_Light(3)
        LSCP P 1 bright A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        LSCP Q 0 bright A_Light(2)
        LSCP Q 1 bright A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        LSCP R 0 bright A_Light(1)
        LSCP R 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        LSCP R 0 bright A_Light(0)
        LSCP R 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        LSCP R 0 A_CheckReload
        LSCP F 0 A_JumpIfInventory("ScrapperAltTimer", 1, 2)
        LSCP R 0 A_JumpIfInventory("AltFired", 1, "PrimaryToAlt")
        LSCP R 0 A_JumpIfInventory("MainFired", 1, "RStart_LEnd")
        goto RStill_LEnd


      REnd_LStill:
        RSCP F 0 A_JumpIfInventory("AltFired", 1, "PrimaryToAlt")
        RSCP F 0 A_JumpIfInventory("MainFired", 1, "REnd_LStart")
        RSCP F 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)

        RSCP G 0 A_JumpIfInventory("AltFired", 1, "PrimaryToAlt")
        RSCP G 0 A_JumpIfInventory("MainFired", 1, "REnd_LStart")
        RSCP G 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)

        RSCP H 0 A_JumpIfInventory("AltFired", 1, "PrimaryToAlt")
        RSCP H 0 A_JumpIfInventory("MainFired", 1, "RAlmost_LStart")
        RSCP H 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)

        RSCP I 0 A_JumpIfInventory("AltFired", 1, "PrimaryToAlt")
        RSCP I 0 A_JumpIfInventory("MainFired", 1, "RAlmost_LStart")
        RSCP I 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)

        RSCP JK 1 A_WeaponReady(WRF_NOBOB)
        goto Ready

      RStill_LEnd:
        LSCP F 0 A_JumpIfInventory("AltFired", 1, "PrimaryToAlt")
        LSCP F 0 A_JumpIfInventory("MainFired", 1, "RStart_LEnd")
        LSCP F 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)

        LSCP G 0 A_JumpIfInventory("AltFired", 1, "PrimaryToAlt")
        LSCP G 0 A_JumpIfInventory("MainFired", 1, "RStart_LEnd")
        LSCP G 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)

        LSCP H 0 A_JumpIfInventory("AltFired", 1, "PrimaryToAlt")
        LSCP H 0 A_JumpIfInventory("MainFired", 1, "RStart_LAlmost")
        LSCP H 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)

        LSCP I 0 A_JumpIfInventory("AltFired", 1, "PrimaryToAlt")
        LSCP I 2 A_JumpIfInventory("MainFired", 1, "RStart_LAlmost")
        LSCP JK 1 A_WeaponReady(WRF_NOBOB)
        goto Ready


      PrimaryToAlt:
        "----" A 1 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        goto AltFire // anyway

      AltToPrimary:
        "----" A 0 A_GiveInventory("ScrapperAtoP")
        "----" A 1 A_WeaponReady(WRF_NOSECONDARY | WRF_NOBOB)
        goto Fire // anyway

      AltToPrimary2:
        "----" A 1 A_WeaponReady(WRF_NOSECONDARY | WRF_NOBOB)
        goto Fire // anyway

      AltFire:
        RSCP A 0 A_JumpIfNoAmmo("AltNo")
        goto AltFire2

      AltFire2:
        RSCP A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 1, 1)
        RSCP A 0 A_AlertMonsters
        RSCP A 0 A_TakeInventory("ScrapperAltTimer")
        RSCP A 0 A_GiveInventory("ScrapperAltTimer", 2)
        RSCP AA 0 A_PlaySound("dakka/scrapperaltfire", CallACS("Dakka_RotateSound"))
        RSCP A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, 6, 3, 16)
        RSCP A 0 ACS_NamedExecuteWithResult("Dakka_UseAmmo", AMMO_SCRAP, 12)
        RSCP A 0 A_FireCustomMissile("ScrapperGrenade", 0, false, -11, 0, 0, 5)
        RSCP A 0 A_FireCustomMissile("ScrapperGrenade", 0, false,  11, 0, 0, 5)
        SCPR B 1 bright A_Light(9)
        SCPR C 1 bright A_Light(8)
        SCPR D 1 bright A_Light(6)
        SCPR E 1 bright A_Light(5)
        SCPR F 1 bright A_Light(4)
        SCPR G 1 bright A_Light(3)
        SCPR G 1 bright A_Light(2)
        SCPR G 1 bright A_Light(1)
        SCPR G 1 bright A_Light(0)
        SCPR HIJK 2 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        SCRR L 0 A_JumpIfInventory("MainFired", 1, "AltToPrimary")
        SCPR L 2 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        SCPR M 0 A_JumpIfInventory("MainFired", 1, "AltToPrimary")
        SCPR M 0 A_Refire
        SCPR M 2 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        SCPR N 0 A_JumpIfInventory("MainFired", 1, "AltToPrimary2")
        SCPR N 0 A_Refire
        SCPR N 0 A_JumpIfNoAmmo("AltEnd_NoAmmo")
        SCPR N 2 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        SCPR N 0 A_JumpIfInventory("MainFired", 1, "AltToPrimary2")
        goto Ready

      AltEnd_NoAmmo:
        SCPR N 2 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        SCPR N 0 A_JumpIfInventory("MainFired", 1, "AltToPrimary2")
        goto Ready

      AltNo:
        SCPR A 0 A_TakeInventory("ScrapperAtoP")
        SCPR A 0 A_TakeInventory("ScrapperAltTimer")
        SCPR A 0 ACS_NamedExecuteWithResult("Dakka_OutOfAmmo", 1)
        SCPR A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready

      Spawn:
        SCPR Z -1
        stop
    }
}


actor ScrapperShotSpawner
{
    Radius 2
    Height 2
    Speed  10

    Projectile
    +NOINTERACTION
    +SERVERSIDEONLY

    var int user_spawncount;
    var int user_spawnvelx;
    var int user_spawnvely;
    var int user_spawnvelz;

    var int user_velx;
    var int user_vely;
    var int user_velz;
    var int user_t;

    Damage 6
    Scale 0.25

    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_JumpIf(CallACS("Dakka_IsClient") == false, 1)
        goto NETCODE

        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_ProjUpdate")
        goto SpawnCommon

      Death:
        TNT1 A 0 A_JumpIf(CallACS("Dakka_IsClient") == false, 1)
        goto NETCODE

        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_ProjDeathUpdate")
        goto SpawnCommon

      NETCODE:
        TNT1 A 0
        stop


      SpawnCommon:
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_CalcProjAngle", 25, random(-1200, 1200), random(300, 1100), true)
        TNT1 A 0 A_SpawnItemEx("ScrapperShot", 0,0,0, user_spawnvelx/65536.0, user_spawnvely/65536.0, user_spawnvelz/65536.0, angle, SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SetUserVar("user_spawncount", user_spawncount+1)
        TNT1 A 0 A_JumpIf(user_spawncount >= 5, "SpawnEnd")
        loop

      SpawnEnd:
        TNT1 A 4
        stop
    }
}

actor ScrapperShot
{
    Radius 3
    Height 6

    Projectile
    DamageType "Scrap"
    Speed 55

    -NOGRAVITY

    +SKYEXPLODE
    +MTHRUSPECIES
    +FORCEXYBILLBOARD
    +BOUNCEONWALLS
    +BOUNCEONFLOORS
    +BOUNCEONCEILINGS
    +BOUNCEAUTOOFFFLOORONLY

    Gravity 0.8
    BounceFactor 0.65
    WallBounceFactor 0.75
    BounceCount 2

    Scale 0.3
    Obituary "$DAKKA_MP_SCRAPPER"

    var int user_spawnvelx;
    var int user_spawnvely;
    var int user_spawnvelz;

    var int user_velx;
    var int user_vely;
    var int user_velz;
    var int user_t;
    var int user_deatht;

    States
    {
      Spawn:
        SCRS A 0 bright
        SCRS A 0 bright A_Jump(64, 2)
        SCRS A 0 bright A_PlaySound("dakka/scrapfly", 7)

        SCRS A 0 bright A_SetUserVar("user_deatht", random(9, 13))
        SCRS A 0 bright A_ChangeFlag("PAINLESS", random(1,8) != 1)
        SCRS A 0 bright A_Jump(255, "Spawn1", "Spawn2", "Spawn3")
        goto Spawn1

      Spawn1:
        SCRS A 0 bright
        goto SpawnCommon

      Spawn2:
        SCRS B 0 bright
        goto SpawnCommon

      Spawn3:
        SCRS C 0 bright
        goto SpawnCommon

      SpawnCommon:
        SCRS "#" 0 bright A_SpawnItemEx("ScrapperTrail", -(1*momx)/3.0, (1*momy)/3.0, -(1*momz)/3.0, 0,0,0, 0, SXF_ABSOLUTEANGLE)
        SCRS "#" 0 bright A_SpawnItemEx("ScrapperTrail", -(2*momx)/3.0, (2*momy)/3.0, -(2*momz)/3.0, 0,0,0, 0, SXF_ABSOLUTEANGLE)
        SCRS "#" 0 bright A_SpawnItemEx("ScrapperTrail", 0,              0,           0)
        SCRS "#" 0 bright ACS_NamedExecuteWithResult("Dakka_ProjUpdate")
        SCRS "#" 0 bright A_Explode(2, 32, false)
        SCRS "#" 1 bright A_JumpIf(user_t == user_deatht, "Death")
        loop

      Death:
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_ProjDeathUpdate")
        TNT1 A 0 A_SpawnItemEx("ScrapShotDeath", 0,0,0, 0,0,0, 0, SXF_CLIENTSIDE)
        TNT1 A 0 A_PlaySound("dakka/scrappersplit")
        TNT1 A 0 A_Explode(3, 96, 0)

        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_CalcProjAngle", random(10, 14), random(-4000, -2000), random(1000, 3000), true)
        TNT1 A 0 A_SpawnItemEx("SplitScrap", 0,0,0, user_spawnvelx/65536.0, user_spawnvely/65536.0, user_spawnvelz/65536.0, angle, SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPOINTERS)

        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_CalcProjAngle", random(10, 14), random(-1000,  1000), random(1000, 3000), true)
        TNT1 A 0 A_SpawnItemEx("SplitScrap", 0,0,0, user_spawnvelx/65536.0, user_spawnvely/65536.0, user_spawnvelz/65536.0, angle, SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPOINTERS)

        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_CalcProjAngle", random(10, 14), random( 2000,  4000), random(1000, 3000), true)
        TNT1 A 4 A_SpawnItemEx("SplitScrap", 0,0,0, user_spawnvelx/65536.0, user_spawnvely/65536.0, user_spawnvelz/65536.0, angle, SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPOINTERS)
        stop
    }
}


actor ScrapperShot_Forward: ScrapperShot
{
    Gravity 0.4

    States
    {
      Death:
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_ProjDeathUpdate")
        TNT1 A 0 A_SpawnItemEx("ScrapShotDeath", 0,0,0, 0,0,0, 0, SXF_CLIENTSIDE)
        TNT1 A 0 A_PlaySound("dakka/scrappersplit")
        TNT1 A 0 A_Explode(3, 96, 0)

        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_CalcProjAngle", random(10, 14), random(-4000, -2000), random(0, 500), true)
        TNT1 A 0 A_SpawnItemEx("SplitScrap", 0,0,0, user_spawnvelx/65536.0, user_spawnvely/65536.0, user_spawnvelz/65536.0, angle, SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPOINTERS)

        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_CalcProjAngle", random(10, 14), random(-1000,  1000), random(0, 500), true)
        TNT1 A 0 A_SpawnItemEx("SplitScrap", 0,0,0, user_spawnvelx/65536.0, user_spawnvely/65536.0, user_spawnvelz/65536.0, angle, SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPOINTERS)

        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_CalcProjAngle", random(10, 14), random( 2000,  4000), random(0, 500), true)
        TNT1 A 4 A_SpawnItemEx("SplitScrap", 0,0,0, user_spawnvelx/65536.0, user_spawnvely/65536.0, user_spawnvelz/65536.0, angle, SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPOINTERS)
        stop
    }
}


actor ScrapShotDeath
{
    +NOINTERACTION
    +FORCEXYBILLBOARD
    // Can't be CLIENTSIDEONLY, or it doesn't spawn online. GENIUS

    Scale 0.5
    RenderStyle Add

    States
    {
      Spawn:
        BAL1 CDE 5 bright
        stop
    }
}

actor ScrapperTrail
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    +FORCEXYBILLBOARD

    RenderStyle Add
    Alpha 0.15
    Scale 0.2

    States
    {
      Spawn:
        SCRT A 2 bright
        SCRT A 2 bright A_JumpIf(CallACS("Dakka_LessEffects") == true, "FastFade")
        SCRT A 1 bright A_FadeOut(0.08)
        wait
      
      FastFade:
        SCRT A 1 bright A_FadeOut(0.08)
        wait
    }
}

actor SplitScrap
{
    Radius 8
    Height 8

    Projectile
    DamageType "Scrap"
    Speed 15

    -NOGRAVITY
    +DOOMBOUNCE
    +SKYEXPLODE
    +FORCERADIUSDMG
    +NODAMAGETHRUST
    +THRUACTORS
    +BLOODLESSIMPACT
    +FORCEXYBILLBOARD

    Gravity 0.65

    BounceFactor 0.35
    WallBounceFactor 0.5
    BounceCount 2

    Scale 0.2
    Obituary "$DAKKA_MP_SCRAPPER"

    var int user_harmlesst;
    var int user_rippert;
    var int user_deatht;
    var int user_t;

    States
    {
      Spawn:
        SCRS A 0
        SCRS A 0 bright A_SetUserVar("user_harmlesst",   1)
        SCRS A 0 bright A_SetUserVar("user_rippert",     random( 5,   8) + user_harmlesst)
        SCRS A 0 bright A_SetUserVar("user_deatht",      random(95, 115))
        SCRS A 0 bright A_Jump(255, "Spawn1", "Spawn2", "Spawn3")
        goto Spawn1

      Spawn1:
        SCRS A 0 bright
        goto SpawnCommon

      Spawn2:
        SCRS B 0 bright
        goto SpawnCommon

      Spawn3:
        SCRS C 0 bright
        goto SpawnCommon

      SpawnCommon:
        SCRS "#" 0 A_SetUserVar("user_t", user_t + 1)
        SCRS "#" 0 bright A_ChangeFlag("PAINLESS", random(1,24) != 1)
        
        SCRS "#" 0 A_JumpIf(user_t >= 70,           3)
        SCRS "#" 0 A_SpawnItemEx("ScrapperTrail2", -(1*momx)/2.0, (1*momy)/2.0, -(1*momz)/2.0, 0,0,0, 0, SXF_ABSOLUTEANGLE)
        SCRS "#" 0 A_SpawnItemEx("ScrapperTrail2", 0,              0,           0)

        SCRS "#" 0 A_JumpIf((user_harmlesst >= user_t) || ((user_t - user_harmlesst) % 2 == 0), 2)
        SCRS "#" 0 bright A_Explode(2, 32, false, false, 32)
        
        SCRS "#" 0 A_JumpIf(user_t >= 105,           "Death")
        SCRS "#" 1 A_JumpIf(user_t == user_rippert,  "Unrip")
        loop
      
      Unrip:
        SCRS "#" 1 bright A_ChangeFlag("THRUACTORS", false)
        goto SpawnCommon

      Death:
        BAL1 C 0 A_ChangeFlag("NOGRAVITY", 1)
        BAL1 C 0 A_SetTranslucent(1, 1)
        BAL1 C 0 A_Stop
        BAL1 CDE 5 bright
        stop
    }
}

actor ScrapperTrail2: ScrapperTrail
{
    Scale 0.15
    Alpha 0.2

    States
    {
      Spawn:
        SCRT A 2 bright
        SCRT A 2 bright A_JumpIf(CallACS("Dakka_LessEffects") == true, "FastFade")
        SCRT A 1 bright A_FadeOut(0.04)
        wait
      
      FastFade:
        SCRT A 1 bright A_FadeOut(0.06)
        wait
    }
}

actor ScrapperTrail3: ScrapperTrail
{
    Scale 0.35
    Alpha 0.3

    States
    {
      Spawn:
        SCRT A 2 bright
        SCRT A 2 bright A_JumpIf(CallACS("Dakka_LessEffects") == true, "FastFade")
        SCRT A 1 bright A_FadeOut(0.04)
        wait
      
      FastFade:
        SCRT A 1 bright A_FadeOut(0.06)
        wait
    }
}


actor ScrapperGrenade
{
    Radius 4
    Height 4

    Projectile
    DamageType "Scrap"
    Speed 50

    +MTHRUSPECIES
    +FORCEXYBILLBOARD
    +HITTRACER
    -NOGRAVITY
    Gravity 0.65  // I sure do love this gravity value.
    
    BounceType Doom
    +BOUNCEONWALLS
    +BOUNCEONCEILINGS
    -BOUNCEONFLOORS
    -ALLOWBOUNCEONACTORS
    BounceCount      2

    Scale 0.3
    Obituary "$DAKKA_MP_SCRAPPER"

    var int user_spawnvelx;
    var int user_spawnvely;
    var int user_spawnvelz;

    var int user_velx;
    var int user_vely;
    var int user_velz;
    var int user_t;

    States
    {
      Spawn:
        SCRN A 0 bright
        SCRN A 0 A_PlaySound("dakka/scrapfly", 7)
      
      SpawnChoose:
        SCRN A 0 A_JumpIf((user_t / 4) % 3 == 0, "Spawn1")
        SCRN A 0 A_JumpIf((user_t / 4) % 3 == 1, "Spawn2")
        SCRN A 0 A_JumpIf((user_t / 4) % 3 == 2, "Spawn3")
        goto Spawn1

      Spawn1:
        SCRN A 0 bright
        goto SpawnCommon

      Spawn2:
        SCRN B 0 bright
        goto SpawnCommon

      Spawn3:
        SCRN C 0 bright
        goto SpawnCommon

      SpawnCommon:
        SCRN "#" 1 bright ACS_NamedExecuteWithResult("Dakka_ProjUpdate")
        SCRN "#" 0 A_SpawnItemEx("ScrapperTrail3", -(1*momx)/2.0, (1*momy)/2.0, -(1*momz)/2.0, 0,0,0, 0, SXF_ABSOLUTEANGLE)
        SCRN "#" 0 A_SpawnItemEx("ScrapperTrail3", 0,              0,           0)
        SCRN "#" 0 A_JumpIf(user_t     == 54, "Detonate")
        SCRN "#" 0 A_JumpIf(user_t     == 21, "NoMoreBounce")
        SCRN "#" 0 A_JumpIf(user_t % 2 == 0,  "SpawnScrap")
        goto SpawnChoose
      
      NoMoreBounce:
        SCRN "#" 0 A_ChangeFlag("BOUNCEONWALLS",    false)
        SCRN "#" 0 A_ChangeFlag("BOUNCEONCEILINGS", false)
        goto SpawnChoose
        
      SpawnScrap:
        SCRN "#" 0 A_SpawnItemEx("SplitScrap", 0,0,0, random(0,90)/10.0, 0, random(-30,60)/10.0, random(0, 360), SXF_TRANSFERPOINTERS)
        goto SpawnChoose

      Detonate:
        TNT1 A 0 A_Stop
        TNT1 A 0 A_NoGravity
        goto Death
        
      Death:
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_ProjDeathUpdate")
        TNT1 A 0 A_SpawnItemEx("ScrapShotDeath", 0,0,0, 0,0,0, 0, SXF_CLIENTSIDE)
        TNT1 A 0 A_PlaySound("dakka/scrapnadeexplode")
        TNT1 A 0 A_Explode(20, 96, 0)

        TNT1 A 0 A_JumpIf(CallACS("Dakka_HitEnemy"), "EnemyBlast")
        TNT1 A 0 A_JumpIf(z - floorz   < 2, "FloorBlast")
        TNT1 A 0 A_JumpIf(ceilingz - z < 8, "CeilingBlast")
        goto NormalBlast


      NormalBlast:
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_CalcProjAngle", random(15, 20), random(12500, 14500), random(-1500, -500), true)
        TNT1 A 0 A_SpawnItemEx("ScrapperShot", 0,0,0, user_spawnvelx/65536.0, user_spawnvely/65536.0, -user_spawnvelz/65536.0, angle+180, SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPOINTERS)

        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_CalcProjAngle", random(15, 20), random(15500, 17500), random(-1500, -500), true)
        TNT1 A 0 A_SpawnItemEx("ScrapperShot", 0,0,0, user_spawnvelx/65536.0, user_spawnvely/65536.0, -user_spawnvelz/65536.0, angle+180, SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPOINTERS)

        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_CalcProjAngle", random(15, 20), random(18500, 20500), random(-1500, -500), true)
        TNT1 A 0 A_SpawnItemEx("ScrapperShot", 0,0,0, user_spawnvelx/65536.0, user_spawnvely/65536.0, -user_spawnvelz/65536.0, angle+180, SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPOINTERS)

        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_CalcProjAngle", random(15, 20), random(21500, 23500), random(-1500, -500), true)
        TNT1 A 4 A_SpawnItemEx("ScrapperShot", 0,0,0, user_spawnvelx/65536.0, user_spawnvely/65536.0, -user_spawnvelz/65536.0, angle+180, SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPOINTERS)
        stop

      EnemyBlast:
        TNT1 A 0 A_SpawnItemEx("ScrapperShot_Forward", 0,0,0, frandom(10,15), 0, frandom(2,5), frandom(-60,-30), SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("ScrapperShot_Forward", 0,0,0, frandom(10,15), 0, frandom(2,5), frandom(-30,  0), SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("ScrapperShot_Forward", 0,0,0, frandom(10,15), 0, frandom(2,5), frandom(  0, 30), SXF_TRANSFERPOINTERS)
        TNT1 A 4 A_SpawnItemEx("ScrapperShot_Forward", 0,0,0, frandom(10,15), 0, frandom(2,5), frandom( 30, 60), SXF_TRANSFERPOINTERS)
        stop

      FloorBlast:
        TNT1 A 0 A_SpawnItemEx("ScrapperShot", 0,0,0, frandom(10,15), 0, frandom(6,9), frandom(-60,-30), SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("ScrapperShot", 0,0,0, frandom(10,15), 0, frandom(6,9), frandom(-30,  0), SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("ScrapperShot", 0,0,0, frandom(10,15), 0, frandom(6,9), frandom(  0, 30), SXF_TRANSFERPOINTERS)
        TNT1 A 4 A_SpawnItemEx("ScrapperShot", 0,0,0, frandom(10,15), 0, frandom(6,9), frandom( 30, 60), SXF_TRANSFERPOINTERS)
        stop

      CeilingBlast:
        TNT1 A 0 A_SpawnItemEx("ScrapperShot", 0,0,0, frandom(8,12), 0, frandom(-8,-6), frandom(-60,-30), SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("ScrapperShot", 0,0,0, frandom(8,12), 0, frandom(-8,-6), frandom(-30,  0), SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("ScrapperShot", 0,0,0, frandom(8,12), 0, frandom(-8,-6), frandom(  0, 30), SXF_TRANSFERPOINTERS)
        TNT1 A 4 A_SpawnItemEx("ScrapperShot", 0,0,0, frandom(8,12), 0, frandom(-8,-6), frandom( 30, 60), SXF_TRANSFERPOINTERS)
        stop
    }
}
