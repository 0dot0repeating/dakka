actor ScrapperSide: Boolean {}
actor ScrapperAtoP: Boolean {}
actor ScrapperAltTimer: Counter {}
actor ScrapTimer: Counter {}

actor DWep_Scrappers: DoomWeapon
{
    Tag "Scrappers"

    Weapon.SlotNumber 1
    Weapon.SelectionOrder 1100

    Weapon.AmmoType1    "DakkaScrap"
    Weapon.AmmoType2    "DakkaScrap"
    Weapon.AmmoUse1     6
    Weapon.AmmoUse2     12
    Weapon.AmmoGive1    48
    
    Weapon.BobStyle InverseSmooth
    Weapon.BobSpeed 2.5
    Weapon.BobRangeX 0.3
    Weapon.BobRangeY 0.4

    Inventory.PickupMessage "$DAKKA_PK_SCRAPPER"
    Obituary "$DAKKA_MP_SCRAPPER"

    +NOALERT

    States
    {
      Select:
        SCPR AA 0 A_Raise
        SCPR A 1 A_Raise
        loop

      Deselect:
        SCPR A   0 A_Light0
        SCPR AAA 0 A_Lower
        SCPR A   1 A_Lower
        loop

      Ready:
        SCPR A 0 A_TakeInventory("ScrapperAtoP")
        SCPR A 0 A_TakeInventory("ScrapperAltTimer")
        SCPR A 1 A_WeaponReady
        loop

      Fire:
        RSCP A 0 A_JumpIfInventory("ScrapperAtoP", 1, "FromAlt")
        RSCP A 0 A_TakeInventory("ScrapperAltTimer")
        RSCP A 0 A_GiveInventory("ScrapperAltTimer", 2)
        RSCP A 0 A_JumpIfInventory("ScrapperSide", 1, "RStill_LStart")
        goto RStart_LStill

      FromAlt:
        SCPR A 0 A_TakeInventory("ScrapperAtoP")
        RSCP A 0 A_JumpIfInventory("ScrapperSide", 1, "RAlmost_LStart")
        goto RStart_LAlmost

      RStart_LStill:
        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 1, 0)
        RSCP A 0 A_AlertMonsters
        RSCP A 0 A_GiveInventory("ScrapperSide")
        RSCP A 0 A_TakeInventory("ScrapperAltTimer", 1)
        RSCP AA 0 A_PlaySound("dakka/scrapperfire", CallACS("Dakka_RotateSound"))
        RSCP AAAAA 0 A_FireCustomMissile("ScrapperShot",
                                    CallACS("Dakka_Spread", -1200, 1200) / 65536.0, 0, 11,0,0,
                                    CallACS("Dakka_Spread", -600, 600) / 65536.0)
        RSCP A 0 A_FireCustomMissile("AmmoWaster")
        RSCP A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, 2, 2, 7)
        RSCP A 1 bright A_Light(6)
        RSCP B 1 bright A_Light(5)
        RSCP C 1 bright A_Light(4)
        RSCP D 1 bright A_Light(3)
        RSCP E 0 bright A_Light(2)
        RSCP E 1 bright A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        RSCP E 0 bright A_Light(1)
        RSCP E 1 bright A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        RSCP E 0 bright A_Light(0)
        RSCP F 3 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        RSCP F 0 A_CheckReload
        RSCP F 0 A_JumpIfInventory("ScrapperAltTimer", 1, 2)
        RSCP F 0 A_JumpIfInventory("AltFired", 1, "PrimaryToAlt")
        RSCP F 0 A_JumpIfInventory("MainFired", 1, "REnd_LStart")
        goto REnd_LStill

      RStill_LStart:
        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 1, 0)
        RSCP A 0 A_AlertMonsters
        RSCP A 0 A_TakeInventory("ScrapperSide")
        RSCP A 0 A_TakeInventory("ScrapperAltTimer", 1)
        RSCP AA 0 A_PlaySound("dakka/scrapperfire", CallACS("Dakka_RotateSound"))
        RSCP AAAAA 0 A_FireCustomMissile("ScrapperShot",
                                    CallACS("Dakka_Spread", -1200, 1200) / 65536.0, 0, -11,0,0,
                                    CallACS("Dakka_Spread", -600, 600) / 65536.0)
        RSCP A 0 A_FireCustomMissile("AmmoWaster")
        RSCP A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, 2, 2, 7)
        LSCP A 1 bright A_Light(6)
        LSCP B 1 bright A_Light(5)
        LSCP C 1 bright A_Light(4)
        LSCP D 1 bright A_Light(3)
        LSCP E 0 bright A_Light(2)
        LSCP E 1 bright A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        LSCP E 0 bright A_Light(1)
        LSCP E 1 bright A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        LSCP E 0 bright A_Light(0)
        LSCP F 3 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        RSCP F 0 A_CheckReload
        RSCP F 0 A_JumpIfInventory("ScrapperAltTimer", 1, 2)
        RSCP F 0 A_JumpIfInventory("AltFired", 1, "PrimaryToAlt")
        RSCP F 0 A_JumpIfInventory("MainFired", 1, "RStart_LEnd")
        goto RStill_LEnd


      RStart_LEnd:
        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 1, 0)
        RSCP A 0 A_AlertMonsters
        RSCP A 0 A_GiveInventory("ScrapperSide")
        RSCP A 0 A_TakeInventory("ScrapperAltTimer", 1)
        RSCP AA 0 A_PlaySound("dakka/scrapperfire", CallACS("Dakka_RotateSound"))
        RSCP AAAAA 0 A_FireCustomMissile("ScrapperShot",
                                    CallACS("Dakka_Spread", -1200, 1200) / 65536.0, 0, 11,0,0,
                                    CallACS("Dakka_Spread", -600, 600) / 65536.0)
        RSCP A 0 A_FireCustomMissile("AmmoWaster")
        RSCP A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, 2, 2, 7)
        DSCP J 1 bright A_Light(7)
        DSCP K 1 bright A_Light(6)
        DSCP L 1 bright A_Light(5)
        DSCP M 1 bright A_Light(4)
        DSCP N 0 bright A_Light(3)
        DSCP O 1 bright A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DSCP O 0 bright A_Light(2)
        DSCP O 1 bright A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DSCP P 0 bright A_Light(1)
        DSCP P 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DSCP P 0 A_Light(0)
        DSCP QR 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DSCP R 0 A_CheckReload
        DSCP R 0 A_JumpIfInventory("ScrapperAltTimer", 1, 2)
        DSCP R 0 A_JumpIfInventory("AltFired", 1, "PrimaryToAlt")
        DSCP R 0 A_JumpIfInventory("MainFired", 1, "REnd_LStart")
        goto REnd_LStill

      REnd_LStart:
        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 1, 0)
        RSCP A 0 A_AlertMonsters
        RSCP A 0 A_TakeInventory("ScrapperSide")
        RSCP A 0 A_TakeInventory("ScrapperAltTimer", 1)
        RSCP AA 0 A_PlaySound("dakka/scrapperfire", CallACS("Dakka_RotateSound"))
        RSCP AAAAA 0 A_FireCustomMissile("ScrapperShot",
                                    CallACS("Dakka_Spread", -1200, 1200) / 65536.0, 0, -11,0,0,
                                    CallACS("Dakka_Spread", -600, 600) / 65536.0)
        RSCP A 0 A_FireCustomMissile("AmmoWaster")
        RSCP A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, 2, 2, 7)
        DSCP A 1 bright A_Light(7)
        DSCP B 1 bright A_Light(6)
        DSCP C 1 bright A_Light(5)
        DSCP D 1 bright A_Light(4)
        DSCP E 0 bright A_Light(3)
        DSCP F 1 bright A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DSCP F 0 bright A_Light(2)
        DSCP F 1 bright A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DSCP G 0 bright A_Light(1)
        DSCP G 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DSCP G 0 A_Light(0)
        DSCP HI 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DSCP I 0 A_CheckReload
        DSCP I 0 A_JumpIfInventory("ScrapperAltTimer", 1, 2)
        DSCP I 0 A_JumpIfInventory("AltFired", 1, "PrimaryToAlt")
        DSCP I 0 A_JumpIfInventory("MainFired", 1, "RStart_LEnd")
        goto RStill_LEnd


      RStart_LAlmost:
        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 1, 0)
        RSCP A 0 A_AlertMonsters
        RSCP A 0 A_GiveInventory("ScrapperSide")
        RSCP A 0 A_TakeInventory("ScrapperAltTimer", 1)
        RSCP AA 0 A_PlaySound("dakka/scrapperfire", CallACS("Dakka_RotateSound"))
        RSCP AAAAA 0 A_FireCustomMissile("ScrapperShot",
                                    CallACS("Dakka_Spread", -1200, 1200) / 65536.0, 0, 11,0,0,
                                    CallACS("Dakka_Spread", -600, 600) / 65536.0)
        RSCP A 0 A_FireCustomMissile("AmmoWaster")
        RSCP A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, 2, 2, 7)
        RSCP L 1 bright A_Light(8)
        RSCP M 1 bright A_Light(7)
        RSCP N 1 bright A_Light(5)
        RSCP O 1 bright A_Light(4)
        RSCP P 0 bright A_Light(3)
        RSCP P 1 bright A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        RSCP Q 0 bright A_Light(2)
        RSCP Q 1 bright A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        RSCP R 0 bright A_Light(1)
        RSCP R 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        RSCP R 0 bright A_Light(0)
        RSCP R 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        RSCP R 0 A_CheckReload
        RSCP R 0 A_JumpIfInventory("ScrapperAltTimer", 1, 2)
        RSCP R 0 A_JumpIfInventory("AltFired", 1, "PrimaryToAlt")
        RSCP R 0 A_JumpIfInventory("MainFired", 1, "REnd_LStart")
        goto REnd_LStill

      RAlmost_LStart:
        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 1, 0)
        RSCP A 0 A_AlertMonsters
        LSCP A 0 A_TakeInventory("ScrapperSide")
        RSCP A 0 A_TakeInventory("ScrapperAltTimer", 1)
        RSCP AA 0 A_PlaySound("dakka/scrapperfire", CallACS("Dakka_RotateSound"))
        RSCP AAAAA 0 A_FireCustomMissile("ScrapperShot",
                                    CallACS("Dakka_Spread", -1200, 1200) / 65536.0, 0, -11,0,0,
                                    CallACS("Dakka_Spread", -600, 600) / 65536.0)
        RSCP A 0 A_FireCustomMissile("AmmoWaster")
        RSCP A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, 2, 2, 7)
        LSCP L 1 bright A_Light(8)
        LSCP M 1 bright A_Light(7)
        LSCP N 1 bright A_Light(5)
        LSCP O 1 bright A_Light(4)
        LSCP P 0 bright A_Light(3)
        LSCP P 1 bright A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        LSCP Q 0 bright A_Light(2)
        LSCP Q 1 bright A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        LSCP R 0 bright A_Light(1)
        LSCP R 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        LSCP R 0 bright A_Light(0)
        LSCP R 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        LSCP R 0 A_CheckReload
        LSCP F 0 A_JumpIfInventory("ScrapperAltTimer", 1, 2)
        LSCP R 0 A_JumpIfInventory("AltFired", 1, "PrimaryToAlt")
        LSCP R 0 A_JumpIfInventory("MainFired", 1, "RStart_LEnd")
        goto RStill_LEnd


      REnd_LStill:
        RSCP F 0 A_JumpIfInventory("AltFired", 1, "PrimaryToAlt")
        RSCP F 0 A_JumpIfInventory("MainFired", 1, "REnd_LStart")
        RSCP F 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)

        RSCP G 0 A_JumpIfInventory("AltFired", 1, "PrimaryToAlt")
        RSCP G 0 A_JumpIfInventory("MainFired", 1, "REnd_LStart")
        RSCP G 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)

        RSCP H 0 A_JumpIfInventory("AltFired", 1, "PrimaryToAlt")
        RSCP H 0 A_JumpIfInventory("MainFired", 1, "RAlmost_LStart")
        RSCP H 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)

        RSCP I 0 A_JumpIfInventory("AltFired", 1, "PrimaryToAlt")
        RSCP I 0 A_JumpIfInventory("MainFired", 1, "RAlmost_LStart")
        RSCP I 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)

        RSCP JK 1 A_WeaponReady(WRF_NOBOB)
        goto Ready

      RStill_LEnd:
        LSCP F 0 A_JumpIfInventory("AltFired", 1, "PrimaryToAlt")
        LSCP F 0 A_JumpIfInventory("MainFired", 1, "RStart_LEnd")
        LSCP F 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)

        LSCP G 0 A_JumpIfInventory("AltFired", 1, "PrimaryToAlt")
        LSCP G 0 A_JumpIfInventory("MainFired", 1, "RStart_LEnd")
        LSCP G 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)

        LSCP H 0 A_JumpIfInventory("AltFired", 1, "PrimaryToAlt")
        LSCP H 0 A_JumpIfInventory("MainFired", 1, "RStart_LAlmost")
        LSCP H 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)

        LSCP I 0 A_JumpIfInventory("AltFired", 1, "PrimaryToAlt")
        LSCP I 2 A_JumpIfInventory("MainFired", 1, "RStart_LAlmost")
        LSCP JK 1 A_WeaponReady(WRF_NOBOB)
        goto Ready


      PrimaryToAlt:
        "----" A 1 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        goto AltFire // anyway

      AltToPrimary:
        "----" A 0 A_GiveInventory("ScrapperAtoP")
        "----" A 1 A_WeaponReady(WRF_NOSECONDARY | WRF_NOBOB)
        goto Fire // anyway

      AltToPrimary2:
        "----" A 1 A_WeaponReady(WRF_NOSECONDARY | WRF_NOBOB)
        goto Fire // anyway

      AltFire:
        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 1, 1)
        RSCP A 0 A_AlertMonsters
        RSCP A 0 A_TakeInventory("ScrapperAltTimer")
        RSCP A 0 A_GiveInventory("ScrapperAltTimer", 2)
        RSCP AA 0 A_PlaySound("dakka/scrapperaltfire", CallACS("Dakka_RotateSound"))
        RSCP A 0 A_FireCustomMissile("ScrapperGrenade", 0, 0, -11)
        RSCP A 0 A_FireCustomMissile("ScrapperGrenade", 0, 0,  11)
        RSCP A 0 A_FireCustomMissile("AmmoWaster")
        RSCP A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, 6, 3, 16)
        SCPR B 1 bright A_Light(9)
        SCPR C 1 bright A_Light(8)
        SCPR D 1 bright A_Light(6)
        SCPR E 1 bright A_Light(5)
        SCPR F 1 bright A_Light(4)
        SCPR G 1 bright A_Light(3)
        SCPR G 1 bright A_Light(2)
        SCPR G 1 bright A_Light(1)
        SCPR G 1 bright A_Light(0)
        SCPR HIJK 2 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        SCRR L 0 A_JumpIfInventory("MainFired", 1, "AltToPrimary")
        SCPR L 2 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        SCPR M 0 A_JumpIfInventory("MainFired", 1, "AltToPrimary")
        SCPR M 0 A_Refire
        SCPR M 2 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        SCPR N 0 A_JumpIfInventory("MainFired", 1, "AltToPrimary2")
        SCPR N 0 A_Refire
        SCPR N 2 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
        SCPR N 0 A_JumpIfInventory("MainFired", 1, "AltToPrimary2")
        goto Ready

      Spawn:
        SCPR Z -1
        stop
    }
}

actor ScrapperShot
{
    Radius 4
    Height 4

    Projectile
    Speed 45

    -NOGRAVITY

    +DOOMBOUNCE
    +SKYEXPLODE
    +MTHRUSPECIES

    BounceFactor 0.35
    WallBounceFactor 0.5

    Scale 0.3

    States
    {
      Spawn:
        SCRS A 0 bright
        SCRS A 0 A_Jump(64, 2)
        SCRS A 0 A_PlaySound("dakka/scrapfly", 7)
        SCRS A 0 A_Jump(255, "Spawn1", "Spawn2", "Spawn3")
        goto Spawn1

      Spawn1:
        SCRS A 0 A_GiveInventory("ScrapTimer")
        SCRS A 0 A_SpawnItemEx("ScrapperTrail", -(1*momx)/3.0, (1*momy)/3.0, -(1*momz)/3.0, 0,0,0, 0, SXF_ABSOLUTEANGLE)
        SCRS A 0 A_SpawnItemEx("ScrapperTrail", -(2*momx)/3.0, (2*momy)/3.0, -(2*momz)/3.0, 0,0,0, 0, SXF_ABSOLUTEANGLE)
        SCRS A 0 A_SpawnItemEx("ScrapperTrail", 0,              0,           0)
        SCRS A 1 bright A_JumpIfInventory("ScrapTimer", random(7, 11), "TimeToDie")
        loop

      Spawn2:
        SCRS B 0 A_GiveInventory("ScrapTimer")
        SCRS B 0 A_SpawnItemEx("ScrapperTrail", -(1*momx)/3.0, (1*momy)/3.0, -(1*momz)/3.0, 0,0,0, 0, SXF_ABSOLUTEANGLE)
        SCRS B 0 A_SpawnItemEx("ScrapperTrail", -(2*momx)/3.0, (2*momy)/3.0, -(2*momz)/3.0, 0,0,0, 0, SXF_ABSOLUTEANGLE)
        SCRS B 0 A_SpawnItemEx("ScrapperTrail", 0,              0,           0)
        SCRS B 1 bright A_JumpIfInventory("ScrapTimer", random(7, 11), "TimeToDie")
        loop

      Spawn3:
        SCRS C 0 A_GiveInventory("ScrapTimer")
        SCRS C 0 A_SpawnItemEx("ScrapperTrail", -(1*momx)/3.0, (1*momy)/3.0, -(1*momz)/3.0, 0,0,0, 0, SXF_ABSOLUTEANGLE)
        SCRS C 0 A_SpawnItemEx("ScrapperTrail", -(2*momx)/3.0, (2*momy)/3.0, -(2*momz)/3.0, 0,0,0, 0, SXF_ABSOLUTEANGLE)
        SCRS C 0 A_SpawnItemEx("ScrapperTrail", 0,              0,           0)
        SCRS C 1 bright A_JumpIfInventory("ScrapTimer", random(7, 11), "TimeToDie")
        loop

      TimeToDie:
        BAL1 C 0 A_SetTranslucent(1, 1)
        SCRS A 0 bright A_Die
        goto Death

      Death:
        BAL1 C 0 A_PlaySound("dakka/scrappersplit")
        SCRS A 0 A_ChangeFlag("PAINLESS", 1 - (random(0, 8) / 8))
        BAL1 C 0 A_Explode(2, 96, 0)
        BAL1 C 0 A_Explode(1, 96, 0)
        // No, I'm not going to do even remotely complicated calculations in DECORATE.
        BAL1 CCC 0 A_SpawnItemEx("SplitScrap", 0,0,0, (CallACS("Dakka_ScrapXY") / 65536.0) * frandom(15,20), 0, (CallACS("Dakka_ScrapZ") / 65536.0) + frandom(3,6), random(-60, 60))
        BAL1 C 0 A_SpawnItemEx("ScrapShotDeath")
        stop
    }
}

actor ScrapShotDeath
{
    +NOINTERACTION
    // Can't be CLIENTSIDEONLY, or it doesn't spawn online. GENIUS

    Scale 0.3
    RenderStyle Add

    States
    {
      Spawn: 
        BAL1 CDE 5 bright
        stop
    }
}

actor ScrapperTrail
{
    +NOINTERACTION
    +CLIENTSIDEONLY

    RenderStyle Add
    Alpha 0.15
    Scale 0.2

    States
    {
      Spawn:
        SCRT A 0 bright
        SCRT A 4 bright A_JumpIf(CallACS("Dakka_LessEffects"), "Disappear")
        SCRT A 1 bright A_FadeOut(0.08)
        wait

      Disappear:
        TNT1 A 0
        stop
    }
}

actor SplitScrap
{
    Radius 8
    Height 8

    Projectile
    Speed 15

    -NOGRAVITY
    +PUFFGETSOWNER
    +DOOMBOUNCE
    +SKYEXPLODE
    +FORCERADIUSDMG
    +NODAMAGETHRUST
    +THRUACTORS
    +BLOODLESSIMPACT
    +MTHRUSPECIES

    Gravity 0.65

    BounceFactor 0.35
    WallBounceFactor 0.5

    Scale 0.2

    States
    {
      Spawn:
        SCRS A 0 bright
        SCRS A 0 A_Jump(192, 2)
        SCRS A 0 A_GiveInventory("ScrapTimer", 3)
        SCRS A 0 A_Jump(255, "Spawn1", "Spawn2", "Spawn3")
        goto Spawn1

      Spawn1:
        SCRS A 0 A_ChangeFlag("PAINLESS", 1 - (random(0, 8) / 8))
        SCRS A 0 A_TakeInventory("ScrapTimer", 1)
        SCRS A 0 A_JumpIfInventory("ScrapTimer", 1, 2)
        SCRS A 0 A_Explode(2, 34, 0)
        SCRS A 0        A_SpawnItemEx("ScrapperTrail2", -(1*momx)/2.0, (1*momy)/2.0, -(1*momz)/2.0, 0,0,0, 0, SXF_ABSOLUTEANGLE)
        SCRS A 1 bright A_SpawnItemEx("ScrapperTrail2", 0,              0,           0)
        loop

      Spawn2:
        SCRS A 0 A_ChangeFlag("PAINLESS", 1 - (random(0, 8) / 8))
        SCRS A 0 A_TakeInventory("ScrapTimer", 1)
        SCRS A 0 A_JumpIfInventory("ScrapTimer", 1, 2)
        SCRS A 0 A_Explode(2, 32, 0)
        SCRS B 0        A_SpawnItemEx("ScrapperTrail2", -(1*momx)/2.0, (1*momy)/2.0, -(1*momz)/2.0, 0,0,0, 0, SXF_ABSOLUTEANGLE)
        SCRS B 1 bright A_SpawnItemEx("ScrapperTrail2", 0,              0,           0)
        loop

      Spawn3:
        SCRS A 0 A_ChangeFlag("PAINLESS", 1 - (random(0, 8) / 8))
        SCRS A 0 A_TakeInventory("ScrapTimer", 1)
        SCRS A 0 A_JumpIfInventory("ScrapTimer", 1, 2)
        SCRS A 0 A_Explode(2, 32, 0)
        SCRS C 0        A_SpawnItemEx("ScrapperTrail2", -(1*momx)/2.0, (1*momy)/2.0, -(1*momz)/2.0, 0,0,0, 0, SXF_ABSOLUTEANGLE)
        SCRS C 1 bright A_SpawnItemEx("ScrapperTrail2", 0,              0,           0)
        loop

      Death:
        BAL1 C 0 A_ChangeFlag("NOGRAVITY", 1)
        BAL1 C 0 A_Stop
        BAL1 C 0 A_SetTranslucent(1, 1)
        BAL1 CDE 5 bright
        stop
    }
}

actor ScrapperTrail2: ScrapperTrail
{
    Scale 0.15
    Alpha 0.2

    States
    {
      Spawn:
        SCRT A 0 bright
        SCRT A 4 bright A_JumpIf(CallACS("Dakka_LessEffects"), "Disappear")
        SCRT A 1 bright A_FadeOut(0.04)
        wait
    }
}

actor ScrapperTrail3: ScrapperTrail
{
    Scale 0.35
    Alpha 0.3

    States
    {
      Spawn:
        SCRT A 0 bright
        SCRT A 4 bright A_JumpIf(CallACS("Dakka_LessEffects"), "Disappear")
        SCRT A 1 bright A_FadeOut(0.04)
        wait
    }
}


actor ScrapperGrenade
{
    Radius 4
    Height 4

    Projectile
    Speed 40

    +MTHRUSPECIES
    -NOGRAVITY
    Gravity 0.65  // I sure do love this gravity value.

    Scale 0.35

    States
    {
      Spawn:
        SCRN A 0 bright
        SCRN A 0 A_PlaySound("dakka/scrapfly", 7)
        SCRN A 0 A_Jump(255, "Spawn1", "Spawn2", "Spawn3")
        goto Spawn1

      Spawn1:
        SCRN A 1 bright
        SCRN A 0 A_SpawnItemEx("ScrapperTrail3", -(1*momx)/2.0, (1*momy)/2.0, -(1*momz)/2.0, 0,0,0, 0, SXF_ABSOLUTEANGLE)
        SCRN A 0 A_SpawnItemEx("ScrapperTrail3", 0,              0,           0)
        SCRN A 1 bright
        SCRN A 0 A_SpawnItemEx("ScrapperTrail3", -(1*momx)/2.0, (1*momy)/2.0, -(1*momz)/2.0, 0,0,0, 0, SXF_ABSOLUTEANGLE)
        SCRN A 0 A_SpawnItemEx("ScrapperTrail3", 0,              0,           0)
        SCRN A 0 A_SpawnItemEx("SplitScrap", 0,0,0, random(0,90)/10.0, 0, random(-30,60)/10.0, random(0, 360))

        SCRN A 1 bright
        SCRN A 0 A_SpawnItemEx("ScrapperTrail3", -(1*momx)/2.0, (1*momy)/2.0, -(1*momz)/2.0, 0,0,0, 0, SXF_ABSOLUTEANGLE)
        SCRN A 0 A_SpawnItemEx("ScrapperTrail3", 0,              0,           0)
        SCRN A 1 bright
        SCRN A 0 A_SpawnItemEx("ScrapperTrail3", -(1*momx)/2.0, (1*momy)/2.0, -(1*momz)/2.0, 0,0,0, 0, SXF_ABSOLUTEANGLE)
        SCRN A 0 A_SpawnItemEx("ScrapperTrail3", 0,              0,           0)
        SCRN A 0 A_SpawnItemEx("SplitScrap", 0,0,0, random(0,90)/10.0, 0, random(-30,60)/10.0, random(0, 360))

        SCRN A 1 bright
        SCRN A 0 A_SpawnItemEx("ScrapperTrail3", -(1*momx)/2.0, (1*momy)/2.0, -(1*momz)/2.0, 0,0,0, 0, SXF_ABSOLUTEANGLE)
        SCRN A 0 A_SpawnItemEx("ScrapperTrail3", 0,              0,           0)
        SCRN A 1 bright
        SCRN A 0 A_SpawnItemEx("ScrapperTrail3", -(1*momx)/2.0, (1*momy)/2.0, -(1*momz)/2.0, 0,0,0, 0, SXF_ABSOLUTEANGLE)
        SCRN A 0 A_SpawnItemEx("ScrapperTrail3", 0,              0,           0)
        SCRN A 0 A_SpawnItemEx("SplitScrap", 0,0,0, random(0,90)/10.0, 0, random(-30,60)/10.0, random(0, 360))
        goto Spawn2

      Spawn2:
        SCRN B 1 bright
        SCRN B 0 A_SpawnItemEx("ScrapperTrail3", -(1*momx)/2.0, (1*momy)/2.0, -(1*momz)/2.0, 0,0,0, 0, SXF_ABSOLUTEANGLE)
        SCRN B 0 A_SpawnItemEx("ScrapperTrail3", 0,              0,           0)
        SCRN B 1 bright
        SCRN B 0 A_SpawnItemEx("ScrapperTrail3", -(1*momx)/2.0, (1*momy)/2.0, -(1*momz)/2.0, 0,0,0, 0, SXF_ABSOLUTEANGLE)
        SCRN B 0 A_SpawnItemEx("ScrapperTrail3", 0,              0,           0)
        SCRN B 0 A_SpawnItemEx("SplitScrap", 0,0,0, random(0,90)/10.0, 0, random(-30,60)/10.0, random(0, 360))

        SCRN B 1 bright
        SCRN B 0 A_SpawnItemEx("ScrapperTrail3", -(1*momx)/2.0, (1*momy)/2.0, -(1*momz)/2.0, 0,0,0, 0, SXF_ABSOLUTEANGLE)
        SCRN B 0 A_SpawnItemEx("ScrapperTrail3", 0,              0,           0)
        SCRN B 1 bright
        SCRN B 0 A_SpawnItemEx("ScrapperTrail3", -(1*momx)/2.0, (1*momy)/2.0, -(1*momz)/2.0, 0,0,0, 0, SXF_ABSOLUTEANGLE)
        SCRN B 0 A_SpawnItemEx("ScrapperTrail3", 0,              0,           0)
        SCRN B 0 A_SpawnItemEx("SplitScrap", 0,0,0, random(0,90)/10.0, 0, random(-30,60)/10.0, random(0, 360))

        SCRN B 1 bright
        SCRN B 0 A_SpawnItemEx("ScrapperTrail3", -(1*momx)/2.0, (1*momy)/2.0, -(1*momz)/2.0, 0,0,0, 0, SXF_ABSOLUTEANGLE)
        SCRN B 0 A_SpawnItemEx("ScrapperTrail3", 0,              0,           0)
        SCRN B 1 bright
        SCRN B 0 A_SpawnItemEx("ScrapperTrail3", -(1*momx)/2.0, (1*momy)/2.0, -(1*momz)/2.0, 0,0,0, 0, SXF_ABSOLUTEANGLE)
        SCRN B 0 A_SpawnItemEx("ScrapperTrail3", 0,              0,           0)
        SCRN B 0 A_SpawnItemEx("SplitScrap", 0,0,0, random(0,90)/10.0, 0, random(-30,60)/10.0, random(0, 360))
        goto Spawn3

      Spawn3:
        SCRN C 1 bright
        SCRN C 0 A_SpawnItemEx("ScrapperTrail3", -(1*momx)/2.0, (1*momy)/2.0, -(1*momz)/2.0, 0,0,0, 0, SXF_ABSOLUTEANGLE)
        SCRN C 0 A_SpawnItemEx("ScrapperTrail3", 0,              0,           0)
        SCRN C 1 bright
        SCRN C 0 A_SpawnItemEx("ScrapperTrail3", -(1*momx)/2.0, (1*momy)/2.0, -(1*momz)/2.0, 0,0,0, 0, SXF_ABSOLUTEANGLE)
        SCRN C 0 A_SpawnItemEx("ScrapperTrail3", 0,              0,           0)
        SCRN C 0 A_SpawnItemEx("SplitScrap", 0,0,0, random(0,90)/10.0, 0, random(-30,60)/10.0, random(0, 360))

        SCRN C 1 bright
        SCRN C 0 A_SpawnItemEx("ScrapperTrail3", -(1*momx)/2.0, (1*momy)/2.0, -(1*momz)/2.0, 0,0,0, 0, SXF_ABSOLUTEANGLE)
        SCRN C 0 A_SpawnItemEx("ScrapperTrail3", 0,              0,           0)
        SCRN C 1 bright
        SCRN C 0 A_SpawnItemEx("ScrapperTrail3", -(1*momx)/2.0, (1*momy)/2.0, -(1*momz)/2.0, 0,0,0, 0, SXF_ABSOLUTEANGLE)
        SCRN C 0 A_SpawnItemEx("ScrapperTrail3", 0,              0,           0)
        SCRN C 0 A_SpawnItemEx("SplitScrap", 0,0,0, random(0,90)/10.0, 0, random(-30,60)/10.0, random(0, 360))

        SCRN C 1 bright
        SCRN C 0 A_SpawnItemEx("ScrapperTrail3", -(1*momx)/2.0, (1*momy)/2.0, -(1*momz)/2.0, 0,0,0, 0, SXF_ABSOLUTEANGLE)
        SCRN C 0 A_SpawnItemEx("ScrapperTrail3", 0,              0,           0)
        SCRN C 1 bright
        SCRN C 0 A_SpawnItemEx("ScrapperTrail3", -(1*momx)/2.0, (1*momy)/2.0, -(1*momz)/2.0, 0,0,0, 0, SXF_ABSOLUTEANGLE)
        SCRN C 0 A_SpawnItemEx("ScrapperTrail3", 0,              0,           0)
        SCRN C 0 A_SpawnItemEx("SplitScrap", 0,0,0, random(0,90)/10.0, 0, random(-30,60)/10.0, random(0, 360))
        goto Spawn1
      
      Death:
        MISL B 0 A_ChangeFlag("NOGRAVITY", 1)
        MISL B 0 A_SetTranslucent(1, 1)
        MISL B 0 A_PlaySound("dakka/scrapnadeexplode")
        MISL B 0 A_SpawnItemEx("ScrapperShot_FromNade", 0,0,0, random(50,90)/10.0,0,random(60,90)/10.0, -45)
        MISL B 0 A_SpawnItemEx("ScrapperShot_FromNade", 0,0,0, random(50,90)/10.0,0,random(60,90)/10.0, -15)
        MISL B 0 A_SpawnItemEx("ScrapperShot_FromNade", 0,0,0, random(50,90)/10.0,0,random(60,90)/10.0,  15)
        MISL B 0 A_SpawnItemEx("ScrapperShot_FromNade", 0,0,0, random(50,90)/10.0,0,random(60,90)/10.0,  45)
        MISL B 0 A_Explode(20, 96, 0)
        MISL BCD 4 bright
        stop
    }
}

actor ScrapperShot_FromNade: ScrapperShot { +PUFFGETSOWNER }
