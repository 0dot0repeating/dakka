actor ImpalerBarrelCounter: Counter {}

actor DWep_Impaler: DoomWeapon
{
    Tag "Impaler"

    Weapon.SlotNumber 5
    Weapon.SelectionOrder 300

    Weapon.AmmoUse1     1
    Weapon.AmmoGive1    15
    Weapon.AmmoType1    "DakkaRockets"

    Inventory.PickupMessage "$DAKKA_PK_IMPALER"
    Obituary "$DAKKA_MP_IMPALER"
    Weapon.YAdjust 0

    States
    {
      Spawn:
        DKIP Z -1
        stop

      Select:
        DKIP A 0 A_TakeInventory("ImpalerBarrelCounter")
        goto Select2

      Select2:
        DKIP AA 0 A_Raise
        DKIP A 1 A_Raise
        loop

      Deselect:
        DKIP AAA 0 A_Lower
        DKIP A 1 A_Lower
        loop

      Ready:
        DKIP A 1 A_WeaponReady
        loop

      Fire:
        DKIP A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 11, 0)
        DKRL A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, 3, 2, 20)
        DKIP A 0 A_JumpIfInventory("ImpalerBarrelCounter", 2, "RightFire")
        DKIP A 0 A_JumpIfInventory("ImpalerBarrelCounter", 1, "LeftFire")
        goto CenterFire

      RightFire:
        DKIP B 0 A_TakeInventory("ImpalerBarrelCounter")
        DKIP B 0 A_FireCustomMissile("ImpalerMissile", 0, true, 2)
        DKIP B 0 bright               A_PlaySound("dakka/impalerfire", 5)
        DKIP B 1 bright offset(0, 38) A_GunFlash
        DKIP C 1 bright
        DKIP D 1 bright               A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP E 1 bright offset(0, 37) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP F 2 bright offset(0, 36) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP H 1        offset(0, 34) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP A 0                      A_Refire
        DKIP A 1        offset(0, 33) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP A 0        offset(0, 32) A_Refire
        DKIP A 2                      A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        goto Ready

      CenterFire:
        DKIP I 0 A_GiveInventory("ImpalerBarrelCounter")
        DKIP I 0 A_FireCustomMissile("ImpalerMissile", 0, true, 0)
        DKIP I 0 bright               A_PlaySound("dakka/impalerfire", 6)
        DKIP I 1 bright offset(0, 38) A_GunFlash
        DKIP J 1 bright
        DKIP K 1 bright               A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP L 1 bright offset(0, 37) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP M 2 bright offset(0, 36) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP O 1        offset(0, 34) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP A 0                      A_Refire
        DKIP A 1        offset(0, 33) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP A 0        offset(0, 32) A_Refire
        DKIP A 2                      A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        goto Ready

      LeftFire:
        DKIP P 0 A_GiveInventory("ImpalerBarrelCounter")
        DKIP P 0 A_FireCustomMissile("ImpalerMissile", 0, true, -2)
        DKIP P 0 bright               A_PlaySound("dakka/impalerfire", 7)
        DKIP P 1 bright offset(0, 38) A_GunFlash
        DKIP Q 1 bright
        DKIP R 1 bright               A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP S 1 bright offset(0, 37) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP T 2 bright offset(0, 36) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP V 1        offset(0, 34) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP A 0                      A_Refire
        DKIP A 1        offset(0, 33) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP A 0        offset(0, 32) A_Refire
        DKIP A 2                      A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        goto Ready

      Flash:
        TNT1 A 2 A_Light(8)
        TNT1 A 1 A_Light(7)
        TNT1 A 1 A_Light(6)
        TNT1 A 1 A_Light(4)
        TNT1 A 1 A_Light(2)
        TNT1 A 1 A_Light(0)
        stop
    }
}

actor ImpalerMissile: FastProjectile
{
    Speed 1

    Radius 8
    Height 8

    Scale 0.5

    var int user_velx;
    var int user_vely;
    var int user_velz;

    +SKYEXPLODE
    +FORCERADIUSDMG
    +EXTREMEDEATH
    
    // When strafing around, it'll appear next to you on uncapped framerate for a tic
    // So disable that
    RenderStyle None
    
    DeathSound "dakka/explode"

    States
    {
      Spawn:
        MIS3 A 0 nodelay
        MIS3 A 0 A_ChangeVelocity(velx * 72.0, vely * 72.0, velz * 72.0, CVF_REPLACE)
        goto Spawn2

      Spawn2:
        MIS3 A 0 A_SetUserVar("user_velx", velx)
        MIS3 A 0 A_SetUserVar("user_vely", vely)
        MIS3 A 0 A_SetUserVar("user_velz", velz)

        MIS3 A 0 A_SpawnItemEx("ImpalerSmoke_DelayOne",  0,0,0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE)
        MIS3 A 0 A_SpawnItemEx("ImpalerSmoke_DelayOne",           (velx *  8)  / 72.0,    (vely *  8)  / 72.0,    (velz *  8)  / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE)
        MIS3 A 0 A_SpawnItemEx("ImpalerSmoke_DelayOne",           (velx *  24) / 72.0,    (vely *  24) / 72.0,    (velz *  24) / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE)
        MIS3 A 0 A_SpawnItemEx("ImpalerSmoke_DelayOne",           (velx *  40) / 72.0,    (vely *  40) / 72.0,    (velz *  40) / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE)
        MIS3 A 0 A_SpawnItemEx("ImpalerSmoke_DelayOne",           (velx *  56) / 72.0,    (vely *  56) / 72.0,    (velz *  56) / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE)

        MIS3 A 1
        MIS3 A 0 A_SetTranslucent(1, 0)

        MIS3 A 0 A_SpawnItemEx("ImpalerSmoke", -(velx *  8)  / 72.0,   -(vely *  8)  / 72.0,   -(velz *  8)  / 72.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE)
        MIS3 A 0 A_SpawnItemEx("ImpalerSmoke", -(velx *  24) / 72.0,   -(vely *  24) / 72.0,   -(velz *  24) / 72.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE)
        MIS3 A 0 A_SpawnItemEx("ImpalerSmoke", -(velx *  40) / 72.0,   -(vely *  40) / 72.0,   -(velz *  40) / 72.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE)
        MIS3 A 0 A_SpawnItemEx("ImpalerSmoke", -(velx *  56) / 72.0,   -(vely *  56) / 72.0,   -(velz *  56) / 72.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE)
        loop

      Death:
        MIS3 A 0 A_SpawnItemEx("ImpalerSmoke", -(user_velx *  8)  / 72.0,   -(user_vely *  8)  / 72.0,   -(user_velz *  8)  / 72.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE)
        MIS3 A 0 A_SpawnItemEx("ImpalerSmoke", -(user_velx *  24) / 72.0,   -(user_vely *  24) / 72.0,   -(user_velz *  24) / 72.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE)
        MIS3 A 0 A_SpawnItemEx("ImpalerSmoke", -(user_velx *  40) / 72.0,   -(user_vely *  40) / 72.0,   -(user_velz *  40) / 72.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE)
        MIS3 A 0 A_SpawnItemEx("ImpalerSmoke", -(user_velx *  56) / 72.0,   -(user_vely *  56) / 72.0,   -(user_velz *  56) / 72.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE)

        TNT1 A 0 A_Explode(48, 128, true, false, 48)
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_InitLanceHit", 128, 32, 40)
        TNT1 A 0 A_RadiusGive("ImpalerPrimaryHitChecker", 384, RGF_MONSTERS | RGF_OBJECTS | RGF_PLAYERS | RGF_VOODOO | RGF_CUBE)
        
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_InitLanceHit", 256, 64, 40)
        TNT1 A 0 A_RadiusGive("ImpalerPrimaryHitChecker", 448, RGF_MONSTERS | RGF_OBJECTS | RGF_PLAYERS | RGF_VOODOO | RGF_CUBE)
        
        TNT1 A 8 A_SpawnItemEx("ImpalerExplosion_Client")
        stop
    }
}

actor ImpalerPrimaryHitChecker: Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 ACS_NamedExecutewithResult("Dakka_CheckLanceHit", 1)
        stop
    }
}

actor ImpalerPrimaryHurter
{
    +BLOODLESSIMPACT
    +EXTREMEDEATH
    
    var int user_damage;
    
    States
    {
      Spawn:
        TNT1 A 0
        goto DoHurt
        
      DoHurt:
        TNT1 A 0 A_Explode(user_damage / 2,       8, false, false, 8)
        TNT1 A 0 A_ChangeFlag("FORCERADIUSDMG", true)
        TNT1 A 0 A_Explode((user_damage + 1) / 2, 8, false, false, 8)
        stop
    }
}

// FastProjectiles behave weird with uncapped framerate interpolation and death states.
//  That's why the explosion actor's separate.
actor ImpalerExplosion_Client
{
    +NOINTERACTION
    +CLIENTSIDEONLY

    RenderStyle Add

    States
    {
      Spawn:
        DMIS F 4 bright
        DMIS G 3 bright
        DMIS HIJ 2 bright
        stop
    }
}


actor ImpalerSmoke
{
    +NOINTERACTION
    +CLIENTSIDEONLY

    RenderStyle Add
    Alpha 0.15

    Scale 0.2

    States
    {
      Spawn:
        TNT1 A 0 nodelay A_SetScale(frandom(0.15, 0.2))
        TNT1 A 0 A_Jump(256, "Smoke1", "Smoke2", "Smoke3", "Smoke4")
        goto Smoke1

      Smoke1:
        SMK1 ABCDEFGHIJKLMNOPQR 1
        stop

      Smoke2:
        SMK2 ABCDEFGHIJKLMNOPQR 1
        stop

      Smoke3:
        SMK2 ABCDEFGHIJKLMNOP 1
        stop

      Smoke4:
        SMK2 ABCDEFGHIJKLMNOP 1
        stop
    }
}


actor ImpalerSmoke_DelayOne: ImpalerSmoke
{
    States
    {
      Spawn:
        TNT1 A 2
        goto Super::Spawn
    }
}
