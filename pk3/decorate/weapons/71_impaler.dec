actor ImpalerBarrelCounter: Counter {}

actor DWep_Impaler: DoomWeapon
{
    Tag "Impaler"

    Weapon.SlotNumber 7
    Weapon.SelectionOrder 301   // literally right behind the rocket launcher

    Weapon.AmmoUse1     1
    Weapon.AmmoGive1    10
    Weapon.AmmoType1    "DakkaRockets"

    Inventory.PickupMessage "$DAKKA_PK_IMPALER"
    Obituary "$DAKKA_MP_IMPALER"
    Weapon.YAdjust 0

    States
    {
      Spawn:
        DKIP Z -1
        stop

      Select:
        DKIP A 0 A_TakeInventory("ImpalerBarrelCounter")
        goto Select2

      Select2:
        DKIP AA 0 A_Raise
        DKIP A 1 A_Raise
        loop

      Deselect:
        DKIP AAA 0 A_Lower
        DKIP A 1 A_Lower
        loop

      Ready:
        DKIP A 1 A_WeaponReady
        loop

      Fire:
        DKIP A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 11, 0)
        DKIP A 0 A_JumpIfInventory("ImpalerBarrelCounter", 2, "RightFire")
        DKIP A 0 A_JumpIfInventory("ImpalerBarrelCounter", 1, "LeftFire")
        goto CenterFire

      RightFire:
        DKIP B 0 A_TakeInventory("ImpalerBarrelCounter")
        DKIP B 0 A_FireCustomMissile("ImpalerMissile", 0, true, 2)
        DKIP B 0 bright               A_PlaySound("dakka/impalerfire", 5)
        DKIP B 1 bright offset(0, 38) A_GunFlash
        DKIP C 1 bright
        DKIP D 1 bright               A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP E 1 bright               A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP F 2 bright offset(0, 37) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP H 1        offset(0, 35) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP A 0                      A_Refire
        DKIP A 1                      A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP A 0        offset(0, 33) A_Refire
        DKIP A 2                      A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        goto Ready

      CenterFire:
        DKIP I 0 A_GiveInventory("ImpalerBarrelCounter")
        DKIP I 0 A_FireCustomMissile("ImpalerMissile", 0, true, 0)
        DKIP I 0 bright               A_PlaySound("dakka/impalerfire", 6)
        DKIP I 1 bright offset(0, 38) A_GunFlash
        DKIP J 1 bright
        DKIP K 1 bright               A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP L 1 bright               A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP M 2 bright offset(0, 37) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP O 1        offset(0, 35) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP A 0                      A_Refire
        DKIP A 1                      A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP A 0        offset(0, 33) A_Refire
        DKIP A 2                      A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        goto Ready

      LeftFire:
        DKIP P 0 A_GiveInventory("ImpalerBarrelCounter")
        DKIP P 0 A_FireCustomMissile("ImpalerMissile", 0, true, -2)
        DKIP P 0 bright               A_PlaySound("dakka/impalerfire", 7)
        DKIP P 1 bright offset(0, 38) A_GunFlash
        DKIP Q 1 bright
        DKIP R 1 bright               A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP S 1 bright               A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP T 2 bright offset(0, 37) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP V 1        offset(0, 35) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP A 0                      A_Refire
        DKIP A 1                      A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP A 0        offset(0, 33) A_Refire
        DKIP A 2                      A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        goto Ready

      Flash:
        TNT1 A 1 A_Light2
        TNT1 A 2 A_Light1
        TNT1 A 0 A_Light0
        stop
    }
}

actor ImpalerMissile: FastProjectile
{
    Speed 1

    Radius 8
    Height 8

    Scale 0.5

    var int user_velx;
    var int user_vely;
    var int user_velz;

    +SKYEXPLODE
    +FORCERADIUSDMG
    +EXTREMEDEATH

    // When strafing around, it'll appear next you on uncapped framerate for a tic
    // So disable that
    RenderStyle None

    States
    {
      Spawn:
        MIS3 A 0 nodelay
        MIS3 A 0 A_ChangeVelocity(velx * 192.0, vely * 192.0, velz * 192.0, CVF_REPLACE)
        goto Spawn2

      Spawn2:
        MIS3 A 0 A_SetUserVar("user_velx", velx)
        MIS3 A 0 A_SetUserVar("user_vely", vely)
        MIS3 A 0 A_SetUserVar("user_velz", velz)

        MIS3 A 0 A_SpawnItemEx("ImpalerSmoke_DelayOne",  0,0,0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE)
        MIS3 A 0 A_SpawnItemEx("ImpalerSmoke_DelayOne",           (velx *  16) / 192.0,    (vely *  16) / 192.0,    (velz *  16) / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE)
        MIS3 A 0 A_SpawnItemEx("ImpalerSmoke_DelayOne",           (velx *  32) / 192.0,    (vely *  32) / 192.0,    (velz *  32) / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE)
        MIS3 A 0 A_SpawnItemEx("ImpalerSmoke_DelayOne",           (velx *  48) / 192.0,    (vely *  48) / 192.0,    (velz *  48) / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE)
        MIS3 A 0 A_SpawnItemEx("ImpalerSmoke_DelayOne",           (velx *  64) / 192.0,    (vely *  64) / 192.0,    (velz *  64) / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE)
        MIS3 A 0 A_SpawnItemEx("ImpalerSmoke_DelayOne",           (velx *  80) / 192.0,    (vely *  80) / 192.0,    (velz *  80) / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE)

        MIS3 A 1
        MIS3 A 0 A_SetTranslucent(1, 0)

        MIS3 A 0 A_SpawnItemEx("ImpalerSmoke", -(velx *  16) / 192.0,   -(vely *  16) / 192.0,   -(velz *  16) / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE)
        MIS3 A 0 A_SpawnItemEx("ImpalerSmoke", -(velx *  32) / 192.0,   -(vely *  32) / 192.0,   -(velz *  32) / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE)
        MIS3 A 0 A_SpawnItemEx("ImpalerSmoke", -(velx *  48) / 192.0,   -(vely *  48) / 192.0,   -(velz *  48) / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE)
        MIS3 A 0 A_SpawnItemEx("ImpalerSmoke", -(velx *  64) / 192.0,   -(vely *  64) / 192.0,   -(velz *  64) / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE)
        MIS3 A 0 A_SpawnItemEx("ImpalerSmoke", -(velx *  80) / 192.0,   -(vely *  80) / 192.0,   -(velz *  80) / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE)
        MIS3 A 0 A_SpawnItemEx("ImpalerSmoke", -(velx *  96) / 192.0,   -(vely *  96) / 192.0,   -(velz *  96) / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE)
        loop

      Death:
        TNT1 A 0 A_SpawnItemEx("ImpalerSmoke", -(user_velx *  16) / 192.0,   -(user_vely *  16) / 192.0,   -(user_velz *  16) / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE)
        TNT1 A 0 A_SpawnItemEx("ImpalerSmoke", -(user_velx *  32) / 192.0,   -(user_vely *  32) / 192.0,   -(user_velz *  32) / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE)
        TNT1 A 0 A_SpawnItemEx("ImpalerSmoke", -(user_velx *  48) / 192.0,   -(user_vely *  48) / 192.0,   -(user_velz *  48) / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE)
        TNT1 A 0 A_SpawnItemEx("ImpalerSmoke", -(user_velx *  64) / 192.0,   -(user_vely *  64) / 192.0,   -(user_velz *  64) / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE)
        TNT1 A 0 A_SpawnItemEx("ImpalerSmoke", -(user_velx *  80) / 192.0,   -(user_vely *  80) / 192.0,   -(user_velz *  80) / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE)
        TNT1 A 0 A_SpawnItemEx("ImpalerSmoke", -(user_velx *  96) / 192.0,   -(user_vely *  96) / 192.0,   -(user_velz *  96) / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE)

        TNT1 A 0 A_Explode(64, 64, 1, false, 48)

        TNT1 A 0 A_SpawnItemEx("ImpalerWallPiercer_Stage1", (user_velx * 8)   / 192.0,   (user_vely * 8)   / 192.0,   (user_velz * 8)   / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE | SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("ImpalerWallPiercer_Stage1", (user_velx * 16)  / 192.0,   (user_vely * 16)  / 192.0,   (user_velz * 16)  / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE | SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("ImpalerWallPiercer_Stage1", (user_velx * 24)  / 192.0,   (user_vely * 24)  / 192.0,   (user_velz * 24)  / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE | SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("ImpalerWallPiercer_Stage1", (user_velx * 32)  / 192.0,   (user_vely * 32)  / 192.0,   (user_velz * 32)  / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE | SXF_TRANSFERPOINTERS)

        TNT1 A 0 A_SpawnItemEx("ImpalerWallPiercer_Stage2", (user_velx * 40)  / 192.0,   (user_vely * 40)  / 192.0,   (user_velz * 40)  / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE | SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("ImpalerWallPiercer_Stage2", (user_velx * 48)  / 192.0,   (user_vely * 48)  / 192.0,   (user_velz * 48)  / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE | SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("ImpalerWallPiercer_Stage2", (user_velx * 56)  / 192.0,   (user_vely * 56)  / 192.0,   (user_velz * 56)  / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE | SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("ImpalerWallPiercer_Stage2", (user_velx * 64)  / 192.0,   (user_vely * 64)  / 192.0,   (user_velz * 64)  / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE | SXF_TRANSFERPOINTERS)

        TNT1 A 0 A_SpawnItemEx("ImpalerWallPiercer_Stage3", (user_velx * 72)  / 192.0,   (user_vely * 72)  / 192.0,   (user_velz * 72)  / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE | SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("ImpalerWallPiercer_Stage3", (user_velx * 80)  / 192.0,   (user_vely * 80)  / 192.0,   (user_velz * 80)  / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE | SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("ImpalerWallPiercer_Stage3", (user_velx * 88)  / 192.0,   (user_vely * 88)  / 192.0,   (user_velz * 88)  / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE | SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("ImpalerWallPiercer_Stage3", (user_velx * 96)  / 192.0,   (user_vely * 96)  / 192.0,   (user_velz * 96)  / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE | SXF_TRANSFERPOINTERS)

        TNT1 A 0 A_SpawnItemEx("ImpalerWallPiercer_Stage4", (user_velx * 104) / 192.0,   (user_vely * 104) / 192.0,   (user_velz * 104) / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE | SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("ImpalerWallPiercer_Stage4", (user_velx * 112) / 192.0,   (user_vely * 112) / 192.0,   (user_velz * 112) / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE | SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("ImpalerWallPiercer_Stage4", (user_velx * 120) / 192.0,   (user_vely * 120) / 192.0,   (user_velz * 120) / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE | SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("ImpalerWallPiercer_Stage4", (user_velx * 128) / 192.0,   (user_vely * 128) / 192.0,   (user_velz * 128) / 192.0,   0,0,0, 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE | SXF_TRANSFERPOINTERS)

        TNT1 A 8 A_SpawnItemEx("ImpalerExplosion_Client")
        stop
    }
}

// FastProjectiles behave weird with uncapped framerate interpolation and death states.
//  That's why the explosion actor's separate.
actor ImpalerExplosion_Client
{
    +NOINTERACTION
    +CLIENTSIDEONLY

    RenderStyle Add

    States
    {
      Spawn:
        DMIS F 4 bright
        DMIS G 3 bright
        DMIS HIJ 2 bright
        stop
    }
}


actor ImpalerWallPiercer_Stage1
{
    Projectile
    +NOINTERACTION

    Radius 2
    Height 2

    +EXTREMEDEATH
    +FORCERADIUSDMG
    +NODAMAGETHRUST

    RenderStyle Add

    States
    {
      Spawn:
        TNT1 A 0 nodelay A_ChangeFlag("PAINLESS", random(0, 8) == 0)
        TNT1 A 0 A_Explode(15, 88, 0, false, 88)
        DMIS F 8 bright
        stop
    }
}

actor ImpalerWallPiercer_Stage2: ImpalerWallPiercer_Stage1
{
    Scale 0.75

    States
    {
      Spawn:
        TNT1 A 0 nodelay A_ChangeFlag("PAINLESS", random(0, 8) == 0)
        TNT1 A 0 A_Explode(15, 80, 0, false, 80)
        DMIS F 8 bright
        stop
    }
}

actor ImpalerWallPiercer_Stage3: ImpalerWallPiercer_Stage1
{
    Scale 0.5

    States
    {
      Spawn:
        TNT1 A 0 nodelay A_ChangeFlag("PAINLESS", random(0, 8) == 0)
        TNT1 A 0 A_Explode(15, 72, 0, false, 72)
        DMIS F 8 bright
        stop
    }
}

actor ImpalerWallPiercer_Stage4: ImpalerWallPiercer_Stage1
{
    Scale 0.25

    States
    {
      Spawn:
        TNT1 A 0 nodelay A_ChangeFlag("PAINLESS", random(0, 8) == 0)
        TNT1 A 0 A_Explode(15, 64, 0, false, 64)
        DMIS F 8 bright
        stop
    }
}


actor ImpalerSmoke
{
    +NOINTERACTION
    +CLIENTSIDEONLY

    RenderStyle Add
    Alpha 0.15

    Scale 0.2

    States
    {
      Spawn:
        TNT1 A 0 nodelay A_SetScale(frandom(0.15, 0.2))
        TNT1 A 0 A_Jump(256, "Smoke1", "Smoke2", "Smoke3", "Smoke4")
        goto Smoke1

      Smoke1:
        SMK1 ABCDEFGHIJKLMNOPQR 1
        stop

      Smoke2:
        SMK2 ABCDEFGHIJKLMNOPQR 1
        stop

      Smoke3:
        SMK2 ABCDEFGHIJKLMNOP 1
        stop

      Smoke4:
        SMK2 ABCDEFGHIJKLMNOP 1
        stop
    }
}


actor ImpalerSmoke_DelayOne: ImpalerSmoke
{
    States
    {
      Spawn:
        TNT1 A 2
        goto Super::Spawn
    }
}
