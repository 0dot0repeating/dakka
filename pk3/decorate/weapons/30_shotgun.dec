actor DWep_Shotgun: DoomWeapon
{
    Tag "Shotgun"

    Weapon.SlotNumber 3
    Weapon.SelectionOrder 1300

    Weapon.AmmoType1    "DakkaShells"
    Weapon.AmmoType2    "DakkaShotGrenades"
    Weapon.AmmoUse1     1
    Weapon.AmmoUse2     1
    Weapon.AmmoGive1    8
    Weapon.AmmoGive2    2
    
    Weapon.BobStyle InverseSmooth
    Weapon.BobSpeed 2.5
    Weapon.BobRangeX 0.3
    Weapon.BobRangeY 0.4

    Weapon.Kickback 200
    Decal BulletChip

    Inventory.PickupMessage "$DAKKA_PK_SHOTGUN"
    Obituary "$DAKKA_MP_SHOTGUN"

    +NOALERT
    +ALT_AMMO_OPTIONAL

    States
    {
      Select:
        DKSG AA 0 A_Raise
        DKSG A 1 A_Raise
        loop

      Deselect:
        DKSG A 0 A_Light0
        DKSG AAA 0 A_Lower
        DKSG A 1 A_Lower
        loop

      Ready:
        DKSG A 1 A_WeaponReady
        loop

      Fire:
        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 3, 0)
        DKSG A 0 A_AlertMonsters
        DKSG A 0 A_FireBullets(3, 2.5, 10, 12, "DakkaPuff", FBF_NORANDOM | FBF_USEAMMO)
        DKSG A 0 A_FireBullets(3, 2.5, 5,  1,  "DakkaPuff", FBF_NORANDOM)
        DKSG B 0 A_GiveInventory("GiveScrapCounter", 40)
        DKSG A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, 4, 1, 14)
        DKSG A 0 A_GunFlash
        DKSG A 1 bright offset(0, 38) A_PlaySound("dakka/shotfire", CHAN_WEAPON)
        DKSG A 2 bright offset(0, 42)
        DKSG A 1 bright offset(0, 41)
        
        DKSG A 0 A_JumpIfInventory("DakkaInfiniteAmmo", 1, 2)
        DKSG A 0 A_JumpIfInventory("DakkaShells", 1, 1)
        goto NoPump
       
        DKSG B 2 offset(0, 39) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG C 1 offset(0, 36) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG DE 1 offset(0, 33) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
      
      WaitDoPump:
        DKSG F 1 offset(0, 32) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG F 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG F 0 A_PlaySound("dakka/shotreload", 5)
        DKSG F 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG G 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG H 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG G 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG F 3 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG EDC 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG B 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG B 0 A_ReFire
        goto Ready
      
      NoPump:
        DKSG A 2 offset(0, 38) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG A 2 offset(0, 36) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG A 1 offset(0, 33) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG A 1 offset(0, 32) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG A 0               A_CheckReload
        DKSG B 2               A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG CDE 1             A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        goto WaitDoPump


      AltFire:
        DKSG A 0 A_JumpIfInventory("DakkaInfiniteAmmo", 1, 2)
        DKSG A 0 A_JumpIfInventory("DakkaShotGrenades", 1, 1)
        goto AltFail

        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 3, 1)
        DKSG A 0 A_AlertMonsters
        DKSG A 0 A_FireCustomMissile("ShotgunGrenade")
        DKSG A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, 2, 2, 14)
        DKSG B 0 A_GiveInventory("GiveScrapCounter", 60)
        DKSG A 0 A_GunFlash
        DKSG A 0 A_PlaySound("dakka/shotgrenade", CHAN_WEAPON)
        DKSG A 1 bright offset(0, 38)
        DKSG A 2 bright offset(0, 41)
        DKSG A 1 bright offset(0, 40)
        
        DKSG A 0 A_JumpIfInventory("DakkaInfiniteAmmo", 1, 2)
        DKSG A 0 A_JumpIfInventory("DakkaShotGrenades", 1, 1)
        goto AltNoPump
        
        DKSG B 2 offset(0, 38) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG C 1 offset(0, 36) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG D 1 offset(0, 34) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG E 1 offset(0, 34) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG F 0 A_PlaySound("dakka/ssgopen", 5)
        DKSG E 1 offset(0, 33) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
      
      AltDoPump:
        DKSG F 2 offset(0, 32) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG G 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG H 4 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG G 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG F 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG F 0 A_PlaySound("dakka/ssgclose", 6)
        DKSG F 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG E 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG D 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG C 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG B 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG B 0 A_ReFire
        goto Ready

      AltFail:
        DKSG A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready
      
      AltNoPump:
        DKSG A 2 offset(0, 38) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG A 2 offset(0, 36) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG A 1 offset(0, 33) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG A 1 offset(0, 32) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG A 0 A_JumpIfInventory("DakkaShotGrenades", 1, 1)
        goto Ready
        
        DKSG B 2   A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG CDE 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKSG E   0 A_PlaySound("dakka/ssgopen", 5)
        DKSG E   1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        goto AltDoPump
        
      Flash:
        DKSF B 1 bright offset(0, 38) A_Light(5)
        DKSF B 1 bright offset(0, 32) A_Light(4)
        DKSF A 1 bright A_Light(3)
        TNT1 A 1 bright A_Light(2)
        TNT1 A 1 bright A_Light(1)
        goto LightDone

      AltFlash:
        DKSF B 1 bright offset(0, 37) A_Light(3)
        DKSF B 1 bright offset(0, 32) A_Light(2)
        DKSF A 2 bright A_Light(1)
        goto LightDone

      Spawn:
        DKSG Z -1
        stop
    }
}


actor ShotgunGrenade
{
    Radius 6
    Height 6
    Speed 55

    Projectile
    Damage (0)

    +MTHRUSPECIES
    +HITTRACER

    -NOGRAVITY
    Gravity 0.6

    ProjectileKickback 100
    Obituary "$DAKKA_MP_SHOTGUN_GRENADE"
    
    var int user_velx;
    var int user_vely;
    var int user_velz;
    var int user_flytime;
    var int user_downtime;

    States
    {
      Spawn:
        DGRN A 0
        DGRN A 0 A_SetUserVar("user_velx", velx * 65536)
        DGRN A 0 A_SetUserVar("user_vely", vely * 65536)
        DGRN A 0 A_SetUserVar("user_velz", velz * 65536)
        DGRN A 0 A_SetUserVar("user_flytime", user_flytime + 1)
        DGRN A 0 bright A_SpawnItemEx("ShotNadeTrail", -momx,   -momy,   -momz,   0,0,0, 0, SXF_ABSOLUTEPOSITION)
        DGRN A 0 bright A_SpawnItemEx("ShotNadeTrail", -momx/3, -momy/3, -momz/3, 0,0,0, 0, SXF_ABSOLUTEPOSITION)
        DGRN A 0 bright A_SpawnItemEx("ShotNadeTrail", -2*momx/3, -2*momy/3, -2*momz/3, 0,0,0, 0, SXF_ABSOLUTEPOSITION)
        DGRN A 0 A_JumpIf(user_flytime < 4, 2)
        DGRN A 0 A_JumpIf(CallACS("Dakka_CheckDetonate"), "EarlyDetonate")
        DGRN A 1
        loop

      Death:
        DGRN A 0 bright A_Stop
        DGRN A 0 bright A_NoGravity
        DGRN A 0 bright ACS_NamedExecuteWithResult("Dakka_StickyGrenade", true, -1)
        goto DeathWait
      
      DeathWait:
        DGRN A 1 bright A_SpawnItemEx("ShotNadeTrail", -8,0,0, 0,frandom(-0.2, 0.2),frandom(1, 1.25))
        DGRN A 0 A_SetUserVar("user_downtime", user_downtime + 1)
        loop
      
      EarlyDetonate:
        TNT1 A 0 A_Stop
        TNT1 A 0 A_NoGravity
        TNT1 A 0 A_GiveToTarget("DakkaGrenadeClick")
        TNT1 A 0 A_Explode(28, 112, 0)
        TNT1 A 0 A_Explode(12, 112)
        TNT1 A 0 A_SpawnItemEx("ImpalerExplosion_Client")
        TNT1 A 0 A_SpawnItemEx("ShotgunMiniGrenade", 0,0,0, frandom(24,36), 0, frandom(2,5), frandom(-25,-16), SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("ShotgunMiniGrenade", 0,0,0, frandom(24,36), 0, frandom(2,5), frandom(-15,-6),  SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("ShotgunMiniGrenade", 0,0,0, frandom(24,36), 0, frandom(2,5), frandom(-5,  5),  SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("ShotgunMiniGrenade", 0,0,0, frandom(24,36), 0, frandom(2,5), frandom( 6,  15), SXF_TRANSFERPOINTERS)
        TNT1 A 4 A_SpawnItemEx("ShotgunMiniGrenade", 0,0,0, frandom(24,36), 0, frandom(2,5), frandom( 16, 25), SXF_TRANSFERPOINTERS)
        stop
      
      Detonate:
        TNT1 A 0 A_Explode(18, 112, 0)
        TNT1 A 0 A_Explode(12, 112)
        TNT1 A 0 A_SpawnItemEx("ImpalerExplosion_Client")
        TNT1 A 0 A_JumpIf(user_downtime < 6, "InstantDetonate")
        TNT1 A 0 A_JumpIf(z - floorz   < 2, "FloorBlast")
        TNT1 A 0 A_JumpIf(ceilingz - z < 8, "CeilingBlast")
        goto NormalBlast
      
      InstantDetonate:
        TNT1 A 0 A_JumpIf(z - floorz   < 2, "FloorBlast_Instant")
        TNT1 A 0 A_JumpIf(ceilingz - z < 8, "CeilingBlast")
        goto NormalBlast
        
      NormalBlast:
        TNT1 A 0 A_SpawnItemEx("ShotgunMiniGrenade", 0,0,0, frandom(16,24), 0, frandom(0,2), frandom(-25, 26),  SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("ShotgunMiniGrenade", 0,0,0, frandom(16,24), 0, frandom(0,2), frandom( 47, 98),  SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("ShotgunMiniGrenade", 0,0,0, frandom(16,24), 0, frandom(0,2), frandom( 119,170), SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("ShotgunMiniGrenade", 0,0,0, frandom(16,24), 0, frandom(0,2), frandom( 191,242), SXF_TRANSFERPOINTERS)
        TNT1 A 4 A_SpawnItemEx("ShotgunMiniGrenade", 0,0,0, frandom(16,24), 0, frandom(0,2), frandom( 263,314), SXF_TRANSFERPOINTERS)
        stop
        
      FloorBlast:
        TNT1 A 0 A_SpawnItemEx("ShotgunMiniGrenade", 0,0,0, frandom(16,24), 0, frandom(3,6), frandom(-25, 26),  SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("ShotgunMiniGrenade", 0,0,0, frandom(16,24), 0, frandom(3,6), frandom( 47, 98),  SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("ShotgunMiniGrenade", 0,0,0, frandom(16,24), 0, frandom(3,6), frandom( 119,170), SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("ShotgunMiniGrenade", 0,0,0, frandom(16,24), 0, frandom(3,6), frandom( 191,242), SXF_TRANSFERPOINTERS)
        TNT1 A 4 A_SpawnItemEx("ShotgunMiniGrenade", 0,0,0, frandom(16,24), 0, frandom(3,6), frandom( 263,314), SXF_TRANSFERPOINTERS)
        stop
        
      FloorBlast_Instant:
        TNT1 A 0 A_SpawnItemEx("ShotgunMiniGrenade", 0,0,0, frandom(16,24), 0, frandom(3,6), frandom(-81,-49), SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("ShotgunMiniGrenade", 0,0,0, frandom(16,24), 0, frandom(3,6), frandom(-48,-17), SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("ShotgunMiniGrenade", 0,0,0, frandom(16,24), 0, frandom(3,6), frandom(-16, 16), SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("ShotgunMiniGrenade", 0,0,0, frandom(16,24), 0, frandom(3,6), frandom( 17, 48), SXF_TRANSFERPOINTERS)
        TNT1 A 4 A_SpawnItemEx("ShotgunMiniGrenade", 0,0,0, frandom(16,24), 0, frandom(3,6), frandom( 49, 81), SXF_TRANSFERPOINTERS)
        stop
        
      CeilingBlast:
        TNT1 A 0 A_SpawnItemEx("ShotgunMiniGrenade", 0,0,0, frandom(8,16), 0, frandom(-6, -9), frandom(-25, 26),  SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("ShotgunMiniGrenade", 0,0,0, frandom(8,16), 0, frandom(-6, -9), frandom( 47, 98),  SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("ShotgunMiniGrenade", 0,0,0, frandom(8,16), 0, frandom(-6, -9), frandom( 119,170), SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SpawnItemEx("ShotgunMiniGrenade", 0,0,0, frandom(8,16), 0, frandom(-6, -9), frandom( 191,242), SXF_TRANSFERPOINTERS)
        TNT1 A 4 A_SpawnItemEx("ShotgunMiniGrenade", 0,0,0, frandom(8,16), 0, frandom(-6, -9), frandom( 263,314), SXF_TRANSFERPOINTERS)
        stop
    }
}

actor ShotgunMiniGrenade
{
    Radius 6
    Height 6
    Speed 55

    Projectile

    +MTHRUSPECIES
    +HITTRACER

    -NOGRAVITY
    Gravity 0.5
    Scale 0.5

    Obituary "$DAKKA_MP_SHOTGUN_GRENADE"
    Decal "BulletScorch"
    
    ProjectileKickback 100
    ReactionTime 14

    States
    {
      Spawn:
        DGRN A 0
        DGRN A 0 bright A_SpawnItemEx("ShotNadeTrail", -momx,   -momy,   -momz,   0,0,0, 0, SXF_ABSOLUTEPOSITION)
        DGRN A 0 bright A_SpawnItemEx("ShotNadeTrail", -momx/3, -momy/3, -momz/3, 0,0,0, 0, SXF_ABSOLUTEPOSITION)
        DGRN A 0 bright A_SpawnItemEx("ShotNadeTrail", -2*momx/3, -2*momy/3, -2*momz/3, 0,0,0, 0, SXF_ABSOLUTEPOSITION)
        DGRN A 1 A_Countdown
        loop

      Death:
        DGRN A 0 bright A_Stop
        DGRN A 0 bright A_NoGravity
        TNT1 A 0 A_Explode(30, 80, 1, false, 40)
        TNT1 A 4 A_SpawnItemEx("MiniGrenadeExplosion_Client")
        stop
    }
}

actor ShotNadeTrail
{
    +NOINTERACTION
    +CLIENTSIDEONLY

    RenderStyle Add
    Alpha 0.05
    Scale 0.1

    States
    {
      Spawn:
        TNT1 A 0 nodelay A_SetScale(frandom(0.125, 0.15))
        TNT1 A 0 A_Jump(256, "Smoke1", "Smoke2", "Smoke3", "Smoke4")
        goto Smoke1

      Smoke1:
        SMK1 ABCDEFGHIJKLMNOPQR 1 bright
        stop

      Smoke2:
        SMK2 ABCDEFGHIJKLMNOPQR 1 bright
        stop

      Smoke3:
        SMK3 ABCDEFGHIJKLMNOP 1 bright
        stop

      Smoke4:
        SMK4 ABCDEFGHIJKLMNOP 1 bright
        stop
    }
}



actor GrenadeExplosion_Client
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	
	RenderStyle Add
	
	var int user_ringangle;
	
	States
	{
	  Spawn:
	    MISL B 0 bright
		MISL B 0 A_PlaySound("dakka/grenadeexplode")
		goto RingSpawn
	
	  RingSpawn:
	    MISL B 0 A_SpawnItemEx("ImpalerRingParticle", 0,0,0, 32, 0, 0, user_ringangle)
		MISL B 0 A_SetUserVar("user_ringangle", user_ringangle + 10)
		MISL B 0 A_JumpIf(user_ringangle >= 360, "ParticleSpawn")
		loop
	
	  ParticleSpawn:
	    MISL B 0 A_JumpIf(z - floorz   < 1, "ParticleFloor")
	    MISL B 0 A_JumpIf(ceilingz - z < 1, "ParticleCeiling")
		goto ParticleOther
	
	  ParticleFloor:
	    MISL BBBBBBBBBBBBBBBB 0 A_SpawnItemEx("ImpalerExplosionSpark", frandom(-6, 6), frandom(-1, 1), frandom( 2,  6), frandom(4, 12), frandom(-3, 3), frandom(0, 10), frandom(0, 360))
		goto SmokeSpawn
	
	  ParticleCeiling:
	    MISL BBBBBBBBBBBBBBBB 0 A_SpawnItemEx("ImpalerExplosionSpark", frandom(-6, 6), frandom(-1, 1), frandom(-6, -2), frandom(4, 12), frandom(-3, 3), frandom(-10, 0), frandom(0, 360))
		goto SmokeSpawn
	
	  ParticleOther:
	    MISL BBBBBBBBBBBBBBBB 0 A_SpawnItemEx("ImpalerExplosionSpark", frandom(-6, 6), frandom(-1, 1), frandom(-3, 3), frandom(4, 12), frandom(-3, 3), frandom(-4, 8), frandom(0, 360))
		goto SmokeSpawn
		
	
	  SmokeSpawn:
		MISL BBBBBBBBBBBB 0 A_SpawnItemEx("ImpalerBigTrailParticle",   frandom(-6, 6), frandom(-1, 1), frandom(-4, 4), frandom(-3, 3), 0,              frandom(-3, 3), frandom(0, 360))
		DMIS GHIJ 2 bright
		stop
	}
}


actor MiniGrenadeExplosion_Client
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	
	RenderStyle Add
    Scale 0.5
	
	var int user_ringangle;
	
	States
	{
	  Spawn:
	    MISL B 0 bright
		MISL B 0 A_PlaySound("dakka/minigrenadeexplode")
		goto RingSpawn
	
	  RingSpawn:
	    MISL B 0 A_SpawnItemEx("MiniGrenadeRingParticle", 0,0,0, 16, 0, 0, user_ringangle)
		MISL B 0 A_SetUserVar("user_ringangle", user_ringangle + 20)
		MISL B 0 A_JumpIf(user_ringangle >= 360, "ParticleSpawn")
		loop
	
	  ParticleSpawn:
	    MISL B 0 A_JumpIf(z - floorz   < 1, "ParticleFloor")
	    MISL B 0 A_JumpIf(ceilingz - z < 1, "ParticleCeiling")
		goto ParticleOther
	
	  ParticleFloor:
	    MISL BBBBBB 0 A_SpawnItemEx("ImpalerExplosionSpark", frandom(-6, 6), frandom(-1, 1), frandom( 2,  6), frandom(4, 12), frandom(-3, 3), frandom(0, 10), frandom(0, 360))
		goto SmokeSpawn
	
	  ParticleCeiling:
	    MISL BBBBBB 0 A_SpawnItemEx("ImpalerExplosionSpark", frandom(-6, 6), frandom(-1, 1), frandom(-6, -2), frandom(4, 12), frandom(-3, 3), frandom(-10, 0), frandom(0, 360))
		goto SmokeSpawn
	
	  ParticleOther:
	    MISL BBBBBB 0 A_SpawnItemEx("ImpalerExplosionSpark", frandom(-6, 6), frandom(-1, 1), frandom(-3, 3), frandom(4, 12), frandom(-3, 3), frandom(-4, 8), frandom(0, 360))
		goto SmokeSpawn
		
	
	  SmokeSpawn:
		MISL BBBBBB 0 A_SpawnItemEx("ImpalerBigTrailParticle",   frandom(-6, 6), frandom(-1, 1), frandom(-4, 4), frandom(-3, 3), 0,              frandom(-3, 3), frandom(0, 360))
		DMIS GHIJ 2 bright
		stop
	}
}

actor MiniGrenadeRingParticle
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	
	RenderStyle Add
    Scale 0.5
	Alpha 0.1
	
	States
	{
	  Spawn:
	    OPRT A 1 bright A_FadeOut(0.01)
		loop
	}
}


// also used for pointer bullshit
actor ShotgunStickyPoint
{
    Radius 0
    Height 0
    
    +SERVERSIDEONLY
    +NOINTERACTION
    +MOVEWITHSECTOR
    RenderStyle None
    Scale 0.2
    
    States
    {
      Spawn: 
        PLSS A -1
        stop
    }
}

actor ShotgunStickyPoint_Floor: ShotgunStickyPoint
{
    -MOVEWITHSECTOR
    
    States
    {
      Spawn:
        PLSS A 0
        PLSS A 1 ACS_NamedExecuteWithResult("Dakka_AlignToSurface", 0)
        loop
    }
}
    

actor ShotgunStickyPoint_Ceiling: ShotgunStickyPoint
{
    -MOVEWITHSECTOR
    
    States
    {
      Spawn:
        PLSS A 0
        PLSS A 1 ACS_NamedExecuteWithResult("Dakka_AlignToSurface", 1)
        loop
    }
}


actor DakkaGrenadeClick: Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 A_PlaySound("dakka/grenadeclick", 7)
        stop
    }
}