actor DWep_Shotgun: DoomWeapon
{
    Tag "Shotgun"

    Weapon.SelectionOrder 1300

    Weapon.AmmoUse1 1
    Weapon.AmmoUse2 1
    Weapon.AmmoGive1  8
    Weapon.AmmoGive2  2
    Weapon.AmmoType1    "DakkaShells"
    Weapon.AmmoType2    "DakkaShotGrenades"

    Weapon.Kickback 200
    Decal BulletChip

    Inventory.PickupMessage "$DAKKA_PK_SHOTGUN"
    Obituary "$DAKKA_MP_SHOTGUN"

    +NOALERT
    +ALT_AMMO_OPTIONAL

    States
    {
      Select:
        PKSG AA 0 A_Raise
        PKSG A 1 A_Raise
        loop

      Deselect:
        PKSG AAA 0 A_Lower
        PKSG A 1 A_Lower
        loop

      Ready:
        PKSG A 1 A_WeaponReady
        loop

      Fire:
        "----" A 0 ACS_ExecuteWithResult(397, 3, 0)
        PKSG A 0 A_AlertMonsters
        PKSG A 0 A_FireBullets(3, 2.5, 10, 12, "DakkaPuff", FBF_NORANDOM | FBF_USEAMMO)
        PKSG B 0 A_GiveInventory("GiveScrapCounter", 4000)
        PKSG A 0 ACS_ExecuteAlways(467, 0, 4, 4, 12)
        PKSG A 0 A_GunFlash
        PKSG A 1 bright offset(0, 38) A_PlaySound("dakka/shotfire", CHAN_WEAPON)
        PKSG A 2 bright offset(0, 42)
        PKSG A 1 bright offset(0, 41)
        PKSG B 2 offset(0, 39)
        PKSG C 1 offset(0, 36)
        PKSG DE 1 offset(0, 33)
        PKSG F 2 offset(0, 32)
        PKSG A 0 A_PlaySound("dakka/shotreload", 5)
        PKSG F 3 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        PKSG G 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        PKSG H 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        PKSG G 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        PKSG F 3 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        PKSG EDC 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        PKSG B 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        PKSG B 0 A_ReFire
        goto Ready


      AltFire:
        PKS2 A 0 A_JumpIfInventory("DakkaInfiniteAmmo", 1, 2)
        PKSG A 0 A_JumpIfInventory("DakkaShotGrenades", 1, 1)
        goto AltFail

        "----" A 0 ACS_ExecuteWithResult(397, 3, 1)
        PKSG A 0 A_AlertMonsters
        PKSG A 0 A_FireCustomMissile("ShotgunGrenade")
        PKSG A 0 ACS_ExecuteAlways(467, 0, 2, 5, 12)
        PKSG A 0 A_GunFlash
        PKSG A 0 A_PlaySound("dakka/shotgrenade", CHAN_WEAPON)
        PKSG A 1 bright offset(0, 38)
        PKSG A 2 bright offset(0, 41)
        PKSG A 1 bright offset(0, 40)
        PKSG B 2 offset(0, 38)
        PKSG C 1 offset(0, 36)
        PKSG D 1 offset(0, 34)
        PKSG E 1 offset(0, 34)
        PKSG F 0 A_PlaySound("dakka/ssgopen", 5)
        PKSG E 1 offset(0, 33)
        PKSG F 2 offset(0, 32) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        PKSG G 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        PKSG H 4 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        PKSG G 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        PKSG F 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        PKSG F 0 A_PlaySound("dakka/ssgclose", 6)
        PKSG F 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        PKSG E 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        PKSG D 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        PKSG C 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        PKSG B 2 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        PKSG B 0 A_ReFire
        goto Ready

      AltFail:
        PKSG A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready

      Flash:
        BSGF B 1 bright offset(0, 38) A_Light2
        BSGF B 1 bright offset(0, 32) A_Light2
        BSGF A 1 bright A_Light1
        goto LightDone

      AltFlash:
        BSGF B 1 bright offset(0, 37) A_Light2
        BSGF B 1 bright offset(0, 32) A_Light2
        BSGF A 2 bright A_Light1
        goto LightDone

      Spawn:
        BSGN A -1
        stop
    }
}


actor ShotgunGrenade
{
    Radius 6
    Height 6
    Speed 55

    Projectile
    Damage (25)

    +MTHRUSPECIES
    +EXTREMEDEATH
    +DEHEXPLOSION

    -NOGRAVITY
    Gravity 0.5

    Obituary "$DAKKA_MP_SHOTGUN_GRENADE"
    Decal "Scorch"

    DeathSound "dakka/explode"

    States
    {
      Spawn:
        SGRN A 1 bright A_SpawnItemEx("ShotNadeTrail", -momx,   -momy,   -momz,   0,0,0, 0, SXF_ABSOLUTEPOSITION)
        SGRN A 0 bright A_SpawnItemEx("ShotNadeTrail", -momx/3, -momy/3, -momz/3, 0,0,0, 0, SXF_ABSOLUTEPOSITION)
        SGRN A 0 bright A_SpawnItemEx("ShotNadeTrail", -2*momx/3, -2*momy/3, -2*momz/3, 0,0,0, 0, SXF_ABSOLUTEPOSITION)
        loop

      Death:
        MIS2 B 0 A_Stop
        MIS2 B 0 A_NoGravity
        MIS2 B 0 bright A_Explode(112, 112, 0)
        MIS2 B 0 bright A_Explode(48, 112)
        MIS2 F 5 bright
        MIS2 G 4 bright
        MIS2 HIJ 3 bright
        stop
    }
}

actor ShotNadeTrail
{
    +NOINTERACTION
    +CLIENTSIDEONLY

    RenderStyle Translucent
    Scale 0.5
    Alpha 0.5

    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_Jump(255, "S1", "S2", "S3", "S4", "S5")

      S1:
        PUFF B 1

      S2:
        PUFF B 1

      S3:
        PUFF B 1

      S4:
        PUFF B 1

      S5:
        PUFF CD 4
        stop
    }
}
