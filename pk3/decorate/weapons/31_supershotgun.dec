actor DakkaSSG_ShotsFired:   Counter { +INTERHUBSTRIP }  // fite me
actor DakkaSSG_PreferAlt:    Boolean {}
actor DakkaSSG_OutOfSelect:  Boolean {}

actor DWep_SuperShotgun: DoomWeapon
{
    Tag "Super Shotgun"

    Weapon.SlotNumber 3
    Weapon.SelectionOrder 450

    Weapon.AmmoUse1 2
    Weapon.AmmoUse2 2
    Weapon.AmmoGive1 16
    Weapon.AmmoGive2 4
    Weapon.AmmoType1 "DakkaShells"
    Weapon.AmmoType2 "DakkaGyroShells"

    Decal BulletChip

    Weapon.Kickback 350

    Inventory.PickupMessage "$DAKKA_PK_SUPERSHOTGUN"
    Obituary "$DAKKA_MP_SUPERSHOTGUN"

    +NOALERT
    +ALT_AMMO_OPTIONAL

    States
    {
      Select:
        DSSG A 0 A_GiveInventory("DakkaSSG_OutOfSelect")
        goto Select2

      Select2:
        DSSG AA 0 A_Raise
        DSSG A 1 A_Raise
        loop

      Deselect:
        DSSG A 0 A_Light0
        DSSG AAA 0 A_Lower
        DSSG A 1 A_Lower
        loop

      Ready:
        DSSG A 0 A_JumpIfInventory("DakkaSSG_OutOfSelect", 1, "FirstReady")
        goto Ready2

      FirstReady:
        DSSG A 0 A_TakeInventory("DakkaSSG_OutOfSelect")
        DSSG A 0 A_JumpIf(CallACS("DSSG_ShotsLeft") <= 0, "FirstReload")
        goto Ready2

      Ready2:
        DSSG A 49 A_WeaponReady(WRF_ALLOWRELOAD)
        goto Ready3

      Ready3:
        DSSG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
        DSSG A 0 A_JumpIfInventory("DakkaSSG_ShotsFired", 1, "IdleReload")
        loop



      Fire:
        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 4, 0)
        DSSG B 0 A_GiveInventory("GiveScrapCounter", 80)
        DSSG A 0 A_AlertMonsters

        
        DSSG A 0 A_JumpIfInventory("DakkaSSG_ShotsFired", 1, "FireLastShell")
        DSSG A 0 A_PlaySound("dakka/ssgfire", CallACS("Dakka_RotateSound"))
        goto FireContinue
      
      FireLastShell:
        DSSG A 0 A_PlaySound("dakka/ssgfire2", CallACS("Dakka_RotateSound"))
        goto FireContinue
      
      FireContinue:
        DSSG A 0 ACS_NamedExecuteWithResult("DSSG_ChangeShots")
        DSSG A 0 A_FireBullets(14.6, 9.4, 20, 12, "DakkaPuff", FBF_NORANDOM | FBF_USEAMMO)
        DSSG A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, 9, 1, 15)
        DSSG A 0 A_GunFlash

        DSSG A 0 A_JumpIf(CallACS("DSSG_ShotsLeft") < 1, "Fire2")
        DSSG A 0 A_JumpIf(CallACS("DSSG_BothClicked") == 1, "DoubleFireCheck")
        goto Fire2

      DoubleFireCheck:
        DSSG A 0 A_JumpIfInventory("DakkaInfiniteAmmo", 1, 2)
        DSSG A 0 A_JumpIfInventory("DakkaShells", 2, 1)
        goto Fire2

        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 4, 0)
        DSSG B 0 A_GiveInventory("GiveScrapCounter", 80)
        DSSG A 0 A_AlertMonsters

        
        DSSG A 0 A_JumpIfInventory("DakkaSSG_ShotsFired", 1, "DoubleFireLastShell")
        DSSG A 0 A_PlaySound("dakka/ssgfire", CallACS("Dakka_RotateSound"))
        goto DoubleFireContinue
      
      DoubleFireLastShell:
        DSSG A 0 A_PlaySound("dakka/ssgfire2", CallACS("Dakka_RotateSound"))
        goto DoubleFireContinue
      
      DoubleFireContinue:
        DSSG A 0 ACS_NamedExecuteWithResult("DSSG_ChangeShots")
        DSSG A 0 A_FireBullets(14.6, 9.4, 20, 12, "DakkaPuff", FBF_NORANDOM | FBF_USEAMMO)
        DSSG A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, 9, 1, 15)
        DSSG A 0 A_GunFlash("FlashDouble")


        DSSG A 1 bright offset(0, 48)
        DSSG A 1 bright offset(0, 54)           A_JumpIf(CallACS("DSSG_Refire") == 1, "Fire")
        DSSG A 1 bright offset(0, 54)           A_JumpIf(CallACS("DSSG_Refire") == 1, "Fire")
        DSSG A 0 offset(0, 32) A_CheckReload 
        DSSG A 1 bright offset(0, 52)           A_JumpIf(CallACS("DSSG_Refire") == 1, "Fire")
        DSSG A 0 A_JumpIf(CallACS("DSSG_ShotsLeft") < 1, "ReloadSequence")
        DSSG A 0 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB | WRF_ALLOWRELOAD)

        DSSG A 1 bright offset(0, 49)          A_JumpIf(CallACS("DSSG_Refire") == 1, "Fire")
        DSSG A 0 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB | WRF_ALLOWRELOAD)
        DSSG A 1 bright offset(0, 46)          A_JumpIf(CallACS("DSSG_Refire") == 1, "RefireCheckMain")
        DSSG A 0 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB | WRF_ALLOWRELOAD)

        DSSG A 1 bright offset(0, 40)          A_JumpIf(CallACS("DSSG_Refire") == 1, "RefireCheckMain")
        DSSG A 0 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB | WRF_ALLOWRELOAD)
        DSSG A 1 bright offset(0, 35)          A_JumpIf(CallACS("DSSG_Refire") == 1, "RefireCheckMain")
        DSSG A 0 A_WeaponReady(WRF_NOBOB | WRF_ALLOWRELOAD)

        DSSG A 1 offset(0, 32) A_JumpIf(CallACS("DSSG_Refire"), "RefireCheckMain")
        DSSG A 0 A_WeaponReady(WRF_NOBOB | WRF_ALLOWRELOAD)
        goto Ready
        


      Fire2:
        DSSG A 1 bright offset(0, 42)
        DSSG A 1 bright offset(0, 46)           A_JumpIf(CallACS("DSSG_Refire") == 1, "Fire")
        DSSG A 1 bright offset(0, 46)           A_JumpIf(CallACS("DSSG_Refire") == 1, "Fire")
        DSSG A 0 offset(0, 32) A_CheckReload 
        DSSG A 1 bright offset(0, 44)           A_JumpIf(CallACS("DSSG_Refire") == 1, "Fire")
        DSSG A 0 A_JumpIf(CallACS("DSSG_ShotsLeft") < 1, "ReloadSequence")
        DSSG A 0 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB | WRF_ALLOWRELOAD)

        DSSG A 1 bright offset(0, 37)          A_JumpIf(CallACS("DSSG_Refire") == 1, "Fire")
        DSSG A 0 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB | WRF_ALLOWRELOAD)
        DSSG A 1 bright offset(0, 37)          A_JumpIf(CallACS("DSSG_Refire") == 1, "RefireCheckMain")
        DSSG A 0 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB | WRF_ALLOWRELOAD)

        DSSG A 1 bright offset(0, 34)          A_JumpIf(CallACS("DSSG_Refire") == 1, "RefireCheckMain")
        DSSG A 0 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB | WRF_ALLOWRELOAD)
        DSSG A 1 bright offset(0, 34)          A_JumpIf(CallACS("DSSG_Refire") == 1, "RefireCheckMain")
        DSSG A 0 A_WeaponReady(WRF_NOBOB | WRF_ALLOWRELOAD)

        DSSG A 1 offset(0, 32) A_JumpIf(CallACS("DSSG_Refire"), "RefireCheckMain")
        DSSG A 0 A_WeaponReady(WRF_NOBOB | WRF_ALLOWRELOAD)
        goto Ready
        


      AltFire:
        DSSG A 0 A_JumpIfInventory("DakkaInfiniteAmmo", 1, "ActualAltFire")
        DSSG A 0 A_JumpIfInventory("DakkaGyroShells", 2, "ActualAltFire")
        goto AltFail


      ActualAltFire:
        "----" A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 4, 1)
        DSSG A 0 A_AlertMonsters
        DSSG A 0 ACS_NamedExecuteWithResult("DSSG_ChangeShots")
        DSSG AAAAAAAAAAAAAAAAAAAA 0 A_FireCustomMissile("DakkaGyrojet", 
                                    CallACS("Dakka_Spread", -1460, 1460) / 65536.0, 0, 0,0,0,
                                    CallACS("Dakka_Spread", -940,  940)  / 65536.0)
        DSSG A 0 A_FireCustomMissile("AmmoWaster")
        DSSG A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, 9, 1, 15)
        DSSG B 0 A_GiveInventory("GiveScrapCounter", 80)
        DSSG A 0 A_PlaySound("dakka/ssgaltfire", CallACS("Dakka_RotateSound"))
        DSSG A 0 A_GunFlash
        DSSG A 1 bright offset(0, 42)
        DSSG A 1 bright offset(0, 46)           A_JumpIf(CallACS("DSSG_Refire") == 1, "AltFire")
        DSSG A 1 bright offset(0, 46)           A_JumpIf(CallACS("DSSG_Refire") == 1, "AltFire")
        DSSG A 0 offset(0, 32) A_CheckReload 
        DSSG A 1 bright offset(0, 44)           A_JumpIf(CallACS("DSSG_Refire") == 1, "AltFire")
        DSSG A 0 A_JumpIf(CallACS("DSSG_ShotsLeft") < 1, "ReloadSequence")
        DSSG A 0 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB | WRF_ALLOWRELOAD)

        DSSG A 1 bright offset(0, 37)          A_JumpIf(CallACS("DSSG_Refire") == 1, "AltFire")
        DSSG A 0 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB | WRF_ALLOWRELOAD)
        DSSG A 1 bright offset(0, 37)          A_JumpIf(CallACS("DSSG_Refire") == 1, "RefireCheckAlt")
        DSSG A 0 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB | WRF_ALLOWRELOAD)

        DSSG A 1 bright offset(0, 34)          A_JumpIf(CallACS("DSSG_Refire") == 1, "RefireCheckAlt")
        DSSG A 0 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB | WRF_ALLOWRELOAD)
        DSSG A 1 bright offset(0, 34)          A_JumpIf(CallACS("DSSG_Refire") == 1, "RefireCheckAlt")
        DSSG A 0 A_WeaponReady(WRF_NOBOB)

        DSSG A 1 offset(0, 32) A_JumpIf(CallACS("DSSG_Refire") == 1, "RefireCheckAlt")
        DSSG A 0 A_WeaponReady(WRF_NOBOB | WRF_ALLOWRELOAD)
        goto Ready

      AltFail:
        DSSG A 1 A_WeaponReady(WRF_NOSECONDARY | WRF_ALLOWRELOAD)
        goto Ready


      // The two states below exist so that the game actually takes the right ammo
      
      RefireCheckMain:
        DSSG A 1 A_WeaponReady(WRF_NOBOB | WRF_NOSWITCH)
        goto Fire
      
      RefireCheckAlt:
        DSSG A 1 A_WeaponReady(WRF_NOBOB | WRF_NOSWITCH)
        goto AltFire
      
      RefireCheckReady:
        DSSG A 1 A_WeaponReady(WRF_NOBOB | WRF_NOSWITCH)
        goto Ready



      ReloadSequence:
        DSSG B 0 A_JumpIfInventory("DakkaShells",     2, "Reload2")
        DSSG B 0 A_JumpIfInventory("DakkaGyroShells", 2, "Reload2")
        goto WhatReload

      Reload2:
        DSSG B 3 offset(0, 32) A_WeaponReady(WRF_NOFIRE)
        DSSG F 0 A_PlaySound("dakka/ssgopen", CHAN_WEAPON)
        DSSG C 1 A_WeaponReady(WRF_NOFIRE)
        DSSG D 1 A_WeaponReady(WRF_NOFIRE)
        DSSG E 5 A_WeaponReady(WRF_NOFIRE)
        DSSG F 2 A_WeaponReady(WRF_NOFIRE)
        DSSG G 2 A_WeaponReady(WRF_NOFIRE)
        DSSG H 3 A_WeaponReady(WRF_NOFIRE)
        DSSG IJ 2 A_WeaponReady(WRF_NOFIRE)
        DSSG K 0 A_PlaySound("dakka/ssgload", CHAN_WEAPON)
        DSSG K 3 A_WeaponReady(WRF_NOFIRE)
        DSSG L 2 A_WeaponReady(WRF_NOFIRE)
        DSSG M 0 ACS_NamedExecuteWithResult("DSSG_ChangeShots", -0x7FFFFFFF)
        DSSG M 3 A_WeaponReady(WRF_NOFIRE)
        DSSG N 2 A_WeaponReady(WRF_NOFIRE)
        DSSG OPQ 1 A_WeaponReady(WRF_NOFIRE)
        DSSG R 0 A_PlaySound("dakka/ssgclose", CHAN_WEAPON)
        DSSG R 3 A_WeaponReady(WRF_NOFIRE)
        DSSG S 3 A_WeaponReady(WRF_NOFIRE)
        DSSG AAAA 1 A_JumpIf(CallACS("DSSG_Refire") == 1, "RefireCheckReady")
        DSSG A 0 A_ReFire
        goto Ready

      WhatReload:
        DSSG A 0
        goto Ready

      Reload:
        DSSG B 0 A_JumpIfInventory("DakkaSSG_ShotsFired", 1, "IdleReload")
        goto Ready3

      IdleReload:
        DSSG B 0 A_JumpIfInventory("DakkaShells", 2, "IReload2")
        DSSG B 0 A_JumpIfInventory("DakkaGyroShells", 2, "IReload2")
        goto Ready3

      IReload2:
        DSSG B 3 offset(0, 32) A_WeaponReady
        DSSG F 0 A_PlaySound("dakka/ssgopen", CHAN_WEAPON)
        DSSG C 1 A_WeaponReady
        DSSG D 1 A_WeaponReady
        DSSG E 5 A_WeaponReady
        DSSG F 2 A_WeaponReady
        DSSG G 2 A_WeaponReady
        DSSG H 3 A_WeaponReady
        DSSG IJ 2 A_WeaponReady
        DSSG K 0 A_PlaySound("dakka/ssgload", CHAN_WEAPON)
        DSSG K 3 A_WeaponReady
        DSSG L 2 A_WeaponReady
        DSSG M 0 ACS_NamedExecuteWithResult("DSSG_ChangeShots", -0x7FFFFFFF)
        DSSG M 3 A_WeaponReady
        DSSG N 2 A_WeaponReady
        DSSG OPQ 1 A_WeaponReady
        DSSG R 0 A_PlaySound("dakka/ssgclose", CHAN_WEAPON)
        DSSG R 3 A_WeaponReady
        DSSG S 3 A_WeaponReady
        goto Ready

      FirstReload:
        DSSG B 0 A_JumpIfInventory("DakkaShells", 2, "FReload2")
        DSSG B 0 A_JumpIfInventory("DakkaGyroShells", 2, "FReload2")
        goto Ready3

      FReload2:
        DSSG B 3 offset(0, 32) A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DSSG F 0 A_PlaySound("dakka/ssgopen", CHAN_WEAPON)
        DSSG C 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DSSG D 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DSSG E 5 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DSSG F 2 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DSSG G 2 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DSSG H 3 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DSSG IJ 2 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DSSG K 0 A_PlaySound("dakka/ssgload", CHAN_WEAPON)
        DSSG K 3 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DSSG L 2 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DSSG M 0 ACS_NamedExecuteWithResult("DSSG_ChangeShots", -0x7FFFFFFF)
        DSSG M 3 A_WeaponReady(WRF_NOBOB)
        DSSG N 2 A_WeaponReady(WRF_NOBOB)
        DSSG OPQ 1 A_WeaponReady(WRF_NOBOB)
        DSSG R 0 A_PlaySound("dakka/ssgclose", CHAN_WEAPON)
        DSSG R 3 A_WeaponReady(WRF_NOBOB)
        DSSG S 3 A_WeaponReady(WRF_NOBOB)
        goto Ready


      Flash:
        DSSF B 1 bright offset(0, 42) A_Light(8)
        DSSF B 1 bright offset(0, 32) A_Light(6)
        DSSF A 1 bright A_Light(4)
        DSSF A 1 bright A_Light(3)
        TNT1 A 1 bright A_Light(2)
        TNT1 A 1 bright A_Light(1)
        goto LightDone

      FlashDouble:
        DSSF B 1 bright offset(0, 48) A_Light(16)
        DSSF B 1 bright offset(0, 32) A_Light(12)
        DSSF A 1 bright A_Light(10)
        DSSF A 1 bright A_Light(8)
        TNT1 A 1 A_Light(6)
        TNT1 A 1 A_Light(4)
        TNT1 A 1 A_Light(3)
        TNT1 A 1 A_Light(2)
        TNT1 A 1 A_Light(1)
        goto LightDone

      Spawn:
        DSSG Z -1
        stop
    }
}

actor DakkaGyrojet: FastProjectile
{
    Radius 4
    Height 3
    Speed 40

    Projectile
    +MTHRUSPECIES
    +FORCERADIUSDMG
    +DEHEXPLOSION
    +SKYEXPLODE

    Scale 0.3
    Decal BulletScorch

    DeathSound  "dakka/gyrojetexplode"
    
    MissileType "DGyroTrail"
    MissileHeight 8

    Obituary "$DAKKA_MP_SUPERSHOTGUN_GYRO"

    States
    {
      Spawn:
        MIS3 AAAAAAAA 1 bright A_ScaleVelocity(frandom(1.08, 1.11))
        MIS3 A -1 bright
        wait

      Death:
        DMIS B 0 bright A_Explode(13, 88, 0, 0, 40)
        DMIS B 0 bright A_Explode(2,  88, 1, 0, 40)
        DMIS F 4 bright
        DMIS G 3 bright
        DMIS HIJ 2 bright
        stop
    }
}

actor DGyroTrail
{
    +NOINTERACTION
    +CLIENTSIDEONLY

    RenderStyle Add
    Alpha 0.02
    Scale 0.1

    States
    {
      Spawn:
        TNT1 A 0 nodelay A_SetScale(frandom(0.075, 0.1))
        TNT1 A 0 A_Jump(256, "Smoke1", "Smoke2", "Smoke3", "Smoke4")
        goto Smoke1

      Smoke1:
        SMK1 ABCDEFGHIJKLMNOPQR 1
        stop

      Smoke2:
        SMK2 ABCDEFGHIJKLMNOPQR 1
        stop

      Smoke3:
        SMK3 ABCDEFGHIJKLMNOP 1
        stop

      Smoke4:
        SMK4 ABCDEFGHIJKLMNOP 1
        stop
    }
}
