actor DakkaSSG_ShotsFired:   Counter { +INTERHUBSTRIP }  // fite me
actor DakkaSSG_RollFire:     Boolean {}
actor DakkaSSG_OutOfSelect:  Boolean {}

actor DWep_SuperShotgun: DoomWeapon
{
    Tag "Super Shotgun"

    Weapon.SlotNumber 3
    Weapon.SelectionOrder 450

    Weapon.AmmoType1    "DakkaShells"
    Weapon.AmmoType2    "DakkaShotGrenades"
    Weapon.AmmoUse1     2
    Weapon.AmmoUse2     2
    Weapon.AmmoGive1    16
    Weapon.AmmoGive2    4

    Weapon.BobStyle InverseSmooth
    Weapon.BobSpeed 2.5
    Weapon.BobRangeX 0.3
    Weapon.BobRangeY 0.4

    Weapon.Kickback 350

    Inventory.PickupMessage "$DAKKA_PK_SUPERSHOTGUN"
    Obituary "$DAKKA_MP_SUPERSHOTGUN"

    +NOALERT
    +ALT_AMMO_OPTIONAL

    States
    {
      Select:
        DSSG A 0 A_GiveInventory("DakkaSSG_OutOfSelect")
        goto Select2

      Select2:
        DSSG AA 0 A_Raise
        DSSG A 1 A_Raise
        loop

      Deselect:
        DSSG A 0 A_Light0
        DSSG AAA 0 A_Lower
        DSSG A 1 A_Lower
        loop

      Ready:
        DSSG A 0 A_JumpIfInventory("DakkaSSG_OutOfSelect", 1, "FirstReady")
        goto Ready2

      FirstReady:
        DSSG A 0 A_TakeInventory("DakkaSSG_OutOfSelect")
        DSSG A 0 A_JumpIf(CallACS("DSSG_ShotsLeft") <= 0, "FirstReload")
        goto Ready2

      Ready2:
        DSSG A 49 A_WeaponReady(WRF_ALLOWRELOAD)
        goto Ready3

      Ready3:
        DSSG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
        DSSG A 0 A_JumpIfInventory("DakkaSSG_ShotsFired", 1, "IdleReload")
        loop



      Fire:
        DSSG A 0 A_JumpIfInventory("DakkaSSG_ShotsFired", 1, "FireLastShell")
        DSSG A 0 A_PlaySound("dakka/ssgfire", CallACS("Dakka_RotateSound"))
        goto FireContinue

      FireLastShell:
        DSSG A 0 A_PlaySound("dakka/ssgfire2", CallACS("Dakka_RotateSound"))
        goto FireContinue


      FireContinue:
        DSSG A 0 A_GunFlash
        DSSG A 0 A_AlertMonsters
        DSSG A 0 ACS_NamedExecuteWithResult("DSSG_ChangeShots")
        DSSG A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 4, 0)
        DSSG A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, 9, 2, 15)

        DSSG A 0 ACS_NamedExecuteWithResult("Dakka_UseAmmo", AMMO_SHELL, 2, 8)
        DSSG A 0 A_FireBullets(14.6, 9.4, 20, 12, "DakkaPuff", FBF_NORANDOM)

        DSSG A 0 A_JumpIf(CallACS("DSSG_ShotsLeft") < 1, "FireRecoil")
        DSSG A 0 A_JumpIfInventory("DakkaSSG_RollFire", 1, 1)
        goto FireRecoil

        DSSG A 0 A_JumpIf(CallACS("DSSG_BothClicked") == 1, "DoubleFireCheck")
        goto FireRecoil


      FireRecoil:
        DSSG A 1 bright offset(0, 42)
        DSSG A 1 bright offset(0, 46)   A_JumpIf(CallACS("DSSG_Refire") == 1, "Fire_Roll")
        DSSG A 1 bright                 A_JumpIf(CallACS("DSSG_Refire") == 1, "Fire_Roll")
        DSSG A 0 bright                 A_CheckReload
        DSSG A 0 bright                 A_JumpIf(CallACS("DSSG_Refire") == 1, "Fire_Roll")
        DSSG A 0 bright                 A_JumpIf(CallACS("DSSG_ShotsLeft") < 1, "ReloadSequence")
        DSSG A 1 bright offset(0, 44)   A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)

        DSSG A 0 bright                 A_JumpIf(CallACS("DSSG_Refire") == 1, "RefireCheckMain")
        DSSG A 1 bright offset(0, 37)   A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DSSG A 0 bright                 A_JumpIf(CallACS("DSSG_Refire") == 1, "RefireCheckMain")
        DSSG A 1 bright                 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)

        DSSG A 0 bright                 A_JumpIf(CallACS("DSSG_Refire") == 1, "RefireCheckMain")
        DSSG A 1 bright offset(0, 34)   A_WeaponReady(WRF_NOFIRE | WRF_NOBOB | WRF_ALLOWRELOAD)
        DSSG A 0 bright                 A_JumpIf(CallACS("DSSG_Refire") == 1, "RefireCheckMain")
        DSSG A 1 bright                 A_WeaponReady(WRF_NOBOB | WRF_ALLOWRELOAD)

        DSSG A 0                        A_JumpIf(CallACS("DSSG_Refire"), "RefireCheckMain")
        DSSG A 1        offset(0, 32)   A_WeaponReady(WRF_NOBOB | WRF_ALLOWRELOAD)
        goto Ready



      DoubleFireCheck:
        DSSG A 0 A_JumpIfInventory("DakkaInfiniteAmmo", 1, 2)
        DSSG A 0 A_JumpIfInventory("DakkaShells", 2, 1)
        goto FireRecoil

        DSSG A 0 A_JumpIfInventory("DakkaSSG_ShotsFired", 1, "DoubleFireLastShell")
        DSSG A 0 A_PlaySound("dakka/ssgfire", CallACS("Dakka_RotateSound"))
        goto DoubleFireContinue

      DoubleFireLastShell:
        DSSG A 0 A_PlaySound("dakka/ssgfire2", CallACS("Dakka_RotateSound"))
        goto DoubleFireContinue


      DoubleFireContinue:
        DSSG A 0 A_GunFlash("FlashDouble")
        DSSG A 0 A_AlertMonsters
        DSSG A 0 ACS_NamedExecuteWithResult("DSSG_ChangeShots")
        DSSG A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 4, 0)
        DSSG A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, 9, 3, 28)

        DSSG A 0 ACS_NamedExecuteWithResult("Dakka_UseAmmo", AMMO_SHELL, 2, 8)
        DSSG A 0 A_FireBullets(14.6, 9.4, 20, 12, "DakkaPuff", FBF_NORANDOM)


        DSSG A 1 bright offset(0, 48)
        DSSG A 1 bright offset(0, 54)   A_JumpIf(CallACS("DSSG_Refire") == 1, "Fire_Roll")
        DSSG A 1 bright                 A_JumpIf(CallACS("DSSG_Refire") == 1, "Fire_Roll")
        DSSG A 0 bright                 A_CheckReload
        DSSG A 1 bright                 A_JumpIf(CallACS("DSSG_Refire") == 1, "Fire_Roll")
        DSSG A 0 bright                 A_JumpIf(CallACS("DSSG_ShotsLeft") < 1, "ReloadSequence")
        DSSG A 1 bright offset(0, 52)   A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)

        DSSG A 0 bright                 A_JumpIf(CallACS("DSSG_Refire") == 1, "RefireCheckMain")
        DSSG A 1 bright offset(0, 49)   A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DSSG A 0 bright                 A_JumpIf(CallACS("DSSG_Refire") == 1, "RefireCheckMain")
        DSSG A 1 bright offset(0, 46)   A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)

        DSSG A 0 bright                 A_JumpIf(CallACS("DSSG_Refire") == 1, "RefireCheckMain")
        DSSG A 1 bright offset(0, 40)   A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DSSG A 0 bright                 A_JumpIf(CallACS("DSSG_Refire") == 1, "RefireCheckMain")
        DSSG A 1 bright offset(0, 35)   A_WeaponReady(WRF_NOBOB | WRF_ALLOWRELOAD)

        DSSG A 1                        A_JumpIf(CallACS("DSSG_Refire"), "RefireCheckMain")
        DSSG A 1        offset(0, 32)   A_WeaponReady(WRF_NOBOB | WRF_ALLOWRELOAD)
        goto Ready



      AltFire:
        DSSG A 0 A_JumpIfInventory("DakkaInfiniteAmmo", 1, "ActualAltFire")
        DSSG A 0 A_JumpIfInventory("DakkaShotGrenades", 2, "ActualAltFire")
        goto AltFail


      ActualAltFire:
        DSSG A 0 A_GunFlash
        DSSG A 0 A_AlertMonsters
        DSSG A 0 ACS_NamedExecuteWithResult("DSSG_ChangeShots")
        DSSG A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 4, 1)
        DSSG A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, 9, 2, 15)

        DSSG A 0 A_PlaySound("dakka/ssgaltfire", CallACS("Dakka_RotateSound"))
        DSSG A 0 A_FireBullets(14.6, 9.4, 5, 12, "NoBulletPuff", FBF_NORANDOM, 96)
        DSSG A 0 ACS_NamedExecuteWithResult("Dakka_UseAmmo", AMMO_GRENADE, 2, 8)
        DSSG A 0 A_FireCustomMissile("DakkaSSGGrenadeSpawner", 0, false, -2)
        DSSG A 0 A_FireCustomMissile("DakkaSSGGrenadeSpawner", 0, false,  2)


        DSSG A 1 bright offset(0, 42)
        DSSG A 1 bright offset(0, 46)   A_JumpIf(CallACS("DSSG_Refire") == 1, "AltFire_Roll")
        DSSG A 1 bright                 A_JumpIf(CallACS("DSSG_Refire") == 1, "AltFire_Roll")
        DSSG A 0 bright                 A_CheckReload
        DSSG A 0 bright                 A_JumpIf(CallACS("DSSG_Refire") == 1, "AltFire_Roll")
        DSSG A 0 bright                 A_JumpIf(CallACS("DSSG_ShotsLeft") < 1, "ReloadSequence")
        DSSG A 1 bright offset(0, 44)   A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)

        DSSG A 0 bright                 A_JumpIf(CallACS("DSSG_Refire") == 1, "RefireCheckAlt")
        DSSG A 1 bright offset(0, 37)   A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DSSG A 0 bright                 A_JumpIf(CallACS("DSSG_Refire") == 1, "RefireCheckAlt")
        DSSG A 1 bright                 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)

        DSSG A 0 bright                 A_JumpIf(CallACS("DSSG_Refire") == 1, "RefireCheckAlt")
        DSSG A 1 bright offset(0, 34)   A_WeaponReady(WRF_NOFIRE | WRF_NOBOB | WRF_ALLOWRELOAD)
        DSSG A 0 bright                 A_JumpIf(CallACS("DSSG_Refire") == 1, "RefireCheckAlt")
        DSSG A 1 bright                 A_WeaponReady(WRF_NOBOB | WRF_ALLOWRELOAD)

        DSSG A 0                        A_JumpIf(CallACS("DSSG_Refire") == 1, "RefireCheckAlt")
        DSSG A 1        offset(0, 32)   A_WeaponReady(WRF_NOBOB | WRF_ALLOWRELOAD)
        goto Ready

      AltFail:
        DSSG A 1 A_WeaponReady(WRF_NOSECONDARY | WRF_ALLOWRELOAD)
        goto Ready


      // The three states below exist so that the game actually takes the right ammo

      RefireCheckMain:
        DSSG A 1 A_WeaponReady(WRF_NOBOB | WRF_NOSWITCH | WRF_NOPRIMARY)
        goto Fire

      RefireCheckAlt:
        DSSG A 1 A_WeaponReady(WRF_NOBOB | WRF_NOSWITCH | WRF_NOSECONDARY)
        goto AltFire

      RefireCheckReady:
        DSSG A 1 A_WeaponReady(WRF_NOBOB | WRF_NOSWITCH)
        goto Ready


      // These states exist for dakka_cl_ssgrollfire

      Fire_Roll:
        DSSG A 0 A_JumpIfInventory("DakkaSSG_RollFire", 1, "Fire")
        goto RefireCheckMain

      AltFire_Roll:
        DSSG A 0 A_JumpIfInventory("DakkaSSG_RollFire", 1, "AltFire")
        goto RefireCheckAlt



      ReloadSequence:
        DSSG B 0 A_JumpIfInventory("DakkaInfiniteAmmo", 1, "Reload2")
        DSSG B 0 A_JumpIfInventory("DakkaShells",       2, "Reload2")
        DSSG B 0 A_JumpIfInventory("DakkaShotGrenades", 2, "Reload2")
        goto WhatReload

      Reload2:
        DSSG B 3 offset(0, 32) A_WeaponReady(WRF_NOFIRE)
        DSSG F 0 A_PlaySound("dakka/ssgopen", CHAN_WEAPON)
        DSSG C 1 A_WeaponReady(WRF_NOFIRE)
        DSSG D 1 A_WeaponReady(WRF_NOFIRE)
        DSSG E 5 A_WeaponReady(WRF_NOFIRE)
        DSSG F 2 A_WeaponReady(WRF_NOFIRE)
        DSSG G 2 A_WeaponReady(WRF_NOFIRE)
        DSSG H 3 A_WeaponReady(WRF_NOFIRE)
        DSSG IJ 2 A_WeaponReady(WRF_NOFIRE)
        DSSG K 2 A_WeaponReady(WRF_NOFIRE)
        DSSG K 0 A_PlaySound("dakka/ssgload", CHAN_WEAPON)
        DSSG K 1 A_WeaponReady(WRF_NOFIRE)
        DSSG L 2 A_WeaponReady(WRF_NOFIRE)
        DSSG M 0 ACS_NamedExecuteWithResult("DSSG_ChangeShots", -0x7FFFFFFF)
        DSSG M 3 A_WeaponReady(WRF_NOFIRE)
        DSSG N 2 A_WeaponReady(WRF_NOFIRE)
        DSSG OPQ 1 A_WeaponReady(WRF_NOFIRE)
        DSSG R 0 A_PlaySound("dakka/ssgclose", CHAN_WEAPON)
        DSSG R 3 A_WeaponReady(WRF_NOFIRE)
        DSSG S 3 A_WeaponReady
        goto Ready

      WhatReload:
        DSSG A 0
        goto Ready

      Reload:
        DSSG B 0 A_JumpIfInventory("DakkaSSG_ShotsFired", 1, "FirstReload")
        goto Ready3

      IdleReload:
        DSSG B 0 A_JumpIfInventory("DakkaInfiniteAmmo", 1, "IReload2")
        DSSG B 0 A_JumpIfInventory("DakkaShells",       2, "IReload2")
        DSSG B 0 A_JumpIfInventory("DakkaShotGrenades", 2, "IReload2")
        goto Ready3

      IReload2:
        DSSG B 3 offset(0, 32) A_WeaponReady
        DSSG F 0 A_PlaySound("dakka/ssgopen", CHAN_WEAPON)
        DSSG C 1 A_WeaponReady
        DSSG D 1 A_WeaponReady
        DSSG E 5 A_WeaponReady
        DSSG F 2 A_WeaponReady
        DSSG G 2 A_WeaponReady
        DSSG H 3 A_WeaponReady
        DSSG IJ 2 A_WeaponReady
        DSSG K 2 A_WeaponReady
        DSSG K 0 A_PlaySound("dakka/ssgload", CHAN_WEAPON)
        DSSG K 1 A_WeaponReady
        DSSG L 2 A_WeaponReady
        DSSG M 0 ACS_NamedExecuteWithResult("DSSG_ChangeShots", -0x7FFFFFFF)
        DSSG M 3 A_WeaponReady
        DSSG N 2 A_WeaponReady
        DSSG OPQ 1 A_WeaponReady
        DSSG R 0 A_PlaySound("dakka/ssgclose", CHAN_WEAPON)
        DSSG R 3 A_WeaponReady
        DSSG S 3 A_WeaponReady
        goto Ready

      FirstReload:
        DSSG B 0 A_JumpIfInventory("DakkaInfiniteAmmo", 1, "FReload2")
        DSSG B 0 A_JumpIfInventory("DakkaShells",       2, "FReload2")
        DSSG B 0 A_JumpIfInventory("DakkaShotGrenades", 2, "FReload2")
        goto Ready3

      FReload2:
        DSSG B 3 offset(0, 32) A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DSSG F 0 A_PlaySound("dakka/ssgopen", CHAN_WEAPON)
        DSSG C 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DSSG D 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DSSG E 5 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DSSG F 2 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DSSG G 2 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DSSG H 3 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DSSG IJ 2 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DSSG K 2 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DSSG K 0 A_PlaySound("dakka/ssgload", CHAN_WEAPON)
        DSSG K 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DSSG L 2 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB)
        DSSG M 0 ACS_NamedExecuteWithResult("DSSG_ChangeShots", -0x7FFFFFFF)
        DSSG M 3 A_WeaponReady(WRF_NOBOB)
        DSSG N 2 A_WeaponReady(WRF_NOBOB)
        DSSG OPQ 1 A_WeaponReady(WRF_NOBOB)
        DSSG R 0 A_PlaySound("dakka/ssgclose", CHAN_WEAPON)
        DSSG R 3 A_WeaponReady(WRF_NOBOB)
        DSSG S 3 A_WeaponReady(WRF_NOBOB)
        goto Ready


      Flash:
        DSSF B 1 bright offset(0, 42) A_Light(8)
        DSSF B 1 bright offset(0, 32) A_Light(6)
        DSSF A 1 bright A_Light(4)
        DSSF A 1 bright A_Light(3)
        TNT1 A 1 bright A_Light(2)
        TNT1 A 1 bright A_Light(1)
        goto LightDone

      FlashDouble:
        DSSF B 1 bright offset(0, 48) A_Light(16)
        DSSF B 1 bright offset(0, 32) A_Light(12)
        DSSF A 1 bright A_Light(10)
        DSSF A 1 bright A_Light(8)
        TNT1 A 1 A_Light(6)
        TNT1 A 1 A_Light(4)
        TNT1 A 1 A_Light(3)
        TNT1 A 1 A_Light(2)
        TNT1 A 1 A_Light(1)
        goto LightDone

      Spawn:
        DSSG Z -1
        stop
    }
}


actor DakkaSSGGrenadeSpawner
{
    Radius 2
    Height 2
    Speed  4

    Projectile
    +NOINTERACTION
    +SERVERSIDEONLY

    var int user_spawnvelx;
    var int user_spawnvely;
    var int user_spawnvelz;

    var int user_velx;
    var int user_vely;
    var int user_velz;
    var int user_t;

    Damage 6
    Scale 0.25

    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_JumpIf(CallACS("Dakka_IsClient") == false, 1)
        goto NETCODE

        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_ProjUpdate")
        goto SpawnCommon

      Death:
        TNT1 A 0 A_JumpIf(CallACS("Dakka_IsClient") == false, 1)
        goto NETCODE

        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_ProjDeathUpdate")
        goto SpawnCommon

      NETCODE:
        TNT1 A 0
        stop


      SpawnCommon:
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_CalcProjAngle", 20, 1860, 740)
        TNT1 A 0 A_SpawnItemEx("DakkaSSG_Grenade1", 0,0,0, user_spawnvelx/65536.0, user_spawnvely/65536.0, user_spawnvelz/65536.0, angle, SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPOINTERS)
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_CalcProjAngle", 30, 1860, 740)
        TNT1 A 0 A_SpawnItemEx("DakkaSSG_Grenade1", 0,0,0, user_spawnvelx/65536.0, user_spawnvely/65536.0, user_spawnvelz/65536.0, angle, SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPOINTERS)
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_CalcProjAngle", 40, 1860, 740)
        TNT1 A 0 A_SpawnItemEx("DakkaSSG_Grenade1", 0,0,0, user_spawnvelx/65536.0, user_spawnvely/65536.0, user_spawnvelz/65536.0, angle, SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPOINTERS)
        
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_CalcProjAngle", 51, 1860, 740)
        TNT1 A 0 A_SpawnItemEx("DakkaSSG_Grenade2", 0,0,0, user_spawnvelx/65536.0, user_spawnvely/65536.0, user_spawnvelz/65536.0, angle, SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPOINTERS)
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_CalcProjAngle", 62, 1860, 740)
        TNT1 A 0 A_SpawnItemEx("DakkaSSG_Grenade2", 0,0,0, user_spawnvelx/65536.0, user_spawnvely/65536.0, user_spawnvelz/65536.0, angle, SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPOINTERS)
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_CalcProjAngle", 73, 1860, 740)
        TNT1 A 0 A_SpawnItemEx("DakkaSSG_Grenade2", 0,0,0, user_spawnvelx/65536.0, user_spawnvely/65536.0, user_spawnvelz/65536.0, angle, SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPOINTERS)
        
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_CalcProjAngle", 84, 1860, 740)
        TNT1 A 0 A_SpawnItemEx("DakkaSSG_Grenade3", 0,0,0, user_spawnvelx/65536.0, user_spawnvely/65536.0, user_spawnvelz/65536.0, angle, SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPOINTERS)
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_CalcProjAngle", 95, 1860, 740)
        TNT1 A 0 A_SpawnItemEx("DakkaSSG_Grenade3", 0,0,0, user_spawnvelx/65536.0, user_spawnvely/65536.0, user_spawnvelz/65536.0, angle, SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPOINTERS)
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_CalcProjAngle", 106, 1860, 740)
        TNT1 A 0 A_SpawnItemEx("DakkaSSG_Grenade3", 0,0,0, user_spawnvelx/65536.0, user_spawnvely/65536.0, user_spawnvelz/65536.0, angle, SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPOINTERS)
        
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_CalcProjAngle", 117, 1860, 740)
        TNT1 A 0 A_SpawnItemEx("DakkaSSG_Grenade4", 0,0,0, user_spawnvelx/65536.0, user_spawnvely/65536.0, user_spawnvelz/65536.0, angle, SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPOINTERS)
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_CalcProjAngle", 128, 1860, 740)
        TNT1 A 0 A_SpawnItemEx("DakkaSSG_Grenade4", 0,0,0, user_spawnvelx/65536.0, user_spawnvely/65536.0, user_spawnvelz/65536.0, angle, SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPOINTERS)
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_CalcProjAngle", 139, 1860, 740)
        TNT1 A 0 A_SpawnItemEx("DakkaSSG_Grenade4", 0,0,0, user_spawnvelx/65536.0, user_spawnvely/65536.0, user_spawnvelz/65536.0, angle, SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPOINTERS)
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_CalcProjAngle", 150, 1860, 740)
        TNT1 A 0 A_SpawnItemEx("DakkaSSG_Grenade4", 0,0,0, user_spawnvelx/65536.0, user_spawnvely/65536.0, user_spawnvelz/65536.0, angle, SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPOINTERS)
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_CalcProjAngle", 161, 1860, 740)
        TNT1 A 0 A_SpawnItemEx("DakkaSSG_Grenade4", 0,0,0, user_spawnvelx/65536.0, user_spawnvely/65536.0, user_spawnvelz/65536.0, angle, SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPOINTERS)
        
        TNT1 A 4
        stop
    }
}


actor DakkaSSG_Grenade
{
    Radius 3
    Height 6
    Speed 55

    Projectile
    Damage (0)

    +FORCEXYBILLBOARD
    +THRUACTORS
    +HITTRACER

    Scale 0.35
    
    +BOUNCEONFLOORS
    +BOUNCEONCEILINGS
    +BOUNCEONWALLS
    BounceFactor 1

    ProjectileKickback 100
    Obituary "$DAKKA_MP_SUPERSHOTGUN_GYRO"

    var int user_spawnvelx;
    var int user_spawnvely;
    var int user_spawnvelz;

    var int user_velx;
    var int user_vely;
    var int user_velz;
    var int user_t;

    States
    {
      Spawn:
        DGRN A 0
        DGRN A 0 ACS_NamedExecuteWithResult("Dakka_ProjUpdate")
        DGRN A 0 bright A_SpawnItemEx("ShotNadeTrail",  0,         0,         0,        0,0,0, 0, SXF_ABSOLUTEPOSITION)
        DGRN A 0 bright A_SpawnItemEx("ShotNadeTrail", -1*momx/2, -1*momy/2, -1*momz/2, 0,0,0, 0, SXF_ABSOLUTEPOSITION)
        DGRN A 1 A_JumpIf(user_t > 2, "Detonate")
        loop
      
      Detonate:
        TNT1 A 0 A_Stop
        goto Death

      Death:
        DGRN A 0 bright ACS_NamedExecuteWithResult("Dakka_ProjDeathUpdate")
        DGRN A 0 bright A_Stop
        DGRN A 0 bright A_NoGravity
        DGRN A 0 bright A_ChangeFlag("PAINLESS",       random(0, 2) != 0)
        DGRN A 0 bright A_ChangeFlag("FORCERADIUSDMG", random(0, 1) == 0)
        DGRN A 0 bright A_Explode(args[0], args[1], false, false, args[2])
        TNT1 A 2 A_SpawnItemEx("SSGAltExplosion_Client")
        stop
    }
}


// args are in order: explosive damage, explosion radius, explosion full damage radius
actor DakkaSSG_Grenade1: DakkaSSG_Grenade { Args 18, 80,  48 }
actor DakkaSSG_Grenade2: DakkaSSG_Grenade { Args 14, 96,  56 }
actor DakkaSSG_Grenade3: DakkaSSG_Grenade { Args 10, 112, 64 }
actor DakkaSSG_Grenade4: DakkaSSG_Grenade { Args 7,  128, 72 }




actor SSGAltExplosion_Client
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    +FORCEXYBILLBOARD

    RenderStyle Add
    Scale 0.5

    States
    {
      Spawn:
        MISL B 0 bright
        MISL B 0 A_Jump(96, 2)
        MISL B 0 A_PlaySound("dakka/ssgaltexplode")
        goto ParticleSpawn

      ParticleSpawn:
        MISL BBB 0 A_SpawnItemEx("GrenadeExplosionSpark", frandom(-6, 6), frandom(-1, 1), frandom( 2,  6), frandom(4, 12), frandom(-3, 3), frandom(0, 10), frandom(0, 360))
        goto SmokeSpawn

      SmokeSpawn:
        MISL B 0 bright A_JumpIf(CallACS("Dakka_LessEffects") == true, "NoSmoke")
        MISL BBB 0 A_SpawnItemEx("ImpalerBigTrailParticle",   frandom(-6, 6), frandom(-1, 1), frandom(-4, 4), frandom(-3, 3), 0,              frandom(-3, 3), frandom(0, 360))

      NoSmoke:
        DMIS GHIJ 2 bright
        stop
    }
}