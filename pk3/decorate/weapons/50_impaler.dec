actor ImpalerBarrelCounter: Counter {}

actor DWep_Impaler: DoomWeapon
{
    Tag "Impaler"

    Weapon.SlotNumber 5
    Weapon.SelectionOrder 300

    Weapon.AmmoType1    "DakkaRockets"
    Weapon.AmmoType2    "DakkaRockets"
    Weapon.AmmoUse1     1
    Weapon.AmmoUse2     3
    Weapon.AmmoGive1    15

    Weapon.BobStyle InverseSmooth
    Weapon.BobSpeed 2.5
    Weapon.BobRangeX 0.3
    Weapon.BobRangeY 0.4

    Inventory.PickupMessage "$DAKKA_PK_IMPALER"
    Obituary "$DAKKA_MP_IMPALER"
    Weapon.YAdjust 0

    +NOALERT
    +ALT_AMMO_OPTIONAL

    States
    {
      Spawn:
        DKIP Z -1
        stop

      Select:
        DKIP A 0 A_TakeInventory("ImpalerBarrelCounter")
        goto Select2

      Select2:
        DKIP AA 0 A_Raise
        DKIP A 1 A_Raise
        loop

      Deselect:
        DKIP F 0 A_Light0
        DKIP AAA 0 A_Lower
        DKIP A 1 A_Lower
        loop

      Ready:
        DKIP A 1 A_WeaponReady
        loop

      Fire:
        DKIP A 0 A_AlertMonsters
        DKIP A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 6, 0)
        DKIP A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, 3, 2, 18)
        DKIP A 0 ACS_NamedExecuteWithResult("Dakka_UseAmmo", AMMO_ROCKET, 1, 3)
        DKIP A 0 A_JumpIfInventory("ImpalerBarrelCounter", 2, "RightFire")
        DKIP A 0 A_JumpIfInventory("ImpalerBarrelCounter", 1, "LeftFire")
        goto CenterFire

      RightFire:
        DKIP B 0 A_TakeInventory("ImpalerBarrelCounter")
        DKIP B 0 A_FireCustomMissile("ImpalerMissile", 0, false, 2)
        DKIP B 0 bright               A_PlaySound("dakka/impalerfire", 5)
        DKIP B 1 bright offset(0, 38) A_GunFlash
        DKIP C 1 bright
        DKIP D 1 bright               A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP E 1 bright offset(0, 37) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP F 2 bright offset(0, 36) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP H 1        offset(0, 34) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP A 0                      A_Refire
        DKIP A 1        offset(0, 33) A_WeaponReady(WRF_NOBOB | WRF_NOPRIMARY)
        DKIP A 0        offset(0, 32) A_Refire
        DKIP A 2                      A_WeaponReady(WRF_NOBOB | WRF_NOPRIMARY)
        goto Ready

      CenterFire:
        DKIP I 0 A_GiveInventory("ImpalerBarrelCounter")
        DKIP I 0 A_FireCustomMissile("ImpalerMissile", 0, false, 0)
        DKIP I 0 bright               A_PlaySound("dakka/impalerfire", 6)
        DKIP I 1 bright offset(0, 38) A_GunFlash
        DKIP J 1 bright
        DKIP K 1 bright               A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP L 1 bright offset(0, 37) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP M 2 bright offset(0, 36) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP O 1        offset(0, 34) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP A 0                      A_Refire
        DKIP A 1        offset(0, 33) A_WeaponReady(WRF_NOBOB | WRF_NOPRIMARY)
        DKIP A 0        offset(0, 32) A_Refire
        DKIP A 2                      A_WeaponReady(WRF_NOBOB | WRF_NOPRIMARY)
        goto Ready

      LeftFire:
        DKIP P 0 A_GiveInventory("ImpalerBarrelCounter")
        DKIP P 0 A_FireCustomMissile("ImpalerMissile", 0, false, -2)
        DKIP P 0 bright               A_PlaySound("dakka/impalerfire", 7)
        DKIP P 1 bright offset(0, 38) A_GunFlash
        DKIP Q 1 bright
        DKIP R 1 bright               A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP S 1 bright offset(0, 37) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP T 2 bright offset(0, 36) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP V 1        offset(0, 34) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKIP A 0                      A_Refire
        DKIP A 1        offset(0, 33) A_WeaponReady(WRF_NOBOB | WRF_NOPRIMARY)
        DKIP A 0        offset(0, 32) A_Refire
        DKIP A 2                      A_WeaponReady(WRF_NOBOB | WRF_NOPRIMARY)
        goto Ready

      AltFire:
        DKIP A 0 A_JumpIfInventory("DakkaInfiniteAmmo", 1, 2)
        DKIP A 0 A_JumpIfInventory("DakkaRockets", 3, 1)
        goto NoAlt

        DKIP A 0 A_GunFlash
        DKIP A 0 A_AlertMonsters
        DKIP A 0 A_PlaySound("dakka/impaleraltfire", CHAN_WEAPON)
        DKIP A 0 ACS_NamedExecuteWithResult("Dakka_Switcharoo", 6, 1)
        DKIP A 0 ACS_NamedExecuteAlways("Dakka_Recoil", 0, 10, 3, 24)
        DKIP A 0 ACS_NamedExecuteWithResult("Dakka_UseAmmo", AMMO_ROCKET, 3, 9)
        DKIP A 0 A_FireCustomMissile("ImpalerAltMissile2", 0, false)
        DKIP A 0 A_FireCustomMissile("ImpalerAltMissile",  0, false, -2)
        DKIP A 0 A_FireCustomMissile("ImpalerAltMissile",  0, false,  2)
        DKP2 E 1 bright offset(0, 38)
        DKP2 F 1 bright offset(0, 43)
        DKP2 G 2 bright offset(0, 44)
        DKP2 H 3 bright offset(0, 43) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKP2 A 1        offset(0, 41) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKP2 A 2                      A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKP2 A 2        offset(0, 38) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKP2 A 2        offset(0, 36) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKP2 B 2        offset(0, 34) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
        DKP2 B 1        offset(0, 33) A_WeaponReady(WRF_NOBOB | WRF_NOSECONDARY)
        DKP2 C 1                      A_WeaponReady(WRF_NOBOB | WRF_NOSECONDARY)
        DKP2 C 2        offset(0, 32) A_WeaponReady(WRF_NOBOB | WRF_NOSECONDARY)
        DKIP A 2                      A_WeaponReady(WRF_NOBOB | WRF_NOSECONDARY)
        DKIP A 0                      A_JumpIfInventory("DakkaInfiniteAmmo", 1, 2)
        DKIP A 0                      A_JumpIfInventory("DakkaRockets", 3, 1)
        goto EndAlt_NoAmmo

        DKIP A 0                      A_Refire
        DKIP A 3                      A_WeaponReady(WRF_NOBOB)
        goto Ready

      NoAlt:
        DKIP A 1 A_WeaponReady(WRF_NOSECONDARY)
        goto Ready

      EndAlt_NoAmmo:
        DKIP A 3 A_WeaponReady(WRF_NOBOB | WRF_NOSECONDARY)
        goto Ready

      Flash:
        TNT1 A 3 A_Light(4)
        TNT1 A 2 A_Light(3)
        TNT1 A 2 A_Light(2)
        TNT1 A 1 A_Light(1)
        TNT1 A 1 A_Light(0)
        stop

      AltFlash:
        TNT1 A 3 A_Light(5)
        TNT1 A 3 A_Light(4)
        TNT1 A 2 A_Light(3)
        TNT1 A 2 A_Light(2)
        TNT1 A 2 A_Light(1)
        TNT1 A 1 A_Light(0)
        stop
    }
}

actor ImpalerMissile: FastProjectile
{
    Speed 72
    Damage (6)

    Radius 6
    Height 4

    Scale 0.5

    var int user_velx;
    var int user_vely;
    var int user_velz;
    var int user_t;

    +SKYEXPLODE
    +FORCEXYBILLBOARD
    +FORCERADIUSDMG
    +EXTREMEDEATH
    +MTHRUSPECIES

    Decal Scorch

    MissileType "ImpalerSmoke"
    MissileHeight 8

    var int user_detonated;

    States
    {
      Spawn:
        MIS3 A 0 bright
        MIS3 A 0 bright A_PlaySound("dakka/impalerfly", CHAN_BODY, 1, true)
        MIS3 A 1 bright ACS_NamedExecuteWithResult("Dakka_ProjUpdate")
        wait

      Death:
        MIS3 A 0 A_StopSound(CHAN_BODY)
        MIS3 A 0 A_JumpIf(user_detonated, "AlreadyDied")
        goto Death2

      Death2:
        TNT1 A 0 A_SetUserVar("user_detonated", true)
        TNT1 A 0 A_Explode(24, 128, true, false, 48)
        TNT1 A 0 A_ChangeFlag("FORCERADIUSDMG", false)
        TNT1 A 0 A_Explode(24, 128, true, false, 48)
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_InitLanceHit", 128, 24, 20, true)
        TNT1 A 0 A_RadiusGive("ImpalerPrimaryHitChecker", 384, RGF_MONSTERS | RGF_OBJECTS | RGF_VOODOO | RGF_CUBE)

        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_InitLanceHit", 256, 48, 20, true)
        TNT1 A 0 A_RadiusGive("ImpalerPrimaryHitChecker", 448, RGF_MONSTERS | RGF_OBJECTS | RGF_VOODOO | RGF_CUBE)

        TNT1 A 8 A_SpawnItemEx("ImpalerExplosion_Client")
        stop

      AlreadyDied:
        TNT1 A 8
        stop
    }
}

actor ImpalerPrimaryHitChecker: Trigger
{
    States
    {
      Pickup:
        TNT1 A 0 ACS_NamedExecutewithResult("Dakka_CheckLanceHit", 1)
        stop
    }
}

actor ImpalerPrimaryHurter
{
    +BLOODLESSIMPACT
    +EXTREMEDEATH

    var int user_damage;

    States
    {
      Spawn:
        TNT1 A 0
        goto DoHurt

      DoHurt:
        TNT1 A 0 A_Explode(user_damage / 2,       8, false, false, 8)
        TNT1 A 0 A_ChangeFlag("FORCERADIUSDMG", true)
        TNT1 A 0 A_Explode((user_damage + 1) / 2, 8, false, false, 8)
        stop
    }
}


actor ImpalerSmoke
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    +FORCEXYBILLBOARD

    RenderStyle Add
    Alpha 0.1

    Scale 0.15
    Radius 0
    Height 0

    States
    {
      Spawn:
        TNT1 A 0 nodelay A_SetScale(frandom(0.15, 0.2))
        TNT1 A 0 A_Jump(256, "Smoke1", "Smoke2", "Smoke3", "Smoke4")
        goto Smoke1

      Smoke1:
        SMK1 ABCDEFGHIJKLMNOPQR 1 bright
        stop

      Smoke2:
        SMK2 ABCDEFGHIJKLMNOPQR 1 bright
        stop

      Smoke3:
        SMK3 ABCDEFGHIJKLMNOP 1 bright
        stop

      Smoke4:
        SMK4 ABCDEFGHIJKLMNOP 1 bright
        stop
    }
}


actor ImpalerAltMissile: FastProjectile
{
    Speed 75
    Damage (6)

    Radius 6
    Height 4

    Scale 0.5

    +SKYEXPLODE
    +EXTREMEDEATH
    +FORCEXYBILLBOARD
    +BLOODLESSIMPACT
    +FORCERADIUSDMG
    +MTHRUSPECIES
    +HITTRACER

    MissileType "ImpalerSmoke"
    MissileHeight 8
    Decal Scorch

    var int user_t;
    var int user_velx;
    var int user_vely;
    var int user_velz;

    States
    {
      Spawn:
        MIS3 A 0 bright
        MIS3 A 0 bright A_PlaySound("dakka/impalerfly", CHAN_BODY, 1, true)
        MIS3 A 1 bright ACS_NamedExecuteWithResult("Dakka_ProjUpdate")
        wait

      Death:
        MIS3 A 0 A_StopSound(CHAN_BODY)
        MIS3 A 0 ACS_NamedExecuteWithResult("Dakka_ImpalerAltHit", 350)
        MIS3 A 0 A_ChangeFlag("NOINTERACTION", 1)
        MIS3 A 0 A_ChangeFlag("NODAMAGETHRUST", 1)
        MIS3 A 8 A_Explode(20, 16, false, false, 16)
        stop

      Detonate:
        TNT1 A 0 A_GiveToTarget("DakkaImpalerResistance")
        TNT1 A 0 A_Explode(40, 160, true, false, 32)
        TNT1 A 0 A_ChangeFlag("FORCERADIUSDMG", 0)
        TNT1 A 0 A_Explode(48, 160, false, false, 32)
        TNT1 A 0 A_TakeFromTarget("DakkaImpalerResistance")
        TNT1 A 35 A_SpawnItemEx("ImpalerExplosion_Client")
        stop
    }
}

actor ImpalerAltMissile_Stage2
{
    +NOINTERACTION
    +EXTREMEDEATH
    +FORCEXYBILLBOARD
    +FORCERADIUSDMG
    +MTHRUSPECIES

    var int user_timecheck;
    
    States
    {
      Spawn:
        MIS3 A  0
        MIS3 A  0 bright A_SetUserVar("user_timecheck", user_timecheck+1)
        MIS3 AA 0 bright A_SpawnItemEx("ImpalerSmoke", frandom(-2, 2), 0, frandom(-2, 2), 0,0,0, frandom(0, 360))
        MIS3 A  1 bright A_SpawnItemEx("ImpalerSmoke", frandom(-2, 2), 0, frandom(-2, 2), 0,0,0, frandom(0, 360))
        loop

      Detonate:
        TNT1 A 0 A_GiveToTarget("DakkaImpalerResistance")
        TNT1 A 0 A_Explode(40, 160, true, false, 32)
        TNT1 A 0 A_ChangeFlag("FORCERADIUSDMG", 0)
        TNT1 A 0 A_Explode(28, 160, false, false, 32)
        TNT1 A 0 A_TakeFromTarget("DakkaImpalerResistance")
        TNT1 A 35 A_SpawnItemEx("ImpalerExplosion_Client")
        stop
    }
}

actor DakkaImpalerResistance: PowerProtection
{
    DamageFactor "None", 0.525
    Powerup.Duration 1
}

actor ImpalerAltMissile2: ImpalerAltMissile { Speed 90 }




actor ImpalerExplosion_Client
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    +FORCEXYBILLBOARD

    RenderStyle Add

    var int user_ringangle;

    States
    {
      Spawn:
        MISL B 0 bright
        MISL B 0 A_PlaySound("dakka/impalerexplode")
        goto RingSpawn

      RingSpawn:
        MISL B 0 A_SpawnItemEx("ImpalerRingParticle", 0,0,0, 32, 0, 0, user_ringangle)
        MISL B 0 A_SetUserVar("user_ringangle", user_ringangle + 10)
        MISL B 0 A_JumpIf(user_ringangle >= 360, "ParticleSpawn")
        loop

      ParticleSpawn:
        MISL B 0 A_JumpIf(z - floorz   < 1, "ParticleFloor")
        MISL B 0 A_JumpIf(ceilingz - z < 1, "ParticleCeiling")
        goto ParticleOther

      ParticleFloor:
        MISL BBBBBBBBBBBBBBBB 0 A_SpawnItemEx("ImpalerExplosionSpark", frandom(-6, 6), frandom(-1, 1), frandom( 2,  6), frandom(4, 12), frandom(-3, 3), frandom(0, 10), frandom(0, 360))
        goto SmokeSpawn

      ParticleCeiling:
        MISL BBBBBBBBBBBBBBBB 0 A_SpawnItemEx("ImpalerExplosionSpark", frandom(-6, 6), frandom(-1, 1), frandom(-6, -2), frandom(4, 12), frandom(-3, 3), frandom(-10, 0), frandom(0, 360))
        goto SmokeSpawn

      ParticleOther:
        MISL BBBBBBBBBBBBBBBB 0 A_SpawnItemEx("ImpalerExplosionSpark", frandom(-6, 6), frandom(-1, 1), frandom(-3, 3), frandom(4, 12), frandom(-3, 3), frandom(-4, 8), frandom(0, 360))
        goto SmokeSpawn


      SmokeSpawn:
        MISL B 0 bright A_JumpIf(CallACS("Dakka_LessEffects") == true, "NoSmoke")
        MISL BBBBBBBBBBBB 0 A_SpawnItemEx("ImpalerBigTrailParticle",   frandom(-6, 6), frandom(-1, 1), frandom(-4, 4), frandom(-3, 3), 0,              frandom(-3, 3), frandom(0, 360))

      NoSmoke:
        DMIS GHIJ 2 bright
        stop
    }
}

actor ImpalerExplosionSpark
{
    +CLIENTSIDEONLY

    Projectile
    -NOGRAVITY
    +THRUACTORS
    +FORCEXYBILLBOARD
    +PAINLESS
    +NOTRIGGER
    Gravity 0.67
    Damage 0

    Radius 3
    Height 3

    BounceType Doom
    BounceFactor 0.25
    WallBounceFactor 1

    Scale 0.075
    RenderStyle Add

    ReactionTime 105

    States
    {
      Spawn:
        OPRT B 0 bright
        OPRT B 0 bright A_JumpIf(CallACS("Dakka_LessEffects") == true, "LessEffects")
        goto Normal

      Normal:
        OPRT B 0 bright A_SpawnItemEx("ImpalerTinyTrailParticle", 0,0,0, frandom(-1, 1), 0, frandom(-1, 1), random(0, 360))
        OPRT B 1 bright A_Countdown
        loop

      LessEffects:
        OPRT B 1 bright A_Countdown
        loop

      Death:
        TNT1 A 0
        stop
    }
}

actor ImpalerRingParticle
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    +FORCEXYBILLBOARD

    RenderStyle Add
    Alpha 0.125

    States
    {
      Spawn:
        OPRT A 1 bright A_FadeOut(0.01875)
        loop
    }
}

actor ImpalerTrailParticle
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    +FORCEXYBILLBOARD

    RenderStyle Add
    Scale 0.35

    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_Jump(256, "A", "B", "C", "D", "E", "F")
        goto A

      A:
        RSMK A 1 bright
        goto B

      B:
        RSMK A 2 bright
        goto C

      C:
        RSMK B 1 bright
        goto D

      D:
        RSMK B 2 bright
        goto E

      E:
        RSMK C 1 bright
        goto F

      F:
        RSMK C  2 bright
        RSMK DE 3 bright
        stop
    }
}

actor ImpalerBigTrailParticle
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    +FORCEXYBILLBOARD

    RenderStyle Add
    Scale 0.5

    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_Jump(256, "A", "B", "C", "D", "E", "F")
        goto A

      A:
        RSMK A 2 bright
        goto B

      B:
        RSMK A 3 bright
        goto C

      C:
        RSMK B 2 bright
        goto D

      D:
        RSMK B 3 bright
        goto E

      E:
        RSMK C 2 bright
        goto F

      F:
        RSMK C  3 bright
        RSMK DE 5 bright
        stop
    }
}

actor ImpalerTinyTrailParticle: ImpalerTrailParticle
{
    Scale 0.2
}