actor Pickup_ArmorBonus: CustomInventory replaces ArmorBonus
{
    +ALWAYSPICKUP
    Inventory.PickupMessage "$DAKKA_PK_ARMORBONUS"

    States
    {
      Spawn:
        BON2 ABCDCB 6
        loop
      
      Pickup:
        TNT1 A 0 A_JumpIf(CallACS("Dakka_VanillaHealthArmor") >  1, "VanillaArmor")
        TNT1 A 0 A_JumpIf(CallACS("Dakka_VanillaHealthArmor") == 1, "CappedArmor")
        TNT1 A 0 A_GiveInventory("Dakka_ArmorBonus", 1)
        stop
      
      CappedArmor:
        TNT1 A 0 A_GiveInventory("Dakka_ArmorBonusCapped", 1)
        stop
      
      VanillaArmor:
        TNT1 A 0 A_GiveInventory("ArmorBonus", 1)
        stop
    }
}


// Since this uses special armor merging behavior,
// it's gonna remain as a pickup script for now
actor Pickup_GreenArmor: ClassBasedPickup replaces GreenArmor
{
    States
    {
      Spawn:
        ARM1 A 6
        ARM1 B 7 bright
        loop

      Pickup:
        TNT1 A 0 A_JumpIf(CallACS("Pickup_Pickup", It_GreenArmor, false) == 1, "PickedUp")
        goto NotPickedUp
    }
}


// Ditto
actor Pickup_BlueArmor: ClassBasedPickup replaces BlueArmor
{
    States
    {
      Spawn:
        ARM2 A 6
        ARM2 B 7 bright
        loop

      Pickup:
        TNT1 A 0 A_JumpIf(CallACS("Pickup_Pickup", It_BlueArmor, false) == 1, "PickedUp")
        goto NotPickedUp
    }
}


actor Pickup_Megasphere: CustomInventory replaces Megasphere
{
    +COUNTITEM
    +ALWAYSPICKUP
    +FANCYPICKUPSOUND

    Inventory.PickupMessage "$DAKKA_PK_MEGASPHERE"
    Inventory.PickupSound   "misc/p_pkup"

    States
    {
      Spawn:
        MEGA ABCD 6 bright
        loop

      Pickup:
        TNT1 A 0 A_JumpIf(CallACS("Dakka_VanillaHealthArmor") > 0, "VanillaHeal")
        TNT1 A 0 A_GiveInventory("Dakka_OneHealth", 300)
        TNT1 A 0 A_GiveInventory("Dakka_BlueArmor")
        stop
      
      VanillaHeal:
        TNT1 A 0 A_GiveInventory("Dakka_OneHealthVanilla", 200)
        TNT1 A 0 A_GiveInventory("BlueArmor")
        stop
    }
}
