actor Dakka_OneHealth: Health
{
    Inventory.Amount 1
    Inventory.MaxAmount 300
}

actor Pickup_HealthBonus: CustomInventory replaces HealthBonus
{
    +ALWAYSPICKUP
    Inventory.PickupMessage "$DAKKA_PK_HEALTHBONUS"

    States
    {
      Spawn:
        BON1 ABCDCB 6
        loop

      Pickup:
        TNT1 A 0 A_JumpIf(health >= 200, "Heal300")
        TNT1 A 0 A_JumpIf(health >= 100, "Heal200")
        TNT1 A 0 A_JumpIf(health >= 50,  "Heal100")
        goto Heal50

      Heal300:
        TNT1 A 0 A_GiveInventory("Dakka_OneHealth", 1)
        stop

      Heal200:
        TNT1 A 0 A_GiveInventory("Dakka_OneHealth", 2)
        stop

      Heal100:
        TNT1 A 0 A_GiveInventory("Dakka_OneHealth", 3)
        stop

      Heal50:
        TNT1 A 0 A_GiveInventory("Dakka_OneHealth", 4)
        stop
    }
}

actor Pickup_Stimpack: Stimpack replaces Stimpack
{
    Inventory.PickupMessage "$DAKKA_PK_STIMPACK"
    Health.LowMessage 25, "$DAKKA_PK_STIMPACKLOW" 
}

actor Pickup_Medikit: Medikit replaces Medikit
{
    Inventory.PickupMessage "$DAKKA_PK_MEDIKIT"
    Health.LowMessage 25, "$DAKKA_PK_MEDIKITLOW" 
}

actor Pickup_Soulsphere: CustomInventory replaces Soulsphere
{
    +COUNTITEM
    +ALWAYSPICKUP
    +FANCYPICKUPSOUND

    Inventory.PickupMessage "$DAKKA_PK_SOULSPHERE"
    Inventory.PickupSound   "misc/p_pkup"

    States
    {
      Spawn:
        SOUL ABCDCB 6 bright
        loop

      Pickup:
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_GiveInventory("Dakka_SoulsphereHealth")
        stop
    }
}

actor Dakka_SoulsphereHealth: CustomInventory
{
    Inventory.MaxAmount 50

    States
    {
      Spawn:
        TNT1 A 0
        stop

      Pickup:
        TNT1 A 0 A_JumpIf(health >= 200, "Heal300")
        goto HealNormal

      Heal300:
        TNT1 A 0 A_GiveInventory("Dakka_OneHealth", 1)
        stop

      HealNormal:
        TNT1 A 0 A_GiveInventory("Dakka_OneHealth", 2)
        stop
    }
}