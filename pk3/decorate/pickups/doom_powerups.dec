actor DakkaVampireSphere: Trigger replaces Berserk
{
    +BIGPOWERUP
    +FANCYPICKUPSOUND
    +ALWAYSPICKUP
    +COUNTITEM
    
    Inventory.PickupMessage "$DAKKA_PK_VAMPIRE"
    Inventory.PickupSound "misc/p_pkup"
    
    States
    {
      Spawn:
        DVMP ABCDCB 6 bright
        loop
      
      Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("Dakka_Vampire", 35 * 30)
        stop
    }
}

actor DakkaVampireTimer: Counter { +INTERHUBSTRIP }

actor DakkaVampireEffect: PowerDamage
{
    DamageFactor "normal", 1
    Powerup.Duration 0x7FFFFFFF
    Powerup.Color "FF 00 00", 0.15
}

actor VampireSoundLooper
{
    +NOINTERACTION
    +NOTIMEFREEZE

    Radius 4
    Height 4

    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0  A_PlaySound("dakka/vampire_pickup", CHAN_WEAPON)
        TNT1 A 0  A_PlaySound("dakka/vampire_loop", CHAN_BODY | CHAN_LISTENERZ, 1.0, true)
        TNT1 A -1 ACS_NamedExecuteWithResult("Dakka_FollowTarget")
        wait

      VampireDone:
        TNT1 A 70 A_PlaySound("dakka/vampire_end", CHAN_BODY)
        stop
    }
}

actor VampireHealSound
{
    +NOINTERACTION
    +NOTIMEFREEZE

    Radius 4
    Height 4

    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_PlaySound("dakka/vampire_heal", CHAN_BODY | CHAN_LISTENERZ)
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_Warp(AAPTR_TARGET, 0,0,0, 0, WARPF_NOCHECKPOSITION | WARPF_COPYINTERPOLATION)
        stop
    }
}



actor Pickup_Blursphere: Blursphere replaces Blursphere
{
    Inventory.PickupMessage "Blursphere!"
}

actor Pickup_InvulnSphere: InvulnerabilitySphere replaces InvulnerabilitySphere
{
    Inventory.PickupMessage "Invulnerability!"
    Powerup.Colormap 0.1,0.05,0, 1,0.67,0.25
}
